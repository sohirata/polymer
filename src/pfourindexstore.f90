SUBROUTINE STORE_FOURINDEX_ERI
! CALCULATE AND STORE FOUR-CENTER TWO-ELECTRON ELECTRON REPULSION INTEGRALS
! USING THE OBARA-SAIKA RECURSION METHOD.  THIS SUBROUTINE WORKS CORRECTLY 
! ONLY WHEN HELIX EQUALS TO ZERO.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   USE FMT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER,PARAMETER :: CASHESIZE = 1000 ! CASHE SIZE MUST BE EXACTLY THE SAME IN THE STORE & RESTORE SUBROUTINES
   DOUBLE PRECISION :: ICPUS,ICPUE
   INTEGER :: Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,Q3X,Q3Y,Q3Z
   INTEGER :: I,J,K,L
   INTEGER :: II,JJ,KK,LL,IJ,KL,M
   INTEGER :: I1,I2,I3,J1,J2,J3,IA,JA
   INTEGER :: IJT,KLT
   INTEGER :: NPSHELL_SQ
   INTEGER :: IJANG,SIJ,SKL,NIJ,IJKLANG,TS
   INTEGER :: DOWN1(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,(BASIS_LMAX+1)**2,3)
   INTEGER :: DOWN2(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,(BASIS_LMAX+1)**2,3)
   INTEGER :: DOWN3(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,(BASIS_LMAX+1)**2)
   INTEGER :: DOWN4(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,(BASIS_LMAX+1)**2)
   INTEGER :: DOWN5(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,(BASIS_LMAX+1)**2)
   INTEGER :: DOWNXYZ(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,(BASIS_LMAX+1)**2)
   INTEGER :: NKL((BASIS_LMAX+1)**2),NKL2
   INTEGER :: CGSI(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12)
   INTEGER :: CGSJ(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12)
   INTEGER :: IJMAP(0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1)
   INTEGER :: KLANGINDX(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,(BASIS_LMAX+1)**2)
   DOUBLE PRECISION :: ICOUNT,ICOUNTTOTAL
   INTEGER :: ICASHECOUNT
   INTEGER(4),ALLOCATABLE :: ICASHE1X(:),ICASHE1Y(:),ICASHE1Z(:),ICASHE2(:)
   INTEGER,ALLOCATABLE :: KLT_S(:),KLANG(:),CGSK(:,:),CGSL(:,:)
   INTEGER,ALLOCATABLE :: P2C(:)
   DOUBLE PRECISION :: ERI(0:(BASIS_LMAX+1)**2,((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12, &
                                               ((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12)
   DOUBLE PRECISION :: AX,AY,AZ,BX,BY,BZ,CX,CY,CZ,DX,DY,DZ,PX,PY,PZ,QX,QY,QZ
   DOUBLE PRECISION :: COMZ,COMZ2,C1,C2,LARGECOMZ,RHO_COMZ1,RHO_COMZ2,RHO,T,H
   DOUBLE PRECISION :: PI_AI(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12),IJIJ,PMAX
   DOUBLE PRECISION :: PISUB,F1(0:(BASIS_LMAX+1)*4),F2(0:(BASIS_LMAX+1)*4),WX,WY,WZ,WI_PI(3),WI_QI(3)
   DOUBLE PRECISION :: DOWN1_C(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,(BASIS_LMAX+1)**2,3)
   DOUBLE PRECISION :: DOWN2_C(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,(BASIS_LMAX+1)**2,3)
   DOUBLE PRECISION :: DOWN4_C(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,(BASIS_LMAX+1)**2)
   DOUBLE PRECISION :: DOWN5_C(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,(BASIS_LMAX+1)**2)
   DOUBLE PRECISION,ALLOCATABLE :: COMZ_S(:),PX_S(:),PY_S(:),PZ_S(:),OV(:),QI_CI(:,:)
   DOUBLE PRECISION,ALLOCATABLE :: KLKL(:)
   DOUBLE PRECISION,ALLOCATABLE :: W1(:,:),W2(:,:),W3(:,:)
   DOUBLE PRECISION :: CCIJ(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12)
   DOUBLE PRECISION,ALLOCATABLE :: FERI(:,:,:,:),CCKL(:,:)
   DOUBLE PRECISION :: A,ANGLE
   DOUBLE PRECISION,ALLOCATABLE :: P3X(:),P3Y(:),P3Z(:),P3C(:),P3S(:)
   DOUBLE PRECISION,ALLOCATABLE :: DCASHE(:)
   INTEGER :: TICKET
   LOGICAL :: LEXIST
   CHARACTER(6) :: FILEAPPENDIX

   IF (MYID == 0) THEN
    WRITE(FILEAPPENDIX,'(I6)') 100000
    INQUIRE(FILE=TRIM(COPTN(1))//'.ao.'//FILEAPPENDIX(2:6),EXIST=LEXIST)
   ENDIF
   CALL MPI_BCAST(LEXIST,1,MPI_LOGICAL,0,MPI_COMM_WORLD,IERR)
   IF (LEXIST.AND.(IOPTN(7) <= 1)) THEN
    IF (MYID == 0) WRITE(6,'(A)') '****** .ao.00000 FILE FOUND; FOUR-INDEX ERI CALCULATION WILL BE SKIPPED ******'
    RETURN
   ELSE
    WRITE(FILEAPPENDIX,'(I6)') 100000+MYID
    OPEN(31,FILE=TRIM(COPTN(1))//'.ao.'//FILEAPPENDIX(2:6),FORM='UNFORMATTED')
    REWIND(31)
   ENDIF

   NPSHELL_SQ=NPSHELL**2
   ALLOCATE(COMZ_S(NPSHELL_SQ),PX_S(NPSHELL_SQ),PY_S(NPSHELL_SQ),PZ_S(NPSHELL_SQ),OV(NPSHELL_SQ))
   ALLOCATE(QI_CI(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,NPSHELL_SQ))
   ALLOCATE(KLT_S(NPSHELL_SQ),KLANG(NPSHELL_SQ),CGSK(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,NPSHELL_SQ))
   ALLOCATE(CGSL(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,NPSHELL_SQ),KLKL(NPSHELL_SQ))
   ALLOCATE(FERI(NCGS,NCGS,NCGS,NCGS),CCKL(((BASIS_LMAX+1)*(BASIS_LMAX+2)*(BASIS_LMAX+3))**2/12,NPSHELL_SQ),P2C(NPGS))
   ALLOCATE(ICASHE1X(CASHESIZE),ICASHE1Y(CASHESIZE),ICASHE1Z(CASHESIZE),ICASHE2(CASHESIZE),DCASHE(CASHESIZE))
   ALLOCATE(P3X(-CEL2X-CEL1X:CEL2X+CEL1X),P3Y(-CEL2Y-CEL1Y:CEL2Y+CEL1Y),P3Z(-CEL2Z-CEL1Z:CEL2Z+CEL1Z))
   ALLOCATE(P3C(-CEL2X-CEL1X:CEL2X+CEL1X),P3S(-CEL2X-CEL1X:CEL2X+CEL1X))

   IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE FOUR-INDEX TWO-ELECTRON INTEGRALS ONCE AND STORE THEM ON DISK'
   IF ((NCGS >= 256).OR.(CEL1X >= 256).OR.(CEL2X >= 256).OR.(CEL1Y >= 256).OR.(CEL2Y >= 256).OR.(CEL1Z >= 256).OR.(CEL2Z >= 256)) &
    CALL PABORT('TWO-ELECTRON INTEGRAL FILE IS TOO LARGE')
   DO I=1,NPGS
    K=0
    DO J=1,NCGS
     IF (CC(J,I) /= 0.0D0) THEN
      K=K+1
      P2C(I)=J
     ENDIF
    ENDDO
    IF (K == 0) THEN
     P2C(I)=1
    ELSE IF (K > 1) THEN
     CALL PABORT('GENERAL CONTRACTION IS NOT YET SUPPORTED FOR MP2 CALCULATIONS')
    ENDIF
   ENDDO
   DO Q3X=-CEL2X-CEL1X,CEL2X+CEL1X
    ANGLE=DFLOAT(Q3X)*HELIX
    P3X(Q3X)=DFLOAT(Q3X)*PERIODX
    P3C(Q3X)=DCOS(ANGLE)
    P3S(Q3X)=DSIN(ANGLE)
   ENDDO
   DO Q3Y=-CEL2Y-CEL1Y,CEL2Y+CEL1Y
    P3Y(Q3Y)=DFLOAT(Q3Y)*PERIODY
   ENDDO
   DO Q3Z=-CEL2Z-CEL1Z,CEL2Z+CEL1Z
    P3Z(Q3Z)=DFLOAT(Q3Z)*PERIODZ
   ENDDO
  
   ! PRELIMINARY
   ICOUNT=0.0D0
   ICASHECOUNT=0
   ICASHE1X=-1
   ICASHE1Y=-1
   ICASHE1Z=-1
   ICASHE2=-1
   PISUB=2.0D0/DSQRT(PI)
   F1(0)=1.0D0/PISUB
   F2(0)=-0.5D0
   DO I=1,(BASIS_LMAX+1)*4
    F1(I)=F1(I-1)*DFLOAT(2*I-1)/2.0D0
    F2(I)=F2(I-1)-1.0D0
   ENDDO
       
   ! PRELIMINARY FOR THE OBARA-SAIKA RECURSION FORMULA
   DOWN1=0
   DOWN2=0
   DOWN3=0
   DOWN4=0
   DOWN5=0
   DO IA=0,BASIS_LMAX
    DO JA=0,BASIS_LMAX
     KLT=IA*(BASIS_LMAX+1)+JA+1
     NKL(KLT)=0
     DO I1=0,IA
      DO I2=0,IA-I1
       DO I3=0,IA-I1-I2
        DO J1=0,JA
         DO J2=0,JA-J1
          DO J3=0,JA-J1-J2
           NKL(KLT)=NKL(KLT)+1
           KLANGINDX(NKL(KLT),KLT)=I1+I2+I3+J1+J2+J3
           IJMAP(I1,I2,I3,J1,J2,J3)=NKL(KLT)
           IF ((I1 >= 1).AND.(J1 >= 1)) THEN
            DOWN1(NKL(KLT),KLT,1)=IJMAP(I1-1,I2,I3,J1,J2,J3)
            DOWN1_C(NKL(KLT),KLT,1)=DFLOAT(I1)/2.0D0
            DOWN2(NKL(KLT),KLT,1)=IJMAP(I1,I2,I3,J1-1,J2,J3)
            DOWN2_C(NKL(KLT),KLT,1)=DFLOAT(J1)/2.0D0
           ELSE IF ((I1 >= 1).AND.(J1 == 0)) THEN
            DOWN1(NKL(KLT),KLT,1)=IJMAP(I1-1,I2,I3,J1,J2,J3)
            DOWN1_C(NKL(KLT),KLT,1)=DFLOAT(I1)/2.0D0
           ELSE IF ((J1 >= 1).AND.(I1 == 0)) THEN
            DOWN2(NKL(KLT),KLT,1)=IJMAP(I1,I2,I3,J1-1,J2,J3)
            DOWN2_C(NKL(KLT),KLT,1)=DFLOAT(J1)/2.0D0
           ENDIF
           IF ((I2 >= 1).AND.(J2 >= 1)) THEN
            DOWN1(NKL(KLT),KLT,2)=IJMAP(I1,I2-1,I3,J1,J2,J3)
            DOWN1_C(NKL(KLT),KLT,2)=DFLOAT(I2)/2.0D0
            DOWN2(NKL(KLT),KLT,2)=IJMAP(I1,I2,I3,J1,J2-1,J3)
            DOWN2_C(NKL(KLT),KLT,2)=DFLOAT(J2)/2.0D0
           ELSE IF ((I2 >= 1).AND.(J2 == 0)) THEN
            DOWN1(NKL(KLT),KLT,2)=IJMAP(I1,I2-1,I3,J1,J2,J3)
            DOWN1_C(NKL(KLT),KLT,2)=DFLOAT(I2)/2.0D0
           ELSE IF ((J2 >= 1).AND.(I2 == 0)) THEN
            DOWN2(NKL(KLT),KLT,2)=IJMAP(I1,I2,I3,J1,J2-1,J3)
            DOWN2_C(NKL(KLT),KLT,2)=DFLOAT(J2)/2.0D0
           ENDIF
           IF ((I3 >= 1).AND.(J3 >= 1)) THEN
            DOWN1(NKL(KLT),KLT,3)=IJMAP(I1,I2,I3-1,J1,J2,J3)
            DOWN1_C(NKL(KLT),KLT,3)=DFLOAT(I3)/2.0D0
            DOWN2(NKL(KLT),KLT,3)=IJMAP(I1,I2,I3,J1,J2,J3-1)
            DOWN2_C(NKL(KLT),KLT,3)=DFLOAT(J3)/2.0D0
           ELSE IF ((I3 >= 1).AND.(J3 == 0)) THEN
            DOWN1(NKL(KLT),KLT,3)=IJMAP(I1,I2,I3-1,J1,J2,J3)
            DOWN1_C(NKL(KLT),KLT,3)=DFLOAT(I3)/2.0D0
           ELSE IF ((J3 >= 1).AND.(I3 == 0)) THEN
            DOWN2(NKL(KLT),KLT,3)=IJMAP(I1,I2,I3,J1,J2,J3-1)
            DOWN2_C(NKL(KLT),KLT,3)=DFLOAT(J3)/2.0D0
           ENDIF

           IF (I1 >= 1) THEN
            DOWNXYZ(NKL(KLT),KLT)=1
            DOWN3(NKL(KLT),KLT)=IJMAP(I1-1,I2,I3,J1,J2,J3)
            IF (I1 >= 2) THEN
             DOWN4(NKL(KLT),KLT)=IJMAP(I1-2,I2,I3,J1,J2,J3)
             DOWN4_C(NKL(KLT),KLT)=DFLOAT(I1-1)/2.0D0
            ENDIF
            IF (J1 >= 1) THEN
             DOWN5(NKL(KLT),KLT)=IJMAP(I1-1,I2,I3,J1-1,J2,J3)
             DOWN5_C(NKL(KLT),KLT)=DFLOAT(J1)/2.0D0
            ENDIF
           ELSE IF (I2 >= 1) THEN
            DOWNXYZ(NKL(KLT),KLT)=2
            DOWN3(NKL(KLT),KLT)=IJMAP(I1,I2-1,I3,J1,J2,J3)
            IF (I2 >= 2) THEN
             DOWN4(NKL(KLT),KLT)=IJMAP(I1,I2-2,I3,J1,J2,J3)
             DOWN4_C(NKL(KLT),KLT)=DFLOAT(I2-1)/2.0D0
            ENDIF
            IF (J2 >= 1) THEN
             DOWN5(NKL(KLT),KLT)=IJMAP(I1,I2-1,I3,J1,J2-1,J3)
             DOWN5_C(NKL(KLT),KLT)=DFLOAT(J2)/2.0D0
            ENDIF
           ELSE IF (I3 >= 1) THEN
            DOWNXYZ(NKL(KLT),KLT)=3
            DOWN3(NKL(KLT),KLT)=IJMAP(I1,I2,I3-1,J1,J2,J3)
            IF (I3 >= 2) THEN
             DOWN4(NKL(KLT),KLT)=IJMAP(I1,I2,I3-2,J1,J2,J3)
             DOWN4_C(NKL(KLT),KLT)=DFLOAT(I3-1)/2.0D0
            ENDIF
            IF (J3 >= 1) THEN
             DOWN5(NKL(KLT),KLT)=IJMAP(I1,I2,I3-1,J1,J2,J3-1)
             DOWN5_C(NKL(KLT),KLT)=DFLOAT(J3)/2.0D0
            ENDIF
           ELSE IF (J1 >= 1) THEN
            DOWNXYZ(NKL(KLT),KLT)=1
            DOWN3(NKL(KLT),KLT)=IJMAP(I1,I2,I3,J1-1,J2,J3)
            IF (J1 >= 2) THEN
             DOWN4(NKL(KLT),KLT)=IJMAP(I1,I2,I3,J1-2,J2,J3)
             DOWN4_C(NKL(KLT),KLT)=DFLOAT(J1-1)/2.0D0
            ENDIF
           ELSE IF (J2 >= 1) THEN
            DOWNXYZ(NKL(KLT),KLT)=2
            DOWN3(NKL(KLT),KLT)=IJMAP(I1,I2,I3,J1,J2-1,J3)
            IF (J2 >= 2) THEN
             DOWN4(NKL(KLT),KLT)=IJMAP(I1,I2,I3,J1,J2-2,J3)
             DOWN4_C(NKL(KLT),KLT)=DFLOAT(J2-1)/2.0D0
            ENDIF
           ELSE IF (J3 >= 1) THEN
            DOWNXYZ(NKL(KLT),KLT)=3
            DOWN3(NKL(KLT),KLT)=IJMAP(I1,I2,I3,J1,J2,J3-1)
            IF (J3 >= 2) THEN
             DOWN4(NKL(KLT),KLT)=IJMAP(I1,I2,I3,J1,J2,J3-2)
             DOWN4_C(NKL(KLT),KLT)=DFLOAT(J3-1)/2.0D0
            ENDIF
           ELSE
            DOWNXYZ(NKL(KLT),KLT)=0
           ENDIF
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDDO
   
   ! LOOP OVER UNIT CELL INDEX 1
   CALL PCPU_TIME(ICPUS)
   TICKET=0
   DO Q1X=-REDUCED_CEL1X,REDUCED_CEL1X
   DO Q1Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
   DO Q1Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
    ! LOOP OVER UNIT CELL INDEX 2
    DO Q2X=-REDUCED_CEL1X,REDUCED_CEL1X
    DO Q2Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
    DO Q2Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
     TICKET=TICKET+1
     IF (TICKET == NUMPROCS) TICKET=TICKET-NUMPROCS
! ------------------------ PARALLEL LOOP
     IF (MYID == TICKET) THEN
! ------------------------ PARALLEL LOOP
     ! LOOP OVER KL SHELL
     DO K=1,NPSHELL
      KK=P_SHELL(K,0,0,0)
      CX=PGSX(KK)
      CY=PGSY(KK)
      CZ=PGSZ(KK)
      DO L=1,NPSHELL
       LL=P_SHELL(L,0,0,0)
       DX=PGSX(LL)+DFLOAT(Q2X)*PERIODX
       DY=PGSY(LL)*P3C(Q2X)-PGSZ(LL)*P3S(Q2X)+DFLOAT(Q2Y)*PERIODY
       DZ=PGSY(LL)*P3S(Q2X)+PGSZ(LL)*P3C(Q2X)+DFLOAT(Q2Z)*PERIODZ
       COMZ=ZT(KK)+ZT(LL)
       PX=(CX*ZT(KK)+DX*ZT(LL))/COMZ
       PY=(CY*ZT(KK)+DY*ZT(LL))/COMZ
       PZ=(CZ*ZT(KK)+DZ*ZT(LL))/COMZ
       SKL=(K-1)*NPSHELL+L   ! SHELL INDEX
       COMZ_S(SKL)=COMZ      ! STORE ZETA+ZETA
       PX_S(SKL)=PX          ! STORE PX, PY, & PZ
       PY_S(SKL)=PY
       PZ_S(SKL)=PZ
       OV(SKL)=S_P(KK,LL,Q2X,Q2Y,Q2Z)   ! STORE UNNORMALIZED OVERLAP INTEGRALS
       KLANG(SKL)=P_SHELL_ANG(K)+P_SHELL_ANG(L)   ! STORE SUM OF ANGULAR MOMENTA
       KLT_S(SKL)=P_SHELL_ANG(K)*(BASIS_LMAX+1)+P_SHELL_ANG(L)+1 ! STORE SHELL TYPE
       NKL2=0
       ! ELECTRON 2
       DO I1=0,P_SHELL_ANG(K)
        DO I2=0,P_SHELL_ANG(K)-I1
         DO I3=0,P_SHELL_ANG(K)-I1-I2
          DO J1=0,P_SHELL_ANG(L)
           DO J2=0,P_SHELL_ANG(L)-J1
            DO J3=0,P_SHELL_ANG(L)-J1-J2
             NKL2=NKL2+1
             CGSK(NKL2,SKL)=P2C(P_SHELL(K,I1,I2,I3))
             CGSL(NKL2,SKL)=P2C(P_SHELL(L,J1,J2,J3))
             CCKL(NKL2,SKL)=CC(P2C(P_SHELL(K,I1,I2,I3)),P_SHELL(K,I1,I2,I3))*CC(P2C(P_SHELL(L,J1,J2,J3)),P_SHELL(L,J1,J2,J3))
             IF (I1 >= 1) THEN
              QI_CI(NKL2,SKL)=PX-CX
             ELSE IF (I2 >= 1) THEN
              QI_CI(NKL2,SKL)=PY-CY
             ELSE IF (I3 >= 1) THEN
              QI_CI(NKL2,SKL)=PZ-CZ
             ELSE IF (J1 >= 1) THEN
              QI_CI(NKL2,SKL)=PX-DX
             ELSE IF (J2 >= 1) THEN
              QI_CI(NKL2,SKL)=PY-DY
             ELSE IF (J3 >= 1) THEN
              QI_CI(NKL2,SKL)=PZ-DZ
             ENDIF
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
       IF (NKL2 /= NKL(KLT_S(SKL))) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
      ENDDO
     ENDDO

     ! FIND THE MAXIMUM (KL|KL) VALUES FOR ALL SHELLS
     KLKL=0.0D0
     DO SKL=1,NPSHELL_SQ
      KLT=KLT_S(SKL)
      LARGECOMZ=2.0D0*COMZ_S(SKL)
      RHO=0.5D0*COMZ_S(SKL)
      IJKLANG=2*KLANG(SKL)
      C2=2.0D0*DSQRT(RHO/PI)*OV(SKL)**2
      IF (IJKLANG > 0) THEN
       DO M=0,IJKLANG
        ERI(M,1,1)=C2/DFLOAT(2*M+1)
       ENDDO
       DO IJ=1,NKL(KLT)
        IF (IJ == 1) THEN
         DO KL=2,NKL(KLT)
          DO M=0,IJKLANG-KLANGINDX(KL,KLT)
           ERI(M,KL,1)=QI_CI(KL,SKL)*ERI(M,DOWN3(KL,KLT),1)
           IF (DOWN4(KL,KLT) /= 0) ERI(M,KL,1)=ERI(M,KL,1) &
           +DOWN4_C(KL,KLT)/COMZ_S(SKL)*(ERI(M,DOWN4(KL,KLT),1)-0.5D0*ERI(M+1,DOWN4(KL,KLT),1))
           IF (DOWN5(KL,KLT) /= 0) ERI(M,KL,1)=ERI(M,KL,1) &
           +DOWN5_C(KL,KLT)/COMZ_S(SKL)*(ERI(M,DOWN5(KL,KLT),1)-0.5D0*ERI(M+1,DOWN5(KL,KLT),1))
          ENDDO
         ENDDO
        ELSE
         DO KL=1,NKL(KLT)
          DO M=0,IJKLANG-KLANGINDX(IJ,KLT)-KLANGINDX(KL,KLT)
           ERI(M,KL,IJ)=QI_CI(IJ,SKL)*ERI(M,KL,DOWN3(IJ,KLT))
           IF (DOWN4(IJ,KLT) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ) &
           +DOWN4_C(IJ,KLT)/COMZ_S(SKL)*(ERI(M,KL,DOWN4(IJ,KLT))-0.5D0*ERI(M+1,KL,DOWN4(IJ,KLT)))
           IF (DOWN5(IJ,KLT) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ) &
           +DOWN5_C(IJ,KLT)/COMZ_S(SKL)*(ERI(M,KL,DOWN5(IJ,KLT))-0.5D0*ERI(M+1,KL,DOWN5(IJ,KLT)))
           IF (DOWN1(KL,KLT,DOWNXYZ(IJ,KLT)) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ)+DOWN1_C(KL,KLT,DOWNXYZ(IJ,KLT))/LARGECOMZ &
                                                   *ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,KLT)),DOWN3(IJ,KLT))
           IF (DOWN2(KL,KLT,DOWNXYZ(IJ,KLT)) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ)+DOWN2_C(KL,KLT,DOWNXYZ(IJ,KLT))/LARGECOMZ &
                                                   *ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,KLT)),DOWN3(IJ,KLT))
          ENDDO
         ENDDO
        ENDIF
       ENDDO
       DO KL=2,NKL(KLT)
        A=DSQRT(ERI(0,KL,KL)*DABS(CCKL(KL,SKL)))
        IF (A > KLKL(SKL)) KLKL(SKL)=A
       ENDDO
      ELSE
       A=DSQRT(C2*DABS(CCKL(1,SKL)))
       IF (A > KLKL(SKL)) KLKL(SKL)=A
      ENDIF
     ENDDO

     ! LOOP OVER UNIT CELL INDEX 3
     DO Q3X=-CEL2X,CEL2X
     DO Q3Y=-CEL2Y,CEL2Y
     DO Q3Z=-CEL2Z,CEL2Z
      FERI=0.0D0
      DO I=1,NPSHELL
       II=P_SHELL(I,0,0,0)
       AX=PGSX(II)
       AY=PGSY(II)
       AZ=PGSZ(II)
       DO J=1,NPSHELL
        JJ=P_SHELL(J,0,0,0)
        BX=PGSX(JJ)+P3X(Q1X)
        BY=PGSY(JJ)*P3C(Q1X)-PGSZ(JJ)*P3S(Q1X)+P3Y(Q1Y)
        BZ=PGSY(JJ)*P3S(Q1X)+PGSZ(JJ)*P3C(Q1X)+P3Z(Q1Z)
        COMZ=ZT(II)+ZT(JJ)
        PX=(AX*ZT(II)+BX*ZT(JJ))/COMZ
        PY=(AY*ZT(II)+BY*ZT(JJ))/COMZ
        PZ=(AZ*ZT(II)+BZ*ZT(JJ))/COMZ
        C1=S_P(II,JJ,Q1X,Q1Y,Q1Z)*2.0D0/DSQRT(PI)
        SIJ=(I-1)*NPSHELL+J   ! SHELL INDEX
        IJT=P_SHELL_ANG(I)*(BASIS_LMAX+1)+P_SHELL_ANG(J)+1
        IJANG=P_SHELL_ANG(I)+P_SHELL_ANG(J)
        
        ! ELECTRON 1
        NIJ=0
        DO I1=0,P_SHELL_ANG(I)
         DO I2=0,P_SHELL_ANG(I)-I1
          DO I3=0,P_SHELL_ANG(I)-I1-I2
           DO J1=0,P_SHELL_ANG(J)
            DO J2=0,P_SHELL_ANG(J)-J1
             DO J3=0,P_SHELL_ANG(J)-J1-J2
              NIJ=NIJ+1
              CGSI(NIJ)=P2C(P_SHELL(I,I1,I2,I3))
              CGSJ(NIJ)=P2C(P_SHELL(J,J1,J2,J3))
              CCIJ(NIJ)=CC(P2C(P_SHELL(I,I1,I2,I3)),P_SHELL(I,I1,I2,I3))*CC(P2C(P_SHELL(J,J1,J2,J3)),P_SHELL(J,J1,J2,J3))
              IF (I1 >= 1) THEN
               PI_AI(NIJ)=PX-AX
              ELSE IF (I2 >= 1) THEN
               PI_AI(NIJ)=PY-AY
              ELSE IF (I3 >= 1) THEN
               PI_AI(NIJ)=PZ-AZ
              ELSE IF (J1 >= 1) THEN
               PI_AI(NIJ)=PX-BX
              ELSE IF (J2 >= 1) THEN
               PI_AI(NIJ)=PY-BY
              ELSE IF (J3 >= 1) THEN
               PI_AI(NIJ)=PZ-BZ
              ENDIF
             ENDDO
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
        IF (NIJ /= NKL(KLT_S(SIJ))) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
     
        ! FIND THE MAXIMUM (IJ|IJ) VALUE FOR THE CURRENT SHELL
        IJIJ=0.0D0
        LARGECOMZ=2.0D0*COMZ
        RHO=0.5D0*COMZ
        IJKLANG=2*IJANG
        C2=2.0D0*DSQRT(RHO/PI)*S_P(II,JJ,Q1X,Q1Y,Q1Z)**2
        IF (IJKLANG > 0) THEN
         DO M=0,IJKLANG
          ERI(M,1,1)=C2/DFLOAT(2*M+1)
         ENDDO
         DO IJ=1,NIJ
          IF (IJ == 1) THEN
           DO KL=2,NIJ
            DO M=0,IJKLANG-KLANGINDX(KL,IJT)
             ERI(M,KL,1)=QI_CI(KL,SIJ)*ERI(M,DOWN3(KL,IJT),1)
             IF (DOWN4(KL,IJT) /= 0) ERI(M,KL,1)=ERI(M,KL,1) &
           +DOWN4_C(KL,IJT)/COMZ*(ERI(M,DOWN4(KL,IJT),1)-0.5D0*ERI(M+1,DOWN4(KL,IJT),1))
             IF (DOWN5(KL,IJT) /= 0) ERI(M,KL,1)=ERI(M,KL,1) &
           +DOWN5_C(KL,IJT)/COMZ*(ERI(M,DOWN5(KL,IJT),1)-0.5D0*ERI(M+1,DOWN5(KL,IJT),1))
            ENDDO
           ENDDO
          ELSE
           DO KL=1,NIJ
            DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,IJT)
             ERI(M,KL,IJ)=QI_CI(IJ,SIJ)*ERI(M,KL,DOWN3(IJ,IJT))
             IF (DOWN4(IJ,IJT) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ) &
           +DOWN4_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN4(IJ,IJT))-0.5D0*ERI(M+1,KL,DOWN4(IJ,IJT)))
             IF (DOWN5(IJ,IJT) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ) &
           +DOWN5_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN5(IJ,IJT))-0.5D0*ERI(M+1,KL,DOWN5(IJ,IJT)))
             IF (DOWN1(KL,IJT,DOWNXYZ(IJ,IJT)) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ)+DOWN1_C(KL,IJT,DOWNXYZ(IJ,IJT))/LARGECOMZ &
                                                     *ERI(M+1,DOWN1(KL,IJT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
             IF (DOWN2(KL,IJT,DOWNXYZ(IJ,IJT)) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ)+DOWN2_C(KL,IJT,DOWNXYZ(IJ,IJT))/LARGECOMZ &
                                                     *ERI(M+1,DOWN2(KL,IJT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
            ENDDO
           ENDDO
          ENDIF
         ENDDO
         DO IJ=2,NIJ
          A=DSQRT(ERI(0,IJ,IJ)*DABS(CCIJ(IJ)))
          IF (A > IJIJ) IJIJ=A
         ENDDO
        ELSE
         A=DSQRT(C2*DABS(CCIJ(1)))
         IF (A > IJIJ) IJIJ=A
        ENDIF
        
        ! LOOP OVER K SHELL
        DO K=1,NPSHELL
         KK=P_SHELL(K,0,0,0)
         CX=PGSX(KK)+P3X(Q3X)
         CY=PGSY(KK)*P3C(Q3X)-PGSZ(KK)*P3S(Q3X)+P3Y(Q3Y)
         CZ=PGSY(KK)*P3S(Q3X)+PGSZ(KK)*P3C(Q3X)+P3Z(Q3Z)
         ! LOOP OVER L SHELL
         DO L=1,NPSHELL
          LL=P_SHELL(L,0,0,0)
          DX=PGSX(LL)+P3X(Q3X+Q2X)
          DY=PGSY(LL)*P3C(Q3X+Q2X)-PGSZ(LL)*P3S(Q3X+Q2X)+P3Y(Q3Y+Q2Y)
          DZ=PGSY(LL)*P3S(Q3X+Q2X)+PGSZ(LL)*P3C(Q3X+Q2X)+P3Z(Q3Z+Q2Z)
          COMZ2=ZT(KK)+ZT(LL)
          QX=(CX*ZT(KK)+DX*ZT(LL))/COMZ2
          QY=(CY*ZT(KK)+DY*ZT(LL))/COMZ2
          QZ=(CZ*ZT(KK)+DZ*ZT(LL))/COMZ2
          SKL=(K-1)*NPSHELL+L   ! SHELL INDEX
          COMZ_S(SKL)=COMZ2     ! STORE ZETA+ZETA
          PX_S(SKL)=QX          ! STORE PX, PY, & PZ
          PY_S(SKL)=QY
          PZ_S(SKL)=QZ
          OV(SKL)=S_P(KK,LL,Q2X,Q2Y,Q2Z)   ! STORE UNNORMALIZED OVERLAP INTEGRALS
          KLANG(SKL)=P_SHELL_ANG(K)+P_SHELL_ANG(L)   ! STORE SUM OF ANGULAR MOMENTA
          KLT_S(SKL)=P_SHELL_ANG(K)*(BASIS_LMAX+1)+P_SHELL_ANG(L)+1 ! STORE SHELL TYPE
          NKL2=0
          ! ELECTRON 2
          DO I1=0,P_SHELL_ANG(K)
           DO I2=0,P_SHELL_ANG(K)-I1
            DO I3=0,P_SHELL_ANG(K)-I1-I2
             DO J1=0,P_SHELL_ANG(L)
              DO J2=0,P_SHELL_ANG(L)-J1
               DO J3=0,P_SHELL_ANG(L)-J1-J2
                NKL2=NKL2+1
                CGSK(NKL2,SKL)=P2C(P_SHELL(K,I1,I2,I3))
                CGSL(NKL2,SKL)=P2C(P_SHELL(L,J1,J2,J3))
                CCKL(NKL2,SKL)=CC(P2C(P_SHELL(K,I1,I2,I3)),P_SHELL(K,I1,I2,I3))*CC(P2C(P_SHELL(L,J1,J2,J3)),P_SHELL(L,J1,J2,J3))
                IF (I1 >= 1) THEN
                 QI_CI(NKL2,SKL)=QX-CX
                ELSE IF (I2 >= 1) THEN
                 QI_CI(NKL2,SKL)=QY-CY
                ELSE IF (I3 >= 1) THEN
                 QI_CI(NKL2,SKL)=QZ-CZ
                ELSE IF (J1 >= 1) THEN
                 QI_CI(NKL2,SKL)=QX-DX
                ELSE IF (J2 >= 1) THEN
                 QI_CI(NKL2,SKL)=QY-DY
                ELSE IF (J3 >= 1) THEN
                 QI_CI(NKL2,SKL)=QZ-DZ
                ENDIF
               ENDDO
              ENDDO
             ENDDO
            ENDDO
           ENDDO
          ENDDO
          IF (NKL2 /= NKL(KLT_S(SKL))) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
         ENDDO
        ENDDO
        ! LOOP OVER KL SHELL
        DO SKL=1,NPSHELL_SQ
         IF (IJIJ*KLKL(SKL) < DOPTN(12)) CYCLE
         LARGECOMZ=COMZ+COMZ_S(SKL)
         RHO_COMZ1=COMZ_S(SKL)/LARGECOMZ
         RHO_COMZ2=COMZ/LARGECOMZ
         RHO=COMZ*RHO_COMZ1
         IJKLANG=IJANG+KLANG(SKL)
         C2=C1*DSQRT(RHO)*OV(SKL)
         KLT=KLT_S(SKL)
         IF (IJKLANG > 0) THEN
          QX=PX_S(SKL)
          QY=PY_S(SKL)
          QZ=PZ_S(SKL)
          T=RHO*((QX-PX)**2+(QY-PY)**2+(QZ-PZ)**2)
          IF (IJKLANG > 21) CALL PABORT('FMT CANNOT BE EVALUATED')
          IF (IJKLANG+6 > 27) CALL PABORT('IGAMMA CANNOT BE EVALUATED')
          DO M=0,IJKLANG
           IF (T < TF(M)) THEN
            TS=NINT(T*20.0D0)
            H=0.05D0*DFLOAT(TS)-T
            ERI(M,1,1)=((((((IGAMMA(TS,M+6)*H*0.166666666666667D0+IGAMMA(TS,M+5))*H*0.2D0+IGAMMA(TS,M+4))*H*0.25D0+ &
            IGAMMA(TS,M+3))*H*0.333333333333333D0+IGAMMA(TS,M+2))*H*0.5D0+IGAMMA(TS,M+1))*H+IGAMMA(TS,M))*C2
           ELSE
            ERI(M,1,1)=C2*F1(M)*DEXP(F2(M)*DLOG(T))
           ENDIF
          ENDDO
          WX=(PX*COMZ+QX*COMZ_S(SKL))/LARGECOMZ
          WY=(PY*COMZ+QY*COMZ_S(SKL))/LARGECOMZ
          WZ=(PZ*COMZ+QZ*COMZ_S(SKL))/LARGECOMZ
          WI_PI(1)=WX-PX
          WI_PI(2)=WY-PY
          WI_PI(3)=WZ-PZ
          WI_QI(1)=WX-QX
          WI_QI(2)=WY-QY
          WI_QI(3)=WZ-QZ
          IF (NKL(KLT) /= 1) THEN
           DO KL=2,NKL(KLT)
            DO M=0,IJKLANG-KLANGINDX(KL,KLT)
             ERI(M,KL,1)=QI_CI(KL,SKL)*ERI(M,DOWN3(KL,KLT),1)+WI_QI(DOWNXYZ(KL,KLT))*ERI(M+1,DOWN3(KL,KLT),1)
             IF (DOWN4(KL,KLT) /= 0) ERI(M,KL,1)=ERI(M,KL,1) &
           +DOWN4_C(KL,KLT)/COMZ_S(SKL)*(ERI(M,DOWN4(KL,KLT),1)-RHO_COMZ2*ERI(M+1,DOWN4(KL,KLT),1))
             IF (DOWN5(KL,KLT) /= 0) ERI(M,KL,1)=ERI(M,KL,1) &
           +DOWN5_C(KL,KLT)/COMZ_S(SKL)*(ERI(M,DOWN5(KL,KLT),1)-RHO_COMZ2*ERI(M+1,DOWN5(KL,KLT),1))
            ENDDO
           ENDDO
          ENDIF
          IF (NIJ /= 1) THEN
           DO IJ=2,NIJ
            IF ((DOWN4(IJ,IJT) == 0).AND.(DOWN5(IJ,IJT) == 0)) THEN
             DO KL=1,NKL(KLT)
              IF ((DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0).AND.(DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0)) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT))
               ENDDO
              ELSE IF (DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE IF (DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                +(DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)))/LARGECOMZ
               ENDDO
              ENDIF
             ENDDO
            ELSE IF (DOWN5(IJ,IJT) == 0) THEN
             DO KL=1,NKL(KLT)
              IF ((DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0).AND.(DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0)) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN4_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT)))
               ENDDO
              ELSE IF (DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN4_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                + DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE IF (DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN4_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN4_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                +(DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)))/LARGECOMZ
               ENDDO
              ENDIF
             ENDDO
            ELSE IF (DOWN4(IJ,IJT) == 0) THEN
             DO KL=1,NKL(KLT)
              IF ((DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0).AND.(DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0)) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN5_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT)))
               ENDDO
              ELSE IF (DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN5_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))) &
                + DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE IF (DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN5_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN5_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))) &
                +(DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)))/LARGECOMZ
               ENDDO
              ENDIF
             ENDDO
            ELSE
             DO KL=1,NKL(KLT)
              IF ((DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0).AND.(DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0)) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                +(DOWN4_C(IJ,IJT)*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                + DOWN5_C(IJ,IJT)*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))))/COMZ
               ENDDO
              ELSE IF (DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                +(DOWN4_C(IJ,IJT)*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                + DOWN5_C(IJ,IJT)*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))))/COMZ &
                + DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE IF (DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                +(DOWN4_C(IJ,IJT)*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                + DOWN5_C(IJ,IJT)*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))))/COMZ &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                +(DOWN4_C(IJ,IJT)*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                + DOWN5_C(IJ,IJT)*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))))/COMZ &
                +(DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)))/LARGECOMZ
               ENDDO
              ENDIF
             ENDDO
            ENDIF
           ENDDO
          ENDIF
          DO IJ=1,NIJ
           DO KL=1,NKL(KLT)
            FERI(CGSI(IJ),CGSJ(IJ),CGSK(KL,SKL),CGSL(KL,SKL)) &
           =FERI(CGSI(IJ),CGSJ(IJ),CGSK(KL,SKL),CGSL(KL,SKL))+ERI(0,KL,IJ)*CCIJ(IJ)*CCKL(KL,SKL)
           ENDDO
          ENDDO
         ELSE
          QX=PX_S(SKL)
          QY=PY_S(SKL)
          QZ=PZ_S(SKL)
          T=RHO*((QX-PX)**2+(QY-PY)**2+(QZ-PZ)**2)
          IF (T < TF(0)) THEN
           TS=NINT(T*20.0D0)
           H=0.05D0*DFLOAT(TS)-T
           ERI(0,1,1)=((((((IGAMMA(TS,6)*H*0.166666666666667D0+IGAMMA(TS,5))*H*0.2D0+IGAMMA(TS,4))*H*0.25D0+ &
           IGAMMA(TS,3))*H*0.333333333333333D0+IGAMMA(TS,2))*H*0.5D0+IGAMMA(TS,1))*H+IGAMMA(TS,0))*C2
          ELSE
           ERI(0,1,1)=C2*F1(0)*DEXP(-0.5D0*DLOG(T))
          ENDIF
          FERI(CGSI(1),CGSJ(1),CGSK(1,SKL),CGSL(1,SKL)) &
           =FERI(CGSI(1),CGSJ(1),CGSK(1,SKL),CGSL(1,SKL))+ERI(0,1,1)*CCIJ(1)*CCKL(1,SKL)
         ENDIF
        ENDDO
       ENDDO
      ENDDO
      DO I=1,NCGS
       DO J=1,NCGS
        DO K=1,NCGS
         DO L=1,NCGS
          IF (DABS(FERI(I,J,K,L)) > DOPTN(12)) THEN
           ICOUNT=ICOUNT+1.0D0
           IF (ICOUNT*16.0D0 > DOPTN(26)*1000000.0D0) CALL PABORT('THE SIZE OF THE INTEGRAL FILE EXCEEDED MAXDISK')
           ICASHECOUNT=ICASHECOUNT+1
           ICASHE1X(ICASHECOUNT)=((Q1X+CEL1X)*256+(Q2X+CEL1X))*256+(Q3X+CEL2X)
           ICASHE1Y(ICASHECOUNT)=((Q1Y+CEL1Y)*256+(Q2Y+CEL1Y))*256+(Q3Y+CEL2Y)
           ICASHE1Z(ICASHECOUNT)=((Q1Z+CEL1Z)*256+(Q2Z+CEL1Z))*256+(Q3Z+CEL2Z)
           ICASHE2(ICASHECOUNT)=((I*256+J)*256+K)*256+L
           DCASHE(ICASHECOUNT) =FERI(I,J,K,L)
           IF (ICASHECOUNT == CASHESIZE) THEN
            WRITE(31) ICASHE1X,ICASHE1Y,ICASHE1Z,ICASHE2,DCASHE
            ICASHECOUNT=0
            ICASHE1X=-1
            ICASHE1Y=-1
            ICASHE1Z=-1
            ICASHE2=-1
           ENDIF
          ENDIF
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
     ENDDO
     ENDDO
! ------------------------ PARALLEL LOOP
     ENDIF                   
! ------------------------ PARALLEL LOOP
    ENDDO
    ENDDO
    ENDDO
    CALL PCPU_TIME(ICPUE)
    IF (MYID == 0) WRITE(6,'(A,F6.2,A,F10.1,A)') ' ... ', &
      DFLOAT(((Q1X+REDUCED_CEL1X)*(2*REDUCED_CEL1Y+1)+(Q1Y+REDUCED_CEL1Y))*(2*REDUCED_CEL1Z+1)+(Q1Z+REDUCED_CEL1Z+1))/ &
      DFLOAT((2*REDUCED_CEL1X+1)*(2*REDUCED_CEL1Y+1)*(2*REDUCED_CEL1Z+1))*100.0D0, &
      ' % OF ERI GENERATION (CPU / SEC = ',ICPUE-ICPUS,')'
    CALL PCPU_TIME(ICPUS)
    CALL PFLUSH(6)
   ENDDO
   ENDDO
   ENDDO
   IF (ICASHECOUNT /= 0) WRITE(31) ICASHE1X,ICASHE1Y,ICASHE1Z,ICASHE2,DCASHE

   WRITE(6,'(F20.0,A,F7.3,A,I7)') ICOUNT,' ERIS (',ICOUNT &
           /DFLOAT((CEL1X*2+1)**2)/DFLOAT(CEL2X*2+1) &
           /DFLOAT((CEL1Y*2+1)**2)/DFLOAT(CEL2Y*2+1) &
           /DFLOAT((CEL1Z*2+1)**2)/DFLOAT(CEL2Z*2+1) &
           /DFLOAT(NCGS**4)*100.0,'% OF TOTAL ERIS) HAVE BEEN STORED BY PROCESS',MYID
   CALL PFLUSH(6)
!  IF (DFLOAT(ICOUNT)*16.0/1000000.0 > 1.0) THEN
!   WRITE(6,'(F9.1,A)') DFLOAT(ICOUNT)*16.0/1000000.0,' MB DISK SPACE HAS BEEN USED'
!  ELSE
!   WRITE(6,'(F9.1,A)') DFLOAT(ICOUNT)*16.0/1000.0,' KB DISK SPACE HAS BEEN USED'
!  ENDIF
   CALL MPI_REDUCE(ICOUNT,ICOUNTTOTAL,1,MPI_DOUBLE_PRECISION,MPI_SUM,0,MPI_COMM_WORLD,IERR)
   IF (MYID == 0) WRITE(6,'(F20.0,A,F7.3,A)') ICOUNTTOTAL,' ERIS (',ICOUNTTOTAL &
           /DFLOAT((CEL1X*2+1)**2)/DFLOAT(CEL2X*2+1) &
           /DFLOAT((CEL1Y*2+1)**2)/DFLOAT(CEL2Y*2+1) &
           /DFLOAT((CEL1Z*2+1)**2)/DFLOAT(CEL2Z*2+1) &
           /DFLOAT(NCGS**4)*100.0,'% OF TOTAL ERIS) HAVE BEEN STORED BY ALL PROCESSES'
   CALL PFLUSH(6)
!  CALL MPI_BARRIER(MPI_COMM_WORLD,IERR)

   CLOSE(31)
   DEALLOCATE(COMZ_S,PX_S,PY_S,PZ_S,OV,QI_CI)
   DEALLOCATE(KLT_S,KLANG,CGSK,CGSL,KLKL)
   DEALLOCATE(FERI,CCKL,P2C,P3C,P3S)
   DEALLOCATE(ICASHE1X,ICASHE1Y,ICASHE1Z,ICASHE2,DCASHE)
   
   RETURN
END SUBROUTINE



SUBROUTINE RESTORE_FOURINDEX_ERI
! RESTORE FOUR-CENTER TWO-ELECTRON ELECTRON REPULSION INTEGRALS FROM THE INTEGRAL FILE.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE BASISSET
   USE INTEGRAL
   USE STRUCTURE
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER,PARAMETER :: CASHESIZE = 1000 ! CASHE SIZE MUST BE EXACTLY THE SAME IN THE STORE & RESTORE SUBROUTINES
   INTEGER :: EOF
   INTEGER :: Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,Q3X,Q3Y,Q3Z
   INTEGER :: I,J,K,L,PX,PY,PZ,QX,QY,QZ,RX,RY,RZ
   INTEGER :: II,JJ,KK,LL
   INTEGER :: ICASHECOUNT
   INTEGER(4),ALLOCATABLE :: ICASHE1X(:),ICASHE1Y(:),ICASHE1Z(:),ICASHE2(:)
   DOUBLE PRECISION,ALLOCATABLE :: DCASHE(:)
   DOUBLE PRECISION :: ICOUNT,ICOUNTTOTAL
   DOUBLE PRECISION,ALLOCATABLE :: C_C_TMP(:,:,:,:,:)
   DOUBLE PRECISION,ALLOCATABLE :: X_C_TMP(:,:,:,:,:)
   DOUBLE PRECISION,ALLOCATABLE :: W1(:,:),W2(:,:)
   INTEGER :: NFILES
   LOGICAL :: LEXIST
   CHARACTER(6) :: FILEAPPENDIX
   INTEGER :: NROUNDS

   ALLOCATE(ICASHE1X(CASHESIZE),ICASHE1Y(CASHESIZE),ICASHE1Z(CASHESIZE),ICASHE2(CASHESIZE),DCASHE(CASHESIZE))
   ALLOCATE(C_C_TMP(NCGS,NCGS,-CEL1X:CEL1X,-CEL1Y:CEL1Y,-CEL1Z:CEL1Z))
   ALLOCATE(X_C_TMP(NCGS,NCGS,-CEL1X:CEL1X,-CEL1Y:CEL1Y,-CEL1Z:CEL1Z))
   ALLOCATE(W1(NCGS,NCGS),W2(NCGS,NCGS))

   C_C=0.0D0
   X_C=0.0D0
   C_C_TMP=0.0D0
   X_C_TMP=0.0D0
   ICOUNT=0.0D0

   NFILES=0
   IF (MYID == 0) THEN
    DO WHILE (.TRUE.)
     WRITE(FILEAPPENDIX,'(I6)') 100000+NFILES
     INQUIRE(FILE=TRIM(COPTN(1))//'.ao.'//FILEAPPENDIX(2:6),EXIST=LEXIST)
     IF (LEXIST) THEN
      IF (IOPTN(9) >= 2) WRITE(6,'(A)') TRIM(COPTN(1))//'.ao.'//FILEAPPENDIX(2:6)//' IS FOUND'
      NFILES=NFILES+1
     ELSE
      EXIT
     ENDIF
    ENDDO
   ENDIF
   CALL MPI_BCAST(NFILES,1,MPI_INTEGER,0,MPI_COMM_WORLD,IERR)
   IF (NFILES == 0) CALL PABORT('NO ERI FILES WAS FOUND')

!----- PARALLEL LOOP
   DO NROUNDS=1,(NFILES-1)/NUMPROCS+1
   WRITE(FILEAPPENDIX,'(I6)') 100000+MYID+(NROUNDS-1)*NUMPROCS
   INQUIRE(FILE=TRIM(COPTN(1))//'.ao.'//FILEAPPENDIX(2:6),EXIST=LEXIST)
   IF (LEXIST) THEN
   IF (IOPTN(9) >= 2) WRITE(6,'(A,I7,A,A)') '****** PROCESS',MYID,' HAS LOCATED ',TRIM(COPTN(1))//'.ao.'//FILEAPPENDIX(2:6)
   OPEN(31,FILE=TRIM(COPTN(1))//'.ao.'//FILEAPPENDIX(2:6),FORM='UNFORMATTED')
   REWIND(31)
!----- PARALLEL LOOP
   DO
    READ(31,IOSTAT=EOF) ICASHE1X,ICASHE1Y,ICASHE1Z,ICASHE2,DCASHE
    IF (EOF /= 0) EXIT
    DO ICASHECOUNT=1,CASHESIZE
     IF (ICASHE1X(ICASHECOUNT) == -1) EXIT
     ICOUNT=ICOUNT+1.0D0
     PX=0
     QX=0
     RX=0
     PY=0
     QY=0
     RY=0
     PZ=0
     QZ=0
     RZ=0
     I=0
     J=0
     K=0
     L=0
     CALL MVBITS(ICASHE1X(ICASHECOUNT),16,8,PX,0)
     CALL MVBITS(ICASHE1X(ICASHECOUNT), 8,8,QX,0)
     CALL MVBITS(ICASHE1X(ICASHECOUNT), 0,8,RX,0)
     CALL MVBITS(ICASHE1Y(ICASHECOUNT),16,8,PY,0)
     CALL MVBITS(ICASHE1Y(ICASHECOUNT), 8,8,QY,0)
     CALL MVBITS(ICASHE1Y(ICASHECOUNT), 0,8,RY,0)
     CALL MVBITS(ICASHE1Z(ICASHECOUNT),16,8,PZ,0)
     CALL MVBITS(ICASHE1Z(ICASHECOUNT), 8,8,QZ,0)
     CALL MVBITS(ICASHE1Z(ICASHECOUNT), 0,8,RZ,0)
     CALL MVBITS(ICASHE2(ICASHECOUNT),24,8,I,0)
     CALL MVBITS(ICASHE2(ICASHECOUNT),16,8,J,0)
     CALL MVBITS(ICASHE2(ICASHECOUNT), 8,8,K,0)
     CALL MVBITS(ICASHE2(ICASHECOUNT), 0,8,L,0)
     Q1X=PX-CEL1X
     Q2X=QX-CEL1X
     Q3X=RX-CEL2X
     Q1Y=PY-CEL1Y
     Q2Y=QY-CEL1Y
     Q3Y=RY-CEL2Y
     Q1Z=PZ-CEL1Z
     Q2Z=QZ-CEL1Z
     Q3Z=RZ-CEL2Z
     IF (HELIX == 0.0D0) THEN
      IF ((Q1X < -REDUCED_CEL1X).OR.(Q1X > REDUCED_CEL1X).OR.(Q2X < -REDUCED_CEL1X).OR.(Q2X > REDUCED_CEL1X).OR. &
          (Q1Y < -REDUCED_CEL1Y).OR.(Q1Y > REDUCED_CEL1Y).OR.(Q2Y < -REDUCED_CEL1Y).OR.(Q2Y > REDUCED_CEL1Y).OR. &
          (Q1Z < -REDUCED_CEL1Z).OR.(Q1Z > REDUCED_CEL1Z).OR.(Q2Z < -REDUCED_CEL1Z).OR.(Q2Z > REDUCED_CEL1Z).OR. &      
          (Q3X < -CEL2X).OR.(Q3X > CEL2X).OR.(Q3Y < -CEL2Y).OR.(Q3Y > CEL2Y).OR.(Q3Z < -CEL2Z).OR.(Q3Z > CEL2Z)) &
      CALL PABORT('INTEGRAL FILE HAS BEEN DEGRADED')
      C_C_TMP(I,J,Q1X,Q1Y,Q1Z)=C_C_TMP(I,J,Q1X,Q1Y,Q1Z)+P_C(K,L,Q2X,Q2Y,Q2Z)*DCASHE(ICASHECOUNT)
      IF ((Q3X+Q2X >= -CEL1X).AND.(Q3X+Q2X <= CEL1X).AND.(Q1X-Q3X >= -CEL1X).AND.(Q1X-Q3X <= CEL1X).AND. &
          (Q3Y+Q2Y >= -CEL1Y).AND.(Q3Y+Q2Y <= CEL1Y).AND.(Q1Y-Q3Y >= -CEL1Y).AND.(Q1Y-Q3Y <= CEL1Y).AND. &
          (Q3Z+Q2Z >= -CEL1Z).AND.(Q3Z+Q2Z <= CEL1Z).AND.(Q1Z-Q3Z >= -CEL1Z).AND.(Q1Z-Q3Z <= CEL1Z)) &
       X_C_TMP(I,L,Q3X+Q2X,Q3Y+Q2Y,Q3Z+Q2Z)=X_C_TMP(I,L,Q3X+Q2X,Q3Y+Q2Y,Q3Z+Q2Z) &
                                           +P_C(K,J,Q1X-Q3X,Q1Y-Q3Y,Q1Z-Q3Z)*DCASHE(ICASHECOUNT)
     ELSE ! --- HELIX
      IF ((Q1X < -REDUCED_CEL1X).OR.(Q1X > REDUCED_CEL1X).OR.(Q2X < -REDUCED_CEL1X).OR.(Q2X > REDUCED_CEL1X).OR. &
          (Q1Y < -REDUCED_CEL1Y).OR.(Q1Y > REDUCED_CEL1Y).OR.(Q2Y < -REDUCED_CEL1Y).OR.(Q2Y > REDUCED_CEL1Y).OR. &
          (Q1Z < -REDUCED_CEL1Z).OR.(Q1Z > REDUCED_CEL1Z).OR.(Q2Z < -REDUCED_CEL1Z).OR.(Q2Z > REDUCED_CEL1Z).OR. &      
          (Q3X < -CEL2X).OR.(Q3X > CEL2X).OR.(Q3Y < -CEL2Y).OR.(Q3Y > CEL2Y).OR.(Q3Z < -CEL2Z).OR.(Q3Z > CEL2Z)) &
      CALL PABORT('INTEGRAL FILE HAS BEEN DEGRADED')
      C_C_TMP(I,J,Q1X,Q1Y,Q1Z)=C_C_TMP(I,J,Q1X,Q1Y,Q1Z)+P_C_HELIX(K,L,Q3X,Q3X+Q2X)*DCASHE(ICASHECOUNT)
      IF ((Q3X+Q2X >= -CEL1X).AND.(Q3X+Q2X <= CEL1X).AND.(Q1X-Q3X >= -CEL1X).AND.(Q1X-Q3X <= CEL1X).AND. &
          (Q3Y+Q2Y >= -CEL1Y).AND.(Q3Y+Q2Y <= CEL1Y).AND.(Q1Y-Q3Y >= -CEL1Y).AND.(Q1Y-Q3Y <= CEL1Y).AND. &
          (Q3Z+Q2Z >= -CEL1Z).AND.(Q3Z+Q2Z <= CEL1Z).AND.(Q1Z-Q3Z >= -CEL1Z).AND.(Q1Z-Q3Z <= CEL1Z)) &
      X_C_TMP(I,L,Q3X+Q2X,Q3Y+Q2Y,Q3Z+Q2Z)=X_C_TMP(I,L,Q3X+Q2X,Q3Y+Q2Y,Q3Z+Q2Z) &
                                           +P_C_HELIX(K,J,Q3X,Q1X)*DCASHE(ICASHECOUNT)
     ENDIF ! --- HELIX END
    ENDDO
   ENDDO

   IF (IOPTN(9) >= 2) WRITE(6,'(F20.0,A,F7.3,A,I7,A,A)') ICOUNT,' ERIS (',ICOUNT/ &
   DFLOAT((CEL1X*2+1)**2*(CEL1Y*2+1)**2*(CEL1Z*2+1)**2*(CEL2X*2+1)*(CEL2Y*2+1)*(CEL2Z*2+1)*NCGS**4)*100.0, &
   '% OF TOTAL ERIS) HAVE BEEN RESTORED BY PROCESS',MYID,' FROM ',TRIM(COPTN(1))//'.ao.'//FILEAPPENDIX(2:6)

!----- PARALLEL LOOP
   ELSE
    IF (IOPTN(9) >= 2) WRITE(6,'(A,I7,A,A)') '****** PROCESS',MYID,' CANNOT LOCATE ',TRIM(COPTN(1))//'.ao.'//FILEAPPENDIX(2:6)
    EXIT
   ENDIF
   ENDDO
!----- PARALLEL LOOP

   IF (IOPTN(9) >= 2) THEN
    WRITE(6,'(F20.0,A,F7.3,A,I7)') ICOUNT,' ERIS (',ICOUNT/ &
    DFLOAT((CEL1X*2+1)**2*(CEL1Y*2+1)**2*(CEL1Z*2+1)**2*(CEL2X*2+1)*(CEL2Y*2+1)*(CEL2Z*2+1)*NCGS**4)*100.0, &
    '% OF TOTAL ERIS) HAVE BEEN RESTORED BY PROCESS',MYID
    CALL MPI_REDUCE(ICOUNT,ICOUNTTOTAL,1,MPI_DOUBLE_PRECISION,MPI_SUM,0,MPI_COMM_WORLD,IERR)
    IF (MYID == 0) WRITE(6,'(F20.0,A,F7.3,A)') ICOUNTTOTAL,' ERIS (',ICOUNTTOTAL/ &
    DFLOAT((CEL1X*2+1)**2*(CEL1Y*2+1)**2*(CEL1Z*2+1)**2*(CEL2X*2+1)*(CEL2Y*2+1)*(CEL2Z*2+1)*NCGS**4)*100.0, &
    '% OF TOTAL ERIS) HAVE BEEN RESTORED BY ALL PROCESSES'
   ENDIF

!  DUMP COULOMB & EXCHANGE MATRICES
   IF (IOPTN(9) >= 2) THEN
    WRITE(6,'(A,I7)') 'COULOMB MATRIX FOR CONTRACTED GAUSSIANS BY PROCESS',MYID
    CALL DUMP1(C_C_TMP,NCGS,CEL1X,CEL1Y,CEL1Z)
    WRITE(6,'(A,I7)') 'EXCHANGE MATRIX FOR CONTRACTED GAUSSIANS BY PROCESS',MYID
    CALL DUMP1(X_C_TMP,NCGS,CEL1X,CEL1Y,CEL1Z)
   ENDIF

   CALL MPI_ALLREDUCE(C_C_TMP,C_C,NCGS**2*(CEL1X*2+1)*(CEL1Y*2+1)*(CEL1Z*2+1),MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(X_C_TMP,X_C,NCGS**2*(CEL1X*2+1)*(CEL1Y*2+1)*(CEL1Z*2+1),MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)

! --- HELIX
   IF (HELIX /= 0.0D0) THEN
    DO Q1X=-CEL1X,CEL1X
     DO I=1,NCGS
      DO J=1,NCGS
       W1(I,J)=0.0D0
       W2(I,J)=0.0D0
       DO JJ=1,NCGS
        W1(I,J)=W1(I,J)+C2H(J,JJ,Q1X)*C_C(I,JJ,Q1X,0,0)
        W2(I,J)=W2(I,J)+C2H(J,JJ,Q1X)*X_C(I,JJ,Q1X,0,0)
       ENDDO
      ENDDO
     ENDDO
     C_C(:,:,Q1X,0,0)=W1
     X_C(:,:,Q1X,0,0)=W2
    ENDDO
   ENDIF
! --- HELIX END

!  DUMP COULOMB & EXCHANGE MATRICES
   IF ((IOPTN(9) >= 2).AND.(MYID == 0)) THEN
    WRITE(6,'(A)') 'COULOMB MATRIX FOR CONTRACTED GAUSSIANS'
    CALL DUMP1(C_C,NCGS,CEL1X,CEL1Y,CEL1Z)
    WRITE(6,'(A)') 'EXCHANGE MATRIX FOR CONTRACTED GAUSSIANS'
    CALL DUMP1(X_C,NCGS,CEL1X,CEL1Y,CEL1Z)
   ENDIF

   CLOSE(31)
   DEALLOCATE(C_C_TMP,X_C_TMP,W1,W2)
   DEALLOCATE(ICASHE1X,ICASHE1Y,ICASHE1Z,ICASHE2,DCASHE)
   
   RETURN
END SUBROUTINE
