SUBROUTINE CONSTRUCT_GRID
! CONSTRUCT GRID FOR NUMERICAL INTEGRATION IN DFT CALCULATIONS.
! NUMBER OF GRID POINTS (DEFAULT) IS 8656 PER NUCLEUS.
! SEE A.D.BECKE, J.CHEM.PHYS. 88,2547 (1988); O.TREUTLER AND R.AHLRICHS, J.CHEM.PHYS. 102,346 (1995).
 
   USE MPI
   USE CONSTANTS
   USE STRUCTURE
   USE DFT
   USE CONTROL

   IMPLICIT NONE
!  INCLUDE "mpif.h"
   DOUBLE PRECISION,PARAMETER :: TARADI(36) = &
     (/0.800D0,0.900D0,1.800D0,1.400D0,1.300D0,1.100D0,0.900D0,0.900D0,0.900D0,0.900D0, &
       1.400D0,1.300D0,1.300D0,1.200D0,1.100D0,1.000D0,1.000D0,1.000D0, &
       1.500D0,1.400D0,1.300D0,1.200D0,1.200D0,1.200D0,1.200D0,1.200D0,1.200D0,1.100D0, &
       1.100D0,1.100D0,1.100D0,1.000D0,0.900D0,0.900D0,0.900D0,0.900D0/)
   DOUBLE PRECISION,ALLOCATABLE :: GAUSS_CHEV(:)
   DOUBLE PRECISION,ALLOCATABLE :: GAUSS_CHEV_W(:)
   DOUBLE PRECISION,ALLOCATABLE :: LEBEDVX(:),LEBEDVY(:),LEBEDVZ(:)
   DOUBLE PRECISION,ALLOCATABLE :: LEBEDVW(:)
   INTEGER :: I,J,K,L
   DOUBLE PRECISION :: R1,R2,X,A

   ALLOCATE(GAUSS_CHEV(MAX(50,IOPTN(81))),GAUSS_CHEV_W(MAX(50,IOPTN(81))))
   ALLOCATE(LEBEDVX(MAX(302,IOPTN(82))),LEBEDVY(MAX(302,IOPTN(82))),LEBEDVZ(MAX(302,IOPTN(82))),LEBEDVW(MAX(302,IOPTN(82))))

   IF ((IOPTN(81) == 0).AND.(IOPTN(82) == 0)) THEN

    IF (MYID == 0) WRITE(6,'(A)') 'NUMERICAL GRID IS DEFAULT 25*302+13*50+12*38'
    DO I=1,NATOM
     NGRID(I)=8656 ! = 25*302+13*50+12*38
     IF (IATOM(I) > 36) CALL PABORT('EXPAND CONTRUCT_GRID SUBROUTINE')
     R1=TARADI(IATOM(I))
     DO J=1,50
      X=DCOS(DFLOAT(J)*PI/51.0D0)
      GAUSS_CHEV(J)=DEXP(0.6D0*DLOG(1.0D0+X))*DLOG(2.0D0/(1.0D0-X))/DLOG(2.0D0)
      GAUSS_CHEV_W(J)=PI/51.0D0*DSIN(DFLOAT(J)*PI/51.0D0)*(0.6D0*DEXP(-0.4D0*DLOG(1.0D0+X))*DLOG(2.0D0/(1.0D0-X)) &
                     +DEXP(0.6D0*DLOG(1.0D0+X))/(1.0D0-X))/DLOG(2.0D0)*GAUSS_CHEV(J)**2*4.0D0*PI
     ENDDO
     J=0
     CALL ANGULAR_GRID(302,LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW)
     DO K=1,25
      R2=GAUSS_CHEV(K)
      DO L=1,302
       J=J+1
       GRIDX(J,I)=R1*R2*LEBEDVX(L)
       GRIDY(J,I)=R1*R2*LEBEDVY(L)
       GRIDZ(J,I)=R1*R2*LEBEDVZ(L)
       ATOMW(J,I)=R1**3*GAUSS_CHEV_W(K)*LEBEDVW(L)
      ENDDO
     ENDDO
     CALL ANGULAR_GRID(50,LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW)
     DO K=26,38
      R2=GAUSS_CHEV(K)
      DO L=1,50
       J=J+1
       GRIDX(J,I)=R1*R2*LEBEDVX(L)
       GRIDY(J,I)=R1*R2*LEBEDVY(L)
       GRIDZ(J,I)=R1*R2*LEBEDVZ(L)
       ATOMW(J,I)=R1**3*GAUSS_CHEV_W(K)*LEBEDVW(L)
      ENDDO
     ENDDO
     CALL ANGULAR_GRID(38,LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW)
     DO K=39,50
      R2=GAUSS_CHEV(K)
      DO L=1,38
       J=J+1
       GRIDX(J,I)=R1*R2*LEBEDVX(L)
       GRIDY(J,I)=R1*R2*LEBEDVY(L)
       GRIDZ(J,I)=R1*R2*LEBEDVZ(L)
       ATOMW(J,I)=R1**3*GAUSS_CHEV_W(K)*LEBEDVW(L)
      ENDDO
     ENDDO
     IF (J /= NGRID(I)) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
    ENDDO

   ELSE

    IF (MYID == 0) WRITE(6,'(A,I0,A,I0)') 'NUMERICAL GRID IS ',IOPTN(81),' x ',IOPTN(82)
    DO I=1,NATOM
     NGRID(I)=IOPTN(81)*IOPTN(82)
     R1=TARADI(IATOM(I))
     DO J=1,IOPTN(81)
      X=DCOS(DFLOAT(J)*PI/DFLOAT(IOPTN(81)+1))
      GAUSS_CHEV(J)=DEXP(0.6D0*DLOG(1.0D0+X))*DLOG(2.0D0/(1.0D0-X))/DLOG(2.0D0)
      GAUSS_CHEV_W(J)=PI/DFLOAT(IOPTN(81)+1)*DSIN(DFLOAT(J)*PI/DFLOAT(IOPTN(81)+1)) &
                     *(0.6D0*DEXP(-0.4D0*DLOG(1.0D0+X))*DLOG(2.0D0/(1.0D0-X)) &
                     +DEXP(0.6D0*DLOG(1.0D0+X))/(1.0D0-X))/DLOG(2.0D0)*GAUSS_CHEV(J)**2*4.0D0*PI
     ENDDO
     J=0
!    CALL ANGULAR_GRID(IOPTN(82),LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW)
     IF (IOPTN(82) == 1) THEN
      LEBEDVX(1)=1.0D0
      LEBEDVY(1)=0.0D0
      LEBEDVZ(1)=0.0D0
      LEBEDVW(1)=1.0D0
     ELSE IF (IOPTN(82) == 6) THEN
      CALL LD0006(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 14) THEN
      CALL LD0014(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 26) THEN
      CALL LD0026(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 38) THEN
      CALL LD0038(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 50) THEN
      CALL LD0050(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 74) THEN
      CALL LD0074(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 86) THEN
      CALL LD0086(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 110) THEN
      CALL LD0110(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 146) THEN
      CALL LD0146(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 170) THEN
      CALL LD0170(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 194) THEN
      CALL LD0194(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 230) THEN
      CALL LD0230(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 266) THEN
      CALL LD0266(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 302) THEN
      CALL LD0302(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 350) THEN
      CALL LD0350(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 434) THEN
      CALL LD0434(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 590) THEN
      CALL LD0590(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 770) THEN
      CALL LD0770(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 974) THEN
      CALL LD0974(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 1202) THEN
      CALL LD1202(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 1454) THEN
      CALL LD1454(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 1730) THEN
      CALL LD1730(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 2030) THEN
      CALL LD2030(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 2354) THEN
      CALL LD2354(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 2702) THEN
      CALL LD2702(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 3074) THEN
      CALL LD3074(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 3470) THEN
      CALL LD3470(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 3890) THEN
      CALL LD3890(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 4334) THEN
      CALL LD4334(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 4802) THEN
      CALL LD4802(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 5294) THEN
      CALL LD5294(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE IF (IOPTN(82) == 5810) THEN
      CALL LD5810(LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW,IOPTN(82))
     ELSE
      CALL PABORT('LEBEDEV GRID NOT FOUND')
     ENDIF
!    DO K=1,IOPTN(82)
!     LEBEDVW(K)=LEBEDVW(K)*4.0D0*PI
!    ENDDO
     A=0.0D0
     DO K=1,IOPTN(82)
      A=A+LEBEDVW(K)
     ENDDO
     IF (DABS(A-1.0D0) > 1.0D-10) CALL PABORT('SUM OF SURFACE QUADRUTURE WEIGHTS DEVIATES FROM UNITY')

     DO K=1,IOPTN(81)
      R2=GAUSS_CHEV(K)
      DO L=1,IOPTN(82)
       J=J+1
       GRIDX(J,I)=R1*R2*LEBEDVX(L)
       GRIDY(J,I)=R1*R2*LEBEDVY(L)
       GRIDZ(J,I)=R1*R2*LEBEDVZ(L)
       ATOMW(J,I)=R1**3*GAUSS_CHEV_W(K)*LEBEDVW(L)
      ENDDO
     ENDDO
     IF (J /= NGRID(I)) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
    ENDDO

   ENDIF

   DEALLOCATE(GAUSS_CHEV,GAUSS_CHEV_W,LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW)

   RETURN
END SUBROUTINE



SUBROUTINE ANGULAR_GRID(I,LEBEDVX,LEBEDVY,LEBEDVZ,LEBEDVW)
! CONSTRUCT ANGULAR GRID POINTS FOR LEBEDEV QUADRATURE.
! SEE V.I.LEBEDEV, ZH.VYCHISL.MAT.MAT.FIZ. 15,1,48 (1975); ZH.VYCHISL.MAT.MAT.FIZ. 16,2,293 (1976); SIB.MAT.ZH. 18,1,132 (1975).
! ***** CAUTION : THIS SUBROUTINE MAY NOT WORK CORRECTLY WHEN COMPILED WITH A VERY STRONG OPTIMIZATION OPTION.
 
   IMPLICIT NONE
   DOUBLE PRECISION,PARAMETER :: L302X(12) = (/0.100000000000D+01,0.577350269190D+00,0.701176641609D+00,0.656632941022D+00, &
       0.472905413258D+00,0.351564034558D+00,0.221964523631D+00,0.961830852303D-01, &
       0.820326419828D+00,0.964408914879D+00,0.251003475177D+00,0.902442529533D+00/)
   DOUBLE PRECISION,PARAMETER :: L302Y(12) = (/0.000000000000D+00,0.577350269190D+00,0.701176641609D+00,0.656632941022D+00, &
       0.472905413258D+00,0.351564034558D+00,0.221964523631D+00,0.961830852303D-01, &
       0.571895589188D+00,0.264415288706D+00,0.800072749407D+00,0.412772408317D+00/)
   DOUBLE PRECISION,PARAMETER :: L302Z(12) = (/0.000000000000D+00,0.577350269190D+00,0.129238672710D+00,0.371034178385D+00, &
       0.743452042987D+00,0.867643624544D+00,0.949454317226D+00,0.990705621379D+00, &
       0.000000000000D+00,0.000000000000D+00,0.544867737258D+00,0.123354853258D+00/)
   DOUBLE PRECISION,PARAMETER :: L302W(12) = (/8.545911728780D-04,3.599119285020D-03,3.650045807680D-03,3.604822601420D-03, &
       3.576729661730D-03,3.449788424290D-03,3.108953122380D-03,2.352101413660D-03, &
       3.600820932220D-03,2.982344963170D-03,3.571540554270D-03,3.392312205010D-03/)
   DOUBLE PRECISION,PARAMETER :: L50X(4) = (/0.100000000000D+01,0.707106781187D+00,0.577350269190D+00,0.301511344578D+00/)
   DOUBLE PRECISION,PARAMETER :: L50Y(4) = (/0.000000000000D+00,0.707106781187D+00,0.577350269190D+00,0.301511344578D+00/)
   DOUBLE PRECISION,PARAMETER :: L50Z(4) = (/0.000000000000D+00,0.000000000000D+00,0.577350269190D+00,0.904534033733D+00/)
   DOUBLE PRECISION,PARAMETER :: L50W(4) = (/1.269841269850D-02,2.257495590830D-02,2.109375000000D-02,2.017333553790D-02/)
   DOUBLE PRECISION,PARAMETER :: L38X(3) = (/0.100000000000D+01,0.577350269190D+00,0.888073833977D+00/)
   DOUBLE PRECISION,PARAMETER :: L38Y(3) = (/0.000000000000D+00,0.577350269190D+00,0.459700843381D+00/)
   DOUBLE PRECISION,PARAMETER :: L38Z(3) = (/0.000000000000D+00,0.577350269190D+00,0.000000000000D+00/)
   DOUBLE PRECISION,PARAMETER :: L38W(3) = (/9.523809523870D-03,3.214285714290D-02,2.857142857140D-02/)
   INTEGER :: I,J,K,L,M,N
   DOUBLE PRECISION :: LEBEDVX(I),LEBEDVY(I),LEBEDVZ(I),LEBEDVW(I)
   DOUBLE PRECISION :: A
 
   K=0
   IF (I == 302) THEN
    DO J=1,12
     K=K+1
     L=K
     LEBEDVX(K)=L302X(J)
     LEBEDVY(K)=L302Y(J)
     LEBEDVZ(K)=L302Z(J)
     LEBEDVW(K)=L302W(J)
     IF ((L302X(J) /= L302Y(J)).OR.(L302Y(J) /= L302Z(J)).OR.(L302Z(J) /= L302X(J))) THEN
      K=K+1
      LEBEDVX(K)=L302Y(J)
      LEBEDVY(K)=L302Z(J)
      LEBEDVZ(K)=L302X(J)
      LEBEDVW(K)=L302W(J)
      K=K+1
      LEBEDVX(K)=L302Z(J)
      LEBEDVY(K)=L302X(J)
      LEBEDVZ(K)=L302Y(J)
      LEBEDVW(K)=L302W(J)
     ENDIF
     IF ((L302X(J) /= L302Y(J)).AND.(L302Y(J) /= L302Z(J)).AND.(L302Z(J) /= L302X(J))) THEN
      K=K+1
      LEBEDVX(K)=L302Y(J)
      LEBEDVY(K)=L302X(J)
      LEBEDVZ(K)=L302Z(J)
      LEBEDVW(K)=L302W(J)
      K=K+1
      LEBEDVX(K)=L302X(J)
      LEBEDVY(K)=L302Z(J)
      LEBEDVZ(K)=L302Y(J)
      LEBEDVW(K)=L302W(J)
      K=K+1
      LEBEDVX(K)=L302Z(J)
      LEBEDVY(K)=L302Y(J)
      LEBEDVZ(K)=L302X(J)
      LEBEDVW(K)=L302W(J)
     ENDIF
     M=K
     DO N=L,M
      IF (LEBEDVX(N) /= 0.0D0) THEN
       K=K+1
       LEBEDVX(K)=-LEBEDVX(N)
       LEBEDVY(K)=LEBEDVY(N)
       LEBEDVZ(K)=LEBEDVZ(N)
       LEBEDVW(K)=LEBEDVW(N)
      ENDIF
     ENDDO
     M=K
     DO N=L,M
      IF (LEBEDVY(N) /= 0.0D0) THEN
       K=K+1
       LEBEDVX(K)=LEBEDVX(N)
       LEBEDVY(K)=-LEBEDVY(N)
       LEBEDVZ(K)=LEBEDVZ(N)
       LEBEDVW(K)=LEBEDVW(N)
      ENDIF
     ENDDO
     M=K
     DO N=L,M
      IF (LEBEDVZ(N) /= 0.0D0) THEN
       K=K+1
       LEBEDVX(K)=LEBEDVX(N)
       LEBEDVY(K)=LEBEDVY(N)
       LEBEDVZ(K)=-LEBEDVZ(N)
       LEBEDVW(K)=LEBEDVW(N)
      ENDIF
     ENDDO
    ENDDO
    IF (K /= 302) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
    A=0.0D0
    DO K=1,302
     A=A+LEBEDVW(K)
    ENDDO
    IF (DABS(A-1.0D0) > 1.0D-10) CALL PABORT('SUM OF SURFACE QUADRUTURE WEIGHTS DEVIATES FROM UNITY')
   ELSE IF (I == 50) THEN
    DO J=1,4
     K=K+1
     L=K
     LEBEDVX(K)=L50X(J)
     LEBEDVY(K)=L50Y(J)
     LEBEDVZ(K)=L50Z(J)
     LEBEDVW(K)=L50W(J)
     IF ((L50X(J) /= L50Y(J)).OR.(L50Y(J) /= L50Z(J)).OR.(L50Z(J) /= L50X(J))) THEN
      K=K+1
      LEBEDVX(K)=L50Y(J)
      LEBEDVY(K)=L50Z(J)
      LEBEDVZ(K)=L50X(J)
      LEBEDVW(K)=L50W(J)
      K=K+1
      LEBEDVX(K)=L50Z(J)
      LEBEDVY(K)=L50X(J)
      LEBEDVZ(K)=L50Y(J)
      LEBEDVW(K)=L50W(J)
     ENDIF
     IF ((L50X(J) /= L50Y(J)).AND.(L50Y(J) /= L50Z(J)).AND.(L50Z(J) /= L50X(J))) THEN
      K=K+1
      LEBEDVX(K)=L50Y(J)
      LEBEDVY(K)=L50X(J)
      LEBEDVZ(K)=L50Z(J)
      LEBEDVW(K)=L50W(J)
      K=K+1
      LEBEDVX(K)=L50X(J)
      LEBEDVY(K)=L50Z(J)
      LEBEDVZ(K)=L50Y(J)
      LEBEDVW(K)=L50W(J)
      K=K+1
      LEBEDVX(K)=L50Z(J)
      LEBEDVY(K)=L50Y(J)
      LEBEDVZ(K)=L50X(J)
      LEBEDVW(K)=L50W(J)
     ENDIF
     M=K
     DO N=L,M
      IF (LEBEDVX(N) /= 0.0D0) THEN
       K=K+1
       LEBEDVX(K)=-LEBEDVX(N)
       LEBEDVY(K)=LEBEDVY(N)
       LEBEDVZ(K)=LEBEDVZ(N)
       LEBEDVW(K)=LEBEDVW(N)
      ENDIF
     ENDDO
     M=K
     DO N=L,M
      IF (LEBEDVY(N) /= 0.0D0) THEN
       K=K+1
       LEBEDVX(K)=LEBEDVX(N)
       LEBEDVY(K)=-LEBEDVY(N)
       LEBEDVZ(K)=LEBEDVZ(N)
       LEBEDVW(K)=LEBEDVW(N)
      ENDIF
     ENDDO
     M=K
     DO N=L,M
      IF (LEBEDVZ(N) /= 0.0D0) THEN
       K=K+1
       LEBEDVX(K)=LEBEDVX(N)
       LEBEDVY(K)=LEBEDVY(N)
       LEBEDVZ(K)=-LEBEDVZ(N)
       LEBEDVW(K)=LEBEDVW(N)
      ENDIF
     ENDDO
    ENDDO
    IF (K /= 50) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
    A=0.0D0
    DO K=1,50
     A=A+LEBEDVW(K)
    ENDDO
    IF (DABS(A-1.0D0) > 1.0D-10) CALL PABORT('SUM OF SURFACE QUADRUTURE WEIGHTS DEVIATES FROM UNITY')
   ELSE IF (I == 38) THEN
    DO J=1,3
     K=K+1
     L=K
     LEBEDVX(K)=L38X(J)
     LEBEDVY(K)=L38Y(J)
     LEBEDVZ(K)=L38Z(J)
     LEBEDVW(K)=L38W(J)
     IF ((L302X(J) /= L302Y(J)).OR.(L302Y(J) /= L302Z(J)).OR.(L302Z(J) /= L302X(J))) THEN
      K=K+1
      LEBEDVX(K)=L38Y(J)
      LEBEDVY(K)=L38Z(J)
      LEBEDVZ(K)=L38X(J)
      LEBEDVW(K)=L38W(J)
      K=K+1
      LEBEDVX(K)=L38Z(J)
      LEBEDVY(K)=L38X(J)
      LEBEDVZ(K)=L38Y(J)
      LEBEDVW(K)=L38W(J)
     ENDIF
     IF ((L38X(J) /= L38Y(J)).AND.(L38Y(J) /= L38Z(J)).AND.(L38Z(J) /= L38X(J))) THEN
      K=K+1
      LEBEDVX(K)=L38Y(J)
      LEBEDVY(K)=L38X(J)
      LEBEDVZ(K)=L38Z(J)
      LEBEDVW(K)=L38W(J)
      K=K+1
      LEBEDVX(K)=L38X(J)
      LEBEDVY(K)=L38Z(J)
      LEBEDVZ(K)=L38Y(J)
      LEBEDVW(K)=L38W(J)
      K=K+1
      LEBEDVX(K)=L38Z(J)
      LEBEDVY(K)=L38Y(J)
      LEBEDVZ(K)=L38X(J)
      LEBEDVW(K)=L38W(J)
     ENDIF
     M=K
     DO N=L,M
      IF (LEBEDVX(N) /= 0.0D0) THEN
       K=K+1
       LEBEDVX(K)=-LEBEDVX(N)
       LEBEDVY(K)=LEBEDVY(N)
       LEBEDVZ(K)=LEBEDVZ(N)
       LEBEDVW(K)=LEBEDVW(N)
      ENDIF
     ENDDO
     M=K
     DO N=L,M
      IF (LEBEDVY(N) /= 0.0D0) THEN
       K=K+1
       LEBEDVX(K)=LEBEDVX(N)
       LEBEDVY(K)=-LEBEDVY(N)
       LEBEDVZ(K)=LEBEDVZ(N)
       LEBEDVW(K)=LEBEDVW(N)
      ENDIF
     ENDDO
     M=K
     DO N=L,M
      IF (LEBEDVZ(N) /= 0.0D0) THEN
       K=K+1
       LEBEDVX(K)=LEBEDVX(N)
       LEBEDVY(K)=LEBEDVY(N)
       LEBEDVZ(K)=-LEBEDVZ(N)
       LEBEDVW(K)=LEBEDVW(N)
      ENDIF
     ENDDO
    ENDDO
    IF (K /= 38) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
    A=0.0D0
    DO K=1,38
     A=A+LEBEDVW(K)
    ENDDO
    IF (DABS(A-1.0D0) > 1.0D-10) CALL PABORT('SUM OF SURFACE QUADRUTURE WEIGHTS DEVIATES FROM UNITY')
   ELSE
    CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
   ENDIF
   RETURN
END SUBROUTINE



SUBROUTINE GRID_WEIGHT
! CALCULATE THE WEIGHT FUNCTION FOR THE BECKE FUZZY CELLS WITH ATOMIC SIZE ADJUSTMENT.
! SEE A.D.BECKE, J.CHEM.PHYS. 88,2547 (1988); O.TREUTLER AND R.AHLRICHS, J.CHEM.PHYS. 102,346 (1995).

   USE MPI
   USE CONSTANTS
   USE STRUCTURE
   USE INTEGRAL
   USE DFT
     
   IMPLICIT NONE
   INCLUDE "mpif.h"
!  DOUBLE PRECISION,PARAMETER :: BSRADI(17) = &
!    (/0.472D0,0.472D0,1.370D0,0.992D0,0.803D0,0.661D0,0.614D0,0.567D0,0.472D0, &
!      0.472D0,1.701D0,1.417D0,1.181D0,1.039D0,0.945D0,0.945D0,0.945D0/) ! Ne DATA WAS MISSING IN THE ORIGINAL
   DOUBLE PRECISION,PARAMETER :: BSRADI(36) = &
     (/0.35D0,0.35D0,1.45D0,1.05D0,0.85D0,0.70D0,0.65D0,0.60D0,0.50D0,0.45D0, &
       1.80D0,1.50D0,1.25D0,1.10D0,1.00D0,1.00D0,1.00D0,1.00D0, &
       2.20D0,1.80D0,1.60D0,1.40D0,1.35D0,1.40D0,1.40D0,1.40D0,1.35D0,1.35D0, &
       1.35D0,1.35D0,1.30D0,1.25D0,1.15D0,1.15D0,1.15D0,1.15D0/)
   INTEGER :: Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,I,J,K,N,U
   DOUBLE PRECISION :: X0,Y0,Z0,X1,Y1,Z1,X2,Y2,Z2,X3,Y3,Z3
   DOUBLE PRECISION :: R1,R2,R3,A0,M0,M1,M2,M3,M4
   DOUBLE PRECISION :: QX1,QY1,QZ1,QX2,QY2,QZ2
   DOUBLE PRECISION,ALLOCATABLE :: W(:,:,:,:,:),S(:)
!--WEIGHT DERIVATIVES
   DOUBLE PRECISION :: X1_TX,Y1_TY,Z1_TZ,Y1_TA,Z1_TA
   DOUBLE PRECISION :: X2_TX,Y2_TY,Z2_TZ,Y2_TA,Z2_TA
   DOUBLE PRECISION :: R1_TX,R1_TY,R1_TZ,R1_TA,R2_TX,R2_TY,R2_TZ,R2_TA,R3_TX,R3_TY,R3_TZ,R3_TA
   DOUBLE PRECISION :: M0_TX,M0_TY,M0_TZ,M0_TA
   DOUBLE PRECISION :: M1_TX,M1_TY,M1_TZ,M1_TA
   DOUBLE PRECISION :: M2_TX,M2_TY,M2_TZ,M2_TA
   DOUBLE PRECISION :: M3_TX,M3_TY,M3_TZ,M3_TA
   DOUBLE PRECISION :: M4_TX,M4_TY,M4_TZ,M4_TA
   DOUBLE PRECISION :: M0_X,M0_Y,M0_Z
   DOUBLE PRECISION :: M1_X,M1_Y,M1_Z
   DOUBLE PRECISION :: M2_X,M2_Y,M2_Z
   DOUBLE PRECISION :: M3_X,M3_Y,M3_Z
   DOUBLE PRECISION :: M4_X,M4_Y,M4_Z
   DOUBLE PRECISION,ALLOCATABLE :: W_TX(:,:,:,:,:),S_TX(:)
   DOUBLE PRECISION,ALLOCATABLE :: W_TY(:,:,:,:,:),S_TY(:)
   DOUBLE PRECISION,ALLOCATABLE :: W_TZ(:,:,:,:,:),S_TZ(:)
   DOUBLE PRECISION,ALLOCATABLE :: W_TA(:,:,:,:,:),S_TA(:)
   DOUBLE PRECISION,ALLOCATABLE :: X0_X(:),Y0_Y(:),Z0_Z(:)
   DOUBLE PRECISION,ALLOCATABLE :: X1_X(:),Y1_Y(:),Y1_Z(:),Z1_Y(:),Z1_Z(:)
   DOUBLE PRECISION,ALLOCATABLE :: X2_X(:),Y2_Y(:),Y2_Z(:),Z2_Y(:),Z2_Z(:)
   DOUBLE PRECISION,ALLOCATABLE :: R1_X(:),R1_Y(:),R1_Z(:)
   DOUBLE PRECISION,ALLOCATABLE :: R2_X(:),R2_Y(:),R2_Z(:)
   DOUBLE PRECISION,ALLOCATABLE :: R3_X(:),R3_Y(:),R3_Z(:)
   DOUBLE PRECISION,ALLOCATABLE :: W_X(:,:,:,:,:,:),S_X(:,:)
   DOUBLE PRECISION,ALLOCATABLE :: W_Y(:,:,:,:,:,:),S_Y(:,:)
   DOUBLE PRECISION,ALLOCATABLE :: W_Z(:,:,:,:,:,:),S_Z(:,:)
!--WEIGHT DERIVATIVES
   INTEGER :: TICKET

   ALLOCATE(W(MAXNGRID,NATOM,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z),S(MAXNGRID))
   ALLOCATE(W_TX(MAXNGRID,NATOM,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(W_TY(MAXNGRID,NATOM,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(W_TZ(MAXNGRID,NATOM,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(W_TA(MAXNGRID,NATOM,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(S_TX(MAXNGRID),S_TY(MAXNGRID),S_TZ(MAXNGRID),S_TA(MAXNGRID))
   ALLOCATE(W_X(MAXNGRID,NATOM,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z,NATOM))
   ALLOCATE(W_Y(MAXNGRID,NATOM,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z,NATOM))
   ALLOCATE(W_Z(MAXNGRID,NATOM,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z,NATOM))
   ALLOCATE(S_X(MAXNGRID,NATOM),S_Y(MAXNGRID,NATOM),S_Z(MAXNGRID,NATOM))
   ALLOCATE(X0_X(NATOM),Y0_Y(NATOM),Z0_Z(NATOM))
   ALLOCATE(X1_X(NATOM),Y1_Y(NATOM),Y1_Z(NATOM),Z1_Y(NATOM),Z1_Z(NATOM))
   ALLOCATE(X2_X(NATOM),Y2_Y(NATOM),Y2_Z(NATOM),Z2_Y(NATOM),Z2_Z(NATOM))
   ALLOCATE(R1_X(NATOM),R1_Y(NATOM),R1_Z(NATOM))
   ALLOCATE(R2_X(NATOM),R2_Y(NATOM),R2_Z(NATOM))
   ALLOCATE(R3_X(NATOM),R3_Y(NATOM),R3_Z(NATOM))
   IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE WEIGHT FACTORS FOR GAUSS-CHEBYSHEV-LEBEDEV QUADRATURE'

   GRIDW=0.0D0
   FUZZY=0.0D0
!--WEIGHT DERIVATIVES
   GRIDW_TX=0.0D0
   GRIDW_TY=0.0D0
   GRIDW_TZ=0.0D0
   GRIDW_TA=0.0D0
   GRIDW_X=0.0D0
   GRIDW_Y=0.0D0
   GRIDW_Z=0.0D0
!--WEIGHT DERIVATIVES
   TICKET=0
   DO I=1,NATOM
    TICKET=TICKET+1
    IF (TICKET == NUMPROCS) TICKET=TICKET-NUMPROCS
! ------------------------ PARALLEL LOOP
    IF (MYID == TICKET) THEN
! ------------------------ PARALLEL LOOP
    IF (IATOM(I) > 36) CALL PABORT('EXPAND GRID_WEIGHT SUBROUTINE')
    X0=ATOMX(I)
    Y0=ATOMY(I)
    Z0=ATOMZ(I)
!--WEIGHT DERIVATIVES
    X0_X=0.0D0
    Y0_Y=0.0D0
    Z0_Z=0.0D0
    X0_X(I)=1.0D0
    Y0_Y(I)=1.0D0
    Z0_Z(I)=1.0D0
    W_TX=0.0D0
    W_TY=0.0D0
    W_TZ=0.0D0
    W_TA=0.0D0
    W_X=0.0D0
    W_Y=0.0D0
    W_Z=0.0D0
!--WEIGHT DERIVATIVES
    DO Q1X=-REDUCED_CEL1X,REDUCED_CEL1X
    DO Q1Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
    DO Q1Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
     DO J=1,NATOM
      DO N=1,NGRID(I)
       W(N,J,Q1X,Q1Y,Q1Z)=1.0D0
      ENDDO
     ENDDO
    ENDDO
    ENDDO
    ENDDO
!   DO N=1,NGRID(I)
!    IF (ABS(INT((GRIDX(N,I)+X0)/PERIOD)) > CEL1) W(N,I,0)=0.0D0
!   ENDDO
    DO Q1X=-REDUCED_CEL1X,REDUCED_CEL1X
    DO Q1Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
    DO Q1Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
     QX1=PERIODX*DFLOAT(Q1X)
     QY1=PERIODY*DFLOAT(Q1Y)
     QZ1=PERIODZ*DFLOAT(Q1Z)
     DO J=1,NATOM
      X1=ATOMX(J)+QX1
      Y1=ATOMY(J)*DCOS(DFLOAT(Q1X)*HELIX)-ATOMZ(J)*DSIN(DFLOAT(Q1X)*HELIX)+QY1
      Z1=ATOMY(J)*DSIN(DFLOAT(Q1X)*HELIX)+ATOMZ(J)*DCOS(DFLOAT(Q1X)*HELIX)+QZ1
!--WEIGHT DERIVATIVES
      X1_X=0.0D0
      Y1_Y=0.0D0
      Y1_Z=0.0D0
      Z1_Y=0.0D0
      Z1_Z=0.0D0
      X1_X(J)=1.0D0
      Y1_Y(J)=DCOS(DFLOAT(Q1X)*HELIX)
      Y1_Z(J)=-DSIN(DFLOAT(Q1X)*HELIX)
      Z1_Y(J)=DSIN(DFLOAT(Q1X)*HELIX)
      Z1_Z(J)=DCOS(DFLOAT(Q1X)*HELIX)
      X1_TX=DFLOAT(Q1X)
      Y1_TY=DFLOAT(Q1Y)
      Z1_TZ=DFLOAT(Q1Z)
      Y1_TA=ATOMY(J)*DFLOAT(Q1X)*(-DSIN(DFLOAT(Q1X)*HELIX))-ATOMZ(J)*DFLOAT(Q1X)*DCOS(DFLOAT(Q1X)*HELIX)
      Z1_TA=ATOMY(J)*DFLOAT(Q1X)*DCOS(DFLOAT(Q1X)*HELIX)+ATOMZ(J)*DFLOAT(Q1X)*(-DSIN(DFLOAT(Q1X)*HELIX))
!--WEIGHT DERIVATIVES
      DO Q2X=-REDUCED_CEL1X,REDUCED_CEL1X
      DO Q2Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
      DO Q2Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
       QX2=PERIODX*DFLOAT(Q1X+Q2X)
       QY2=PERIODY*DFLOAT(Q1Y+Q2Y)
       QZ2=PERIODZ*DFLOAT(Q1Z+Q2Z)
       DO K=1,NATOM
        IF ((Q2X == 0).AND.(Q2Y == 0).AND.(Q2Z == 0).AND.(K == J)) CYCLE
        X2=ATOMX(K)+QX2
        Y2=ATOMY(K)*DCOS(DFLOAT(Q1X+Q2X)*HELIX)-ATOMZ(K)*DSIN(DFLOAT(Q1X+Q2X)*HELIX)+QY2
        Z2=ATOMY(K)*DSIN(DFLOAT(Q1X+Q2X)*HELIX)+ATOMZ(K)*DCOS(DFLOAT(Q1X+Q2X)*HELIX)+QZ2
!--WEIGHT DERIVATIVES
        X2_X=0.0D0
        Y2_Y=0.0D0
        Y2_Z=0.0D0
        Z2_Y=0.0D0
        Z2_Z=0.0D0
        X2_X(K)=1.0D0
        Y2_Y(K)=DCOS(DFLOAT(Q1X+Q2X)*HELIX)
        Y2_Z(K)=-DSIN(DFLOAT(Q1X+Q2X)*HELIX)
        Z2_Y(K)=DSIN(DFLOAT(Q1X+Q2X)*HELIX)
        Z2_Z(K)=DCOS(DFLOAT(Q1X+Q2X)*HELIX)
        X2_TX=DFLOAT(Q1X+Q2X)
        Y2_TY=DFLOAT(Q1Y+Q2Y)
        Z2_TZ=DFLOAT(Q1Z+Q2Z)
        Y2_TA=ATOMY(K)*DFLOAT(Q1X+Q2X)*(-DSIN(DFLOAT(Q1X+Q2X)*HELIX))-ATOMZ(K)*DFLOAT(Q1X+Q2X)*DCOS(DFLOAT(Q1X+Q2X)*HELIX)
        Z2_TA=ATOMY(K)*DFLOAT(Q1X+Q2X)*DCOS(DFLOAT(Q1X+Q2X)*HELIX)+ATOMZ(K)*DFLOAT(Q1X+Q2X)*(-DSIN(DFLOAT(Q1X+Q2X)*HELIX))
!--WEIGHT DERIVATIVES
        R1=DSQRT((X1-X2)**2+(Y1-Y2)**2+(Z1-Z2)**2)
!--WEIGHT DERIVATIVES
        IF (R1 == 0.0D0) CALL PABORT('AN INTERNAL ERROR IN GRID_WEIGHT')
        DO U=1,NATOM
         R1_X(U)=(X1_X(U)-X2_X(U))*(X1-X2)/R1
         R1_Y(U)=(Y1_Y(U)-Y2_Y(U))*(Y1-Y2)/R1+(Z1_Y(U)-Z2_Y(U))*(Z1-Z2)/R1
         R1_Z(U)=(Y1_Z(U)-Y2_Z(U))*(Y1-Y2)/R1+(Z1_Z(U)-Z2_Z(U))*(Z1-Z2)/R1
        ENDDO
        R1_TX=(X1_TX-X2_TX)*(X1-X2)/R1
        R1_TY=(Y1_TY-Y2_TY)*(Y1-Y2)/R1
        R1_TZ=(Z1_TZ-Z2_TZ)*(Z1-Z2)/R1
        R1_TA=(Y1_TA-Y2_TA)*(Y1-Y2)/R1+(Z1_TA-Z2_TA)*(Z1-Z2)/R1
!--WEIGHT DERIVATIVES
        A0=DSQRT(BSRADI(IATOM(J))/BSRADI(IATOM(K)))
        A0=(A0-1.0D0)/(A0+1.0D0)
        A0=A0/(A0*A0-1.0D0)
        DO N=1,NGRID(I)
         X3=GRIDX(N,I)+X0
!        IF (ABS(INT(X3/PERIOD)) > CEL1) CYCLE
         Y3=GRIDY(N,I)+Y0
         Z3=GRIDZ(N,I)+Z0
         R2=DSQRT((X1-X3)**2+(Y1-Y3)**2+(Z1-Z3)**2)
         R3=DSQRT((X2-X3)**2+(Y2-Y3)**2+(Z2-Z3)**2)
!--WEIGHT DERIVATIVES
         DO U=1,NATOM
          R2_X(U)=(X1_X(U)-X0_X(U))*(X1-X3)/R2
          R2_Y(U)=(Y1_Y(U)-Y0_Y(U))*(Y1-Y3)/R2+(Z1_Y(U)        )*(Z1-Z3)/R2
          R2_Z(U)=(Y1_Z(U)        )*(Y1-Y3)/R2+(Z1_Z(U)-Z0_Z(U))*(Z1-Z3)/R2
         ENDDO
         R2_TX=X1_TX*(X1-X3)/R2
         R2_TY=Y1_TY*(Y1-Y3)/R2
         R2_TZ=Z1_TZ*(Z1-Z3)/R2
         R2_TA=(Y1_TA)*(Y1-Y3)/R2+(Z1_TA)*(Z1-Z3)/R2
         DO U=1,NATOM
          R3_X(U)=(X2_X(U)-X0_X(U))*(X2-X3)/R3
          R3_Y(U)=(Y2_Y(U)-Y0_Y(U))*(Y2-Y3)/R3+(Z2_Y(U)        )*(Z2-Z3)/R3
          R3_Z(U)=(Y2_Z(U)        )*(Y2-Y3)/R3+(Z2_Z(U)-Z0_Z(U))*(Z2-Z3)/R3
         ENDDO
         R3_TX=X2_TX*(X2-X3)/R3
         R3_TY=Y2_TY*(Y2-Y3)/R3
         R3_TZ=Z2_TZ*(Z2-Z3)/R3
         R3_TA=(Y2_TA)*(Y2-Y3)/R3+(Z2_TA)*(Z2-Z3)/R3
!--WEIGHT DERIVATIVES
         M0=(R2-R3)/R1
         M1=M0+A0*(1.0D0-M0*M0)
         M2=(1.5D0-0.5D0*M1*M1)*M1
         M3=(1.5D0-0.5D0*M2*M2)*M2
         M4=(1.5D0-0.5D0*M3*M3)*M3
!--WEIGHT DERIVATIVES
         DO U=1,NATOM
          M0_X=((R2_X(U)-R3_X(U))*R1-(R2-R3)*R1_X(U))/R1**2
          M0_Y=((R2_Y(U)-R3_Y(U))*R1-(R2-R3)*R1_Y(U))/R1**2
          M0_Z=((R2_Z(U)-R3_Z(U))*R1-(R2-R3)*R1_Z(U))/R1**2
          M1_X=M0_X+A0*(-2.0D0*M0_X*M0)
          M1_Y=M0_Y+A0*(-2.0D0*M0_Y*M0)
          M1_Z=M0_Z+A0*(-2.0D0*M0_Z*M0)
          M2_X=1.5D0*M1_X-1.5D0*M1_X*M1*M1
          M2_Y=1.5D0*M1_Y-1.5D0*M1_Y*M1*M1
          M2_Z=1.5D0*M1_Z-1.5D0*M1_Z*M1*M1
          M3_X=1.5D0*M2_X-1.5D0*M2_X*M2*M2
          M3_Y=1.5D0*M2_Y-1.5D0*M2_Y*M2*M2
          M3_Z=1.5D0*M2_Z-1.5D0*M2_Z*M2*M2
          M4_X=1.5D0*M3_X-1.5D0*M3_X*M3*M3
          M4_Y=1.5D0*M3_Y-1.5D0*M3_Y*M3*M3
          M4_Z=1.5D0*M3_Z-1.5D0*M3_Z*M3*M3
          W_X(N,J,Q1X,Q1Y,Q1Z,U)=W_X(N,J,Q1X,Q1Y,Q1Z,U)*(0.5D0-0.5D0*M4)+W(N,J,Q1X,Q1Y,Q1Z)*(-0.5D0*M4_X)
          W_Y(N,J,Q1X,Q1Y,Q1Z,U)=W_Y(N,J,Q1X,Q1Y,Q1Z,U)*(0.5D0-0.5D0*M4)+W(N,J,Q1X,Q1Y,Q1Z)*(-0.5D0*M4_Y)
          W_Z(N,J,Q1X,Q1Y,Q1Z,U)=W_Z(N,J,Q1X,Q1Y,Q1Z,U)*(0.5D0-0.5D0*M4)+W(N,J,Q1X,Q1Y,Q1Z)*(-0.5D0*M4_Z)
         ENDDO
         M0_TX=((R2_TX-R3_TX)*R1-(R2-R3)*R1_TX)/R1**2
         M0_TY=((R2_TY-R3_TY)*R1-(R2-R3)*R1_TY)/R1**2
         M0_TZ=((R2_TZ-R3_TZ)*R1-(R2-R3)*R1_TZ)/R1**2
         M0_TA=((R2_TA-R3_TA)*R1-(R2-R3)*R1_TA)/R1**2
         M1_TX=M0_TX+A0*(-2.0D0*M0_TX*M0)
         M1_TY=M0_TY+A0*(-2.0D0*M0_TY*M0)
         M1_TZ=M0_TZ+A0*(-2.0D0*M0_TZ*M0)
         M1_TA=M0_TA+A0*(-2.0D0*M0_TA*M0)
         M2_TX=1.5D0*M1_TX-1.5D0*M1_TX*M1*M1
         M2_TY=1.5D0*M1_TY-1.5D0*M1_TY*M1*M1
         M2_TZ=1.5D0*M1_TZ-1.5D0*M1_TZ*M1*M1
         M2_TA=1.5D0*M1_TA-1.5D0*M1_TA*M1*M1
         M3_TX=1.5D0*M2_TX-1.5D0*M2_TX*M2*M2
         M3_TY=1.5D0*M2_TY-1.5D0*M2_TY*M2*M2
         M3_TZ=1.5D0*M2_TZ-1.5D0*M2_TZ*M2*M2
         M3_TA=1.5D0*M2_TA-1.5D0*M2_TA*M2*M2
         M4_TX=1.5D0*M3_TX-1.5D0*M3_TX*M3*M3
         M4_TY=1.5D0*M3_TY-1.5D0*M3_TY*M3*M3
         M4_TZ=1.5D0*M3_TZ-1.5D0*M3_TZ*M3*M3
         M4_TA=1.5D0*M3_TA-1.5D0*M3_TA*M3*M3
         W_TX(N,J,Q1X,Q1Y,Q1Z)=W_TX(N,J,Q1X,Q1Y,Q1Z)*(0.5D0-0.5D0*M4)+W(N,J,Q1X,Q1Y,Q1Z)*(-0.5D0*M4_TX)
         W_TY(N,J,Q1X,Q1Y,Q1Z)=W_TY(N,J,Q1X,Q1Y,Q1Z)*(0.5D0-0.5D0*M4)+W(N,J,Q1X,Q1Y,Q1Z)*(-0.5D0*M4_TY)
         W_TZ(N,J,Q1X,Q1Y,Q1Z)=W_TZ(N,J,Q1X,Q1Y,Q1Z)*(0.5D0-0.5D0*M4)+W(N,J,Q1X,Q1Y,Q1Z)*(-0.5D0*M4_TZ)
         W_TA(N,J,Q1X,Q1Y,Q1Z)=W_TA(N,J,Q1X,Q1Y,Q1Z)*(0.5D0-0.5D0*M4)+W(N,J,Q1X,Q1Y,Q1Z)*(-0.5D0*M4_TA)
!--WEIGHT DERIVATIVES
         W(N,J,Q1X,Q1Y,Q1Z)=W(N,J,Q1X,Q1Y,Q1Z)*(0.5D0-0.5D0*M4)
        ENDDO
       ENDDO
      ENDDO
      ENDDO
      ENDDO
     ENDDO
    ENDDO
    ENDDO
    ENDDO
    DO N=1,NGRID(I)
     S(N)=0.0D0
    ENDDO
!--WEIGHT DERIVATIVES
    S_X=0.0D0
    S_Y=0.0D0
    S_Z=0.0D0
    S_TX=0.0D0
    S_TY=0.0D0
    S_TZ=0.0D0
    S_TA=0.0D0
!--WEIGHT DERIVATIVES
    DO Q1X=-REDUCED_CEL1X,REDUCED_CEL1X
    DO Q1Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
    DO Q1Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
     DO J=1,NATOM
      DO N=1,NGRID(I)
       S(N)=S(N)+W(N,J,Q1X,Q1Y,Q1Z)
!--WEIGHT DERIVATIVES
       DO U=1,NATOM
        S_X(N,U)=S_X(N,U)+W_X(N,J,Q1X,Q1Y,Q1Z,U)
        S_Y(N,U)=S_Y(N,U)+W_Y(N,J,Q1X,Q1Y,Q1Z,U)
        S_Z(N,U)=S_Z(N,U)+W_Z(N,J,Q1X,Q1Y,Q1Z,U)
       ENDDO
       S_TX(N)=S_TX(N)+W_TX(N,J,Q1X,Q1Y,Q1Z)
       S_TY(N)=S_TY(N)+W_TY(N,J,Q1X,Q1Y,Q1Z)
       S_TZ(N)=S_TZ(N)+W_TZ(N,J,Q1X,Q1Y,Q1Z)
       S_TA(N)=S_TA(N)+W_TA(N,J,Q1X,Q1Y,Q1Z)
!--WEIGHT DERIVATIVES
      ENDDO
     ENDDO
    ENDDO
    ENDDO
    ENDDO
    DO N=1,NGRID(I)
     IF (DABS(S(N)) <= 1.0D-20) THEN
!     IF (W(N,I,0,0,0) <= S(N)) CALL PABORT('GRID_WEIGHT FAILED')
      GRIDW(N,I)=ATOMW(N,I)
      FUZZY(N,I)=0.0D0
      DO U=1,NATOM
       GRIDW_X(N,I,U)=0.0D0
       GRIDW_Y(N,I,U)=0.0D0
       GRIDW_Z(N,I,U)=0.0D0
      ENDDO
      GRIDW_TX(N,I)=0.0D0
      GRIDW_TY(N,I)=0.0D0
      GRIDW_TZ(N,I)=0.0D0
      GRIDW_TA(N,I)=0.0D0
      GRIDW(N,I)=0.0D0
     ELSE
      GRIDW(N,I)=ATOMW(N,I)
      FUZZY(N,I)=W(N,I,0,0,0)/S(N)
!--WEIGHT DERIVATIVES
      DO U=1,NATOM
       GRIDW_X(N,I,U)=GRIDW(N,I)*(W_X(N,I,0,0,0,U)*S(N)-W(N,I,0,0,0)*S_X(N,U))/S(N)**2+GRIDW_X(N,I,U)*W(N,I,0,0,0)/S(N)
       GRIDW_Y(N,I,U)=GRIDW(N,I)*(W_Y(N,I,0,0,0,U)*S(N)-W(N,I,0,0,0)*S_Y(N,U))/S(N)**2+GRIDW_Y(N,I,U)*W(N,I,0,0,0)/S(N)
       GRIDW_Z(N,I,U)=GRIDW(N,I)*(W_Z(N,I,0,0,0,U)*S(N)-W(N,I,0,0,0)*S_Z(N,U))/S(N)**2+GRIDW_Z(N,I,U)*W(N,I,0,0,0)/S(N)
      ENDDO
      GRIDW_TX(N,I)=GRIDW(N,I)*(W_TX(N,I,0,0,0)*S(N)-W(N,I,0,0,0)*S_TX(N))/S(N)**2+GRIDW_TX(N,I)*W(N,I,0,0,0)/S(N)
      GRIDW_TY(N,I)=GRIDW(N,I)*(W_TY(N,I,0,0,0)*S(N)-W(N,I,0,0,0)*S_TY(N))/S(N)**2+GRIDW_TY(N,I)*W(N,I,0,0,0)/S(N)
      GRIDW_TZ(N,I)=GRIDW(N,I)*(W_TZ(N,I,0,0,0)*S(N)-W(N,I,0,0,0)*S_TZ(N))/S(N)**2+GRIDW_TZ(N,I)*W(N,I,0,0,0)/S(N)
      GRIDW_TA(N,I)=GRIDW(N,I)*(W_TA(N,I,0,0,0)*S(N)-W(N,I,0,0,0)*S_TA(N))/S(N)**2+GRIDW_TA(N,I)*W(N,I,0,0,0)/S(N)
!--WEIGHT DERIVATIVES
      GRIDW(N,I)=GRIDW(N,I)*W(N,I,0,0,0)/S(N)
     ENDIF
    ENDDO
! ------------------------ PARALLEL LOOP
    ENDIF
! ------------------------ PARALLEL LOOP
   ENDDO

   CALL MPI_ALLREDUCE(MPI_IN_PLACE,GRIDW,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FUZZY,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,GRIDW_X,MAXNGRID*NATOM**2,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,GRIDW_Y,MAXNGRID*NATOM**2,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,GRIDW_Z,MAXNGRID*NATOM**2,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,GRIDW_TX,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,GRIDW_TY,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,GRIDW_TZ,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,GRIDW_TA,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)

!dump
!open(110,file="gridweight",form="formatted")
!do i=1,natom
! do j=1,ngrid(i)
!  read(110,*) gridw_tx(j,i),x0,y0
! enddo
!enddo
!close(110)
!write(140,*) natom,ngrid(1)
!do i=1,natom
! do j=1,ngrid(i)
!  write(140,*) gridw(j,i),gridw_y(j,i,2),rho(j,i)
! enddo
!enddo
!write(160,*) natom*ngrid(1)
!write(160,*) 'xyz'
!do i=1,natom
! do j=1,ngrid(i)
!  if (gridz(j,i) == 0.0d0) then
!  if (gridw_tx(j,i) > 0.0d0) &
!  write(160,'(A,4f20.5)') 'H ',gridx(j,i)*ang_bohr,gridy(j,i)*ang_bohr,gridz(j,i)*ang_bohr, &
!  min(5.0,max(0.1,gridw_tx(j,i)/max(dabs(gridw(j,i)),0.0001d0)))
!  if (gridw_tx(j,i) < 0.0d0) &
!  write(161,'(A,4f20.5)') 'H ',gridx(j,i)*ang_bohr,gridy(j,i)*ang_bohr,gridz(j,i)*ang_bohr, &
!  min(5.0,max(0.1,-gridw_tx(j,i)/max(dabs(gridw(j,i)),0.0001d0)))
!  endif
! enddo
!enddo
!call pabort('debug')
!dump
   DEALLOCATE(W,S)
   DEALLOCATE(W_TX,W_TY,W_TZ,W_TA,S_TX,S_TY,S_TZ,S_TA)
   DEALLOCATE(W_X,W_Y,W_Z,S_X,S_Y,S_Z)
   DEALLOCATE(X0_X,Y0_Y,Z0_Z,X1_X,Y1_Y,Y1_Z,Z1_Y,Z1_Z,X2_X,Y2_Y,Y2_Z,Z2_Y,Z2_Z,R1_X,R1_Y,R1_Z,R2_X,R2_Y,R2_Z,R3_X,R3_Y,R3_Z)

   RETURN
END SUBROUTINE



SUBROUTINE PACK_GRID
! REMOVE GRID POINTS WHOSE WEIGHT FACTORS ARE VERY SMALL.

   USE MPI
   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE DFT
   
   IMPLICIT NONE
   INTEGER :: I,J,K
   DOUBLE PRECISION,PARAMETER :: THRESHOLD = 1.0D-10
   DOUBLE PRECISION,ALLOCATABLE :: GRIDX_(:,:),GRIDY_(:,:),GRIDZ_(:,:),GRIDW_(:,:)

   ALLOCATE(GRIDX_(MAXNGRID,NATOM),GRIDY_(MAXNGRID,NATOM),GRIDZ_(MAXNGRID,NATOM),GRIDW_(MAXNGRID,NATOM))

   CALL PABORT('OBSOLETE')

   DO I=1,NATOM
    J=0
    DO K=1,NGRID(I)
     IF (DABS(GRIDW(K,I)) > THRESHOLD) THEN
      J=J+1
      GRIDX_(J,I)=GRIDX(K,I)
      GRIDY_(J,I)=GRIDY(K,I)
      GRIDZ_(J,I)=GRIDZ(K,I)
      GRIDW_(J,I)=GRIDW(K,I)
     ENDIF
    ENDDO
    NGRID(I)=J
   ENDDO
   GRIDX=GRIDX_
   GRIDY=GRIDY_
   GRIDZ=GRIDZ_
   GRIDW=GRIDW_

   IF ((IOPTN(9) >= 0).AND.(MYID == 0)) THEN
    WRITE(6,'(A,I6,A)') 'NUMBER OF GRID POINTS HAS BEEN REDUCED FROM ',MAXNGRID,' TO'
    DO I=1,NATOM
     WRITE(6,'(I6,A,I3)') NGRID(I),' FOR ATOM ',I
    ENDDO
   ENDIF

   DEALLOCATE(GRIDX_,GRIDY_,GRIDZ_,GRIDW_)

   RETURN
END SUBROUTINE
