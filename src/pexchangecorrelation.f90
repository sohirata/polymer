SUBROUTINE INITIAL_ELECTRON_DENSITY
! CALCULATE ELECTRON DENSITY FOR UNIT DENSITY MATRIX AND 
! DETERMINE THE CUTOFF PARAMETER FOR DENSITY UPDATE.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE DFT

   IMPLICIT NONE
!  INCLUDE "mpif.h"
   DOUBLE PRECISION,PARAMETER :: THRESHOLD = 1.0D-6
   INTEGER :: I,J,K,L,Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z
   DOUBLE PRECISION,ALLOCATABLE :: CGS(:,:,:,:)
   INTEGER,ALLOCATABLE :: C2P(:) ! 'C2P' IS WORKING ARRAY FOR CGS_GRID
   DOUBLE PRECISION,ALLOCATABLE :: RHO_CEL(:,:,:,:,:,:)
   DOUBLE PRECISION :: A
   INTEGER :: TICKET

   ALLOCATE(CGS(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(C2P(NPGS))
   ALLOCATE(RHO_CEL(-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z, &
                    -REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))

   IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE INITIAL ELECTRON DENSITY'
   TICKET=0
   RHO_CEL=0.0D0
   DO I=1,NATOM
    DO J=1,NGRID(I)
     TICKET=TICKET+1
     IF (TICKET == NUMPROCS) TICKET=TICKET-NUMPROCS
! ------------------------ PARALLEL LOOP
     IF (MYID == TICKET) THEN
! ------------------------ PARALLEL LOOP
     CALL CGS_GRID(CGS,C2P,NCGS,REDUCED_CEL1X,REDUCED_CEL1Y,REDUCED_CEL1Z,NPGS,J,I)
     DO Q1X=-REDUCED_CEL1X,REDUCED_CEL1X
     DO Q1Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
     DO Q1Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
      DO Q2X=-REDUCED_CEL1X,REDUCED_CEL1X
      DO Q2Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
      DO Q2Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
       A=0.0D0
       IF ((Q1X+Q2X <= REDUCED_CEL1X).AND.(Q1X+Q2X >= -REDUCED_CEL1X).AND. &
           (Q1Y+Q2Y <= REDUCED_CEL1Y).AND.(Q1Y+Q2Y >= -REDUCED_CEL1Y).AND. &
           (Q1Z+Q2Z <= REDUCED_CEL1Z).AND.(Q1Z+Q2Z >= -REDUCED_CEL1Z)) THEN
        DO K=1,NCGS
         DO L=1,NCGS
          A=A+CGS(L,Q1X,Q1Y,Q1Z)*CGS(K,Q1X+Q2X,Q1Y+Q2Y,Q1Z+Q2Z)
         ENDDO
        ENDDO
       ENDIF
       RHO_CEL(Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z)=RHO_CEL(Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z)+GRIDW(J,I)*A
      ENDDO
      ENDDO
      ENDDO
     ENDDO
     ENDDO
     ENDDO
! ------------------------ PARALLEL LOOP
     ENDIF
! ------------------------ PARALLEL LOOP
    ENDDO
   ENDDO

   CALL MPI_ALLREDUCE(MPI_IN_PLACE,RHO_CEL,(2*REDUCED_CEL1X+1)**2*(2*REDUCED_CEL1Y+1)**2*(2*REDUCED_CEL1Z+1)**2,&
                      MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)

   REDUCED_CEL1X_XC=0
   REDUCED_CEL1Y_XC=0
   REDUCED_CEL1Z_XC=0
   DO Q1X=-REDUCED_CEL1X,REDUCED_CEL1X
   DO Q1Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
   DO Q1Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
    DO Q2X=-REDUCED_CEL1X,REDUCED_CEL1X
    DO Q2Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
    DO Q2Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
     IF ((Q1X+Q2X <= REDUCED_CEL1X).AND.(Q1X+Q2X >= -REDUCED_CEL1X).AND. &
         (Q1Y+Q2Y <= REDUCED_CEL1Y).AND.(Q1Y+Q2Y >= -REDUCED_CEL1Y).AND. &
         (Q1Z+Q2Z <= REDUCED_CEL1Z).AND.(Q1Z+Q2Z >= -REDUCED_CEL1Z)) THEN
      IF (DABS(RHO_CEL(Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z)) > THRESHOLD) THEN
       IF (ABS(Q1X) > REDUCED_CEL1X_XC) REDUCED_CEL1X_XC=ABS(Q1X)
       IF (ABS(Q2X) > REDUCED_CEL1X_XC) REDUCED_CEL1X_XC=ABS(Q2X)
       IF (ABS(Q1X+Q2X) > REDUCED_CEL1X_XC) REDUCED_CEL1X_XC=ABS(Q1X+Q2X)
       IF (ABS(Q1Y) > REDUCED_CEL1Y_XC) REDUCED_CEL1Y_XC=ABS(Q1Y)
       IF (ABS(Q2Y) > REDUCED_CEL1Y_XC) REDUCED_CEL1Y_XC=ABS(Q2Y)
       IF (ABS(Q1Y+Q2Y) > REDUCED_CEL1Y_XC) REDUCED_CEL1Y_XC=ABS(Q1Y+Q2Y)
       IF (ABS(Q1Z) > REDUCED_CEL1Z_XC) REDUCED_CEL1Z_XC=ABS(Q1Z)
       IF (ABS(Q2Z) > REDUCED_CEL1Z_XC) REDUCED_CEL1Z_XC=ABS(Q2Z)
       IF (ABS(Q1Z+Q2Z) > REDUCED_CEL1Z_XC) REDUCED_CEL1Z_XC=ABS(Q1Z+Q2Z)
      ENDIF
     ENDIF
    ENDDO
    ENDDO
    ENDDO
   ENDDO
   ENDDO
   ENDDO

   IF ((REDUCED_CEL1X_XC < CEL1X).AND.(MYID == 0)) WRITE(6,'(A,I3,A,I3,A)') &
   'X CUTOFF1 PARAMETER WILL BE REDUCED FROM ',CEL1X,' TO ',REDUCED_CEL1X_XC,' IN ELECTRON DENSITY UPDATE'
   IF ((REDUCED_CEL1Y_XC < CEL1Y).AND.(MYID == 0)) WRITE(6,'(A,I3,A,I3,A)') &
   'Y CUTOFF1 PARAMETER WILL BE REDUCED FROM ',CEL1Y,' TO ',REDUCED_CEL1Y_XC,' IN ELECTRON DENSITY UPDATE'
   IF ((REDUCED_CEL1Z_XC < CEL1Z).AND.(MYID == 0)) WRITE(6,'(A,I3,A,I3,A)') &
   'Z CUTOFF1 PARAMETER WILL BE REDUCED FROM ',CEL1Z,' TO ',REDUCED_CEL1Z_XC,' IN ELECTRON DENSITY UPDATE'

   DEALLOCATE(CGS,C2P,RHO_CEL)
!  CALL PCPU_TIME(CPUE)
!  WRITE(6,'(A,F10.1)') 'CPU / SEC = ',CPUE-CPUS

END SUBROUTINE



SUBROUTINE ELECTRON_DENSITY
! CALCULATE ELECTRON DENSITY BY THE DIRECT ALGORITHM.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE DFT

   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: I,J,K,L,Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,Q3X,Q3Y,Q3Z
   DOUBLE PRECISION,ALLOCATABLE :: CGS(:,:,:,:)
!  DOUBLE PRECISION,ALLOCATABLE :: CGSDX(:,:,:,:),CGSDY(:,:,:,:),CGSDZ(:,:,:,:),CGSDA(:,:,:,:)
   INTEGER,ALLOCATABLE :: C2P(:) ! 'C2P' IS WORKING ARRAY FOR CGS_GRID
   DOUBLE PRECISION :: A,B
!  DOUBLE PRECISION :: TX,TY,TZ,TA
   INTEGER :: TICKET

   ALLOCATE(CGS(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDX(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDY(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDZ(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDA(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
   ALLOCATE(C2P(NPGS))

   TICKET=0
   RHO=0.0D0
!  RHO_GTX=0.0D0
!  RHO_GTY=0.0D0
!  RHO_GTZ=0.0D0
!  RHO_GTA=0.0D0
   DO I=1,NATOM
    DO J=1,NGRID(I)
     TICKET=TICKET+1
     IF (TICKET == NUMPROCS) TICKET=TICKET-NUMPROCS
! ------------------------ PARALLEL LOOP
     IF (MYID == TICKET) THEN
! ------------------------ PARALLEL LOOP
     CALL CGS_GRID(CGS,C2P,NCGS,REDUCED_CEL1X_XC,REDUCED_CEL1Y_XC,REDUCED_CEL1Z_XC,NPGS,J,I)
!    CALL CGS_DERIVATIVE_GRID(CGSDX,CGSDY,CGSDZ,CGSDA,C2P,NCGS,REDUCED_CEL1X_XC,REDUCED_CEL1Y_XC,REDUCED_CEL1Z_XC,NPGS,J,I)
     A=0.0D0
!    TX=0.0D0
!    TY=0.0D0
!    TZ=0.0D0
!    TA=0.0D0
     DO Q1X=-REDUCED_CEL1X_XC,REDUCED_CEL1X_XC
     DO Q1Y=-REDUCED_CEL1Y_XC,REDUCED_CEL1Y_XC
     DO Q1Z=-REDUCED_CEL1Z_XC,REDUCED_CEL1Z_XC
      DO Q2X=-REDUCED_CEL1X_XC,REDUCED_CEL1X_XC
      DO Q2Y=-REDUCED_CEL1Y_XC,REDUCED_CEL1Y_XC
      DO Q2Z=-REDUCED_CEL1Z_XC,REDUCED_CEL1Z_XC
       Q3X=Q1X+Q2X
       Q3Y=Q1Y+Q2Y
       Q3Z=Q1Z+Q2Z
       IF ((Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC).AND. &
           (Q3Y <= REDUCED_CEL1Y_XC).AND.(Q3Y >= -REDUCED_CEL1Y_XC).AND. &
           (Q3Z <= REDUCED_CEL1Z_XC).AND.(Q3Z >= -REDUCED_CEL1Z_XC)) THEN
        DO K=1,NCGS
         DO L=1,NCGS
          A=A+P_C(L,K,Q2X,Q2Y,Q2Z)*CGS(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z)
!         TX=TX+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*DFLOAT(Q3X)*CGSDX(K,Q3X,Q3Y,Q3Z) &
!                                    +DFLOAT(Q1X)*CGSDX(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z))
!         TY=TY+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*DFLOAT(Q3Y)*CGSDY(K,Q3X,Q3Y,Q3Z) &
!                                    +DFLOAT(Q1Y)*CGSDY(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z))
!         TZ=TZ+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*DFLOAT(Q3Z)*CGSDZ(K,Q3X,Q3Y,Q3Z) &
!                                    +DFLOAT(Q1Z)*CGSDZ(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z))
!         TA=TA+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*CGSDA(K,Q3X,Q3Y,Q3Z) &
!                                    +CGSDA(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z))
         ENDDO
        ENDDO
       ENDIF
      ENDDO
      ENDDO
      ENDDO
     ENDDO
     ENDDO
     ENDDO
     RHO(J,I)=RHO(J,I)+A
!    RHO_GTX(J,I)=RHO_GTX(J,I)+TX
!    RHO_GTY(J,I)=RHO_GTY(J,I)+TY
!    RHO_GTZ(J,I)=RHO_GTZ(J,I)+TZ
!    RHO_GTA(J,I)=RHO_GTA(J,I)+TA
! ------------------------ PARALLEL LOOP
     ENDIF
! ------------------------ PARALLEL LOOP
    ENDDO
   ENDDO

   CALL MPI_ALLREDUCE(MPI_IN_PLACE,RHO,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
!  CALL MPI_ALLREDUCE(MPI_IN_PLACE,RHO_GTX,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
!  CALL MPI_ALLREDUCE(MPI_IN_PLACE,RHO_GTY,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
!  CALL MPI_ALLREDUCE(MPI_IN_PLACE,RHO_GTZ,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
!  CALL MPI_ALLREDUCE(MPI_IN_PLACE,RHO_GTA,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
 
   A=0.0D0
   DO Q1X=-CEL1X,CEL1X
   DO Q1Y=-CEL1Y,CEL1Y
   DO Q1Z=-CEL1Z,CEL1Z
    DO I=1,NCGS
     DO J=1,NCGS
      A=A+S_C(J,I,Q1X,Q1Y,Q1Z)*P_C(J,I,Q1X,Q1Y,Q1Z)
     ENDDO
    ENDDO
   ENDDO
   ENDDO
   ENDDO
   IF ((IOPTN(9) >= 2).AND.(MYID == 0)) WRITE(6,'(A,F20.15)') 'INTEGRATED ELECTRON DENSITY (OVERLAP)    = ',A
   B=0.0D0
   DO I=1,NATOM
    DO J=1,NGRID(I)
     B=B+GRIDW(J,I)*RHO(J,I)
!write(*,*) i,j,gridw(j,i),rho(j,i),B
    ENDDO
   ENDDO
   IF ((IOPTN(9) >= 2).AND.(MYID == 0)) WRITE(6,'(A,F20.15)') 'INTEGRATED ELECTRON DENSITY (QUADRATURE) = ',B
   IF ((DABS(A-B) > 0.0001*A).AND.(MYID == 0)) THEN
    CALL WARNING('NUMERICAL INTEGRATION ACCURACY MAY NOT BE SUFFICIENT')
    IF (IOPTN(9) < 2) THEN
     WRITE(6,'(A,F20.15)') 'INTEGRATED ELECTRON DENSITY (OVERLAP)    = ',A
     WRITE(6,'(A,F20.15)') 'INTEGRATED ELECTRON DENSITY (QUADRATURE) = ',B
    ENDIF
   ENDIF

   DEALLOCATE(CGS,C2P)
!  DEALLOCATE(CGSDX,CGSDY,CGSDZ,CGSDA)
!  CALL PCPU_TIME(CPUE)
!  WRITE(6,'(A,F10.1)') 'CPU / SEC = ',CPUE-CPUS

   RETURN
END SUBROUTINE



SUBROUTINE INTEGRATE_ELECTRON_DENSITY
! INTEGRATE ELECTRON DENSITY AND PRINT THE RESULTS.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE DFT

   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: I,J,Q1X,Q1Y,Q1Z
   DOUBLE PRECISION :: A,B

   A=0.0D0
   DO Q1X=-CEL1X,CEL1X
   DO Q1Y=-CEL1Y,CEL1Y
   DO Q1Z=-CEL1Z,CEL1Z
    DO I=1,NCGS
     DO J=1,NCGS
      A=A+S_C(J,I,Q1X,Q1Y,Q1Z)*P_C(J,I,Q1X,Q1Y,Q1Z)
     ENDDO
    ENDDO
   ENDDO
   ENDDO
   ENDDO
   IF (MYID == 0) WRITE(6,'(A,F20.15)') 'INTEGRATED ELECTRON DENSITY (OVERLAP)    = ',A
   B=0.0D0
   DO I=1,NATOM
    DO J=1,NGRID(I)
     B=B+GRIDW(J,I)*RHO(J,I)
    ENDDO
   ENDDO
   IF (MYID == 0) WRITE(6,'(A,F20.15)') 'INTEGRATED ELECTRON DENSITY (QUADRATURE) = ',B
   IF ((DABS(A-B) > 0.0001*A).AND.(MYID == 0)) THEN
    CALL WARNING('NUMERICAL INTEGRATION ACCURACY MAY NOT BE SUFFICIENT')
    WRITE(6,'(A,F20.15)') 'INTEGRATED ELECTRON DENSITY (OVERLAP)    = ',A
    WRITE(6,'(A,F20.15)') 'INTEGRATED ELECTRON DENSITY (QUADRATURE) = ',B
   ENDIF

   RETURN
END SUBROUTINE



SUBROUTINE ELECTRON_DENSITY_GRADIENT
! CALCULATE ELECTRON DENSITY GRADIENTS BY THE DIRECT ALGORITHM.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE DFT

   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: I,J,K,L,Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,Q3X,Q3Y,Q3Z
   DOUBLE PRECISION,ALLOCATABLE :: CGS(:,:,:,:)
   INTEGER,ALLOCATABLE :: C2P(:) ! 'C2P' IS WORKING ARRAY FOR CGS_GRID
   DOUBLE PRECISION,ALLOCATABLE :: CGSDX(:,:,:,:),CGSDY(:,:,:,:),CGSDZ(:,:,:,:),CGSDA(:,:,:,:)
!  DOUBLE PRECISION,ALLOCATABLE :: CGSDXX(:,:,:,:),CGSDYY(:,:,:,:),CGSDZZ(:,:,:,:)
!  DOUBLE PRECISION,ALLOCATABLE :: CGSDXY(:,:,:,:),CGSDYZ(:,:,:,:),CGSDZX(:,:,:,:)
!  DOUBLE PRECISION,ALLOCATABLE :: CGSDXA(:,:,:,:),CGSDYA(:,:,:,:),CGSDZA(:,:,:,:)
   DOUBLE PRECISION :: X,Y,Z
!  DOUBLE PRECISION :: X_A,Y_A,Z_A
   DOUBLE PRECISION :: ANGLE1,CS1,SN1,ANGLE2,CS2,SN2
   INTEGER :: TICKET

   ALLOCATE(CGS(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
   ALLOCATE(C2P(NPGS))
   ALLOCATE(CGSDX(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
   ALLOCATE(CGSDY(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
   ALLOCATE(CGSDZ(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
   ALLOCATE(CGSDA(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDXX(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDYY(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDZZ(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDXY(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDYZ(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDZX(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDXA(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDYA(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
!  ALLOCATE(CGSDZA(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))

   TICKET=0
   RHO_GX=0.0D0
   RHO_GY=0.0D0
   RHO_GZ=0.0D0
!  RHO_GX_A=0.0D0
!  RHO_GY_A=0.0D0
!  RHO_GZ_A=0.0D0
   DO I=1,NATOM
    DO J=1,NGRID(I)
     TICKET=TICKET+1
     IF (TICKET == NUMPROCS) TICKET=TICKET-NUMPROCS
! ------------------------ PARALLEL LOOP
     IF (MYID == TICKET) THEN
! ------------------------ PARALLEL LOOP
     CALL CGS_GRID(CGS,C2P,NCGS,REDUCED_CEL1X_XC,REDUCED_CEL1Y_XC,REDUCED_CEL1Z_XC,NPGS,J,I)
     CALL CGS_DERIVATIVE_GRID(CGSDX,CGSDY,CGSDZ,CGSDA,C2P,NCGS,REDUCED_CEL1X_XC,REDUCED_CEL1Y_XC,REDUCED_CEL1Z_XC,NPGS,J,I)
!    CALL CGS_SECOND_DERIVATIVE_GRID(CGSDXX,CGSDYY,CGSDZZ,CGSDXY,CGSDYZ,CGSDZX,CGSDXA,CGSDYA,CGSDZA,C2P,NCGS, &
!                                    REDUCED_CEL1X_XC,REDUCED_CEL1Y_XC,REDUCED_CEL1Z_XC,NPGS,J,I)
     X=0.0D0
     Y=0.0D0
     Z=0.0D0
!    X_A=0.0D0
!    Y_A=0.0D0
!    Z_A=0.0D0
     DO Q1X=-REDUCED_CEL1X_XC,REDUCED_CEL1X_XC
     DO Q1Y=-REDUCED_CEL1Y_XC,REDUCED_CEL1Y_XC
     DO Q1Z=-REDUCED_CEL1Z_XC,REDUCED_CEL1Z_XC
      ANGLE1=DFLOAT(Q1X)*HELIX
      CS1=DCOS(ANGLE1)
      SN1=DSIN(ANGLE1)
      DO Q2X=-REDUCED_CEL1X_XC,REDUCED_CEL1X_XC
      DO Q2Y=-REDUCED_CEL1Y_XC,REDUCED_CEL1Y_XC
      DO Q2Z=-REDUCED_CEL1Z_XC,REDUCED_CEL1Z_XC
       Q3X=Q1X+Q2X
       Q3Y=Q1Y+Q2Y
       Q3Z=Q1Z+Q2Z
       IF ((Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC).AND. &
           (Q3Y <= REDUCED_CEL1Y_XC).AND.(Q3Y >= -REDUCED_CEL1Y_XC).AND. &
           (Q3Z <= REDUCED_CEL1Z_XC).AND.(Q3Z >= -REDUCED_CEL1Z_XC)) THEN
        ANGLE2=DFLOAT(Q3X)*HELIX
        CS2=DCOS(ANGLE2)
        SN2=DSIN(ANGLE2)
        DO K=1,NCGS
         DO L=1,NCGS
          IF (HELIX == 0.0D0) THEN
           X=X+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*CGSDX(K,Q3X,Q3Y,Q3Z)+CGSDX(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z))
           Y=Y+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*CGSDY(K,Q3X,Q3Y,Q3Z)+CGSDY(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z))
           Z=Z+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*CGSDZ(K,Q3X,Q3Y,Q3Z)+CGSDZ(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z))
          ELSE ! --- HELIX
           X=X+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*CGSDX(K,Q3X,Q3Y,Q3Z)+CGSDX(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z))
           Y=Y+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*(CGSDY(K,Q3X,Q3Y,Q3Z)*CS2-CGSDZ(K,Q3X,Q3Y,Q3Z)*SN2) &
                                   +(CGSDY(L,Q1X,Q1Y,Q1Z)*CS1-CGSDZ(L,Q1X,Q1Y,Q1Z)*SN1)*CGS(K,Q3X,Q3Y,Q3Z))
           Z=Z+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*(CGSDZ(K,Q3X,Q3Y,Q3Z)*CS2+CGSDY(K,Q3X,Q3Y,Q3Z)*SN2) &
                                   +(CGSDZ(L,Q1X,Q1Y,Q1Z)*CS1+CGSDY(L,Q1X,Q1Y,Q1Z)*SN1)*CGS(K,Q3X,Q3Y,Q3Z))
!          X_A=X_A+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGSDA(L,Q1X,Q1Y,Q1Z)*CGSDX(K,Q3X,Q3Y,Q3Z)+CGS(L,Q1X,Q1Y,Q1Z)*CGSDXA(K,Q3X,Q3Y,Q3Z) &
!                                       +CGSDXA(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z)+CGSDX(L,Q1X,Q1Y,Q1Z)*CGSDA(K,Q3X,Q3Y,Q3Z))
!          Y_A=Y_A+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGSDA(L,Q1X,Q1Y,Q1Z)*(CGSDY(K,Q3X,Q3Y,Q3Z)*CS2-CGSDZ(K,Q3X,Q3Y,Q3Z)*SN2) &
!                                      +(CGSDY(L,Q1X,Q1Y,Q1Z)*CS1-CGSDZ(L,Q1X,Q1Y,Q1Z)*SN1)*CGSDA(K,Q3X,Q3Y,Q3Z)) &
!                 +P_C(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*(CGSDYA(K,Q3X,Q3Y,Q3Z)*CS2-CGSDZA(K,Q3X,Q3Y,Q3Z)*SN2) &
!                                      +(CGSDYA(L,Q1X,Q1Y,Q1Z)*CS1-CGSDZA(L,Q1X,Q1Y,Q1Z)*SN1)*CGS(K,Q3X,Q3Y,Q3Z)) &
!                 +P_C(L,K,Q2X,Q2Y,Q2Z) &
!                 *(CGS(L,Q1X,Q1Y,Q1Z)*(CGSDY(K,Q3X,Q3Y,Q3Z)*DFLOAT(Q3X)*(-SN2)-CGSDZ(K,Q3X,Q3Y,Q3Z)*DFLOAT(Q3X)*CS2) &
!                 +(CGSDY(L,Q1X,Q1Y,Q1Z)*DFLOAT(Q1X)*(-SN1)-CGSDZ(L,Q1X,Q1Y,Q1Z)*DFLOAT(Q1X)*CS1)*CGS(K,Q3X,Q3Y,Q3Z))
!          Z_A=Z_A+P_C(L,K,Q2X,Q2Y,Q2Z)*(CGSDA(L,Q1X,Q1Y,Q1Z)*(CGSDZ(K,Q3X,Q3Y,Q3Z)*CS2+CGSDY(K,Q3X,Q3Y,Q3Z)*SN2) &
!                                      +(CGSDZ(L,Q1X,Q1Y,Q1Z)*CS1+CGSDY(L,Q1X,Q1Y,Q1Z)*SN1)*CGSDA(K,Q3X,Q3Y,Q3Z)) &
!                 +P_C(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*(CGSDZA(K,Q3X,Q3Y,Q3Z)*CS2+CGSDYA(K,Q3X,Q3Y,Q3Z)*SN2) &
!                                      +(CGSDZA(L,Q1X,Q1Y,Q1Z)*CS1+CGSDYA(L,Q1X,Q1Y,Q1Z)*SN1)*CGS(K,Q3X,Q3Y,Q3Z)) &
!                 +P_C(L,K,Q2X,Q2Y,Q2Z) &
!                 *(CGS(L,Q1X,Q1Y,Q1Z)*(CGSDZ(K,Q3X,Q3Y,Q3Z)*DFLOAT(Q3X)*(-SN2)+CGSDY(K,Q3X,Q3Y,Q3Z)*DFLOAT(Q3X)*CS2) &
!                 +(CGSDZ(L,Q1X,Q1Y,Q1Z)*DFLOAT(Q1X)*(-SN1)+CGSDY(L,Q1X,Q1Y,Q1Z)*DFLOAT(Q1X)*CS1)*CGS(K,Q3X,Q3Y,Q3Z))
          ENDIF ! --- HELIX END
         ENDDO
        ENDDO
       ENDIF
      ENDDO
      ENDDO
      ENDDO
     ENDDO
     ENDDO
     ENDDO
     RHO_GX(J,I)=RHO_GX(J,I)+X
     RHO_GY(J,I)=RHO_GY(J,I)+Y
     RHO_GZ(J,I)=RHO_GZ(J,I)+Z
!    RHO_GX_A(J,I)=RHO_GX_A(J,I)+X_A
!    RHO_GY_A(J,I)=RHO_GY_A(J,I)+Y_A
!    RHO_GZ_A(J,I)=RHO_GZ_A(J,I)+Z_A
! ------------------------ PARALLEL LOOP
     ENDIF
! ------------------------ PARALLEL LOOP
    ENDDO
   ENDDO

   CALL MPI_ALLREDUCE(MPI_IN_PLACE,RHO_GX,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,RHO_GY,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,RHO_GZ,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
!  CALL MPI_ALLREDUCE(MPI_IN_PLACE,RHO_GX_A,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
!  CALL MPI_ALLREDUCE(MPI_IN_PLACE,RHO_GY_A,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
!  CALL MPI_ALLREDUCE(MPI_IN_PLACE,RHO_GZ_A,MAXNGRID*NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   
   DO I=1,NATOM
    DO J=1,NGRID(I)
     GAMMA(J,I)=RHO_GX(J,I)**2+RHO_GY(J,I)**2+RHO_GZ(J,I)**2
    ENDDO
   ENDDO

   DEALLOCATE(CGS,C2P)
   DEALLOCATE(CGSDX,CGSDY,CGSDZ,CGSDA)
!  DEALLOCATE(CGSDXX,CGSDYY,CGSDZZ,CGSDXY,CGSDYZ,CGSDZX)

   RETURN
END SUBROUTINE



SUBROUTINE LOCAL_EXCHANGE_CORRELATION
! NUMERICALLY CALCULATE THE EXCHANGE-CORRELATION INTEGRALS FOR LOCAL FUNCTIONALS.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE DFT

   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: I,J,JJ,K,L,Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,Q3X,Q3Y,Q3Z
   DOUBLE PRECISION,ALLOCATABLE :: CGS(:,:,:,:)
   INTEGER,ALLOCATABLE :: C2P(:) ! 'C2P' IS WORKING ARRAY FOR CGS_GRID
   DOUBLE PRECISION :: A,B
   DOUBLE PRECISION,ALLOCATABLE :: W3(:,:)
   INTEGER :: TICKET

   ALLOCATE(CGS(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z),C2P(NPGS))
   ALLOCATE(W3(NCGS,NCGS))

   TICKET=0
   Y_C=0.0D0
   DO I=1,NATOM
    DO J=1,NGRID(I)
     TICKET=TICKET+1
     IF (TICKET == NUMPROCS) TICKET=TICKET-NUMPROCS
! ------------------------ PARALLEL LOOP
     IF (MYID == TICKET) THEN
! ------------------------ PARALLEL LOOP
     CALL CGS_GRID(CGS,C2P,NCGS,REDUCED_CEL1X,REDUCED_CEL1Y,REDUCED_CEL1Z,NPGS,J,I)
     A=GRIDW(J,I)*D1F1(J,I)
     DO Q1X=-REDUCED_CEL1X,REDUCED_CEL1X
     DO Q1Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
     DO Q1Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
      DO Q2X=-REDUCED_CEL1X,REDUCED_CEL1X
      DO Q2Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
      DO Q2Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
       Q3X=Q1X+Q2X
       Q3Y=Q1Y+Q2Y
       Q3Z=Q1Z+Q2Z
       IF ((Q3X <= REDUCED_CEL1X).AND.(Q3X >= -REDUCED_CEL1X).AND. &
           (Q3Y <= REDUCED_CEL1Y).AND.(Q3Y >= -REDUCED_CEL1Y).AND. &
           (Q3Z <= REDUCED_CEL1Z).AND.(Q3Z >= -REDUCED_CEL1Z)) THEN
        DO K=1,NCGS
         B=CGS(K,Q1X,Q1Y,Q1Z)*A
         DO L=1,NCGS
          Y_C(K,L,Q2X,Q2Y,Q2Z)=Y_C(K,L,Q2X,Q2Y,Q2Z)+CGS(L,Q3X,Q3Y,Q3Z)*B
         ENDDO
        ENDDO
       ENDIF
      ENDDO
      ENDDO
      ENDDO
     ENDDO
     ENDDO
     ENDDO
! ------------------------ PARALLEL LOOP
     ENDIF
! ------------------------ PARALLEL LOOP
    ENDDO
   ENDDO

   CALL MPI_ALLREDUCE(MPI_IN_PLACE,Y_C,NCGS**2*(CEL1X*2+1)*(CEL1Y*2+1)*(CEL1Z*2+1), &
                      MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)

   IF ((IOPTN(9) >= 2).AND.(MYID == 0)) THEN
    WRITE(6,'(A)') 'EXCHANGE-CORRELATION MATRIX FOR CONTRACTED GAUSSIANS'
    CALL DUMP1(Y_C,NCGS,CEL1X,CEL1Y,CEL1Z)
   ENDIF
   DEALLOCATE(CGS,C2P,W3)

   RETURN
END SUBROUTINE



SUBROUTINE GC_EXCHANGE_CORRELATION
! NUMERICALLY CALCULATE THE EXCHANGE-CORRELATION INTEGRALS FOR GRADIENT CORRECTED FUNCTIONALS.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE DFT

   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: I,J,JJ,K,L,Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,Q3X,Q3Y,Q3Z
   DOUBLE PRECISION,ALLOCATABLE :: CGS(:,:,:,:)
   INTEGER,ALLOCATABLE :: C2P(:) ! 'C2P' IS WORKING ARRAY FOR CGS_GRID
   DOUBLE PRECISION,ALLOCATABLE :: CGSDX(:,:,:,:),CGSDY(:,:,:,:),CGSDZ(:,:,:,:),CGSDA(:,:,:,:)
   DOUBLE PRECISION :: A,B,W,X,Y,Z,RHO_GY_1,RHO_GZ_1,RHO_GY_2,RHO_GZ_2
   DOUBLE PRECISION,ALLOCATABLE :: W3(:,:)
   INTEGER :: TICKET

   ALLOCATE(CGS(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z),C2P(NPGS))
   ALLOCATE(CGSDX(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDY(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDZ(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDA(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(W3(NCGS,NCGS))

   TICKET=0
   Y_C=0.0D0
   DO I=1,NATOM
    DO J=1,NGRID(I)
     TICKET=TICKET+1
     IF (TICKET == NUMPROCS) TICKET=TICKET-NUMPROCS
! ------------------------ PARALLEL LOOP
     IF (MYID == TICKET) THEN
! ------------------------ PARALLEL LOOP
     CALL CGS_GRID(CGS,C2P,NCGS,REDUCED_CEL1X,REDUCED_CEL1Y,REDUCED_CEL1Z,NPGS,J,I)
     CALL CGS_DERIVATIVE_GRID(CGSDX,CGSDY,CGSDZ,CGSDA,C2P,NCGS,REDUCED_CEL1X,REDUCED_CEL1Y,REDUCED_CEL1Z,NPGS,J,I)
     A=GRIDW(J,I)*D1F1(J,I)
     B=GRIDW(J,I)*D1F2(J,I)
     DO Q1X=-REDUCED_CEL1X,REDUCED_CEL1X
     DO Q1Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
     DO Q1Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
      RHO_GY_1=RHO_GY(J,I)*DCOS(DFLOAT(Q1X)*HELIX)+RHO_GZ(J,I)*DSIN(DFLOAT(Q1X)*HELIX)
      RHO_GZ_1=RHO_GZ(J,I)*DCOS(DFLOAT(Q1X)*HELIX)-RHO_GY(J,I)*DSIN(DFLOAT(Q1X)*HELIX)
      DO Q2X=-REDUCED_CEL1X,REDUCED_CEL1X
      DO Q2Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
      DO Q2Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
       Q3X=Q1X+Q2X
       Q3Y=Q1Y+Q2Y
       Q3Z=Q1Z+Q2Z
       IF ((Q3X <= REDUCED_CEL1X).AND.(Q3X >= -REDUCED_CEL1X).AND. &
           (Q3Y <= REDUCED_CEL1Y).AND.(Q3Y >= -REDUCED_CEL1Y).AND. &
           (Q3Z <= REDUCED_CEL1Z).AND.(Q3Z >= -REDUCED_CEL1Z)) THEN
        RHO_GY_2=RHO_GY(J,I)*DCOS(DFLOAT(Q3X)*HELIX)+RHO_GZ(J,I)*DSIN(DFLOAT(Q3X)*HELIX)
        RHO_GZ_2=RHO_GZ(J,I)*DCOS(DFLOAT(Q3X)*HELIX)-RHO_GY(J,I)*DSIN(DFLOAT(Q3X)*HELIX)
        DO K=1,NCGS
         W=CGS(K,Q1X,Q1Y,Q1Z)*A+(RHO_GX(J,I)*CGSDX(K,Q1X,Q1Y,Q1Z) &
                                +RHO_GY_1*CGSDY(K,Q1X,Q1Y,Q1Z) &
                                +RHO_GZ_1*CGSDZ(K,Q1X,Q1Y,Q1Z))*B
         X=RHO_GX(J,I)*CGS(K,Q1X,Q1Y,Q1Z)*B
         Y=RHO_GY_2*CGS(K,Q1X,Q1Y,Q1Z)*B
         Z=RHO_GZ_2*CGS(K,Q1X,Q1Y,Q1Z)*B
         DO L=1,NCGS
          Y_C(K,L,Q2X,Q2Y,Q2Z)=Y_C(K,L,Q2X,Q2Y,Q2Z)+W*CGS(L,Q3X,Q3Y,Q3Z) &
                              +X*CGSDX(L,Q3X,Q3Y,Q3Z)+Y*CGSDY(L,Q3X,Q3Y,Q3Z)+Z*CGSDZ(L,Q3X,Q3Y,Q3Z)
         ENDDO
        ENDDO
       ENDIF
      ENDDO
      ENDDO
      ENDDO
     ENDDO
     ENDDO
     ENDDO
! ------------------------ PARALLEL LOOP
     ENDIF
! ------------------------ PARALLEL LOOP
    ENDDO
   ENDDO

   CALL MPI_ALLREDUCE(MPI_IN_PLACE,Y_C,NCGS**2*(CEL1X*2+1)*(CEL1Y*2+1)*(CEL1Z*2+1), &
                      MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)

   IF ((IOPTN(9) >= 2).AND.(MYID == 0)) THEN
    WRITE(6,'(A)') 'EXCHANGE-CORRELATION MATRIX FOR CONTRACTED GAUSSIANS'
    CALL DUMP1(Y_C,NCGS,CEL1X,CEL1Y,CEL1Z)
   ENDIF

   DEALLOCATE(CGS,C2P,W3)
   DEALLOCATE(CGSDX,CGSDY,CGSDZ,CGSDA)

   RETURN
END SUBROUTINE



SUBROUTINE LOCAL_XC_GRADIENT
! NUMERICALLY CALCULATE THE EXCHANGE-CORRELATION INTEGRALS FOR LOCAL FUNCTIONALS.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE GRADIENT
   USE DFT

   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: I,J,K,L,Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,Q3X,Q3Y,Q3Z
   INTEGER,ALLOCATABLE :: M(:)
   DOUBLE PRECISION,ALLOCATABLE :: CGS(:,:,:,:)
   INTEGER,ALLOCATABLE :: C2P(:) ! 'C2P' IS WORKING ARRAY FOR CGS_GRID
   DOUBLE PRECISION,ALLOCATABLE :: CGSDX(:,:,:,:),CGSDY(:,:,:,:),CGSDZ(:,:,:,:),CGSDA(:,:,:,:)
   DOUBLE PRECISION :: A,BX,BY,BZ
   DOUBLE PRECISION,ALLOCATABLE :: FORCEX_TMP(:),FORCEY_TMP(:),FORCEZ_TMP(:)
   DOUBLE PRECISION,ALLOCATABLE :: FORCEX_TMP2(:),FORCEY_TMP2(:),FORCEZ_TMP2(:)
   DOUBLE PRECISION :: FORCETX_TMP,FORCETY_TMP,FORCETZ_TMP,FORCETA_TMP
   DOUBLE PRECISION :: FORCETX_TMP2,FORCETY_TMP2,FORCETZ_TMP2,FORCETA_TMP2
   DOUBLE PRECISION :: FORCETX_TMP3
   INTEGER :: TICKET

   ALLOCATE(CGS(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z),C2P(NPGS))
   ALLOCATE(CGSDX(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDY(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDZ(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDA(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(M(NCGS))
   ALLOCATE(FORCEX_TMP(NATOM),FORCEY_TMP(NATOM),FORCEZ_TMP(NATOM))
   ALLOCATE(FORCEX_TMP2(NATOM),FORCEY_TMP2(NATOM),FORCEZ_TMP2(NATOM))

   IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE LOCAL EXCHANGE-CORRELATION INTEGRAL DERIVATIVES'

   FORCEX_TMP=0.0D0
   FORCEY_TMP=0.0D0
   FORCEZ_TMP=0.0D0
   FORCETX_TMP=0.0D0
   FORCETY_TMP=0.0D0
   FORCETZ_TMP=0.0D0
   FORCETA_TMP=0.0D0
   FORCEX_TMP2=0.0D0
   FORCEY_TMP2=0.0D0
   FORCEZ_TMP2=0.0D0
   FORCETX_TMP2=0.0D0
   FORCETY_TMP2=0.0D0
   FORCETZ_TMP2=0.0D0
   FORCETA_TMP2=0.0D0
   FORCETX_TMP3=0.0D0

   M=0 ! ZERO IF A CGS IS DUMMY 
   DO I=1,NCGS
    DO J=1,NPGS
     IF (CC(I,J) /= 0.0D0) M(I)=PGS_ATOM(J)
    ENDDO
   ENDDO

   TICKET=0
   DO I=1,NATOM
    DO J=1,NGRID(I)
     TICKET=TICKET+1
     IF (TICKET == NUMPROCS) TICKET=TICKET-NUMPROCS
! ------------------------ PARALLEL LOOP
     IF (MYID == TICKET) THEN
! ------------------------ PARALLEL LOOP
     CALL CGS_GRID(CGS,C2P,NCGS,REDUCED_CEL1X,REDUCED_CEL1Y,REDUCED_CEL1Z,NPGS,J,I)
     CALL CGS_DERIVATIVE_GRID(CGSDX,CGSDY,CGSDZ,CGSDA,C2P,NCGS,REDUCED_CEL1X,REDUCED_CEL1Y,REDUCED_CEL1Z,NPGS,J,I)
     A=GRIDW(J,I)*D1F1(J,I)
! -- SURFACE INTEGRAL -> VOLUME INTEGRAL METHOD
     IF (PERIODX /= 0.0D0) FORCETX_TMP3=FORCETX_TMP3+XCF(J,I)*GRIDW(J,I)/PERIODX
! -- SURFACE INTEGRAL -> VOLUME INTEGRAL METHOD
     DO Q1X=-REDUCED_CEL1X,REDUCED_CEL1X
     DO Q1Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
     DO Q1Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
      BX=-DFLOAT(Q1X)
      BY=-DFLOAT(Q1Y)
      BZ=-DFLOAT(Q1Z)
      IF (PERIODX /= 0.0D0) BX=BX+(GRIDX(J,I)+ATOMX(I))/PERIODX
      IF (PERIODY /= 0.0D0) BY=BY+(GRIDY(J,I)+ATOMY(I))/PERIODY
      IF (PERIODZ /= 0.0D0) BZ=BZ+(GRIDZ(J,I)+ATOMZ(I))/PERIODZ
      DO Q2X=-REDUCED_CEL1X,REDUCED_CEL1X
      DO Q2Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
      DO Q2Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
       Q3X=Q1X+Q2X
       Q3Y=Q1Y+Q2Y
       Q3Z=Q1Z+Q2Z
       IF ((Q3X <= REDUCED_CEL1X).AND.(Q3X >= -REDUCED_CEL1X).AND. &
           (Q3Y <= REDUCED_CEL1Y).AND.(Q3Y >= -REDUCED_CEL1Y).AND. &
           (Q3Z <= REDUCED_CEL1Z).AND.(Q3Z >= -REDUCED_CEL1Z)) THEN
        DO K=1,NCGS
         IF (M(K) == 0) CYCLE
         DO L=1,NCGS
          IF (M(L) == 0) CYCLE
          FORCEX_TMP(M(L))=FORCEX_TMP(M(L))-P_C(K,L,Q2X,Q2Y,Q2Z)*CGS(K,Q1X,Q1Y,Q1Z)*CGSDX(L,Q3X,Q3Y,Q3Z)*A
          FORCEY_TMP(M(L))=FORCEY_TMP(M(L))-P_C(K,L,Q2X,Q2Y,Q2Z)*CGS(K,Q1X,Q1Y,Q1Z)*CGSDY(L,Q3X,Q3Y,Q3Z)*A
          FORCEZ_TMP(M(L))=FORCEZ_TMP(M(L))-P_C(K,L,Q2X,Q2Y,Q2Z)*CGS(K,Q1X,Q1Y,Q1Z)*CGSDZ(L,Q3X,Q3Y,Q3Z)*A
          FORCEX_TMP(M(K))=FORCEX_TMP(M(K))-P_C(K,L,Q2X,Q2Y,Q2Z)*CGSDX(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)*A
          FORCEY_TMP(M(K))=FORCEY_TMP(M(K))-P_C(K,L,Q2X,Q2Y,Q2Z)*CGSDY(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)*A
          FORCEZ_TMP(M(K))=FORCEZ_TMP(M(K))-P_C(K,L,Q2X,Q2Y,Q2Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)*A
          FORCEX_TMP2(I)=FORCEX_TMP2(I)+P_C(K,L,Q2X,Q2Y,Q2Z)*CGS(K,Q1X,Q1Y,Q1Z)*CGSDX(L,Q3X,Q3Y,Q3Z)*A
          FORCEY_TMP2(I)=FORCEY_TMP2(I)+P_C(K,L,Q2X,Q2Y,Q2Z)*CGS(K,Q1X,Q1Y,Q1Z) &
                         *(DCOS(DFLOAT(Q3X)*HELIX)*CGSDY(L,Q3X,Q3Y,Q3Z)-DSIN(DFLOAT(Q3X)*HELIX)*CGSDZ(L,Q3X,Q3Y,Q3Z))*A
          FORCEZ_TMP2(I)=FORCEZ_TMP2(I)+P_C(K,L,Q2X,Q2Y,Q2Z)*CGS(K,Q1X,Q1Y,Q1Z) &
                         *(DSIN(DFLOAT(Q3X)*HELIX)*CGSDY(L,Q3X,Q3Y,Q3Z)+DCOS(DFLOAT(Q3X)*HELIX)*CGSDZ(L,Q3X,Q3Y,Q3Z))*A
          FORCEX_TMP2(I)=FORCEX_TMP2(I)+P_C(K,L,Q2X,Q2Y,Q2Z)*CGSDX(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)*A
          FORCEY_TMP2(I)=FORCEY_TMP2(I)+P_C(K,L,Q2X,Q2Y,Q2Z)*CGS(L,Q3X,Q3Y,Q3Z) &
                         *(DCOS(DFLOAT(Q1X)*HELIX)*CGSDY(K,Q1X,Q1Y,Q1Z)-DSIN(DFLOAT(Q1X)*HELIX)*CGSDZ(K,Q1X,Q1Y,Q1Z))*A
          FORCEZ_TMP2(I)=FORCEZ_TMP2(I)+P_C(K,L,Q2X,Q2Y,Q2Z)*CGS(L,Q3X,Q3Y,Q3Z) &
                         *(DSIN(DFLOAT(Q1X)*HELIX)*CGSDY(K,Q1X,Q1Y,Q1Z)+DCOS(DFLOAT(Q1X)*HELIX)*CGSDZ(K,Q1X,Q1Y,Q1Z))*A
! -- SURFACE INTEGRAL -> VOLUME INTEGRAL METHOD
          FORCETX_TMP3=FORCETX_TMP3-P_C(K,L,Q2X,Q2Y,Q2Z)*((DFLOAT(Q2X)-BX)*CGS(K,Q1X,Q1Y,Q1Z)*CGSDX(L,Q3X,Q3Y,Q3Z) &
                         -BX*CGSDX(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z))*A
! -- SURFACE INTEGRAL -> VOLUME INTEGRAL METHOD
          FORCETX_TMP=FORCETX_TMP-P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q3X)*CGS(K,Q1X,Q1Y,Q1Z)*CGSDX(L,Q3X,Q3Y,Q3Z)*A &
                                 -P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q1X)*CGSDX(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)*A
          FORCETY_TMP=FORCETY_TMP-P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q3Y)*CGS(K,Q1X,Q1Y,Q1Z)*CGSDY(L,Q3X,Q3Y,Q3Z)*A &
                                 -P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q1Y)*CGSDY(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)*A
          FORCETZ_TMP=FORCETZ_TMP-P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*CGSDZ(L,Q3X,Q3Y,Q3Z)*A &
                                 -P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q1Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)*A
          FORCETA_TMP=FORCETA_TMP+P_C(K,L,Q2X,Q2Y,Q2Z)*CGS(K,Q1X,Q1Y,Q1Z)*CGSDA(L,Q3X,Q3Y,Q3Z)*A &
                                 +P_C(K,L,Q2X,Q2Y,Q2Z)*CGSDA(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)*A
         ENDDO
        ENDDO
       ENDIF
      ENDDO
      ENDDO
      ENDDO
     ENDDO
     ENDDO
     ENDDO
     DO K=1,NATOM
      FORCEX_TMP2(K)=FORCEX_TMP2(K)+GRIDW_X(J,I,K)*XCF(J,I)
      FORCEY_TMP2(K)=FORCEY_TMP2(K)+GRIDW_Y(J,I,K)*XCF(J,I)
      FORCEZ_TMP2(K)=FORCEZ_TMP2(K)+GRIDW_Z(J,I,K)*XCF(J,I)
     ENDDO
     FORCETX_TMP2=FORCETX_TMP2+GRIDW_TX(J,I)*XCF(J,I)
     FORCETY_TMP2=FORCETY_TMP2+GRIDW_TY(J,I)*XCF(J,I)
     FORCETZ_TMP2=FORCETZ_TMP2+GRIDW_TZ(J,I)*XCF(J,I)
     FORCETA_TMP2=FORCETA_TMP2+GRIDW_TA(J,I)*XCF(J,I)
! ------------------------ PARALLEL LOOP
     ENDIF
! ------------------------ PARALLEL LOOP
    ENDDO
   ENDDO

   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETX_TMP3,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) WRITE(6,'(A,F20.10)') 'SURFACE INTEGRAL METHOD LATTICE GRADIENT ',FORCETX+FORCETX_TMP3

   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEX_TMP,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEY_TMP,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEZ_TMP,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETX_TMP,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETY_TMP,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETZ_TMP,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETA_TMP,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   FORCEX=FORCEX+FORCEX_TMP
   FORCEY=FORCEY+FORCEY_TMP
   FORCEZ=FORCEZ+FORCEZ_TMP
   FORCETX=FORCETX+FORCETX_TMP
   FORCETY=FORCETY+FORCETY_TMP
   FORCETZ=FORCETZ+FORCETZ_TMP
   FORCETA=FORCETA+FORCETA_TMP
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) WRITE(6,'(A)') 'ENERGY GRADIENTS WITHOUT QUADRATURE DERIVATIVES'
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) CALL DUMP2

   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEX_TMP2,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEY_TMP2,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEZ_TMP2,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETX_TMP2,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETY_TMP2,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETZ_TMP2,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETA_TMP2,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   FORCEX=FORCEX+FORCEX_TMP2
   FORCEY=FORCEY+FORCEY_TMP2
   FORCEZ=FORCEZ+FORCEZ_TMP2
   FORCETX=FORCETX+FORCETX_TMP2
   FORCETY=FORCETY+FORCETY_TMP2
   FORCETZ=FORCETZ+FORCETZ_TMP2
   FORCETA=FORCETA+FORCETA_TMP2
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) WRITE(6,'(A)') 'ENERGY GRADIENTS WITH QUADRATURE DERIVATIVES'
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) CALL DUMP2

   IF ((IOPTN(9) == 3).AND.(MYID == 0)) THEN
    CALL DUMP2
    WRITE(6,'(A)')               '                BASIS               GRID WEIGHT         TOTAL               SURFACE'
    WRITE(6,'(A,4F20.10)')       'PERIODX:',FORCETX_TMP-FORCETX_TMP2,FORCETX_TMP2,FORCETX_TMP,FORCETX_TMP3
    WRITE(6,'(A,3F20.10)')       'HELIX  :',FORCETA_TMP-FORCETA_TMP2,FORCETA_TMP2,FORCETA_TMP
    DO I=1,NATOM
     WRITE(6,'(A,I0,A,3F20.10)') 'ATOM',I,'X :',FORCEX_TMP(I)-FORCEX_TMP2(I),FORCEX_TMP2(I),FORCEX_TMP(I)
     WRITE(6,'(A,I0,A,3F20.10)') 'ATOM',I,'Y :',FORCEY_TMP(I)-FORCEY_TMP2(I),FORCEY_TMP2(I),FORCEY_TMP(I)
     WRITE(6,'(A,I0,A,3F20.10)') 'ATOM',I,'Z :',FORCEZ_TMP(I)-FORCEZ_TMP2(I),FORCEZ_TMP2(I),FORCEZ_TMP(I)
    ENDDO
   ENDIF

!write(150,*) natom,ngrid(1)
!do i=1,natom
! do j=1,ngrid(i)
!  write(150,*) gridw(j,i),gridw_y(j,i,2),xcf(j,i)
! enddo
!enddo

   DEALLOCATE(FORCEX_TMP2,FORCEY_TMP2,FORCEZ_TMP2)
   DEALLOCATE(FORCEX_TMP,FORCEY_TMP,FORCEZ_TMP)
   DEALLOCATE(CGS,C2P,CGSDX,CGSDY,CGSDZ,CGSDA,M)

   RETURN
END SUBROUTINE



SUBROUTINE GC_XC_GRADIENT
! NUMERICALLY CALCULATE THE EXCHANGE-CORRELATION INTEGRAL DERIVATIVES FOR GRADIENT-CORRECTED FUNCTIONALS.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE GRADIENT
   USE DFT

   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: I,J,K,L,Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,Q3X,Q3Y,Q3Z
   INTEGER,ALLOCATABLE :: M(:)
   DOUBLE PRECISION,ALLOCATABLE :: CGS(:,:,:,:)
   INTEGER,ALLOCATABLE :: C2P(:) ! 'C2P' IS WORKING ARRAY FOR CGS_GRID
   DOUBLE PRECISION,ALLOCATABLE :: CGSDX(:,:,:,:),CGSDY(:,:,:,:),CGSDZ(:,:,:,:),CGSDA(:,:,:,:)
   DOUBLE PRECISION,ALLOCATABLE :: CGSDXX(:,:,:,:),CGSDYY(:,:,:,:),CGSDZZ(:,:,:,:)
   DOUBLE PRECISION,ALLOCATABLE :: CGSDXY(:,:,:,:),CGSDYZ(:,:,:,:),CGSDZX(:,:,:,:)
   DOUBLE PRECISION,ALLOCATABLE :: CGSDXA(:,:,:,:),CGSDYA(:,:,:,:),CGSDZA(:,:,:,:)
   DOUBLE PRECISION :: A,B,CX,CY,CZ,RHO_GY_1,RHO_GZ_1,RHO_GY_2,RHO_GZ_2
   DOUBLE PRECISION :: RHO_GY_1_A,RHO_GZ_1_A,RHO_GY_2_A,RHO_GZ_2_A
   DOUBLE PRECISION,ALLOCATABLE :: FORCEX_TMP(:),FORCEY_TMP(:),FORCEZ_TMP(:)
   DOUBLE PRECISION,ALLOCATABLE :: FORCEX_TMP2(:),FORCEY_TMP2(:),FORCEZ_TMP2(:)
   DOUBLE PRECISION :: FORCETX_TMP,FORCETY_TMP,FORCETZ_TMP,FORCETA_TMP
   DOUBLE PRECISION :: FORCETX_TMP2,FORCETY_TMP2,FORCETZ_TMP2,FORCETA_TMP2
   DOUBLE PRECISION :: FORCETX_TMP3
   DOUBLE PRECISION :: ANGLE1,CS1,SN1,ANGLE2,CS2,SN2
   INTEGER :: TICKET

   ALLOCATE(CGS(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z),C2P(NPGS))
   ALLOCATE(CGSDX(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDY(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDZ(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDA(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDXX(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDYY(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDZZ(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDXY(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDYZ(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDZX(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDXA(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDYA(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDZA(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(M(NCGS))
   ALLOCATE(FORCEX_TMP(NATOM),FORCEY_TMP(NATOM),FORCEZ_TMP(NATOM))
   ALLOCATE(FORCEX_TMP2(NATOM),FORCEY_TMP2(NATOM),FORCEZ_TMP2(NATOM))

   IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE GRADIENT-CORRECTED EXCHANGE-CORRELATION INTEGRAL DERIVATIVES'

   FORCEX_TMP=0.0D0
   FORCEY_TMP=0.0D0
   FORCEZ_TMP=0.0D0
   FORCETX_TMP=0.0D0
   FORCETY_TMP=0.0D0
   FORCETZ_TMP=0.0D0
   FORCETA_TMP=0.0D0
   FORCEX_TMP2=0.0D0
   FORCEY_TMP2=0.0D0
   FORCEZ_TMP2=0.0D0
   FORCETX_TMP2=0.0D0
   FORCETY_TMP2=0.0D0
   FORCETZ_TMP2=0.0D0
   FORCETA_TMP2=0.0D0
   FORCETX_TMP3=0.0D0

   M=0 ! ZERO IF A CGS IS DUMMY
   DO I=1,NCGS
    DO J=1,NPGS
     IF (CC(I,J) /= 0.0D0) M(I)=PGS_ATOM(J)
    ENDDO
   ENDDO

   TICKET=0
   DO I=1,NATOM
    DO J=1,NGRID(I)
     TICKET=TICKET+1
     IF (TICKET == NUMPROCS) TICKET=TICKET-NUMPROCS
! ------------------------ PARALLEL LOOP
     IF (MYID == TICKET) THEN
! ------------------------ PARALLEL LOOP
     CALL CGS_GRID(CGS,C2P,NCGS,REDUCED_CEL1X,REDUCED_CEL1Y,REDUCED_CEL1Z,NPGS,J,I)
     CALL CGS_DERIVATIVE_GRID(CGSDX,CGSDY,CGSDZ,CGSDA,C2P,NCGS,REDUCED_CEL1X,REDUCED_CEL1Y,REDUCED_CEL1Z,NPGS,J,I)
     CALL CGS_SECOND_DERIVATIVE_GRID(CGSDXX,CGSDYY,CGSDZZ,CGSDXY,CGSDYZ,CGSDZX,CGSDXA,CGSDYA,CGSDZA,C2P,NCGS, &
                                     REDUCED_CEL1X,REDUCED_CEL1Y,REDUCED_CEL1Z,NPGS,J,I)
     A=GRIDW(J,I)*D1F1(J,I)
     B=GRIDW(J,I)*D1F2(J,I)
! -- SURFACE INTEGRAL -> VOLUME INTEGRAL METHOD
     IF (PERIODX /= 0.0D0) FORCETX_TMP3=FORCETX_TMP3+XCF(J,I)*GRIDW(J,I)/PERIODX
! -- SURFACE INTEGRAL -> VOLUME INTEGRAL METHOD
     DO Q1X=-REDUCED_CEL1X,REDUCED_CEL1X
     DO Q1Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
     DO Q1Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
      ANGLE1=DFLOAT(Q1X)*HELIX
      CS1=DCOS(ANGLE1)
      SN1=DSIN(ANGLE1)
      RHO_GY_1=RHO_GY(J,I)*DCOS(DFLOAT(Q1X)*HELIX)+RHO_GZ(J,I)*DSIN(DFLOAT(Q1X)*HELIX)
      RHO_GZ_1=RHO_GZ(J,I)*DCOS(DFLOAT(Q1X)*HELIX)-RHO_GY(J,I)*DSIN(DFLOAT(Q1X)*HELIX)
!     RHO_GY_1_A= &
!                RHO_GY_A(J,I)*DCOS(DFLOAT(Q1X)*HELIX)+RHO_GZ_A(J,I)*DSIN(DFLOAT(Q1X)*HELIX) &
!               +RHO_GY(J,I)*DFLOAT(Q1X)*(-DSIN(DFLOAT(Q1X)*HELIX))+RHO_GZ(J,I)*DFLOAT(Q1X)*( DCOS(DFLOAT(Q1X)*HELIX))
!     RHO_GZ_1_A= &
!                RHO_GZ_A(J,I)*DCOS(DFLOAT(Q1X)*HELIX)-RHO_GY_A(J,I)*DSIN(DFLOAT(Q1X)*HELIX) &
!               +RHO_GZ(J,I)*DFLOAT(Q1X)*(-DSIN(DFLOAT(Q1X)*HELIX))-RHO_GY(J,I)*DFLOAT(Q1X)*( DCOS(DFLOAT(Q1X)*HELIX))
      CX=-DFLOAT(Q1X)
      CY=-DFLOAT(Q1Y)
      CZ=-DFLOAT(Q1Z)
      IF (PERIODX /= 0.0D0) CX=CX+(GRIDX(J,I)+ATOMX(I))/PERIODX
      IF (PERIODY /= 0.0D0) CY=CY+(GRIDY(J,I)+ATOMY(I))/PERIODY
      IF (PERIODZ /= 0.0D0) CZ=CZ+(GRIDZ(J,I)+ATOMZ(I))/PERIODZ
      DO Q2X=-REDUCED_CEL1X,REDUCED_CEL1X
      DO Q2Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
      DO Q2Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
       Q3X=Q1X+Q2X
       Q3Y=Q1Y+Q2Y
       Q3Z=Q1Z+Q2Z
       IF ((Q3X <= REDUCED_CEL1X).AND.(Q3X >= -REDUCED_CEL1X).AND. &
           (Q3Y <= REDUCED_CEL1Y).AND.(Q3Y >= -REDUCED_CEL1Y).AND. &
           (Q3Z <= REDUCED_CEL1Z).AND.(Q3Z >= -REDUCED_CEL1Z)) THEN
        ANGLE2=DFLOAT(Q3X)*HELIX
        CS2=DCOS(ANGLE2)
        SN2=DSIN(ANGLE2)
        RHO_GY_2=RHO_GY(J,I)*DCOS(DFLOAT(Q3X)*HELIX)+RHO_GZ(J,I)*DSIN(DFLOAT(Q3X)*HELIX)
        RHO_GZ_2=RHO_GZ(J,I)*DCOS(DFLOAT(Q3X)*HELIX)-RHO_GY(J,I)*DSIN(DFLOAT(Q3X)*HELIX)
!       RHO_GY_2_A= &
!                  RHO_GY_A(J,I)*DCOS(DFLOAT(Q3X)*HELIX)+RHO_GZ_A(J,I)*DSIN(DFLOAT(Q3X)*HELIX) &
!                 +RHO_GY(J,I)*DFLOAT(Q3X)*(-DSIN(DFLOAT(Q3X)*HELIX))+RHO_GZ(J,I)*DFLOAT(Q3X)*( DCOS(DFLOAT(Q3X)*HELIX))
!       RHO_GZ_2_A= &
!                  RHO_GZ_A(J,I)*DCOS(DFLOAT(Q3X)*HELIX)-RHO_GY_A(J,I)*DSIN(DFLOAT(Q3X)*HELIX) &
!                 +RHO_GZ(J,I)*DFLOAT(Q3X)*(-DSIN(DFLOAT(Q3X)*HELIX))-RHO_GY(J,I)*DFLOAT(Q3X)*( DCOS(DFLOAT(Q3X)*HELIX))
        DO K=1,NCGS
         IF (M(K) == 0) CYCLE
         DO L=1,NCGS
          IF (M(L) == 0) CYCLE
          FORCEX_TMP(M(L))=FORCEX_TMP(M(L))-P_C(K,L,Q2X,Q2Y,Q2Z)*(CGSDX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDXX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDXY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)))
          FORCEY_TMP(M(L))=FORCEY_TMP(M(L))-P_C(K,L,Q2X,Q2Y,Q2Z)*(CGSDY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDXY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDYY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDYZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)))
          FORCEZ_TMP(M(L))=FORCEZ_TMP(M(L))-P_C(K,L,Q2X,Q2Y,Q2Z)*(CGSDZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDZX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDYZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)))
          FORCEX_TMP(M(K))=FORCEX_TMP(M(K))-P_C(K,L,Q2X,Q2Y,Q2Z)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDXX(K,Q1X,Q1Y,Q1Z)+CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDXY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDZX(K,Q1X,Q1Y,Q1Z)))
          FORCEY_TMP(M(K))=FORCEY_TMP(M(K))-P_C(K,L,Q2X,Q2Y,Q2Z)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDXY(K,Q1X,Q1Y,Q1Z)+CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDYY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDYZ(K,Q1X,Q1Y,Q1Z)))
          FORCEZ_TMP(M(K))=FORCEZ_TMP(M(K))-P_C(K,L,Q2X,Q2Y,Q2Z)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDZX(K,Q1X,Q1Y,Q1Z)+CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDYZ(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDZZ(K,Q1X,Q1Y,Q1Z)))
          FORCEX_TMP2(I)=FORCEX_TMP2(I)+P_C(K,L,Q2X,Q2Y,Q2Z)*(CGSDX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDXX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDXY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)))
          FORCEY_TMP2(I)=FORCEY_TMP2(I)+P_C(K,L,Q2X,Q2Y,Q2Z) &
          *(DCOS(DFLOAT(Q3X)*HELIX)*(CGSDY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDXY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDYY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDYZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z))) &
          -DSIN(DFLOAT(Q3X)*HELIX)*(CGSDZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDZX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDYZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z))) )
          FORCEZ_TMP2(I)=FORCEZ_TMP2(I)+P_C(K,L,Q2X,Q2Y,Q2Z) &
          *(DSIN(DFLOAT(Q3X)*HELIX)*(CGSDY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDXY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDYY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDYZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z))) &
          +DCOS(DFLOAT(Q3X)*HELIX)*(CGSDZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDZX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDYZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z))) )
          FORCEX_TMP2(I)=FORCEX_TMP2(I)+P_C(K,L,Q2X,Q2Y,Q2Z)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDXX(K,Q1X,Q1Y,Q1Z)+CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDXY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDZX(K,Q1X,Q1Y,Q1Z)))
          FORCEY_TMP2(I)=FORCEY_TMP2(I)+P_C(K,L,Q2X,Q2Y,Q2Z) &
          *(DCOS(DFLOAT(Q1X)*HELIX)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDXY(K,Q1X,Q1Y,Q1Z)+CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDYY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDYZ(K,Q1X,Q1Y,Q1Z))) &
          -DSIN(DFLOAT(Q1X)*HELIX)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDZX(K,Q1X,Q1Y,Q1Z)+CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDYZ(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDZZ(K,Q1X,Q1Y,Q1Z))) )
          FORCEZ_TMP2(I)=FORCEZ_TMP2(I)+P_C(K,L,Q2X,Q2Y,Q2Z) &
          *(DSIN(DFLOAT(Q1X)*HELIX)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDXY(K,Q1X,Q1Y,Q1Z)+CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDYY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDYZ(K,Q1X,Q1Y,Q1Z))) &
          +DCOS(DFLOAT(Q1X)*HELIX)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDZX(K,Q1X,Q1Y,Q1Z)+CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDYZ(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDZZ(K,Q1X,Q1Y,Q1Z))) )
! -- SURFACE INTEGRAL -> VOLUME INTEGRAL METHOD
          FORCETX_TMP3=FORCETX_TMP3-P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q2X)*(CGSDX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDXX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDXY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z))) &
          +P_C(K,L,Q2X,Q2Y,Q2Z)*CX*(CGSDX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDXX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDXY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z))) &
          +P_C(K,L,Q2X,Q2Y,Q2Z)*CX*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDXX(K,Q1X,Q1Y,Q1Z)+CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDXY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDZX(K,Q1X,Q1Y,Q1Z)))
! -- SURFACE INTEGRAL -> VOLUME INTEGRAL METHOD
          FORCETX_TMP=FORCETX_TMP-P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q3X)*(CGSDX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDXX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDXY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z))) &
          -P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q1X)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDXX(K,Q1X,Q1Y,Q1Z)+CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDXY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDZX(K,Q1X,Q1Y,Q1Z)))
          FORCETY_TMP=FORCETY_TMP-P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q3Y)*(CGSDY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDXY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDYY(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDYZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z))) &
          -P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q1Y)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDXY(K,Q1X,Q1Y,Q1Z)+CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDYY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDYZ(K,Q1X,Q1Y,Q1Z)))
          FORCETZ_TMP=FORCETZ_TMP-P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q3Z)*(CGSDZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDX(K,Q1X,Q1Y,Q1Z)+CGSDZX(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDYZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDY(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZZ(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z))) &
          -P_C(K,L,Q2X,Q2Y,Q2Z)*DFLOAT(Q1Z)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)*A &
          +B*RHO_GX(J,I)*(CGS(L,Q3X,Q3Y,Q3Z)*CGSDZX(K,Q1X,Q1Y,Q1Z)+CGSDX(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GY_2*CGSDY(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)+RHO_GY_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDYZ(K,Q1X,Q1Y,Q1Z)) &
          +B*(RHO_GZ_2*CGSDZ(L,Q3X,Q3Y,Q3Z)*CGSDZ(K,Q1X,Q1Y,Q1Z)+RHO_GZ_1*CGS(L,Q3X,Q3Y,Q3Z)*CGSDZZ(K,Q1X,Q1Y,Q1Z)))
          FORCETA_TMP=FORCETA_TMP+P_C(K,L,Q2X,Q2Y,Q2Z)*( CGSDA(L,Q3X,Q3Y,Q3Z)*CGS(K,Q1X,Q1Y,Q1Z)*A &
                                                        +CGS(L,Q3X,Q3Y,Q3Z)*CGSDA(K,Q1X,Q1Y,Q1Z)*A &
           +B*RHO_GX(J,I)*(CGSDA(K,Q1X,Q1Y,Q1Z)*CGSDX(L,Q3X,Q3Y,Q3Z)+CGS(K,Q1X,Q1Y,Q1Z)*CGSDXA(L,Q3X,Q3Y,Q3Z) &
                          +CGSDXA(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)+CGSDX(K,Q1X,Q1Y,Q1Z)*CGSDA(L,Q3X,Q3Y,Q3Z)) &
           +B*RHO_GY(J,I)*(CGSDA(K,Q1X,Q1Y,Q1Z)*(CGSDY(L,Q3X,Q3Y,Q3Z)*CS2-CGSDZ(L,Q3X,Q3Y,Q3Z)*SN2) &
                         +(CGSDY(K,Q1X,Q1Y,Q1Z)*CS1-CGSDZ(K,Q1X,Q1Y,Q1Z)*SN1)*CGSDA(L,Q3X,Q3Y,Q3Z)) &
           +B*RHO_GY(J,I)*(CGS(K,Q1X,Q1Y,Q1Z)*(CGSDYA(L,Q3X,Q3Y,Q3Z)*CS2-CGSDZA(L,Q3X,Q3Y,Q3Z)*SN2) &
                         +(CGSDYA(K,Q1X,Q1Y,Q1Z)*CS1-CGSDZA(K,Q1X,Q1Y,Q1Z)*SN1)*CGS(L,Q3X,Q3Y,Q3Z)) &
           +B*RHO_GY(J,I) &
                  *(CGS(K,Q1X,Q1Y,Q1Z)*(CGSDY(L,Q3X,Q3Y,Q3Z)*DFLOAT(Q3X)*(-SN2)-CGSDZ(L,Q3X,Q3Y,Q3Z)*DFLOAT(Q3X)*CS2) &
                  +(CGSDY(K,Q1X,Q1Y,Q1Z)*DFLOAT(Q1X)*(-SN1)-CGSDZ(K,Q1X,Q1Y,Q1Z)*DFLOAT(Q1X)*CS1)*CGS(L,Q3X,Q3Y,Q3Z)) &
           +B*RHO_GZ(J,I)*(CGSDA(K,Q1X,Q1Y,Q1Z)*(CGSDZ(L,Q3X,Q3Y,Q3Z)*CS2+CGSDY(L,Q3X,Q3Y,Q3Z)*SN2) &
                         +(CGSDZ(K,Q1X,Q1Y,Q1Z)*CS1+CGSDY(K,Q1X,Q1Y,Q1Z)*SN1)*CGSDA(L,Q3X,Q3Y,Q3Z)) &
           +B*RHO_GZ(J,I)*(CGS(K,Q1X,Q1Y,Q1Z)*(CGSDZA(L,Q3X,Q3Y,Q3Z)*CS2+CGSDYA(L,Q3X,Q3Y,Q3Z)*SN2) &
                         +(CGSDZA(K,Q1X,Q1Y,Q1Z)*CS1+CGSDYA(K,Q1X,Q1Y,Q1Z)*SN1)*CGS(L,Q3X,Q3Y,Q3Z)) &
           +B*RHO_GZ(J,I) &
                 *(CGS(K,Q1X,Q1Y,Q1Z)*(CGSDZ(L,Q3X,Q3Y,Q3Z)*DFLOAT(Q3X)*(-SN2)+CGSDY(L,Q3X,Q3Y,Q3Z)*DFLOAT(Q3X)*CS2) &
                 +(CGSDZ(K,Q1X,Q1Y,Q1Z)*DFLOAT(Q1X)*(-SN1)+CGSDY(K,Q1X,Q1Y,Q1Z)*DFLOAT(Q1X)*CS1)*CGS(L,Q3X,Q3Y,Q3Z)) )
         ENDDO
        ENDDO
       ENDIF
      ENDDO
      ENDDO
      ENDDO
     ENDDO
     ENDDO
     ENDDO
     DO K=1,NATOM
      FORCEX_TMP2(K)=FORCEX_TMP2(K)+GRIDW_X(J,I,K)*XCF(J,I)
      FORCEY_TMP2(K)=FORCEY_TMP2(K)+GRIDW_Y(J,I,K)*XCF(J,I)
      FORCEZ_TMP2(K)=FORCEZ_TMP2(K)+GRIDW_Z(J,I,K)*XCF(J,I)
     ENDDO
     FORCETX_TMP2=FORCETX_TMP2+GRIDW_TX(J,I)*XCF(J,I)
     FORCETY_TMP2=FORCETY_TMP2+GRIDW_TY(J,I)*XCF(J,I)
     FORCETZ_TMP2=FORCETZ_TMP2+GRIDW_TZ(J,I)*XCF(J,I)
     FORCETA_TMP2=FORCETA_TMP2+GRIDW_TA(J,I)*XCF(J,I)
! ------------------------ PARALLEL LOOP
     ENDIF
! ------------------------ PARALLEL LOOP
    ENDDO
   ENDDO

   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETX_TMP3,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) WRITE(6,'(A,F20.10)') 'SURFACE INTEGRAL METHOD LATTICE GRADIENT ',FORCETX+FORCETX_TMP3

   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEX_TMP,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEY_TMP,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEZ_TMP,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETX_TMP,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETY_TMP,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETZ_TMP,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETA_TMP,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   FORCEX=FORCEX+FORCEX_TMP
   FORCEY=FORCEY+FORCEY_TMP
   FORCEZ=FORCEZ+FORCEZ_TMP
   FORCETX=FORCETX+FORCETX_TMP
   FORCETY=FORCETY+FORCETY_TMP
   FORCETZ=FORCETZ+FORCETZ_TMP
   FORCETA=FORCETA+FORCETA_TMP
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) WRITE(6,'(A)') 'ENERGY GRADIENTS WITHOUT QUADRATURE DERIVATIVES'
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) CALL DUMP2

   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEX_TMP2,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEY_TMP2,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEZ_TMP2,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETX_TMP2,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETY_TMP2,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETZ_TMP2,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETA_TMP2,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
   FORCEX=FORCEX+FORCEX_TMP2
   FORCEY=FORCEY+FORCEY_TMP2
   FORCEZ=FORCEZ+FORCEZ_TMP2
   FORCETX=FORCETX+FORCETX_TMP2
   FORCETY=FORCETY+FORCETY_TMP2
   FORCETZ=FORCETZ+FORCETZ_TMP2
   FORCETA=FORCETA+FORCETA_TMP2
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) WRITE(6,'(A)') 'ENERGY GRADIENTS WITH QUADRATURE DERIVATIVES'
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) CALL DUMP2

   IF ((IOPTN(9) == 3).AND.(MYID == 0)) THEN
    CALL DUMP2
    WRITE(6,'(A)')               '                BASIS               GRID WEIGHT         TOTAL               SURFACE'
    WRITE(6,'(A,4F20.10)')       'PERIODX:',FORCETX_TMP-FORCETX_TMP2,FORCETX_TMP2,FORCETX_TMP,FORCETX_TMP3
    WRITE(6,'(A,3F20.10)')       'HELIX  :',FORCETA_TMP-FORCETA_TMP2,FORCETA_TMP2,FORCETA_TMP
    DO I=1,NATOM
     WRITE(6,'(A,I0,A,3F20.10)') 'ATOM',I,'X :',FORCEX_TMP(I)-FORCEX_TMP2(I),FORCEX_TMP2(I),FORCEX_TMP(I)
     WRITE(6,'(A,I0,A,3F20.10)') 'ATOM',I,'Y :',FORCEY_TMP(I)-FORCEY_TMP2(I),FORCEY_TMP2(I),FORCEY_TMP(I)
     WRITE(6,'(A,I0,A,3F20.10)') 'ATOM',I,'Z :',FORCEZ_TMP(I)-FORCEZ_TMP2(I),FORCEZ_TMP2(I),FORCEZ_TMP(I)
    ENDDO
   ENDIF

   DEALLOCATE(FORCEX_TMP2,FORCEY_TMP2,FORCEZ_TMP2)
   DEALLOCATE(FORCEX_TMP,FORCEY_TMP,FORCEZ_TMP)
   DEALLOCATE(CGS,C2P,CGSDX,CGSDY,CGSDZ,CGSDA)
   DEALLOCATE(CGSDXX,CGSDYY,CGSDZZ,CGSDXY,CGSDYZ,CGSDZX,M)

   RETURN
END SUBROUTINE



SUBROUTINE CGS_GRID(CGS,C2P,N,MX,MY,MZ,L,IGRID,INUC)
! CALCULATE THE AMPLITUDE OF CONTRACTED GAUSSIANS AT GRID POINTS

   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE DFT

   IMPLICIT NONE
   INTEGER :: N,MX,MY,MZ,L,IGRID,INUC
   INTEGER :: C2P(L)
   DOUBLE PRECISION :: CGS(N,-MX:MX,-MY:MY,-MZ:MZ)
   INTEGER :: I,J,K,Q1X,Q1Y,Q1Z
   DOUBLE PRECISION :: QX,QY,QZ,X0,Y0,Z0,X1,Y1,Z1,X2,Y2,Z2,R,P
   DOUBLE PRECISION :: ANGLE,CS,SN

   X0=ATOMX(INUC)
   Y0=ATOMY(INUC)
   Z0=ATOMZ(INUC)
   DO I=1,NCGS
    K=0
    DO J=1,NPGS
     IF (CC(I,J) /= 0.0D0) THEN
      K=K+1
      C2P(K)=J
     ENDIF
    ENDDO
    DO Q1X=-MX,MX
    DO Q1Y=-MY,MY
    DO Q1Z=-MZ,MZ
     CGS(I,Q1X,Q1Y,Q1Z)=0.0D0
     ANGLE=DFLOAT(Q1X)*HELIX
     CS=DCOS(ANGLE)
     SN=DSIN(ANGLE)
     QX=DFLOAT(Q1X)*PERIODX
     QY=DFLOAT(Q1Y)*PERIODY
     QZ=DFLOAT(Q1Z)*PERIODZ
     X1=X0-CGSX(I)-QX
     Y1=Y0*CS+Z0*SN-CGSY(I)-QY
     Z1=Z0*CS-Y0*SN-CGSZ(I)-QZ
     X2=GRIDX(IGRID,INUC)+X1
     Y2=GRIDY(IGRID,INUC)*CS+GRIDZ(IGRID,INUC)*SN+Y1
     Z2=GRIDZ(IGRID,INUC)*CS-GRIDY(IGRID,INUC)*SN+Z1
     R=X2**2+Y2**2+Z2**2
     P=1.0D0
     IF (PAX(C2P(1)) == 1) THEN
      P=P*X2
     ELSE IF (PAX(C2P(1)) == 2) THEN
      P=P*X2**2
     ELSE IF (PAX(C2P(1)) == 3) THEN
      P=P*X2**3
     ELSE IF (PAX(C2P(1)) == 4) THEN
      CALL PABORT('G FUNCTION NOT YET IMPLEMENTED IN CGS_GRID')
     ENDIF
     IF (PAY(C2P(1)) == 1) THEN
      P=P*Y2
     ELSE IF (PAY(C2P(1)) == 2) THEN
      P=P*Y2**2
     ELSE IF (PAY(C2P(1)) == 3) THEN
      P=P*Y2**3
     ELSE IF (PAY(C2P(1)) == 4) THEN
      CALL PABORT('G FUNCTION NOT YET IMPLEMENTED IN CGS_GRID')
     ENDIF
     IF (PAZ(C2P(1)) == 1) THEN
      P=P*Z2
     ELSE IF (PAZ(C2P(1)) == 2) THEN
      P=P*Z2**2
     ELSE IF (PAZ(C2P(1)) == 3) THEN
      P=P*Z2**3
     ELSE IF (PAZ(C2P(1)) == 4) THEN
      CALL PABORT('G FUNCTION NOT YET IMPLEMENTED IN CGS_GRID')
     ENDIF
     DO J=1,K
      CGS(I,Q1X,Q1Y,Q1Z)=CGS(I,Q1X,Q1Y,Q1Z)+DEXP(-ZT(C2P(J))*R)*CC(I,C2P(J))*P
     ENDDO
    ENDDO
    ENDDO
    ENDDO
   ENDDO

   RETURN
END SUBROUTINE



SUBROUTINE CGS_DERIVATIVE_GRID(CGSDX,CGSDY,CGSDZ,CGSDA,C2P,N,MX,MY,MZ,L,IGRID,INUC)
! CALCULATE THE AMPLITUDE OF CONTRACTED GAUSSIAN DERIVATIVES AT GRID POINTS

   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE DFT

   IMPLICIT NONE
   INTEGER :: N,MX,MY,MZ,L,IGRID,INUC
   INTEGER :: C2P(L)
   DOUBLE PRECISION :: CGSDX(N,-MX:MX,-MY:MY,-MZ:MZ)
   DOUBLE PRECISION :: CGSDY(N,-MX:MX,-MY:MY,-MZ:MZ)
   DOUBLE PRECISION :: CGSDZ(N,-MX:MX,-MY:MY,-MZ:MZ)
   DOUBLE PRECISION :: CGSDA(N,-MX:MX,-MY:MY,-MZ:MZ)
   INTEGER :: I,J,K,Q1X,Q1Y,Q1Z
   DOUBLE PRECISION :: QX,QY,QZ,X0,Y0,Z0,X1,Y1,Z1,X2,Y2,Z2,R
   DOUBLE PRECISION :: PX1,PY1,PZ1,PX2,PY2,PZ2,PX3,PY3,PZ3,T
   DOUBLE PRECISION :: ANGLE,CS,SN
   DOUBLE PRECISION :: Y1_A,Z1_A,Y2_A,Z2_A

   X0=ATOMX(INUC)
   Y0=ATOMY(INUC)
   Z0=ATOMZ(INUC)
   DO I=1,NCGS
    K=0
    DO J=1,NPGS
     IF (CC(I,J) /= 0.0D0) THEN
      K=K+1
      C2P(K)=J
     ENDIF
    ENDDO
    DO Q1X=-MX,MX
    DO Q1Y=-MY,MY
    DO Q1Z=-MZ,MZ
     CGSDX(I,Q1X,Q1Y,Q1Z)=0.0D0
     CGSDY(I,Q1X,Q1Y,Q1Z)=0.0D0
     CGSDZ(I,Q1X,Q1Y,Q1Z)=0.0D0
     CGSDA(I,Q1X,Q1Y,Q1Z)=0.0D0
     ANGLE=DFLOAT(Q1X)*HELIX
     CS=DCOS(ANGLE)
     SN=DSIN(ANGLE)
     QX=DFLOAT(Q1X)*PERIODX
     QY=DFLOAT(Q1Y)*PERIODY
     QZ=DFLOAT(Q1Z)*PERIODZ
     X1=X0-CGSX(I)-QX
     Y1=Y0*CS+Z0*SN-CGSY(I)-QY
     Z1=Z0*CS-Y0*SN-CGSZ(I)-QZ
     Y1_A=Y0*DFLOAT(Q1X)*(-SN)+Z0*DFLOAT(Q1X)*CS
     Z1_A=Z0*DFLOAT(Q1X)*(-SN)-Y0*DFLOAT(Q1X)*CS
     X2=GRIDX(IGRID,INUC)+X1
     Y2=GRIDY(IGRID,INUC)*CS+GRIDZ(IGRID,INUC)*SN+Y1
     Z2=GRIDZ(IGRID,INUC)*CS-GRIDY(IGRID,INUC)*SN+Z1
     Y2_A=GRIDY(IGRID,INUC)*DFLOAT(Q1X)*(-SN)+GRIDZ(IGRID,INUC)*DFLOAT(Q1X)*CS+Y1_A
     Z2_A=GRIDZ(IGRID,INUC)*DFLOAT(Q1X)*(-SN)-GRIDY(IGRID,INUC)*DFLOAT(Q1X)*CS+Z1_A
     R=X2**2+Y2**2+Z2**2
     PX2=1.0D0
     PY2=1.0D0
     PZ2=1.0D0
     IF (PAX(C2P(1)) == 0) THEN
      PX1=0.0D0
      PX3=X2
     ELSE IF (PAX(C2P(1)) == 1) THEN
      PX1=1.0D0
      PY2=PY2*X2
      PZ2=PZ2*X2
      PX3=X2**2
     ELSE IF (PAX(C2P(1)) == 2) THEN
      PX1=2.0D0*X2
      PY2=PY2*X2**2
      PZ2=PZ2*X2**2
      PX3=X2**3
     ELSE IF (PAX(C2P(1)) == 3) THEN
      PX1=3.0D0*X2**2
      PY2=PY2*X2**3
      PZ2=PZ2*X2**3
      PX3=X2**4
     ENDIF
     IF (PAY(C2P(1)) == 0) THEN
      PY1=0.0D0
      PY3=Y2
     ELSE IF (PAY(C2P(1)) == 1) THEN
      PY1=1.0D0
      PX2=PX2*Y2
      PZ2=PZ2*Y2
      PY3=Y2**2
     ELSE IF (PAY(C2P(1)) == 2) THEN
      PY1=2.0D0*Y2
      PX2=PX2*Y2**2
      PZ2=PZ2*Y2**2
      PY3=Y2**3
     ELSE IF (PAY(C2P(1)) == 3) THEN
      PY1=3.0D0*Y2**2
      PX2=PX2*Y2**3
      PZ2=PZ2*Y2**3
      PY3=Y2**4
     ENDIF
     IF (PAZ(C2P(1)) == 0) THEN
      PZ1=0.0D0
      PZ3=Z2
     ELSE IF (PAZ(C2P(1)) == 1) THEN
      PZ1=1.0D0
      PY2=PY2*Z2
      PX2=PX2*Z2
      PZ3=Z2**2
     ELSE IF (PAZ(C2P(1)) == 2) THEN
      PZ1=2.0D0*Z2
      PY2=PY2*Z2**2
      PX2=PX2*Z2**2
      PZ3=Z2**3
     ELSE IF (PAZ(C2P(1)) == 3) THEN
      PZ1=3.0D0*Z2**2
      PY2=PY2*Z2**3
      PX2=PX2*Z2**3
      PZ3=Z2**4
     ENDIF
     DO J=1,K
      T=DEXP(-ZT(C2P(J))*R)*CC(I,C2P(J))
      CGSDX(I,Q1X,Q1Y,Q1Z)=CGSDX(I,Q1X,Q1Y,Q1Z)+T*PX2*(PX1-2.0D0*ZT(C2P(J))*PX3)
      CGSDY(I,Q1X,Q1Y,Q1Z)=CGSDY(I,Q1X,Q1Y,Q1Z)+T*PY2*(PY1-2.0D0*ZT(C2P(J))*PY3)
      CGSDZ(I,Q1X,Q1Y,Q1Z)=CGSDZ(I,Q1X,Q1Y,Q1Z)+T*PZ2*(PZ1-2.0D0*ZT(C2P(J))*PZ3)
      CGSDA(I,Q1X,Q1Y,Q1Z)=CGSDA(I,Q1X,Q1Y,Q1Z)+Y2_A*T*PY2*(PY1-2.0D0*ZT(C2P(J))*PY3)+Z2_A*T*PZ2*(PZ1-2.0D0*ZT(C2P(J))*PZ3)
     ENDDO
    ENDDO
    ENDDO
    ENDDO
   ENDDO

   RETURN
END SUBROUTINE



SUBROUTINE CGS_SECOND_DERIVATIVE_GRID(CGSDXX,CGSDYY,CGSDZZ,CGSDXY,CGSDYZ,CGSDZX,CGSDXA,CGSDYA,CGSDZA,C2P,N,MX,MY,MZ,L,IGRID,INUC)
! CALCULATE THE AMPLITUDE OF CONTRACTED GAUSSIAN SECOND DERIVATIVES AT GRID POINTS

   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE DFT

   IMPLICIT NONE
   INTEGER :: N,MX,MY,MZ,L,IGRID,INUC
   INTEGER :: C2P(L)
   DOUBLE PRECISION :: CGSDXX(N,-MX:MX,-MY:MY,-MZ:MZ)
   DOUBLE PRECISION :: CGSDYY(N,-MX:MX,-MY:MY,-MZ:MZ)
   DOUBLE PRECISION :: CGSDZZ(N,-MX:MX,-MY:MY,-MZ:MZ)
   DOUBLE PRECISION :: CGSDXY(N,-MX:MX,-MY:MY,-MZ:MZ)
   DOUBLE PRECISION :: CGSDYZ(N,-MX:MX,-MY:MY,-MZ:MZ)
   DOUBLE PRECISION :: CGSDZX(N,-MX:MX,-MY:MY,-MZ:MZ)
   DOUBLE PRECISION :: CGSDXA(N,-MX:MX,-MY:MY,-MZ:MZ)
   DOUBLE PRECISION :: CGSDYA(N,-MX:MX,-MY:MY,-MZ:MZ)
   DOUBLE PRECISION :: CGSDZA(N,-MX:MX,-MY:MY,-MZ:MZ)
   INTEGER :: I,J,K,Q1X,Q1Y,Q1Z
   DOUBLE PRECISION :: QX,QY,QZ,X0,Y0,Z0,X1,Y1,Z1,X2,Y2,Z2,R
   DOUBLE PRECISION :: PX1,PY1,PZ1,PX2,PY2,PZ2,PX3,PY3,PZ3,PX4,PY4,PZ4,PX5,PY5,PZ5,PCX,PCY,PCZ,T
   DOUBLE PRECISION :: ANGLE,CS,SN
   DOUBLE PRECISION :: Y1_A,Z1_A,Y2_A,Z2_A

   X0=ATOMX(INUC)
   Y0=ATOMY(INUC)
   Z0=ATOMZ(INUC)
   DO I=1,NCGS
    K=0
    DO J=1,NPGS
     IF (CC(I,J) /= 0.0D0) THEN
      K=K+1
      C2P(K)=J
     ENDIF
    ENDDO
    DO Q1X=-MX,MX
    DO Q1Y=-MY,MY
    DO Q1Z=-MZ,MZ
     CGSDXX(I,Q1X,Q1Y,Q1Z)=0.0D0
     CGSDYY(I,Q1X,Q1Y,Q1Z)=0.0D0
     CGSDZZ(I,Q1X,Q1Y,Q1Z)=0.0D0
     CGSDXY(I,Q1X,Q1Y,Q1Z)=0.0D0
     CGSDYZ(I,Q1X,Q1Y,Q1Z)=0.0D0
     CGSDZX(I,Q1X,Q1Y,Q1Z)=0.0D0
     CGSDXA(I,Q1X,Q1Y,Q1Z)=0.0D0
     CGSDYA(I,Q1X,Q1Y,Q1Z)=0.0D0
     CGSDZA(I,Q1X,Q1Y,Q1Z)=0.0D0
     ANGLE=DFLOAT(Q1X)*HELIX
     CS=DCOS(ANGLE)
     SN=DSIN(ANGLE)
     QX=DFLOAT(Q1X)*PERIODX
     QY=DFLOAT(Q1Y)*PERIODY
     QZ=DFLOAT(Q1Z)*PERIODZ
     X1=X0-CGSX(I)-QX
     Y1=Y0*CS+Z0*SN-CGSY(I)-QY
     Z1=Z0*CS-Y0*SN-CGSZ(I)-QZ
     Y1_A=Y0*DFLOAT(Q1X)*(-SN)+Z0*DFLOAT(Q1X)*CS
     Z1_A=Z0*DFLOAT(Q1X)*(-SN)-Y0*DFLOAT(Q1X)*CS
     X2=GRIDX(IGRID,INUC)+X1
     Y2=GRIDY(IGRID,INUC)*CS+GRIDZ(IGRID,INUC)*SN+Y1
     Z2=GRIDZ(IGRID,INUC)*CS-GRIDY(IGRID,INUC)*SN+Z1
     Y2_A=GRIDY(IGRID,INUC)*DFLOAT(Q1X)*(-SN)+GRIDZ(IGRID,INUC)*DFLOAT(Q1X)*CS+Y1_A
     Z2_A=GRIDZ(IGRID,INUC)*DFLOAT(Q1X)*(-SN)-GRIDY(IGRID,INUC)*DFLOAT(Q1X)*CS+Z1_A
     R=X2**2+Y2**2+Z2**2
     IF (PAX(C2P(1)) == 0) THEN
      PX1=0.0D0
      PX2=0.0D0
      PX3=1.0D0
      PX4=X2
      PX5=X2*X2
      PCX=-2.0D0
     ELSE IF (PAX(C2P(1)) == 1) THEN
      PX1=0.0D0
      PX2=1.0D0
      PX3=X2
      PX4=X2*X2
      PX5=X2**3
      PCX=-6.0D0
     ELSE IF (PAX(C2P(1)) == 2) THEN
      PX1=2.0D0
      PX2=2.0D0*X2
      PX3=X2*X2
      PX4=X2**3
      PX5=X2**4
      PCX=-10.0D0
     ELSE IF (PAX(C2P(1)) == 3) THEN
      PX1=6.0D0*X2
      PX2=3.0D0*X2**2
      PX3=X2**3
      PX4=X2**4
      PX5=X2**5
      PCX=-14.0D0
     ENDIF
     IF (PAY(C2P(1)) == 0) THEN
      PY1=0.0D0
      PY2=0.0D0
      PY3=1.0D0
      PY4=Y2
      PY5=Y2*Y2
      PCY=-2.0D0
     ELSE IF (PAY(C2P(1)) == 1) THEN
      PY1=0.0D0
      PY2=1.0D0
      PY3=Y2
      PY4=Y2*Y2
      PY5=Y2**3
      PCY=-6.0D0
     ELSE IF (PAY(C2P(1)) == 2) THEN
      PY1=2.0D0
      PY2=2.0D0*Y2
      PY3=Y2*Y2
      PY4=Y2**3
      PY5=Y2**4
      PCY=-10.0D0
     ELSE IF (PAY(C2P(1)) == 3) THEN
      PY1=6.0D0*Y2
      PY2=3.0D0*Y2**2
      PY3=Y2**3
      PY4=Y2**4
      PY5=Y2**5
      PCY=-14.0D0
     ENDIF
     IF (PAZ(C2P(1)) == 0) THEN
      PZ1=0.0D0
      PZ2=0.0D0
      PZ3=1.0D0
      PZ4=Z2
      PZ5=Z2*Z2
      PCZ=-2.0D0
     ELSE IF (PAZ(C2P(1)) == 1) THEN
      PZ1=0.0D0
      PZ2=1.0D0
      PZ3=Z2
      PZ4=Z2*Z2
      PZ5=Z2**3
      PCZ=-6.0D0
     ELSE IF (PAZ(C2P(1)) == 2) THEN
      PZ1=2.0D0
      PZ2=2.0D0*Z2
      PZ3=Z2*Z2
      PZ4=Z2**3
      PZ5=Z2**4
      PCZ=-10.0D0
     ELSE IF (PAZ(C2P(1)) == 3) THEN
      PZ1=6.0D0*Z2
      PZ2=3.0D0*Z2**2
      PZ3=Z2**3
      PZ4=Z2**4
      PZ5=Z2**5
      PCZ=-14.0D0
     ENDIF
     DO J=1,K
      T=DEXP(-ZT(C2P(J))*R)*CC(I,C2P(J))
      CGSDXX(I,Q1X,Q1Y,Q1Z)=CGSDXX(I,Q1X,Q1Y,Q1Z)+T*PY3*PZ3*(PX1+ZT(C2P(J))*(PCX*PX3+4.0D0*ZT(C2P(J))*PX5))
      CGSDYY(I,Q1X,Q1Y,Q1Z)=CGSDYY(I,Q1X,Q1Y,Q1Z)+T*PZ3*PX3*(PY1+ZT(C2P(J))*(PCY*PY3+4.0D0*ZT(C2P(J))*PY5))
      CGSDZZ(I,Q1X,Q1Y,Q1Z)=CGSDZZ(I,Q1X,Q1Y,Q1Z)+T*PX3*PY3*(PZ1+ZT(C2P(J))*(PCZ*PZ3+4.0D0*ZT(C2P(J))*PZ5))
      CGSDXY(I,Q1X,Q1Y,Q1Z)=CGSDXY(I,Q1X,Q1Y,Q1Z)+T*PZ3*(PX2-2.0D0*ZT(C2P(J))*PX4)*(PY2-2.0D0*ZT(C2P(J))*PY4)
      CGSDYZ(I,Q1X,Q1Y,Q1Z)=CGSDYZ(I,Q1X,Q1Y,Q1Z)+T*PX3*(PY2-2.0D0*ZT(C2P(J))*PY4)*(PZ2-2.0D0*ZT(C2P(J))*PZ4)
      CGSDZX(I,Q1X,Q1Y,Q1Z)=CGSDZX(I,Q1X,Q1Y,Q1Z)+T*PY3*(PZ2-2.0D0*ZT(C2P(J))*PZ4)*(PX2-2.0D0*ZT(C2P(J))*PX4)
      CGSDXA(I,Q1X,Q1Y,Q1Z)=CGSDXA(I,Q1X,Q1Y,Q1Z)+Y2_A*T*PZ3*(PX2-2.0D0*ZT(C2P(J))*PX4)*(PY2-2.0D0*ZT(C2P(J))*PY4) &
                                                 +Z2_A*T*PY3*(PZ2-2.0D0*ZT(C2P(J))*PZ4)*(PX2-2.0D0*ZT(C2P(J))*PX4)
      CGSDYA(I,Q1X,Q1Y,Q1Z)=CGSDYA(I,Q1X,Q1Y,Q1Z)+Y2_A*T*PZ3*PX3*(PY1+ZT(C2P(J))*(PCY*PY3+4.0D0*ZT(C2P(J))*PY5)) &
                                                 +Z2_A*T*PX3*(PY2-2.0D0*ZT(C2P(J))*PY4)*(PZ2-2.0D0*ZT(C2P(J))*PZ4)
      CGSDZA(I,Q1X,Q1Y,Q1Z)=CGSDZA(I,Q1X,Q1Y,Q1Z)+Y2_A*T*PX3*(PY2-2.0D0*ZT(C2P(J))*PY4)*(PZ2-2.0D0*ZT(C2P(J))*PZ4) &
                                                 +Z2_A*T*PX3*PY3*(PZ1+ZT(C2P(J))*(PCZ*PZ3+4.0D0*ZT(C2P(J))*PZ5))
     ENDDO
    ENDDO
    ENDDO
    ENDDO
   ENDDO

   RETURN
END SUBROUTINE



SUBROUTINE TRIAL_ELECTRON_DENSITY(T,N,Q0X,Q0Y,Q0Z)
! CALCULATE TRIAL ELECTRON DENSITY BY THE DIRECT ALGORITHM.

   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE DFT

   IMPLICIT NONE
   INTEGER :: N,Q0X,Q0Y,Q0Z
   INTEGER :: I,J,K,L,Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,Q3X,Q3Y,Q3Z
   DOUBLE PRECISION,ALLOCATABLE :: CGS(:,:,:,:)
   INTEGER,ALLOCATABLE :: C2P(:) ! 'C2P' IS WORKING ARRAY FOR CGS_GRID
   DOUBLE COMPLEX :: A
   DOUBLE COMPLEX :: T(N,N,-Q0X:Q0X,-Q0Y:Q0Y,-Q0Z:Q0Z)
!  REAL :: CPUS,CPUE

!  CALL PCPU_TIME(CPUS)

   ALLOCATE(CGS(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
   ALLOCATE(C2P(NPGS))

   IF (HELIX /= 0.0D0) CALL PABORT('HELICAL POLYMERS NOT YET IMPLEMENTED')

   TRHO=DCMPLX(0.0D0,0.0D0)
   DO I=1,NATOM
    DO J=1,NGRID(I)
     CALL CGS_GRID(CGS,C2P,NCGS,REDUCED_CEL1X_XC,REDUCED_CEL1Y_XC,REDUCED_CEL1Z_XC,NPGS,J,I)
     A=DCMPLX(0.0D0,0.0D0)
     DO Q1X=-REDUCED_CEL1X_XC,REDUCED_CEL1X_XC
     DO Q1Y=-REDUCED_CEL1Y_XC,REDUCED_CEL1Y_XC
     DO Q1Z=-REDUCED_CEL1Z_XC,REDUCED_CEL1Z_XC
      DO Q2X=-REDUCED_CEL1X_XC,REDUCED_CEL1X_XC
      DO Q2Y=-REDUCED_CEL1Y_XC,REDUCED_CEL1Y_XC
      DO Q2Z=-REDUCED_CEL1Z_XC,REDUCED_CEL1Z_XC
       Q3X=Q1X+Q2X
       Q3Y=Q1Y+Q2Y
       Q3Z=Q1Z+Q2Z
       IF ((Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC).AND. &
           (Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC).AND. &
           (Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC)) THEN
        DO K=1,NCGS
         DO L=1,NCGS
          A=A+T(L,K,Q2X,Q2Y,Q2Z)*CGS(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z)
         ENDDO
        ENDDO
       ENDIF
      ENDDO
      ENDDO
      ENDDO
     ENDDO
     ENDDO
     ENDDO
     TRHO(J,I)=TRHO(J,I)+A
    ENDDO
   ENDDO

   A=0.0D0
   DO I=1,NATOM
    DO J=1,NGRID(I)
     A=A+GRIDW(J,I)*TRHO(J,I)
    ENDDO
   ENDDO
   IF (IOPTN(9) >= 2) WRITE(6,'(A,2F20.15)') 'INTEGRATED TRIAL ELECTRON DENSITY (QUADRATURE) = ',A
   IF (CDABS(A) > 0.0001) THEN
    CALL WARNING('NUMERICAL INTEGRATION ACCURACY MAY NOT BE SUFFICIENT')
    WRITE(6,'(A,F20.15)') 'INTEGRATED TRIAL ELECTRON DENSITY (QUADRATURE) = ',A
   ENDIF

   DEALLOCATE(CGS,C2P)
!  CALL PCPU_TIME(CPUE)
!  WRITE(6,'(A,F10.1)') 'CPU / SEC = ',CPUE-CPUS

   RETURN
END SUBROUTINE



SUBROUTINE TRIAL_ELECTRON_DENSITY_GRADIENT(T,N,Q0X,Q0Y,Q0Z)
! CALCULATE TRIAL ELECTRON DENSITY GRADIENTS BY THE DIRECT ALGORITHM.

   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE DFT

   IMPLICIT NONE
   INTEGER :: N,Q0X,Q0Y,Q0Z
   INTEGER :: I,J,K,L,Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,Q3X,Q3Y,Q3Z
   DOUBLE PRECISION,ALLOCATABLE :: CGS(:,:,:,:)
   INTEGER,ALLOCATABLE :: C2P(:) ! 'C2P' IS WORKING ARRAY FOR CGS_GRID
   DOUBLE PRECISION,ALLOCATABLE :: CGSDX(:,:,:,:),CGSDY(:,:,:,:),CGSDZ(:,:,:,:),CGSDA(:,:,:,:)
   DOUBLE COMPLEX :: X,Y,Z
   DOUBLE COMPLEX :: T(N,N,-Q0X:Q0X,-Q0Y:Q0Y,-Q0Z:Q0Z)
!  REAL :: CPUS,CPUE

!  CALL PCPU_TIME(CPUS)
   ALLOCATE(CGS(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
   ALLOCATE(C2P(NPGS))
   ALLOCATE(CGSDX(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
   ALLOCATE(CGSDY(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
   ALLOCATE(CGSDZ(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))
   ALLOCATE(CGSDA(NCGS,-REDUCED_CEL1X_XC:REDUCED_CEL1X_XC,-REDUCED_CEL1Y_XC:REDUCED_CEL1Y_XC,-REDUCED_CEL1Z_XC:REDUCED_CEL1Z_XC))

   IF (HELIX /= 0.0D0) CALL PABORT('HELICAL POLYMERS NOT YET IMPLEMENTED')

   TRHO_GX=DCMPLX(0.0D0,0.0D0)
   TRHO_GY=DCMPLX(0.0D0,0.0D0)
   TRHO_GZ=DCMPLX(0.0D0,0.0D0)
   DO I=1,NATOM
    DO J=1,NGRID(I)
     CALL CGS_GRID(CGS,C2P,NCGS,REDUCED_CEL1X_XC,REDUCED_CEL1Y_XC,REDUCED_CEL1Z_XC,NPGS,J,I)
     CALL CGS_DERIVATIVE_GRID(CGSDX,CGSDY,CGSDZ,CGSDA,C2P,NCGS,REDUCED_CEL1X_XC,REDUCED_CEL1Y_XC,REDUCED_CEL1Z_XC,NPGS,J,I)
     X=DCMPLX(0.0D0,0.0D0)
     Y=DCMPLX(0.0D0,0.0D0)
     Z=DCMPLX(0.0D0,0.0D0)
     DO Q1X=-REDUCED_CEL1X_XC,REDUCED_CEL1X_XC
     DO Q1Y=-REDUCED_CEL1Y_XC,REDUCED_CEL1Y_XC
     DO Q1Z=-REDUCED_CEL1Z_XC,REDUCED_CEL1Z_XC
      DO Q2X=-REDUCED_CEL1X_XC,REDUCED_CEL1X_XC
      DO Q2Y=-REDUCED_CEL1Y_XC,REDUCED_CEL1Y_XC
      DO Q2Z=-REDUCED_CEL1Z_XC,REDUCED_CEL1Z_XC
       Q3X=Q1X+Q2X
       Q3Y=Q1Y+Q2Y
       Q3Z=Q1Z+Q2Z
       IF ((Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC).AND. &
           (Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC).AND. &
           (Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC)) THEN
        DO K=1,NCGS
         DO L=1,NCGS
          X=X+T(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*CGSDX(K,Q3X,Q3Y,Q3Z)+CGSDX(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z))
          Y=Y+T(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*CGSDY(K,Q3X,Q3Y,Q3Z)+CGSDY(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z))
          Z=Z+T(L,K,Q2X,Q2Y,Q2Z)*(CGS(L,Q1X,Q1Y,Q1Z)*CGSDZ(K,Q3X,Q3Y,Q3Z)+CGSDZ(L,Q1X,Q1Y,Q1Z)*CGS(K,Q3X,Q3Y,Q3Z))
         ENDDO
        ENDDO
       ENDIF
      ENDDO
      ENDDO
      ENDDO
     ENDDO
     ENDDO
     ENDDO
     TRHO_GX(J,I)=TRHO_GX(J,I)+X
     TRHO_GY(J,I)=TRHO_GY(J,I)+Y
     TRHO_GZ(J,I)=TRHO_GZ(J,I)+Z
    ENDDO
   ENDDO
   
   DEALLOCATE(CGS,C2P)
   DEALLOCATE(CGSDX,CGSDY,CGSDZ,CGSDA)
!  CALL PCPU_TIME(CPUE)
!  WRITE(6,'(A,F10.1)') 'CPU / SEC = ',CPUE-CPUS

   RETURN
END SUBROUTINE



SUBROUTINE CONTRACT_LOCAL_XC(K_C,N,Q0X,Q0Y,Q0Z)
! NUMERICALLY CALCULATE THE EXCHANGE-CORRELATION INTEGRALS FOR LOCAL FUNCTIONALS.

   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE DFT

   IMPLICIT NONE
   INTEGER :: N,Q0X,Q0Y,Q0Z  
   INTEGER :: I,J,K,L,Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,Q3X,Q3Y,Q3Z  
   DOUBLE PRECISION,ALLOCATABLE :: CGS(:,:,:,:)
   INTEGER,ALLOCATABLE :: C2P(:) ! 'C2P' IS WORKING ARRAY FOR CGS_GRID
   DOUBLE COMPLEX :: A,B
   DOUBLE COMPLEX :: K_C(N,N,-Q0X:Q0X,-Q0Y:Q0Y,-Q0Z:Q0Z)
!  REAL :: CPUS,CPUE

!  CALL PCPU_TIME(CPUS)

   ALLOCATE(CGS(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z),C2P(NPGS))

   IF (HELIX /= 0.0D0) CALL PABORT('HELICAL POLYMERS NOT YET IMPLEMENTED')

   ! DO NOT ZERO SCRATCH K_C
   DO I=1,NATOM
    DO J=1,NGRID(I)
     CALL CGS_GRID(CGS,C2P,NCGS,REDUCED_CEL1X,REDUCED_CEL1Y,REDUCED_CEL1Z,NPGS,J,I)
     A=GRIDW(J,I)*D2F1(J,I)*TRHO(J,I)
     DO Q1X=-REDUCED_CEL1X,REDUCED_CEL1X
     DO Q1Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
     DO Q1Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
      DO Q2X=-REDUCED_CEL1X,REDUCED_CEL1X
      DO Q2Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
      DO Q2Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
       Q3X=Q1X+Q2X
       Q3Y=Q1Y+Q2Y
       Q3Z=Q1Z+Q2Z
       IF ((Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC).AND. &
           (Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC).AND. &
           (Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC)) THEN
        DO K=1,NCGS
         B=CGS(K,Q1X,Q1Y,Q1Z)*A
         DO L=1,NCGS
          K_C(K,L,Q2X,Q2Y,Q2Z)=K_C(K,L,Q2X,Q2Y,Q2Z)+CGS(L,Q3X,Q3Y,Q3Z)*B
         ENDDO
        ENDDO
       ENDIF
      ENDDO
      ENDDO
      ENDDO
     ENDDO
     ENDDO
     ENDDO
    ENDDO
   ENDDO

   DEALLOCATE(CGS,C2P)
!  CALL PCPU_TIME(CPUE)
!  WRITE(6,'(A,F10.1)') 'CPU / SEC = ',CPUE-CPUS

   RETURN
END SUBROUTINE



SUBROUTINE CONTRACT_GC_XC(K_C,N,Q0X,Q0Y,Q0Z)
! NUMERICALLY CALCULATE THE EXCHANGE-CORRELATION INTEGRALS FOR GRADIENT CORRECTED FUNCTIONALS.

   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE DFT

   IMPLICIT NONE
   INTEGER :: N,Q0X,Q0Y,Q0Z
   INTEGER :: I,J,K,L,Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,Q3X,Q3Y,Q3Z
   DOUBLE PRECISION,ALLOCATABLE :: CGS(:,:,:,:)
   INTEGER,ALLOCATABLE :: C2P(:) ! 'C2P' IS WORKING ARRAY FOR CGS_GRID
   DOUBLE PRECISION,ALLOCATABLE :: CGSDX(:,:,:,:),CGSDY(:,:,:,:),CGSDZ(:,:,:,:),CGSDA(:,:,:,:)
   DOUBLE COMPLEX :: AX,AY,AZ,B,CX,CY,CZ,D,EX,EY,EZ,FX,FY,FZ,G
   DOUBLE COMPLEX :: K_C(N,N,-Q0X:Q0X,-Q0Y:Q0Y,-Q0Z:Q0Z)
!  REAL :: CPUS,CPUE

!  CALL PCPU_TIME(CPUS)

   ALLOCATE(CGS(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z),C2P(NPGS))
   ALLOCATE(CGSDX(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDY(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDZ(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))
   ALLOCATE(CGSDA(NCGS,-REDUCED_CEL1X:REDUCED_CEL1X,-REDUCED_CEL1Y:REDUCED_CEL1Y,-REDUCED_CEL1Z:REDUCED_CEL1Z))

   IF (HELIX /= 0.0D0) CALL PABORT('HELICAL POLYMERS NOT YET IMPLEMENTED')

   ! DO NOT ZERO SCRATCH K_C
   DO I=1,NATOM
    DO J=1,NGRID(I)
     CALL CGS_GRID(CGS,C2P,NCGS,REDUCED_CEL1X,REDUCED_CEL1Y,REDUCED_CEL1Z,NPGS,J,I)
     CALL CGS_DERIVATIVE_GRID(CGSDX,CGSDY,CGSDZ,CGSDA,C2P,NCGS,REDUCED_CEL1X,REDUCED_CEL1Y,REDUCED_CEL1Z,NPGS,J,I)
     AX=GRIDW(J,I)*2.0D0*D1F2(J,I)*TRHO_GX(J,I)
     AY=GRIDW(J,I)*2.0D0*D1F2(J,I)*TRHO_GY(J,I)
     AZ=GRIDW(J,I)*2.0D0*D1F2(J,I)*TRHO_GZ(J,I)
     B =GRIDW(J,I)*D2F1(J,I)*TRHO(J,I)
     CX=GRIDW(J,I)*D2F2(J,I)*(RHO_GX(J,I)*TRHO_GX(J,I)+RHO_GY(J,I)*TRHO_GY(J,I)+RHO_GZ(J,I)*TRHO_GZ(J,I))*RHO_GX(J,I)
     CY=GRIDW(J,I)*D2F2(J,I)*(RHO_GX(J,I)*TRHO_GX(J,I)+RHO_GY(J,I)*TRHO_GY(J,I)+RHO_GZ(J,I)*TRHO_GZ(J,I))*RHO_GY(J,I)
     CZ=GRIDW(J,I)*D2F2(J,I)*(RHO_GX(J,I)*TRHO_GX(J,I)+RHO_GY(J,I)*TRHO_GY(J,I)+RHO_GZ(J,I)*TRHO_GZ(J,I))*RHO_GZ(J,I)
     D =GRIDW(J,I)*D2F3(J,I)*(RHO_GX(J,I)*TRHO_GX(J,I)+RHO_GY(J,I)*TRHO_GY(J,I)+RHO_GZ(J,I)*TRHO_GZ(J,I))
     EX=GRIDW(J,I)*D2F3(J,I)*RHO_GX(J,I)*TRHO(J,I)
     EY=GRIDW(J,I)*D2F3(J,I)*RHO_GY(J,I)*TRHO(J,I)
     EZ=GRIDW(J,I)*D2F3(J,I)*RHO_GZ(J,I)*TRHO(J,I)
     DO Q1X=-REDUCED_CEL1X,REDUCED_CEL1X
     DO Q1Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
     DO Q1Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
      DO Q2X=-REDUCED_CEL1X,REDUCED_CEL1X
      DO Q2Y=-REDUCED_CEL1Y,REDUCED_CEL1Y
      DO Q2Z=-REDUCED_CEL1Z,REDUCED_CEL1Z
       Q3X=Q1X+Q2X
       Q3Y=Q1Y+Q2Y
       Q3Z=Q1Z+Q2Z
       IF ((Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC).AND. &
           (Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC).AND. &
           (Q3X <= REDUCED_CEL1X_XC).AND.(Q3X >= -REDUCED_CEL1X_XC)) THEN
        DO K=1,NCGS
         DO L=1,NCGS
          FX=CGS(K,Q1X,Q1Y,Q1Z)*CGSDX(L,Q3X,Q3Y,Q3Z)+CGSDX(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)
          FY=CGS(K,Q1X,Q1Y,Q1Z)*CGSDY(L,Q3X,Q3Y,Q3Z)+CGSDY(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)
          FZ=CGS(K,Q1X,Q1Y,Q1Z)*CGSDZ(L,Q3X,Q3Y,Q3Z)+CGSDZ(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)
          G =CGS(K,Q1X,Q1Y,Q1Z)*CGS(L,Q3X,Q3Y,Q3Z)
          K_C(K,L,Q2X,Q2Y,Q2Z)=K_C(K,L,Q2X,Q2Y,Q2Z)+AX*FX+AY*FY+AZ*FZ+B*G+CX*FX+CY*FY+CZ*FZ+D*G+EX*FX+EY*FY+EZ*FZ
         ENDDO
        ENDDO
       ENDIF
      ENDDO
      ENDDO
      ENDDO
     ENDDO
     ENDDO
     ENDDO
    ENDDO
   ENDDO

   DEALLOCATE(CGS,C2P)
   DEALLOCATE(CGSDX,CGSDY,CGSDZ,CGSDA)
!  CALL PCPU_TIME(CPUE)
!  WRITE(6,'(A,F10.1)') 'CPU / SEC = ',CPUE-CPUS

   RETURN
END SUBROUTINE



SUBROUTINE SLATER_EXCHANGE(IFLAG)
! CALCULATE SLATER LOCAL EXCHANGE POTENTIAL.
! SEE B.G.JOHNSON,P.M.W.GILL,AND J.A.POPLE,J.CHEM.PHYS.98,5612(1993).
! IFLAG = 0 : EXCHANGE CORRELATION ENERGY & POTENTIAL FOR SCF-KS.
! IFLAG = 1 : SINGLET EXCITATION ENERGY CALCULATIONS.
! IFLAG = 2 : TRIPLET EXCITATION ENERGY CALCULATIONS.

   USE CONTROL
   USE CONSTANTS
   USE STRUCTURE
   USE DFT
    
   IMPLICIT NONE
   INTEGER :: IFLAG
   INTEGER :: I,J
   DOUBLE PRECISION,PARAMETER :: ALPHA = 0.6666666666666667D+00
   DOUBLE PRECISION :: A,B,C,D
   DOUBLE PRECISION :: COEF

   IF (IFLAG == 0) THEN
    COEF=DOPTN(20)
   ELSE
    COEF=DOPTN(77)
   ENDIF

   B=-3.0D0*ALPHA*DEXP(DLOG(3.0D0/(4.0D0*PI))/3.0D0)
   C=-9.0D0/2.0D0*ALPHA*DEXP(DLOG(3.0D0/(4.0D0*PI))/3.0D0)
   D=-ALPHA*DEXP(DLOG(3.0D0/(4.0D0*PI))/3.0D0)
   DO I=1,NATOM
    DO J=1,NGRID(I)
     IF (RHO(J,I) /= 0.0D0) THEN
      A=DLOG(RHO(J,I)/2.0D0)
      D1F1(J,I)=D1F1(J,I)+COEF*B*DEXP(A/3.0D0)
      XCF(J,I)=XCF(J,I)+COEF*C*DEXP(4.0D0/3.0D0*A)
      IF (IFLAG > 0) D2F1(J,I)=D2F1(J,I)+COEF*D*DEXP(-2.0D0/3.0D0*A)
     ENDIF
    ENDDO
   ENDDO
   RETURN
END SUBROUTINE
 
 
 
SUBROUTINE VOSKO_WILK_NUSAIR_CORRELATION(IFLAG)
! CALCULATE VOSKO-WILK-NUSAIR LOCAL CORRELATION POTENTIAL.
! SEE B.G.JOHNSON,P.M.W.GILL,AND J.A.POPLE,J.CHEM.PHYS.98,5612(1993).
! SEE S.H.VOSKO,L.WILK,AND M.NUSAIR,CAN.J.PHYS.58,1200(1980).
! IFLAG = 0 : EXCHANGE CORRELATION ENERGY & POTENTIAL FOR SCF-KS.
! IFLAG = 1 : SINGLET EXCITATION ENERGY CALCULATIONS.
! IFLAG = 2 : TRIPLET EXCITATION ENERGY CALCULATIONS.

   USE CONTROL
   USE CONSTANTS
   USE STRUCTURE
   USE DFT
 
   IMPLICIT NONE
   INTEGER :: IFLAG
   INTEGER :: I,J
!  DOUBLE PRECISION,PARAMETER :: AP=0.0310907D0
!  DOUBLE PRECISION,PARAMETER :: BP=13.0720D0
!  DOUBLE PRECISION,PARAMETER :: CP=42.7198D0
!  DOUBLE PRECISION,PARAMETER :: X0P=-0.409286D0
   DOUBLE PRECISION,PARAMETER :: AP=0.0310907D0,AF=0.01554535D0,AA=-0.01688686D0
   DOUBLE PRECISION,PARAMETER :: BP=3.72744D0,BF=7.06042D0,BA=1.13107D0
   DOUBLE PRECISION,PARAMETER :: CP=12.9352D0,CF=18.0578D0,CA=13.0045D0
   DOUBLE PRECISION,PARAMETER :: X0P=-0.10498D0,X0F=-32.5D0,X0A=-0.0047584D0
   DOUBLE PRECISION :: X,LXP,LX0P,QP,LXA,LX0A,QA,EP,EA,EPP,EPPP
   DOUBLE PRECISION :: COEF

   IF (IFLAG == 0) THEN
    COEF=DOPTN(21)
   ELSE
    COEF=DOPTN(78)
   ENDIF

   QP=DSQRT(4.0D0*CP-BP*BP)
   LX0P=X0P*X0P+BP*X0P+CP
   QA=DSQRT(4.0D0*CA-BA*BA)
   LX0A=X0A*X0A+BA*X0A+CA
   DO I=1,NATOM
    DO J=1,NGRID(I)
     IF (RHO(J,I) /= 0.0D0) THEN
      X=DEXP(DLOG(3.0D0/(4.0D0*PI*RHO(J,I)))/6.0D0)
      LXP=X*X+BP*X+CP
      LXA=X*X+BA*X+CA
      D1F1(J,I)=D1F1(J,I)+COEF*(AP*(DLOG(X*X/LXP)+2.0D0*BP/QP*DATAN(QP/(2.0D0*X+BP)) &
               -BP*X0P/LX0P*(DLOG((X-X0P)**2/LXP)+2.0D0*(2.0D0*X0P+BP)/QP*DATAN(QP/(2.0D0*X+BP)))) &
               -X/6.0D0*AP*(2.0D0/X-(2.0D0*X+BP)/LXP-4.0D0*BP/((2.0D0*X+BP)**2+QP**2) &
               -BP*X0P/LX0P*(2.0D0/(X-X0P)-(2.0D0*X+BP)/LXP-4.0D0*(2.0D0*X0P+BP)/((2.0D0*X+BP)**2+QP**2))))
      XCF(J,I)=XCF(J,I)+COEF*(RHO(J,I)*AP*(DLOG(X*X/LXP)+2.0D0*BP/QP*DATAN(QP/(2.0D0*X+BP)) &
              -BP*X0P/LX0P*(DLOG((X-X0P)**2/LXP)+2.0D0*(2.0D0*X0P+BP)/QP*DATAN(QP/(2.0D0*X+BP)))) )
      IF (IFLAG > 0) THEN
       EP=AP*(DLOG(X*X/LXP)+2.0D0*BP/QP*DATAN(QP/(2.0D0*X+BP)) &
         -BP*X0P/LX0P*(DLOG((X-X0P)**2/LXP)+2.0D0*(2.0D0*X0P+BP)/QP*DATAN(QP/(2.0D0*X+BP))))
       EA=AA*(DLOG(X*X/LXA)+2.0D0*BA/QA*DATAN(QA/(2.0D0*X+BA)) &
         -BA*X0A/LX0A*(DLOG((X-X0A)**2/LXA)+2.0D0*(2.0D0*X0A+BA)/QA*DATAN(QA/(2.0D0*X+BA))))
       EPP=AP*(2.0D0/X-(2.0D0*X+BP)/LXP-4.0D0*BP/((2.0D0*X+BP)**2+QP**2) &
         -BP*X0P/LX0P*(2.0D0/(X-X0P)-(2.0D0*X+BP)/LXP-4.0D0*(2.0D0*X0P+BP)/((2.0D0*X+BP)**2+QP**2)))
       EPPP=AP*(-2.0D0/X**2-(2.0D0*LXP-(2.0D0*X+BP)**2)/LXP**2 &
         +16.0D0*BP*(2.0D0*X+BP)/((2.0D0*X+BP)**2+QP**2)**2 &
           -BP*X0P/LX0P*(-2.0D0/(X-X0P)**2-(2.0D0*LXP-(2.0D0*X+BP)**2)/LXP**2 &
         +16.0D0*(2.0D0*X0P+BP)*(2.0D0*X+BP)/((2.0D0*X+BP)**2+QP**2)**2))
       IF (IFLAG == 1) D2F1(J,I)=D2F1(J,I)+COEF*(-5.0D0*EPP+X*EPPP)*X/RHO(J,I)/18.0D0
       IF (IFLAG == 2) D2F1(J,I)=D2F1(J,I)+COEF*2.0D0/RHO(J,I)*EA
      ENDIF
     ENDIF
    ENDDO
   ENDDO
   RETURN
END SUBROUTINE
 
 
 
SUBROUTINE BECKE88_EXCHANGE(IFLAG)
! CALCULATE BECKE88 GRADIENT-CORRECTED EXCHANGE POTENTIAL.
! SEE B.G.JOHNSON,P.M.W.GILL,AND J.A.POPLE,J.CHEM.PHYS.98,5612(1993).
! SEE A.D.BECKE,PHYS.REV.A,38,3098(1988).
! IFLAG = 0 : EXCHANGE CORRELATION ENERGY & POTENTIAL FOR SCF-KS.
! IFLAG = 1 : SINGLET EXCITATION ENERGY CALCULATIONS.
! IFLAG = 2 : TRIPLET EXCITATION ENERGY CALCULATIONS.

   USE CONTROL
   USE CONSTANTS
   USE STRUCTURE
   USE DFT
 
   IMPLICIT NONE
   INTEGER :: IFLAG
   INTEGER :: I,J
   DOUBLE PRECISION,PARAMETER :: B=0.42D-02
   DOUBLE PRECISION :: C,G,GP,GPP,SH,X
   DOUBLE PRECISION :: COEF

   IF (IFLAG == 0) THEN
    COEF=DOPTN(22)
   ELSE
    COEF=DOPTN(79)
   ENDIF
 
   C=-1.5D0*DEXP(DLOG(3.0D0/(4.0D0*PI))/3.0D0)
   DO I=1,NATOM
    DO J=1,NGRID(I)
     IF (RHO(J,I) /= 0.0D0) THEN
      X=DSQRT(GAMMA(J,I)/4.0D0)/DEXP(4.0D0/3.0D0*DLOG(RHO(J,I)/2.0D0))
      SH=DLOG(X+DSQRT(X*X+1.0D0))
      G=C-B*X*X/(1.0D0+6.0D0*B*X*SH)
      GP=(6.0D0*B*B*X*X*(X/DSQRT(X*X+1.0D0)-SH)-2.0D0*B*X)/(1.0D0+6.0D0*B*X*SH)**2
      GPP=(12.0D0*B*B*X*(X/DSQRT(X*X+1.0D0)-SH)-6.0D0*B*B*X**4/DSQRT((X*X+1.0D0)**3)-2.0D0*B)/(1.0D0+6.0D0*B*X*SH)**2 &
         -(12.0D0*B*SH+12.0D0*B*X/DSQRT(X*X+1.0D0))*(6.0D0*B*B*X*X*(X/DSQRT(X*X+1.0D0)-SH)-2.0D0*B*X)/(1.0D0+6.0D0*B*X*SH)**3
      D1F1(J,I)=D1F1(J,I)+COEF*4.0D0/3.0D0*DEXP(DLOG(RHO(J,I)/2.0D0)/3.0D0)*(G-X*GP)
      XCF(J,I)=XCF(J,I)+COEF*DEXP(4.0D0/3.0D0*DLOG(RHO(J,I)/2.0D0))*2.0D0*G
      IF (IFLAG > 0) D2F1(J,I)=D2F1(J,I)+COEF*4.0D0/9.0D0*DEXP(-2.0D0/3.0D0*DLOG(RHO(J,I)/2.0D0))*(G-X*GP+4.0D0*X*X*GPP)
     ENDIF
     IF (GAMMA(J,I) /= 0.0D0) THEN
      X=DSQRT(GAMMA(J,I)/4.0D0)/DEXP(4.0D0/3.0D0*DLOG(RHO(J,I)/2.0D0))
      SH=DLOG(X+DSQRT(X*X+1.0D0))
      G=C-B*X*X/(1.0D0+6.0D0*B*X*SH)
      GP=(6.0D0*B*B*X*X*(X/DSQRT(X*X+1.0D0)-SH)-2.0D0*B*X)/(1.0D0+6.0D0*B*X*SH)**2
      GPP=(12.0D0*B*B*X*(X/DSQRT(X*X+1.0D0)-SH)-6.0D0*B*B*X**4/DSQRT((X*X+1.0D0)**3)-2.0D0*B)/(1.0D0+6.0D0*B*X*SH)**2 &
         -(12.0D0*B*SH+12.0D0*B*X/DSQRT(X*X+1.0D0))*(6.0D0*B*B*X*X*(X/DSQRT(X*X+1.0D0)-SH)-2.0D0*B*X)/(1.0D0+6.0D0*B*X*SH)**3
      D1F2(J,I)=D1F2(J,I)+COEF*0.5D0*GP/DSQRT(GAMMA(J,I)/4.0D0)
      IF (IFLAG > 0) THEN
       D2F2(J,I)=D2F2(J,I)+COEF*((-0.25D0)/DSQRT(GAMMA(J,I)/4.0D0)**3*GP &
         +0.25D0/(GAMMA(J,I)/4.0D0)*DEXP(-4.0D0/3.0D0*DLOG(RHO(J,I)/2.0D0))*GPP)
       D2F3(J,I)=D2F3(J,I)+COEF*(-2.0D0/3.0D0)/DSQRT(GAMMA(J,I)/4.0D0)/(RHO(J,I)/2.0D0)*X*GPP
      ENDIF
     ENDIF
    ENDDO
   ENDDO
   RETURN
END SUBROUTINE
 
 
 
SUBROUTINE LEE_YANG_PARR_CORRELATION(IFLAG)
! CALCULATE LEE-YANG-PARR GRADIENT-CORRECTED CORRELATION POTENTIAL.
! SEE B.G.JOHNSON,P.M.W.GILL,AND J.A.POPLE,J.CHEM.PHYS.98,5612(1993).
! SEE C.LEE,W.YANG,AND R.G.PARR,PHYS.REV.B,37,785(1988).
! SEE B.MIEHLICH,A.SAVIN,H.STOLL,AND H.PREUSS,CHEM.PHYS.LETT.157,200(1989).
! IFLAG = 0 : EXCHANGE CORRELATION ENERGY & POTENTIAL FOR SCF-KS.
! IFLAG = 1 : SINGLET EXCITATION ENERGY CALCULATIONS.
! IFLAG = 2 : TRIPLET EXCITATION ENERGY CALCULATIONS.

   USE CONTROL
   USE CONSTANTS
   USE STRUCTURE
   USE DFT
 
   IMPLICIT NONE
   INTEGER :: IFLAG
   INTEGER :: I,J
   DOUBLE PRECISION,PARAMETER :: A=0.04918D0
   DOUBLE PRECISION,PARAMETER :: B=0.132D0
   DOUBLE PRECISION,PARAMETER :: C=0.2533D0
   DOUBLE PRECISION,PARAMETER :: D=0.349D0
   DOUBLE PRECISION OMEGA,DELTA,OMEGAP,DELTAP,OMEGAPP,DELTAPP
   DOUBLE PRECISION AA,AB,AAA,AAB,ABB,AAAA,AAAB,AABB,BAAA,BAAB,BABB,DFRARA,DFRARB
   DOUBLE PRECISION :: COEF

   IF (IFLAG == 0) THEN
    COEF=DOPTN(23)
   ELSE
    COEF=DOPTN(80)
   ENDIF
 
   DO I=1,NATOM
    DO J=1,NGRID(I)
     IF (RHO(J,I) > 1.0D-15) THEN
      OMEGA=DEXP(-C*RHO(J,I)**(-1.0D0/3.0D0))/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0))*DEXP(-DLOG(RHO(J,I))*11.0D0/3.0D0)
      IF (OMEGA < 1.0D-15) OMEGA=0.0D0
      DELTA=D*RHO(J,I)**(-1.0D0/3.0D0)/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0))+C*RHO(J,I)**(-1.0D0/3.0D0)
      AA=-A*B*OMEGA*(1.0D0/9.0D0*(RHO(J,I)/2.0D0)**2*(1.0D0-3.0D0*DELTA-(DELTA-11.0D0)/2.0D0)-(RHO(J,I)/2.0D0)**2)
      AB=-A*B*OMEGA*(1.0D0/9.0D0*(RHO(J,I)/2.0D0)**2*(47.0D0-7.0D0*DELTA)-4.0D0/3.0D0*RHO(J,I)**2)
      IF ((IFLAG == 0).OR.(IFLAG == 1)) THEN
       D1F2(J,I)=D1F2(J,I)+COEF*(AA+0.5D0*AB)
      ELSE IF (IFLAG == 2) THEN
       D1F2(J,I)=D1F2(J,I)+COEF*(AA-0.5D0*AB)
      ENDIF
      OMEGAP=-1.0D0/3.0D0*RHO(J,I)**(-1.0D0/3.0D0)/RHO(J,I)*OMEGA*(11.0D0*DEXP(DLOG(RHO(J,I))/3.0D0) &
         -C-D/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)))
      DELTAP=1.0D0/3.0D0*(D*D*DEXP(-5.0D0/3.0D0*DLOG(RHO(J,I)))/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0))**2-DELTA/RHO(J,I))
      IF (OMEGA == 0.0D0) THEN
       AAA=0.0D0
       AAB=0.0D0
       ABB=0.0D0
      ELSE
       AAA=OMEGAP/OMEGA*AA-A*B*OMEGA*(1.0D0/9.0D0*RHO(J,I)/2.0D0*(1.0D0-3.0D0*DELTA-(DELTA-11.0D0)/2.0D0) &
          -1.0D0/9.0D0*(RHO(J,I)/2.0D0)**2*(3.5D0*DELTAP+(DELTA-11.0D0)/2.0D0/RHO(J,I)))
       AAB=OMEGAP/OMEGA*AB-A*B*OMEGA*(1.0D0/9.0D0*(RHO(J,I)/2.0D0)*(47.0D0-7.0D0*DELTA) &
          -7.0D0/9.0D0*(RHO(J,I)/2.0D0)**2*DELTAP-8.0D0/3.0D0*RHO(J,I))
       ABB=OMEGAP/OMEGA*AA-A*B*OMEGA*(1.0D0/9.0D0*RHO(J,I)/2.0D0*(1.0D0-3.0D0*DELTA-(DELTA-11.0D0)/2.0D0) &
          -1.0D0/9.0D0*(RHO(J,I)/2.0D0)**2*(3.5D0*DELTAP-(DELTA-11.0D0)/2.0D0/RHO(J,I))-RHO(J,I))
      ENDIF
      IF (IFLAG == 1) THEN
       D2F3(J,I)=D2F3(J,I)+COEF*(AAA+ABB+AAB)
      ELSE IF (IFLAG == 2) THEN
       D2F3(J,I)=D2F3(J,I)+COEF*(AAA-ABB)
      ENDIF
      D1F1(J,I)=D1F1(J,I)+COEF*(-4.0D0*A/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0))* &
         RHO(J,I)/4.0D0*(1.0D0/3.0D0*D*RHO(J,I)**(-1.0D0/3.0D0) &
         /RHO(J,I)/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0))+2.0D0/RHO(J,I)-1.0D0/RHO(J,I)) &
         -DEXP(11.0D0/3.0D0*DLOG(2.0D0))*3.0D0/10.0D0 &
         *DEXP(2.0D0/3.0D0*DLOG(3.0D0*PI*PI))*A*B*(OMEGAP*(RHO(J,I)/2.0D0)**2*2.0D0*DEXP(8.0D0/3.0D0*DLOG(RHO(J,I)/2.0D0)) &
         +OMEGA*RHO(J,I)/2.0D0*14.0D0/3.0D0*DEXP(8.0D0/3.0D0*DLOG(RHO(J,I)/2.0D0)))+(AAA+AAB+ABB)*GAMMA(J,I)/4.0D0)
      XCF(J,I)=XCF(J,I)+COEF*(-4.0D0*A/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0))*RHO(J,I)/4.0D0 &
         -DEXP(11.0D0/3.0D0*DLOG(2.0D0))*3.0D0/10.0D0 &
         *DEXP(2.0D0/3.0D0*DLOG(3.0D0*PI*PI))*A*B*OMEGA*(RHO(J,I)/2.0D0)**2*2.0D0 &
         *DEXP(8.0D0/3.0D0*DLOG(RHO(J,I)/2.0D0))+(AA+AA+AB)*GAMMA(J,I)/4.0D0)
      IF (IFLAG > 0) THEN
       OMEGAPP=(4.0D0/9.0D0*RHO(J,I)**(-7.0D0/3.0D0)*OMEGA-1.0D0/3.0D0 &
         *RHO(J,I)**(-4.0D0/3.0D0)*OMEGAP)*(11.0D0*RHO(J,I)**(1.0D0/3.0D0) &
         -C-D/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)))-1.0D0/3.0D0 &
         *RHO(J,I)**(-4.0D0/3.0D0)*OMEGA*(11.0D0/3.0D0*RHO(J,I)**(-2.0D0/3.0D0) &
         -D**2/3.0D0*RHO(J,I)**(-4.0D0/3.0D0)/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0))**2)
       DELTAPP=(-5.0D0/9.0D0)*D*D*RHO(J,I)**(-8.0D0/3.0D0)/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0))**2+(2.0D0/9.0D0)*(D/RHO(J,I) &
              /(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)))**3+DELTA/3.0D0/RHO(J,I)**2-DELTAP/3.0D0/RHO(J,I)
       IF (OMEGA == 0.0D0) THEN
        AAAA=0.0D0
        AAAB=0.0D0
        AABB=0.0D0
        BAAA=0.0D0
        BAAB=0.0D0
        BABB=0.0D0
       ELSE
        AAAA=(OMEGAPP*OMEGA-OMEGAP**2)/OMEGA**2*AA+OMEGAP/OMEGA*AAA-A*B &
            *OMEGAP*((RHO(J,I)*0.5D0)/9.0D0*(1.0D0-3.0D0*DELTA-(DELTA-11.0D0)*0.5D0) &
            -(RHO(J,I)*0.5D0)**2/9.0D0*(3.5D0*DELTAP+(DELTA-11.0D0)*0.5D0/RHO(J,I))) &
            -A*B*OMEGA*((RHO(J,I)*0.5D0)/9.0D0*(-3.5D0*DELTAP &
            -(DELTA-11.0D0)*0.5D0/RHO(J,I))-(RHO(J,I)*0.5D0)/9.0D0*(3.5D0*DELTAP &
            +(DELTA-11.0D0)*0.5D0/RHO(J,I))-(RHO(J,I)*0.5D0)**2/9.0D0 &
            *(1.0D0/RHO(J,I)*DELTAP+3.5D0*DELTAPP-(DELTA-11.0D0)/RHO(J,I)**2))
        AAAB=(OMEGAPP*OMEGA-OMEGAP**2)/OMEGA**2*AB+OMEGAP/OMEGA*AAB-A*B &
            *OMEGAP*((RHO(J,I)*0.5D0)/9.0D0*(47.0D0-7.0D0*DELTA)-7.0D0/9.0D0 &
            *(RHO(J,I)*0.5D0)**2*DELTAP-8.0D0/3.0D0*RHO(J,I))-A*B &
            *OMEGA*(-14.0D0/9.0D0*(RHO(J,I)*0.5D0)*DELTAP-7.0D0/9.0D0 &
            *(RHO(J,I)*0.5D0)**2*DELTAPP-8.0D0/3.0D0)
        AABB=(OMEGAPP*OMEGA-OMEGAP**2)/OMEGA**2*AA+OMEGAP/OMEGA*ABB-A*B &
            *OMEGAP*((RHO(J,I)*0.5D0)/9.0D0*(1.0D0-3.0D0*DELTA-(DELTA-11.0D0)*0.5D0) &
            -(RHO(J,I)*0.5D0)**2/9.0D0*(3.5D0*DELTAP-(DELTA-11.0D0)*0.5D0/RHO(J,I))-RHO(J,I))-A*B &
            *OMEGA*((RHO(J,I)*0.5D0)/9.0D0*(-3.5D0*DELTAP &
            +(DELTA-11.0D0)*0.5D0/RHO(J,I))-(RHO(J,I)*0.5D0)/9.0D0*(3.5D0 &
            *DELTAP-(DELTA-11.0D0)*0.5D0/RHO(J,I))-(RHO(J,I)*0.5D0)**2/9.0D0 &
            *(-1.0D0/RHO(J,I)*DELTAP+3.5D0*DELTAPP+(DELTA-11.0D0)/RHO(J,I)**2)-2.0D0)
        BAAA=(OMEGAPP*OMEGA-OMEGAP**2)/OMEGA**2*AA+OMEGAP/OMEGA*ABB-A*B &
            *OMEGAP*((RHO(J,I)*0.5D0)/9.0D0*(1.0D0-3.0D0*DELTA-(DELTA-11.0D0)*0.5D0) &
            -(RHO(J,I)*0.5D0)**2/9.0D0*(3.5D0*DELTAP+(DELTA-11.0D0)*0.5D0/RHO(J,I)))-A*B &
            *OMEGA*(1.0D0/9.0D0*(6.5D0-3.5D0*DELTA)+(RHO(J,I)*0.5D0) &
            /9.0D0*(-3.5D0*DELTAP+(DELTA-11.0D0)*0.5D0/RHO(J,I)) &
            -(RHO(J,I)*0.5D0)/9.0D0*(3.5D0*DELTAP+(DELTA-11.0D0)*0.5D0/RHO(J,I)) &
            -(RHO(J,I)*0.5D0)**2/9.0D0*3.5D0*DELTAPP)
        BAAB=(OMEGAPP*OMEGA-OMEGAP**2)/OMEGA**2*AB+OMEGAP/OMEGA*AAB-A*B &
            *OMEGAP*((RHO(J,I)*0.5D0)/9.0D0*(47.0D0-7.0D0*DELTA)-7.0D0/9.0D0 &
            *(RHO(J,I)*0.5D0)**2*DELTAP-8.0D0/3.0D0*RHO(J,I))-A*B &
            *OMEGA*(1.0D0/9.0D0*(47.0D0-7.0D0*DELTA)-14.0D0/9.0D0*(RHO(J,I)*0.5D0) &
            *DELTAP-7.0D0/9.0D0*(RHO(J,I)*0.5D0)**2*DELTAPP-8.0D0/3.0D0)
        BABB=(OMEGAPP*OMEGA-OMEGAP**2)/OMEGA**2*AA+OMEGAP/OMEGA*AAA-A*B &
            *OMEGAP*((RHO(J,I)*0.5D0)/9.0D0*(1.0D0-3.0D0*DELTA-(DELTA-11.0D0)*0.5D0) &
            -(RHO(J,I)*0.5D0)**2/9.0D0*(3.5D0*DELTAP-(DELTA-11.0D0)*0.5D0/RHO(J,I))-RHO(J,I)) &
            -A*B*OMEGA*(1.0D0/9.0D0*(6.5D0-3.5D0*DELTA) &
            +(RHO(J,I)*0.5D0)/9.0D0*(-3.5D0*DELTAP-(DELTA-11.0D0)*0.5D0/RHO(J,I)) &
            -(RHO(J,I)*0.5D0)/9.0D0*(3.5D0*DELTAP-(DELTA-11.0D0) &
            *0.5D0/RHO(J,I))-(RHO(J,I)*0.5D0)**2/9.0D0*3.5D0*DELTAPP)
       ENDIF
       DFRARA=-4.0D0*A/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0))*(D/3.0D0*RHO(J,I)**(-4.0D0/3.0D0)/ &
            (1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)))*RHO(J,I) &
             /4.0D0*(1.0D0/3.0D0*D*RHO(J,I)**(-4.0D0/3.0D0)/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)) &
            +1.0D0/RHO(J,I))-4.0D0*A/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)) &
             *0.25D0*(1.0D0/3.0D0*D*RHO(J,I)**(-4.0D0/3.0D0)/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)) &
            +1.0D0/RHO(J,I))-4.0D0*A/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)) &
             *RHO(J,I)/4.0D0*(1.0D0/3.0D0*D/3.0D0*RHO(J,I)**(-4.0D0/3.0D0)/(1.0D0 &
            +D*RHO(J,I)**(-1.0D0/3.0D0))**2*D*RHO(J,I)**(-4.0D0/3.0D0)+1.0D0/3.0D0*D &
             /(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0))*(-4.0D0/3.0D0)*RHO(J,I)**(-7.0D0/3.0D0) &
            -3.0D0/RHO(J,I)**2)-2.0D0**(11.0D0/3.0D0)*0.3D0 &
             *(3.0D0*PI*PI)**(2.0D0/3.0D0)*A*B*(OMEGAPP*(RHO(J,I)*0.5D0)*(RHO(J,I)*0.5D0) &
            *(2.0D0*(RHO(J,I)*0.5D0)**(8.0D0/3.0D0))+2.0D0*OMEGAP &
             *(11.0D0/3.0D0*(RHO(J,I)*0.5D0)**(11.0D0/3.0D0)+(RHO(J,I)*0.5D0)**(11.0D0/3.0D0)) &
            +OMEGA*(88.0D0/9.0D0*(RHO(J,I)*0.5D0)**(8.0D0/3.0D0))) &
             +(AAAA+AAAB+AABB)*GAMMA(J,I)/4.0D0
       DFRARB=-4.0D0*A/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0))*(D/3.0D0*RHO(J,I)**(-4.0D0/3.0D0)/ &
            (1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)))*RHO(J,I) &
             /4.0D0*(1.0D0/3.0D0*D*RHO(J,I)**(-4.0D0/3.0D0)/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)) &
            +1.0D0/RHO(J,I))-4.0D0*A/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)) &
             *0.25D0*(1.0D0/3.0D0*D*RHO(J,I)**(-4.0D0/3.0D0)/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)) &
            +1.0D0/RHO(J,I))-4.0D0*A/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0)) &
             *RHO(J,I)/4.0D0*(1.0D0/3.0D0*D/3.0D0*RHO(J,I)**(-4.0D0/3.0D0)/(1.0D0 &
            +D*RHO(J,I)**(-1.0D0/3.0D0))**2*D*RHO(J,I)**(-4.0D0/3.0D0) &
             +1.0D0/3.0D0*D/(1.0D0+D*RHO(J,I)**(-1.0D0/3.0D0))*(-4.0D0/3.0D0) &
            *RHO(J,I)**(-7.0D0/3.0D0)+1.0D0/RHO(J,I)**2)-2.0D0**(11.0D0/3.0D0)*0.3D0 &
             *(3.0D0*PI*PI)**(2.0D0/3.0D0)*A*B*(OMEGAPP*(RHO(J,I)*0.5D0)*(RHO(J,I)*0.5D0) &
            *(2.0D0*(RHO(J,I)*0.5D0)**(8.0D0/3.0D0))+2.0D0*OMEGAP &
             *(11.0D0/3.0D0*(RHO(J,I)*0.5D0)**(11.0D0/3.0D0)+(RHO(J,I)*0.5D0)**(11.0D0/3.0D0)) &
            +OMEGA*(22.0D0/3.0D0*(RHO(J,I)*0.5D0)**(8.0D0/3.0D0))) &
             +(BAAA+BAAB+BABB)*GAMMA(J,I)/4.0D0
       IF (IFLAG == 1) THEN
        D2F1(J,I)=D2F1(J,I)+COEF*(DFRARA+DFRARB)
       ELSE IF (IFLAG == 2) THEN
        D2F1(J,I)=D2F1(J,I)+COEF*(DFRARA-DFRARB)
       ENDIF
      ENDIF
     ENDIF
    ENDDO
   ENDDO
   RETURN
END SUBROUTINE
