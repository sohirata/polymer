SUBROUTINE OPTIMIZATION
! OPTIMIZE THE GEOMETRICAL PARAMETERS BY SIMPLE RELAXATION AND GDIIS EXTRAPOLATION.

   USE MPI
   USE CONTROL
   USE STRUCTURE
   USE GRADIENT
   USE OPTIMIZE
   
   IMPLICIT NONE
   INTEGER :: IOPTCYCLE
   REAL :: ICPUS,ICPUE
   INTEGER :: I,J

   ! INITIALIZATION OF VARIABLES
   GDIISTERM=IOPTN(42)
   IF ((GDIISTERM < 0).OR.(GDIISTERM == 1)) CALL PABORT('INVALID GDIISTERM')
   RELAX1=1.0D0
   RELAX2=1.0D0
   RELAX3=1.0D0
   DO I=1,MAXOPTCYCLES
    DO J=1,256
     ATOMXHIST(J,I)=0.0D0
     ATOMXGRADHIST(J,I)=0.0D0
     ATOMYHIST(J,I)=0.0D0
     ATOMYGRADHIST(J,I)=0.0D0
     ATOMZHIST(J,I)=0.0D0
     ATOMZGRADHIST(J,I)=0.0D0
    ENDDO
    PERIODXHIST(I)=0.0D0
    PERIODXGRADHIST(I)=0.0D0
    PERIODYHIST(I)=0.0D0
    PERIODYGRADHIST(I)=0.0D0
    PERIODZHIST(I)=0.0D0
    PERIODZGRADHIST(I)=0.0D0
    HELIXHIST(I)=0.0D0
    HELIXGRADHIST(I)=0.0D0
   ENDDO
  
   ! OPTIMIZATION LOOP
   DO IOPTCYCLE=1,MAXOPTCYCLES
    CALL SINGLEPOINT
    IF (MYID == 0) WRITE(6,'(A)') 'OPTMSG: **************************************************************'
    CALL DUMPGEOMETRY(IOPTCYCLE)
    CALL GDONE(IOPTCYCLE)
    IF (GDIISTERM /= 0) THEN
     IF ((IOPTCYCLE == 1).OR.(MOD(IOPTCYCLE,GDIISTERM) /= 0)) THEN
      CALL GRELAX(IOPTCYCLE,GDIISTERM)
     ELSE
      CALL GDIIS(IOPTCYCLE,GDIISTERM)
     ENDIF
    ELSE
     CALL GRELAX(IOPTCYCLE,GDIISTERM)
    ENDIF
    IF (MYID == 0) WRITE(6,'(A)') 'OPTMSG: **************************************************************'
    CALL CHECK_GEOMETRY
   ENDDO
   CALL PABORT('OPTMSG: TOO MANY ITERATIONS IN GEOMETRY OPTIMIZATION')

   RETURN
END SUBROUTINE



SUBROUTINE DUMPGEOMETRY(IOPTCYCLE)
! PRINTS GEOMETRICAL PARAMETERS

   USE MPI
   USE CONSTANTS
   USE STRUCTURE
   USE GRADIENT
   USE INTEGRAL
   USE OPTIMIZE

   IMPLICIT NONE
   INTEGER :: IOPTCYCLE
   INTEGER :: I,J,K,L,M,N
   INTEGER :: PX,PY,PZ
   INTEGER :: QX,QY,QZ
   INTEGER :: RX,RY,RZ
   DOUBLE PRECISION :: A,R,R1,R2,R3,R4,R5,R6,X,Y
   DOUBLE PRECISION :: B1(3),B2(3),B3(3),N1(3),N2(3),M1(3)
   DOUBLE PRECISION,ALLOCATABLE :: NX(:,:,:,:),NY(:,:,:,:),NZ(:,:,:,:)
   
   IF (IOPTCYCLE < MAXOPTCYCLES) THEN
    IF (MYID == 0) WRITE(6,'(A,I3)') 'OPTMSG: OPTIMIZATION CYCLE',IOPTCYCLE
    DO I=1,NATOM
     IF (MYID == 0) WRITE(6,'(A,A2,3F20.15)') 'OPTMSG: ',CATOM(IATOM(I)),ATOMX(I),ATOMY(I),ATOMZ(I)
    ENDDO
    IF (CEL1X+CEL2X+CEL1Y+CEL2Y+CEL1Z+CEL2Z > 0) THEN
     IF (HELIX /= 0.0D0) THEN
      IF (MYID == 0) WRITE(6,'(A,F20.15)') 'OPTMSG: X PERIOD = ',PERIODX
      IF (MYID == 0) WRITE(6,'(A,F20.15,A)') 'OPTMSG: HELIX  = ',HELIX,' RADIAN'
      IF (MYID == 0) WRITE(6,'(A,F20.15,A)') 'OPTMSG: HELIX  = ',HELIX/PI*180.0D0,' DEGREES'
     ELSE
      IF (MYID == 0) WRITE(6,'(A,F20.15)') 'OPTMSG: X PERIOD = ',PERIODX
      IF (MYID == 0) WRITE(6,'(A,F20.15)') 'OPTMSG: Y PERIOD = ',PERIODY
      IF (MYID == 0) WRITE(6,'(A,F20.15)') 'OPTMSG: Z PERIOD = ',PERIODZ
     ENDIF
    ENDIF
    IF (MYID == 0) WRITE(6,'(A,F25.15)') 'OPTMSG: TOTAL ENERGY = ',EHFKS
    IF (MYID == 0) WRITE(6,'(A)') 'OPTMSG: RESIDUAL ENERGY GRADIENTS'
    DO I=1,NATOM
     IF (MYID == 0) WRITE(6,'(A,A2,3F20.15)') 'OPTMSG: ',CATOM(IATOM(I)),FORCEX(I),FORCEY(I),FORCEZ(I)
    ENDDO
    IF (HELIX /= 0.0D0) THEN
     IF (MYID == 0) WRITE(6,'(A,F20.15)') 'OPTMSG: X TRANSLATIONAL GRADIENT ',FORCETX
     IF (MYID == 0) WRITE(6,'(A,F20.15)') 'OPTMSG: X HELICAL GRADIENT ',FORCETA
    ELSE
     IF ((CEL1X+CEL2X > 0).AND.(MYID == 0)) WRITE(6,'(A,F20.15)') 'OPTMSG: X TRANSLATIONAL GRADIENT ',FORCETX
     IF ((CEL1Y+CEL2Y > 0).AND.(MYID == 0)) WRITE(6,'(A,F20.15)') 'OPTMSG: Y TRANSLATIONAL GRADIENT ',FORCETY
     IF ((CEL1Z+CEL2Z > 0).AND.(MYID == 0)) WRITE(6,'(A,F20.15)') 'OPTMSG: Z TRANSLATIONAL GRADIENT ',FORCETZ
    ENDIF
   ELSE
    ALLOCATE(NX(NATOM,-CEL1X:CEL1X,-CEL1Y:CEL1Y,-CEL1Z:CEL1Z))
    ALLOCATE(NY(NATOM,-CEL1X:CEL1X,-CEL1Y:CEL1Y,-CEL1Z:CEL1Z))
    ALLOCATE(NZ(NATOM,-CEL1X:CEL1X,-CEL1Y:CEL1Y,-CEL1Z:CEL1Z))
    DO QX=-CEL1X,CEL1X
    DO QY=-CEL1Y,CEL1Y
    DO QZ=-CEL1Z,CEL1Z
     DO I=1,NATOM
      NX(I,QX,QY,QZ)=ATOMX(I)+DFLOAT(QX)*PERIODX
      NY(I,QX,QY,QZ)=ATOMY(I)*DCOS(DFLOAT(QX)*HELIX)-ATOMZ(I)*DSIN(DFLOAT(QX)*HELIX)+DFLOAT(QY)*PERIODY
      NZ(I,QX,QY,QZ)=ATOMY(I)*DSIN(DFLOAT(QX)*HELIX)+ATOMZ(I)*DCOS(DFLOAT(QX)*HELIX)+DFLOAT(QZ)*PERIODZ
     ENDDO
    ENDDO
    ENDDO
    ENDDO
    IF (MYID == 0) WRITE(6,'(A)') 'OPTMSG: BOND LENGTHS'
    DO I=1,NATOM
     DO QX=-CEL1X,CEL1X
     DO QY=-CEL1Y,CEL1Y
     DO QZ=-CEL1Z,CEL1Z
      DO K=1,NATOM
       R=DSQRT((NX(I,0,0,0)-NX(K,QX,QY,QZ))**2+(NY(I,0,0,0)-NY(K,QX,QY,QZ))**2+(NZ(I,0,0,0)-NZ(K,QX,QY,QZ))**2)
       R=R*ANG_BOHR
       IF ((R /= 0.0D0).AND.(R < 3.0D0).AND.((K > I).OR.(QX+QY+QZ /= 0)).AND.(MYID == 0)) &
        WRITE(6,'(A,I3,A,I3,A,I0,A,I0,A,I0,A,F14.10,A)') 'OPTMSG: ',I,'(0,0,0)  -',K,'(',QX,',',QY,',',QZ,') = ',R,' ANGSTROM'
      ENDDO
     ENDDO
     ENDDO
     ENDDO
    ENDDO
    IF (MYID == 0) WRITE(6,'(A)') 'OPTMSG: BOND ANGLES'
    DO I=1,NATOM
     DO QX=-CEL1X,CEL1X
     DO QY=-CEL1Y,CEL1Y
     DO QZ=-CEL1Z,CEL1Z
      DO K=1,NATOM
       R1=DSQRT((NX(I,0,0,0)-NX(K,QX,QY,QZ))**2+(NY(I,0,0,0)-NY(K,QX,QY,QZ))**2+(NZ(I,0,0,0)-NZ(K,QX,QY,QZ))**2)
       IF ((R1 /= 0.0D0).AND.(R1 < 4.0D0)) THEN
        DO PX=-CEL1X,CEL1X
        DO PY=-CEL1Y,CEL1Y
        DO PZ=-CEL1Z,CEL1Z
         DO M=1,NATOM
          R2=DSQRT((NX(I,0,0,0)-NX(M,PX,PY,PZ))**2+(NY(I,0,0,0)-NY(M,PX,PY,PZ))**2+(NZ(I,0,0,0)-NZ(M,PX,PY,PZ))**2)
          R3=DSQRT((NX(M,PX,PY,PZ)-NX(K,QX,QY,QZ))**2+(NY(M,PX,PY,PZ)-NY(K,QX,QY,QZ))**2+(NZ(M,PX,PY,PZ)-NZ(K,QX,QY,QZ))**2)
          IF ((R2 /= 0.0D0).AND.(R3 /= 0.0D0).AND.(R2 < 4.0D0)) THEN
           A=DACOS((R1**2+R2**2-R3**2)/R1/R2*0.5D0)
           A=A*45.0D0/DATAN(1.0D0)
           IF (MYID == 0) WRITE(6,'(A,I3,A,I0,A,I0,A,I0,A,I3,A,I3,A,I0,A,I0,A,I0,A,F14.6,A)') &
            'OPTMSG: ',M,'(',PX,',',PY,',',PZ,')  -',I,'(0,0,0)  -',K,'(',QX,',',QY,',',QZ,') = ',A,' DEGREES'
          ENDIF
         ENDDO
        ENDDO
        ENDDO
        ENDDO
       ENDIF
      ENDDO
     ENDDO
     ENDDO
     ENDDO
    ENDDO
    IF (MYID == 0) WRITE(6,'(A)') 'OPTMSG: DIHEDRAL ANGLES'
    DO I=1,NATOM
     DO QX=-CEL1X,CEL1X
     DO QY=-CEL1Y,CEL1Y
     DO QZ=-CEL1Z,CEL1Z
      DO K=1,NATOM
       R1=DSQRT((NX(I,0,0,0)-NX(K,QX,QY,QZ))**2+(NY(I,0,0,0)-NY(K,QX,QY,QZ))**2+(NZ(I,0,0,0)-NZ(K,QX,QY,QZ))**2)
       IF ((R1 /= 0.0D0).AND.(R1 < 4.0D0)) THEN
        DO PX=-CEL1X,CEL1X
        DO PY=-CEL1Y,CEL1Y
        DO PZ=-CEL1Z,CEL1Z
         DO M=1,NATOM
          R2=DSQRT((NX(I,0,0,0)-NX(M,PX,PY,PZ))**2+(NY(I,0,0,0)-NY(M,PX,PY,PZ))**2+(NZ(I,0,0,0)-NZ(M,PX,PY,PZ))**2)
          R3=DSQRT((NX(M,PX,PY,PZ)-NX(K,QX,QY,QZ))**2+(NY(M,PX,PY,PZ)-NY(K,QX,QY,QZ))**2+(NZ(M,PX,PY,PZ)-NZ(K,QX,QY,QZ))**2)
          IF ((R2 /= 0.0D0).AND.(R3 /= 0.0D0).AND.(R2 < 4.0D0)) THEN
           DO RX=-CEL1X,CEL1X
           DO RY=-CEL1Y,CEL1Y
           DO RZ=-CEL1Z,CEL1Z
            DO N=1,NATOM
             R4=DSQRT((NX(N,RX,RY,RZ)-NX(M,PX,PY,PZ))**2+(NY(N,RX,RY,RZ)-NY(M,PX,PY,PZ))**2+(NZ(N,RX,RY,RZ)-NZ(M,PX,PY,PZ))**2)
             R5=DSQRT((NX(N,RX,RY,RZ)-NX(K,QX,QY,QZ))**2+(NY(N,RX,RY,RZ)-NY(K,QX,QY,QZ))**2+(NZ(N,RX,RY,RZ)-NZ(K,QX,QY,QZ))**2)
             R6=DSQRT((NX(N,RX,RY,RZ)-NX(I,0, 0, 0 ))**2+(NY(N,RX,RY,RZ)-NY(I,0, 0, 0 ))**2+(NZ(N,RX,RY,RZ)-NZ(I,0, 0, 0 ))**2)
             IF ((R4 /= 0.0D0).AND.(R5 /= 0.0D0).AND.(R6 /= 0.0D0).AND.(R4 < 4.0D0)) THEN
              B1(1)=(NX(I,0,0,0)-NX(K,QX,QY,QZ))/R1
              B1(2)=(NY(I,0,0,0)-NY(K,QX,QY,QZ))/R1
              B1(3)=(NZ(I,0,0,0)-NZ(K,QX,QY,QZ))/R1
              B2(1)=(NX(M,PX,PY,PZ)-NX(I,0,0,0))/R2
              B2(2)=(NY(M,PX,PY,PZ)-NY(I,0,0,0))/R2
              B2(3)=(NZ(M,PX,PY,PZ)-NZ(I,0,0,0))/R2
              B3(1)=(NX(N,RX,RY,RZ)-NX(M,PX,PY,PZ))/R4
              B3(2)=(NY(N,RX,RY,RZ)-NY(M,PX,PY,PZ))/R4
              B3(3)=(NZ(N,RX,RY,RZ)-NZ(M,PX,PY,PZ))/R4
              N1(1)=B1(2)*B2(3)-B1(3)*B2(2)
              N1(2)=B1(3)*B2(1)-B1(1)*B2(3)
              N1(3)=B1(1)*B2(2)-B1(2)*B2(1)
              N2(1)=B2(2)*B3(3)-B2(3)*B3(2)
              N2(2)=B2(3)*B3(1)-B2(1)*B3(3)
              N2(3)=B2(1)*B3(2)-B2(2)*B3(1)
              M1(1)=N1(2)*B2(3)-N1(3)*B2(2)
              M1(2)=N1(3)*B2(1)-N1(1)*B2(3)
              M1(3)=N1(1)*B2(2)-N1(2)*B2(1)
              X=N1(1)*N2(1)+N1(2)*N2(2)+N1(3)*N2(3)
              Y=M1(1)*N2(1)+M1(2)*N2(2)+M1(3)*N2(3)
              A=DATAN2(Y,X)*45.0D0/DATAN(1.0D0)
              IF (MYID == 0) WRITE(6,'(A,I3,A,I0,A,I0,A,I0,A,I3,A,I0,A,I0,A,I0,A,I3,A,I3,A,I0,A,I0,A,I0,A,F14.6,A)') &
               'OPTMSG: ',N,'(',RX,',',RY,',',RZ,')  -',M,'(',PX,',',PY,',',PZ,')  -',I,'(0,0,0)  -',&
                          K,'(',QX,',',QY,',',QZ,') = ',A,' DEGREES'
             ENDIF
            ENDDO
           ENDDO
           ENDDO
           ENDDO
          ENDIF
         ENDDO
        ENDDO
        ENDDO
        ENDDO
       ENDIF
      ENDDO
     ENDDO
     ENDDO
     ENDDO
    ENDDO
    DEALLOCATE(NX,NY,NZ)
   ENDIF
   RETURN
END SUBROUTINE



SUBROUTINE GDONE(IOPTCYCLE)
! CHECKS IF THE GEOMETRY OPTIMIZATION CONVERGED
   
   USE MPI
   USE CONTROL
   USE STRUCTURE
   USE GRADIENT
   USE INTEGRAL
   USE OPTIMIZE

   IMPLICIT NONE
   INCLUDE "mpif.h"
   INTEGER :: IOPTCYCLE
   INTEGER :: I,J
   
   DO I=1,NATOM
    ATOMXHIST(I,IOPTCYCLE)=ATOMX(I)
    ATOMXGRADHIST(I,IOPTCYCLE)=FORCEX(I)
    ATOMYHIST(I,IOPTCYCLE)=ATOMY(I)
    ATOMYGRADHIST(I,IOPTCYCLE)=FORCEY(I)
    ATOMZHIST(I,IOPTCYCLE)=ATOMZ(I)
    ATOMZGRADHIST(I,IOPTCYCLE)=FORCEZ(I)
   ENDDO
   PERIODXHIST(IOPTCYCLE)=PERIODX
   PERIODXGRADHIST(IOPTCYCLE)=FORCETX
   PERIODYHIST(IOPTCYCLE)=PERIODY
   PERIODYGRADHIST(IOPTCYCLE)=FORCETY
   PERIODZHIST(IOPTCYCLE)=PERIODZ
   PERIODZGRADHIST(IOPTCYCLE)=FORCETZ
   HELIXHIST(IOPTCYCLE)=HELIX
   HELIXGRADHIST(IOPTCYCLE)=FORCETA

   J=0
   DO I=1,NATOM
    IF (DABS(ATOMXGRADHIST(I,IOPTCYCLE)) > DOPTN(41)) J=1
    IF (DABS(ATOMYGRADHIST(I,IOPTCYCLE)) > DOPTN(41)) J=1
    IF (DABS(ATOMZGRADHIST(I,IOPTCYCLE)) > DOPTN(41)) J=1
   ENDDO
   IF ((CEL1X+CEL2X > 0).AND.(DABS(PERIODXGRADHIST(IOPTCYCLE)) > DOPTN(41))) J=1
   IF ((CEL1Y+CEL2Y > 0).AND.(DABS(PERIODYGRADHIST(IOPTCYCLE)) > DOPTN(41))) J=1
   IF ((CEL1Z+CEL2Z > 0).AND.(DABS(PERIODZGRADHIST(IOPTCYCLE)) > DOPTN(41))) J=1
   IF ((HELIX /= 0.0D0).AND.(DABS(HELIXGRADHIST(IOPTCYCLE)) > DOPTN(41))) J=1
   IF (J == 0) THEN
    IF (MYID == 0) WRITE(6,'(A)') 'OPTMSG: GEOMETRY OPTIMIZATION CONVERGENCE CRITERION MET'
    CALL DUMPGEOMETRY(MAXOPTCYCLES)
    IF (MYID == 0) WRITE(6,'(A)') 'OPTMSG: **************************************************************'
    CALL MPI_FINALIZE(IERR)
    STOP
   ENDIF
   
   RETURN
END SUBROUTINE



SUBROUTINE GRELAX(IOPTCYCLE,ITERM)
! SIMPLE RELAXATION TO THE GEOMETRY

   USE MPI
   USE CONTROL
   USE STRUCTURE
   USE GRADIENT
   USE INTEGRAL
   USE OPTIMIZE

   IMPLICIT NONE
   INTEGER :: IOPTCYCLE
   INTEGER :: ITERM
   INTEGER :: I,J
   DOUBLE PRECISION :: MAXGRAD
   
   IF (MYID == 0) WRITE(6,'(A)') 'OPTMSG: STEEPEST DESCENT RELAXATION'

   IF (((ITERM /= 0).AND.(MOD(IOPTCYCLE,ITERM) /= 1)).OR.((ITERM == 0).AND.(IOPTCYCLE /= 1))) THEN
    IF ((ATOMXGRADHIST(IMAXGRAD,IOPTCYCLE-1)-ATOMXGRADHIST(IMAXGRAD,IOPTCYCLE) /= 0.0D0).AND.(IMAXGRADXYZ == 1)) &
    RELAX1=RELAX1*ATOMXGRADHIST(IMAXGRAD,IOPTCYCLE-1)/(ATOMXGRADHIST(IMAXGRAD,IOPTCYCLE-1)-ATOMXGRADHIST(IMAXGRAD,IOPTCYCLE))
    IF ((ATOMYGRADHIST(IMAXGRAD,IOPTCYCLE-1)-ATOMYGRADHIST(IMAXGRAD,IOPTCYCLE) /= 0.0D0).AND.(IMAXGRADXYZ == 2)) &
    RELAX1=RELAX1*ATOMYGRADHIST(IMAXGRAD,IOPTCYCLE-1)/(ATOMYGRADHIST(IMAXGRAD,IOPTCYCLE-1)-ATOMYGRADHIST(IMAXGRAD,IOPTCYCLE))
    IF ((ATOMZGRADHIST(IMAXGRAD,IOPTCYCLE-1)-ATOMZGRADHIST(IMAXGRAD,IOPTCYCLE) /= 0.0D0).AND.(IMAXGRADXYZ == 3)) &
    RELAX1=RELAX1*ATOMZGRADHIST(IMAXGRAD,IOPTCYCLE-1)/(ATOMZGRADHIST(IMAXGRAD,IOPTCYCLE-1)-ATOMZGRADHIST(IMAXGRAD,IOPTCYCLE))
    IF (CEL1X+CEL2X > 0) THEN
     IF (PERIODXGRADHIST(IOPTCYCLE-1)-PERIODXGRADHIST(IOPTCYCLE) /= 0.0D0) &
     RELAX2=RELAX2*PERIODXGRADHIST(IOPTCYCLE-1)/(PERIODXGRADHIST(IOPTCYCLE-1)-PERIODXGRADHIST(IOPTCYCLE))
    ENDIF
    IF (CEL1Y+CEL2Y > 0) THEN
     IF (PERIODYGRADHIST(IOPTCYCLE-1)-PERIODYGRADHIST(IOPTCYCLE) /= 0.0D0) &
     RELAX2=RELAX2*PERIODYGRADHIST(IOPTCYCLE-1)/(PERIODYGRADHIST(IOPTCYCLE-1)-PERIODYGRADHIST(IOPTCYCLE))
    ENDIF
    IF (CEL1Z+CEL2Z > 0) THEN
     IF (PERIODZGRADHIST(IOPTCYCLE-1)-PERIODZGRADHIST(IOPTCYCLE) /= 0.0D0) &
     RELAX2=RELAX2*PERIODZGRADHIST(IOPTCYCLE-1)/(PERIODZGRADHIST(IOPTCYCLE-1)-PERIODZGRADHIST(IOPTCYCLE))
    ENDIF
    IF (HELIX /= 0.0D0) THEN
     IF (HELIXGRADHIST(IOPTCYCLE-1)-HELIXGRADHIST(IOPTCYCLE) /= 0.0D0) &
     RELAX3=RELAX3*HELIXGRADHIST(IOPTCYCLE-1)/(HELIXGRADHIST(IOPTCYCLE-1)-HELIXGRADHIST(IOPTCYCLE))
    ENDIF
   ENDIF 
   IF (RELAX1 <= 0.0D0) RELAX1=10.0D0
   IF (RELAX2 <= 0.0D0) RELAX2=10.0D0
   IF (RELAX3 <= 0.0D0) RELAX3=1.0D0
   IF (RELAX1 > 100.0D0) RELAX1=100.0D0
   IF (RELAX2 > 100.0D0) RELAX2=100.0D0
   IF (RELAX3 > 20.0D0) RELAX3=20.0D0
   IF (CEL1X+CEL2X+CEL1Y+CEL2Y+CEL1Z+CEL2Z > 0) THEN
    IF (MYID == 0) WRITE(6,'(A,3F7.2)') 'OPTMSG: RELAXATION PARAMETERS ARE ',RELAX1,RELAX2,RELAX3
   ELSE
    IF (MYID == 0) WRITE(6,'(A,F7.2)') 'OPTMSG: RELAXATION PARAMETER IS ',RELAX1
   ENDIF

   DO I=1,NATOM
    ATOMX(I)=ATOMX(I)-RELAX1*ATOMXGRADHIST(I,IOPTCYCLE)
    ATOMY(I)=ATOMY(I)-RELAX1*ATOMYGRADHIST(I,IOPTCYCLE)
    ATOMZ(I)=ATOMZ(I)-RELAX1*ATOMZGRADHIST(I,IOPTCYCLE)
   ENDDO 
   IF (CEL1X+CEL2X > 0) PERIODX=PERIODX-RELAX2*PERIODXGRADHIST(IOPTCYCLE)
   IF (CEL1Y+CEL2Y > 0) PERIODY=PERIODY-RELAX2*PERIODYGRADHIST(IOPTCYCLE)
   IF (CEL1Z+CEL2Z > 0) PERIODZ=PERIODZ-RELAX2*PERIODZGRADHIST(IOPTCYCLE)
   IF (HELIX /= 0.0D0) HELIX=HELIX-RELAX3*HELIXGRADHIST(IOPTCYCLE)

   MAXGRAD=0.0D0
   DO I=1,NATOM
    IF (DABS(ATOMXGRADHIST(I,IOPTCYCLE)) > MAXGRAD) THEN
     IMAXGRAD=I
     IMAXGRADXYZ=1
     MAXGRAD=DABS(ATOMXGRADHIST(I,IOPTCYCLE))
    ENDIF
    IF (DABS(ATOMYGRADHIST(I,IOPTCYCLE)) > MAXGRAD) THEN
     IMAXGRAD=I
     IMAXGRADXYZ=2
     MAXGRAD=DABS(ATOMYGRADHIST(I,IOPTCYCLE))
    ENDIF
    IF (DABS(ATOMZGRADHIST(I,IOPTCYCLE)) > MAXGRAD) THEN
     IMAXGRAD=I
     IMAXGRADXYZ=3
     MAXGRAD=DABS(ATOMZGRADHIST(I,IOPTCYCLE))
    ENDIF
   ENDDO

   RETURN
END SUBROUTINE



SUBROUTINE GDIIS(IOPTCYCLE,ITERM)
! SIMPLE RELAXATION TO THE GEOMETRY

   USE MPI
   USE CONTROL
   USE STRUCTURE
   USE GRADIENT
   USE INTEGRAL
   USE OPTIMIZE

   IMPLICIT NONE
   INTEGER :: IOPTCYCLE
   INTEGER :: ITERM
   INTEGER :: I,J,K
   INTEGER,ALLOCATABLE :: INDX(:)
   DOUBLE PRECISION,ALLOCATABLE :: A(:,:),AS(:,:)
   DOUBLE PRECISION,ALLOCATABLE :: X(:),XS(:)
   DOUBLE PRECISION :: D
   
   ALLOCATE(A(ITERM+1,ITERM+1),AS(ITERM+1,ITERM+1))
   ALLOCATE(INDX(ITERM+1),X(ITERM+1),XS(ITERM+1))

   IF (MYID == 0) WRITE(6,'(A,I1,A)') 'OPTMSG: ',ITERM,' TERM GDIIS EXTRAPOLATION'

   DO I=1,ITERM
    DO J=1,ITERM
     A(J,I)=0.0D0
     DO K=1,NATOM
      A(J,I)=A(J,I)+ATOMXGRADHIST(K,IOPTCYCLE-ITERM+J)*ATOMXGRADHIST(K,IOPTCYCLE-ITERM+I) &
                   +ATOMYGRADHIST(K,IOPTCYCLE-ITERM+J)*ATOMYGRADHIST(K,IOPTCYCLE-ITERM+I) &
                   +ATOMZGRADHIST(K,IOPTCYCLE-ITERM+J)*ATOMZGRADHIST(K,IOPTCYCLE-ITERM+I)
     ENDDO
     IF (CEL1X+CEL2X > 0) A(J,I)=A(J,I)+PERIODXGRADHIST(IOPTCYCLE-ITERM+J)*PERIODXGRADHIST(IOPTCYCLE-ITERM+I)
     IF (CEL1Y+CEL2Y > 0) A(J,I)=A(J,I)+PERIODYGRADHIST(IOPTCYCLE-ITERM+J)*PERIODYGRADHIST(IOPTCYCLE-ITERM+I)
     IF (CEL1Z+CEL2Z > 0) A(J,I)=A(J,I)+PERIODZGRADHIST(IOPTCYCLE-ITERM+J)*PERIODZGRADHIST(IOPTCYCLE-ITERM+I)
     IF (HELIX /= 0.0D0) A(J,I)=A(J,I)+HELIXGRADHIST(IOPTCYCLE-ITERM+J)*HELIXGRADHIST(IOPTCYCLE-ITERM+I)
    ENDDO
   ENDDO
   DO I=1,ITERM
    A(I,ITERM+1)=-1.0D0
    A(ITERM+1,I)=-1.0D0
    X(I)=0.0D0
   ENDDO
   A(ITERM+1,ITERM+1)=0.0D0
   X(ITERM+1)=-1.0D0
   DO I=1,ITERM+1
    DO J=1,ITERM+1
     AS(J,I)=A(J,I)
    ENDDO
    XS(I)=X(I)
   ENDDO

   CALL LUDCMP(A,ITERM+1,ITERM+1,INDX,D)
   CALL LUBKSB(A,ITERM+1,ITERM+1,INDX,X)
   CALL MPROVE(AS,A,ITERM+1,ITERM+1,INDX,XS,X)

   IF (MYID == 0) WRITE(6,'(A,100F7.3:)') 'OPTMSG: GDIIS COEFFS = ',(X(I),I=1,ITERM)
   DO I=1,NATOM
    ATOMX(I)=0.0D0
    ATOMY(I)=0.0D0
    ATOMZ(I)=0.0D0
    DO J=1,ITERM
     ATOMX(I)=ATOMX(I)+ATOMXHIST(I,IOPTCYCLE-ITERM+J)*X(J)
     ATOMY(I)=ATOMY(I)+ATOMYHIST(I,IOPTCYCLE-ITERM+J)*X(J)
     ATOMZ(I)=ATOMZ(I)+ATOMZHIST(I,IOPTCYCLE-ITERM+J)*X(J)
    ENDDO
   ENDDO
   IF (CEL1X+CEL2X > 0) THEN
    PERIODX=0.0D0
    DO J=1,ITERM
     PERIODX=PERIODX+PERIODXHIST(IOPTCYCLE-ITERM+J)*X(J)
    ENDDO
   ENDIF
   IF (CEL1Y+CEL2Y > 0) THEN
    PERIODY=0.0D0
    DO J=1,ITERM
     PERIODY=PERIODY+PERIODYHIST(IOPTCYCLE-ITERM+J)*X(J)
    ENDDO
   ENDIF
   IF (CEL1Z+CEL2Z > 0) THEN
    PERIODZ=0.0D0
    DO J=1,ITERM
     PERIODZ=PERIODZ+PERIODZHIST(IOPTCYCLE-ITERM+J)*X(J)
    ENDDO
   ENDIF
   IF (HELIX /= 0.0D0) THEN
    HELIX=0.0D0
    DO J=1,ITERM
     HELIX=HELIX+HELIXHIST(IOPTCYCLE-ITERM+J)*X(J)
    ENDDO
   ENDIF

   DEALLOCATE(A,AS,X,XS,INDX)
   RETURN
END SUBROUTINE
