SUBROUTINE HIGHORDER_CORRELATION
! PERFORM FULL AND TRUNCATED CI, HIGH-ORDER RECURSIVE MP, AND HIGH-ORDER CC CALCULATIONS.

   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI
   USE GRADIENT

   IMPLICIT NONE
   DOUBLE PRECISION :: OMEGA
   INTEGER,PARAMETER :: ISCAN=2000    ! NUMBER OF FREQ POINTS IN EACH SIDE OF OMEGA 
   INTEGER,PARAMETER :: ISCANSTEP=100 ! SCAN INTERVAL = 1 eV / ISCANSTEP
   INTEGER,PARAMETER :: ISELF=2       ! 1: SELF-ENERGY ; 2: SELF-ENERGY & GREEN'S FUNCTION ; 3: IP ; 4: EA ; 5: ENERGY
   INTEGER :: I,J,K,L,JSELF
   INTEGER :: NROOTS,NTRIALS
   INTEGER :: MOP,MOQ
   INTEGER,PARAMETER :: NSCALES=100
   DOUBLE PRECISION :: SCALEFACTOR,SCSAVE
   DOUBLE PRECISION,ALLOCATABLE :: A(:,:),AS(:,:),B(:),BS(:)
   DOUBLE PRECISION,ALLOCATABLE :: SELF(:,:,:)
   DOUBLE PRECISION :: C
   INTEGER,ALLOCATABLE :: INDX(:)
   DOUBLE PRECISION :: DIAG(IALL(0,0,0),0:IOPTN(97)-1),EIGN(IALL(0,0,0),0:IOPTN(97)-1)
   DOUBLE PRECISION :: GREN(IALL(0,0,0),0:IOPTN(97)-1),GDYS(IALL(0,0,0),0:IOPTN(97)-1)
   DOUBLE PRECISION :: OMEGA2
   DOUBLE PRECISION,ALLOCATABLE :: ER(:),EI(:),VR(:,:),VL(:,:),WK(:)
   INTEGER :: INFO
   INTEGER :: JSCAN

   IF ((KVCX /= 0).OR.(KVCY /= 0).OR.(KVCZ /= 0)) CALL PABORT('FOR MOLECULES ONLY')
   IF (LOPTN(25)) CALL PABORT('USE NON-DIRECT ALGORITHM')
   IF (IALL(0,0,0)-IOCC-IVIRTCORE <= 0) CALL PABORT('NO VIRTUAL ORBITAL')

   ALLOCATE(H(IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE))
   ALLOCATE(G(IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE))
   CALL ONE_ELECTRON_INTEGRALS
   CALL TWO_ELECTRON_INTEGRALS
   CALL GENERATE_CONFIGURATIONS(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE))
   IF (LOPTN(60)) CALL GENERATE_IP_CONFIGURATIONS(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE+1))
   IF (LOPTN(61)) CALL GENERATE_EA_CONFIGURATIONS(MIN(IOCC-ICORE+1,IALL(0,0,0)-IOCC-IVIRTCORE))
! ---------------------------------------------------------------
! HIGH-ORDER MP
! ---------------------------------------------------------------
   IF ((IOPTN(49) > 0).AND.(IOPTN(97) == 0)) THEN
    ALLOCATE(MP_STORED(0:IOPTN(49)))
    CALL HIGHORDER_MP(IOPTN(49))
    DEALLOCATE(MP_STORED)
   ENDIF
! ---------------------------------------------------------------
! HIGH-ORDER DEGENERATE MP
! ---------------------------------------------------------------
   IF ((IOPTN(49) > 0).AND.(IOPTN(97) == 0).AND.LOPTN(109)) THEN
    CALL DEGENERATE_MP(IOPTN(49))
   ENDIF
! ---------------------------------------------------------------
! HIGH-ORDER GF
! ---------------------------------------------------------------
   IF (IOPTN(97) > 0) THEN
    IF (ICORE /= 0) CALL WARNING('FROZEN CORE IS DANGEROUS FOR MBGF')
    OMEGA=DOPTN(87)
    WRITE(6,'(/,A)') '***********************************************************'
    WRITE(6,'(A,F15.10,A)') '* GENERAL-ORDER MBGF CALCULATION AT OMEGA=',OMEGA,       ' *'
    IF (IOPTN(104)==-1) THEN
     WRITE(6,'(A)') '* ALGORITHM = RECURSION 1 FREQUENCY SCAN                  *'
    ELSE IF (IOPTN(104)==1) THEN
     WRITE(6,'(A)') '* ALGORITHM = RECURSION 1 + LAMBDA VARIATION              *'
    ELSE IF (IOPTN(104)==2) THEN
     WRITE(6,'(A)') '* ALGORITHM = RECURSIONS 1 & 2                            *'
    ELSE IF (IOPTN(104)==3) THEN
     WRITE(6,'(A)') '* ALGORITHM = RECURSIONS 1 & 2 & 3                        *'
    ELSE IF (IOPTN(104)==4) THEN
     WRITE(6,'(A)') '* ALGORITHM = RECURSIONS 1 & 2 & 3 + LAMBDA VARIATION     *'
    ELSE IF (IOPTN(104)==99) THEN
     WRITE(6,'(A)') "* ALGORITHM = EXACT SELF-ENERGY & GREEN'S FUNCTION        *"
    ELSE IF (IOPTN(104)==-99) THEN
     WRITE(6,'(A)') "* ALGORITHM = EXACT SELF-ENERGY & GREEN'S FUNCTION SCAN   *"
    ELSE IF (IOPTN(104)==999) THEN
     WRITE(6,'(A)') "* ALGORITHM = EXACT ALL POLES & RESIDUES                  *"
    ELSE
     CALL PABORT('UNRECOGNIZED MBGFALG')
    ENDIF
    WRITE(6,'(A,/)')  '***********************************************************'
    ! **********************
    ! * MBGF AS DELTA MBPT *
    ! **********************
    ALLOCATE(MP_STORED(0:IOPTN(97)))
    CALL HIGHORDER_MP(IOPTN(97))
    IF (LOPTN(60)) CALL HIGHORDER_MP_IP(IOPTN(97))
    IF (LOPTN(61)) CALL HIGHORDER_MP_EA(IOPTN(97))
    ! ********************
    ! * RECURSION 1 SCAN *
    ! ********************
    IF (IOPTN(104)==-1) THEN
     DO J=ICORE+1,IALL(0,0,0)-IVIRTCORE
      WRITE(200+J,'(2I5)') ISCAN*2+1,IOPTN(97)-1
      WRITE(300+J,'(2I5)') ISCAN*2+1,IOPTN(97)-1
     ENDDO
     DO I=-ISCAN,ISCAN
      OMEGA2=OMEGA+DFLOAT(I)/DFLOAT(ISCANSTEP)
      WRITE(6,'(/,A)') '************************'
      WRITE(6,'(A,F12.5,A)') '* OMEGA = ',OMEGA2,' *'
      WRITE(6,'(A)') '************************'
      CALL HIGHORDER_GF_REDUX1(OMEGA2,IOPTN(97),DIAG,EIGN,GREN,GDYS,IALL(0,0,0))
      DO J=ICORE+1,IALL(0,0,0)-IVIRTCORE
       DO K=1,IOPTN(97)-1
        IF (DIAG(J,K) > 1.0D6)  DIAG(J,K)=9.99999D5
        IF (DIAG(J,K) < -1.0D6) DIAG(J,K)=-9.99999D5
        IF (EIGN(J,K) > 1.0D6)  EIGN(J,K)=9.99999D5
        IF (EIGN(J,K) < -1.0D6) EIGN(J,K)=-9.99999D5
        IF (GREN(J,K) > 1.0D6)  GREN(J,K)=9.99999D5
        IF (GREN(J,K) < -1.0D6) GREN(J,K)=-9.99999D5
        IF (GDYS(J,K) > 1.0D6)  GDYS(J,K)=9.99999D5
        IF (GDYS(J,K) < -1.0D6) GDYS(J,K)=-9.99999D5
       ENDDO
       WRITE(200+J,'(F20.10,20F20.10)') OMEGA2,(DIAG(J,K),K=1,IOPTN(97)-1)
       WRITE(300+J,'(F20.10,20F20.10)') OMEGA2,(EIGN(J,K),K=1,IOPTN(97)-1)
       WRITE(400+J,'(F20.10,20F20.10)') OMEGA2,(GREN(J,K),K=1,IOPTN(97)-1)
       WRITE(500+J,'(F20.10,20F20.10)') OMEGA2,(GDYS(J,K),K=1,IOPTN(97)-1)
      ENDDO
     ENDDO
     WRITE(6,'(A)') "fort.20X contains the diagonal self-energy for the Xth orbital"
     WRITE(6,'(A)') "fort.30X contains the Xth eigenvalue of the self-energy matrix"
     WRITE(6,'(A)') "fort.40X contains the diagonal Green's function for the Xth orbital"
     WRITE(6,'(A)') "fort.50X contains the diagonal Dyson-Green's function for the Xth orbital"
     WRITE(6,'(A)') '../misc/dyson < fort.20X (with diagonal approx)'
     WRITE(6,'(A)') '../misc/dyson < fort.30X (w/o  diagonal approx)'
    ENDIF
    ! ***************
    ! * RECURSION 1 *
    ! ***************
    IF ((IOPTN(104)>=1).AND.(IOPTN(104)<=4)) CALL HIGHORDER_GF_REDUX1(OMEGA,IOPTN(97),DIAG,EIGN,GREN,GDYS,IALL(0,0,0))
    IF ((IOPTN(104)>=2).AND.(IOPTN(104)<=4)) THEN
     ! ********
     ! * HCPT *
     ! ********
     IF (LOPTN(60)) CALL HIGHORDER_MP_IP_ALLSTATES(IOPTN(97))
     IF (LOPTN(61)) CALL HIGHORDER_MP_EA_ALLSTATES(IOPTN(97))
     ! ***************
     ! * RECURSION 2 *
     ! ***************
     CALL HIGHORDER_GF_REDUX2(OMEGA,IOPTN(97))
    ENDIF
    ! ***************
    ! * RECURSION 3 *
    ! ***************
    IF ((IOPTN(104)>=3).AND.(IOPTN(104)<=4)) CALL HIGHORDER_GF_REDUX3(OMEGA,IOPTN(97))
    DEALLOCATE(MP_STORED)
    IF ((IOPTN(104)==99).OR.(IOPTN(104)==-99)) THEN
    ! **************
    ! * EXACT MBGF *
    ! **************
    ALLOCATE(B(NSCALES))
    ALLOCATE(SELF(IALL(0,0,0)-IVIRTCORE-ICORE,IALL(0,0,0)-IVIRTCORE-ICORE,NSCALES))
    ALLOCATE(A(IALL(0,0,0)-IVIRTCORE-ICORE,IALL(0,0,0)-IVIRTCORE-ICORE))
    ALLOCATE(ER(IALL(0,0,0)-IVIRTCORE-ICORE))
    ALLOCATE(EI(IALL(0,0,0)-IVIRTCORE-ICORE))
    ALLOCATE(VR(IALL(0,0,0)-IVIRTCORE-ICORE,IALL(0,0,0)-IVIRTCORE-ICORE))
    ALLOCATE(VL(1,IALL(0,0,0)-IVIRTCORE-ICORE))
    ALLOCATE(WK(4*(IALL(0,0,0)-IVIRTCORE-ICORE)))
    IF (IOPTN(104)==99) JSCAN=0
    IF (IOPTN(104)==-99) JSCAN=ISCAN
    DO J=-JSCAN,JSCAN
     OMEGA2=OMEGA+DFLOAT(J)/DFLOAT(ISCANSTEP)
     WRITE(6,'(/,A)') "**************************************"
     WRITE(6,'(A,F12.5,A)')   "* EXACT MBGF AT OMEGA = ",OMEGA2," *"
     WRITE(6,'(A,/)') "**************************************"

!    EXACT SELF-ENERGY

     NROOTS=IOPTN(56)
     CALL CI_GUESS(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS)
     B(1)=1.0D0
     CALL HIGHORDER_CI_SCALE(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS,B(1))
     B(1)=1.0D0
     CALL FULL_IP_HAMILTONIAN_SCALE(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE+1),B(1))
     B(1)=1.0D0
     CALL FULL_EA_HAMILTONIAN_SCALE(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE+1,IALL(0,0,0)-IOCC-IVIRTCORE),B(1))
     C=0.0D0
     CALL FULL_MBGF(OMEGA2,C,SELF(:,:,1),1)
     WRITE(6,'(A)') " EXACT SELF-ENERGY"
     CALL DUMP5(SELF(:,:,1),IALL(0,0,0)-IVIRTCORE-ICORE)
     WRITE(6,'(A)') " EXACT SELF-ENERGY+FOCK"
     DO I=1,IALL(0,0,0)-IVIRTCORE-ICORE
      SELF(I,I,1)=SELF(I,I,1)+EPSILON(ICORE+I,0,0,0)
     ENDDO
     CALL DUMP5(SELF(:,:,1),IALL(0,0,0)-IVIRTCORE-ICORE)
     WRITE(998,'(F20.10,20F20.10)') OMEGA2,(MAX(MIN(SELF(I,I,1),9.99999D5),-9.99999D5),I=1,IALL(0,0,0)-IVIRTCORE-ICORE)
     CALL DGEEV('N','V',IALL(0,0,0)-IVIRTCORE-ICORE,SELF(:,:,1),IALL(0,0,0)-IVIRTCORE-ICORE,ER,EI,VL,1,VR,&
      IALL(0,0,0)-IVIRTCORE-ICORE,WK,4*(IALL(0,0,0)-IVIRTCORE-ICORE),INFO)
     IF (INFO /= 0) CALL PABORT('DGEEV FAILED TO DIAGONALIZE A MATRIX')
     CALL PIKSRT(IALL(0,0,0)-IVIRTCORE-ICORE,IALL(0,0,0)-IVIRTCORE-ICORE,ER,VR,EI)
     WRITE(999,'(F20.10,20F20.10)') OMEGA2,(MAX(MIN(ER(I),9.99999D5),-9.99999D5),I=1,IALL(0,0,0)-IVIRTCORE-ICORE)
     WRITE(6,'(A)') " EXACT SELF-ENERGY DERIVATIVE"
     CALL FULL_MBGF(OMEGA2,C,SELF(:,:,1),-1)
     CALL DUMP5(SELF(:,:,1),IALL(0,0,0)-IVIRTCORE-ICORE)
     DO I=1,IALL(0,0,0)-IVIRTCORE-ICORE
      EI(I)=0.0D0
      DO K=1,IALL(0,0,0)-IVIRTCORE-ICORE
      DO L=1,IALL(0,0,0)-IVIRTCORE-ICORE
       EI(I)=EI(I)+VR(L,I)*SELF(L,K,1)*VR(K,I)
      ENDDO
      ENDDO
      EI(I)=1.0D0/(1.0D0-EI(I))
     ENDDO
     WRITE(6,'(A)') " EXACT SELF-ENERGY+FOCK (EIGENVALUES & RESIDUES)"
     DO I=1,IALL(0,0,0)-IVIRTCORE-ICORE
      WRITE(6,'(2F20.12)') ER(I),EI(I)
     ENDDO
 
!    EXACT GREEN'S FUNCTION
 
     NROOTS=IOPTN(56)
     CALL CI_GUESS(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS)
     B(1)=1.0D0
     CALL HIGHORDER_CI_SCALE(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS,B(1))
     B(1)=1.0D0
     CALL FULL_IP_HAMILTONIAN_SCALE(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE+1),B(1))
     B(1)=1.0D0
     CALL FULL_EA_HAMILTONIAN_SCALE(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE+1,IALL(0,0,0)-IOCC-IVIRTCORE),B(1))
     C=0.0D0
     CALL FULL_MBGF(OMEGA2,C,SELF(:,:,1),2)
     WRITE(6,'(A)') " EXACT GREEN'S FUNCTION"
     CALL DUMP5(SELF(:,:,1),IALL(0,0,0)-IVIRTCORE-ICORE)
     WRITE(997,'(F20.10,20F20.10)') OMEGA2,(MAX(MIN(SELF(I,I,1),9.99999D5),-9.99999D5),I=1,IALL(0,0,0)-IVIRTCORE-ICORE)
    ENDDO 
    DEALLOCATE(ER,EI,VR,VL,WK)
    DEALLOCATE(B,SELF,A)
    WRITE(6,'(A)') "fort.997 lists omega, diagonal Green's function"
    WRITE(6,'(A)') 'fort.998 lists omega, diagonal self-energies'
    WRITE(6,'(A)') 'fort.999 lists omega, eigenvalue self-energies'
    ENDIF

    IF (IOPTN(104)==999) THEN
     WRITE(6,'(/,A)') "***************************************"
     WRITE(6,'(A)')   "* EXACT MBGF FOR ALL POLES & RESIDUES *"
     WRITE(6,'(A,/)') "***************************************"
     NROOTS=IOPTN(56)
     ALLOCATE(B(NSCALES))
     CALL CI_GUESS(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS)
     B(1)=1.0D0
     CALL HIGHORDER_CI_SCALE(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS,B(1))
     B(1)=1.0D0
     CALL FULL_IP_HAMILTONIAN_SCALE(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE+1),B(1))
     B(1)=1.0D0
     CALL FULL_EA_HAMILTONIAN_SCALE(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE+1,IALL(0,0,0)-IOCC-IVIRTCORE),B(1))
     CALL FULL_MBGF_ROOTS
    ENDIF

    IF ((IOPTN(104)==1).OR.(IOPTN(104)==4)) THEN
    ! ****************************************
    ! * MBGF WITH LAMBDA VARIATION TECHNIQUE *
    ! ****************************************
    WRITE(6,'(/,A)') "********************"
    WRITE(6,'(A)')   "* LAMBDA-VARIATION *"
    WRITE(6,'(A,/)') "********************"
    ALLOCATE(B(NSCALES))
    ALLOCATE(SELF(IALL(0,0,0)-IVIRTCORE-ICORE,IALL(0,0,0)-IVIRTCORE-ICORE,NSCALES))
    ALLOCATE(A(IALL(0,0,0)-IVIRTCORE-ICORE,IALL(0,0,0)-IVIRTCORE-ICORE))
    IF (ISELF==1) THEN
     WRITE(6,'(A)') 'SELF-ENERGY BY LAMBDA-VARIATION METHOD'
    ELSE IF (ISELF==2) THEN
     WRITE(6,'(A)') "SELF-ENERGY & GREEN'S FUNCTION BY LAMBDA-VARIATION METHOD"
    ELSE IF (ISELF==3) THEN
     WRITE(6,'(A)') "DELTA MP (N-1) BY LAMBDA-VARIATION METHOD"
    ELSE IF (ISELF==4) THEN
     WRITE(6,'(A)') "DELTA MP (N+1) BY LAMBDA-VARIATION METHOD"
    ELSE IF (ISELF==5) THEN
     WRITE(6,'(A)') "DELTA MP (N) BY LAMBDA-VARIATION METHOD"
    ENDIF
write(*,*) "/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\"
write(*,*) " !!!! CAUTION: VERY tight CICONV is necessary for best results !!!! "
write(*,*) "\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/"
    ! 5-POINT FINITE DIFFERENCE
!   WRITE(6,'(A,E10.3,/)') 'DELTA LAMBDA =',1.0D0/DFLOAT(NSCALES)
!   WRITE(6,'(A)') 'FIVE-POINT FORMULA'
!   DO I=1,5
!    NROOTS=IOPTN(56)
!    CALL CI_GUESS(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS)
!    B(I)=DFLOAT(I-3)/DFLOAT(NSCALES)
!    CALL HIGHORDER_CI_SCALE(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS,B(I))
!    C=B(I)-NUCLEAR_REPULSION ! EXACT N-ELECTRON GS ENERGY
!    B(I)=DFLOAT(I-3)/DFLOAT(NSCALES)
!    CALL FULL_IP_HAMILTONIAN_SCALE(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE+1),B(I))
!    B(I)=DFLOAT(I-3)/DFLOAT(NSCALES)
!    CALL FULL_EA_HAMILTONIAN_SCALE(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE+1,IALL(0,0,0)-IOCC-IVIRTCORE),B(I))
!    B(I)=DFLOAT(I-3)/DFLOAT(NSCALES)
!    CALL FULL_MBGF(OMEGA,C,SELF(:,:,I),ISELF)
!    WRITE(6,'(I2,A)') I-3,' EXACT SELF-ENERGY'
!    CALL DUMP5(SELF(:,:,I),IALL(0,0,0)-IVIRTCORE-ICORE)
!   ENDDO
!   WRITE(6,'(A)') 'CONSTANT'
!   A(:,:)=SELF(:,:,3)
!   CALL DUMP5(A,IALL(0,0,0)-IVIRTCORE-ICORE)
!   WRITE(6,'(A)') 'FIRST DERIVATIVE'
!   A(:,:)=(2.0D0*SELF(:,:,1)-16.0D0*SELF(:,:,2)+16.0D0*SELF(:,:,4)-2.0D0*SELF(:,:,5)) &
!          /24.0D0*DFLOAT(NSCALES)
!   CALL DUMP5(A,IALL(0,0,0)-IVIRTCORE-ICORE)
!   WRITE(6,'(A)') 'SECOND DERIVATIVE'
!   A(:,:)=(-SELF(:,:,1)+16.0D0*SELF(:,:,2)-30.0D0*SELF(:,:,3)+16.0D0*SELF(:,:,4)-SELF(:,:,5)) &
!          /24.0D0*(DFLOAT(NSCALES)**2)
!   CALL DUMP5(A,IALL(0,0,0)-IVIRTCORE-ICORE)
!   WRITE(6,'(A)') 'THIRD DERIVATIVE'
!   A(:,:)=(-2.0D0*SELF(:,:,1)+4.0D0*SELF(:,:,2)-4.0D0*SELF(:,:,4)+2.0D0*SELF(:,:,5)) &
!          /24.0D0*(DFLOAT(NSCALES)**3)
!   CALL DUMP5(A,IALL(0,0,0)-IVIRTCORE-ICORE)
!   WRITE(6,'(A)') 'FOURTH DERIVATIVE'
!   A(:,:)=(SELF(:,:,1)-4.0D0*SELF(:,:,2)+6.0D0*SELF(:,:,3)-4.0D0*SELF(:,:,4)+SELF(:,:,5)) &
!          /24.0D0*(DFLOAT(NSCALES)**4)
!   CALL DUMP5(A,IALL(0,0,0)-IVIRTCORE-ICORE)
!   ! 7-POINT FINITE DIFFERENCE
    IF (ISELF <= 2) THEN
     DO JSELF=1,ISELF
     IF (JSELF==1) WRITE(6,'(/,A,/)') 'SELF-ENERGY (LAMBDA-VARIATION WITH A SEVEN-POINT FORMULA)'
     IF (JSELF==2) WRITE(6,'(/,A,/)') "GREEN'S FUNCTION (LAMBDA-VARIATION WITH A SEVEN-POINT FORMULA)"
     DO I=1,7
      NROOTS=IOPTN(56)
      CALL CI_GUESS(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS)
      B(I)=DFLOAT(I-4)/DFLOAT(NSCALES)
      CALL HIGHORDER_CI_SCALE(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS,B(I))
      C=B(I)-NUCLEAR_REPULSION ! EXACT N-ELECTRON GS ENERGY
      B(I)=DFLOAT(I-4)/DFLOAT(NSCALES)
      CALL FULL_IP_HAMILTONIAN_SCALE(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE+1),B(I))
      B(I)=DFLOAT(I-4)/DFLOAT(NSCALES)
      CALL FULL_EA_HAMILTONIAN_SCALE(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE+1,IALL(0,0,0)-IOCC-IVIRTCORE),B(I))
      B(I)=DFLOAT(I-4)/DFLOAT(NSCALES)
      CALL FULL_MBGF(OMEGA,C,SELF(:,:,I),JSELF)
!     WRITE(6,'(I2,A)') I-4,' EXACT SELF-ENERGY'
!     CALL DUMP5(SELF(:,:,I),IALL(0,0,0)-IVIRTCORE-ICORE)
     ENDDO
     WRITE(6,'(A)') 'CONSTANT'
     A(:,:)=SELF(:,:,4)
     CALL DUMP5(A,IALL(0,0,0)-IVIRTCORE-ICORE)
     WRITE(6,'(A)') 'FIRST DERIVATIVE'
     A(:,:)=(-SELF(:,:,1)+9.0D0*SELF(:,:,2)-45.0D0*SELF(:,:,3)+45.0D0*SELF(:,:,5) &
             -9.0D0*SELF(:,:,6)+SELF(:,:,7))/60.0D0*DFLOAT(NSCALES)
     CALL DUMP5(A,IALL(0,0,0)-IVIRTCORE-ICORE)
     WRITE(6,'(A)') 'SECOND DERIVATIVE'
     A(:,:)=(2.0D0*SELF(:,:,1)-27.0D0*SELF(:,:,2)+270.0D0*SELF(:,:,3)-490.0D0*SELF(:,:,4) &
             +270.0D0*SELF(:,:,5)-27.0D0*SELF(:,:,6)+2.0D0*SELF(:,:,7)) &
            /180.0D0*(DFLOAT(NSCALES)**2)/2.0D0
     CALL DUMP5(A,IALL(0,0,0)-IVIRTCORE-ICORE)
     WRITE(6,'(A)') 'THIRD DERIVATIVE'
     A(:,:)=(SELF(:,:,1)-8.0D0*SELF(:,:,2)+13.0D0*SELF(:,:,3)-13.0D0*SELF(:,:,5) &
             +8.0D0*SELF(:,:,6)-SELF(:,:,7))/8.0D0*(DFLOAT(NSCALES)**3)/6.0D0
     CALL DUMP5(A,IALL(0,0,0)-IVIRTCORE-ICORE)
     WRITE(6,'(A)') 'FOURTH DERIVATIVE'
     A(:,:)=(-SELF(:,:,1)+12.0D0*SELF(:,:,2)-39.0D0*SELF(:,:,3)+56.0D0*SELF(:,:,4) &
             -39.0D0*SELF(:,:,5)+12.0D0*SELF(:,:,6)-SELF(:,:,7)) &
            /6.0D0*(DFLOAT(NSCALES)**4)/24.0D0
     CALL DUMP5(A,IALL(0,0,0)-IVIRTCORE-ICORE)
     WRITE(6,'(A)') 'FIFTH DERIVATIVE'
     A(:,:)=(-SELF(:,:,1)+4.0D0*SELF(:,:,2)-5.0D0*SELF(:,:,3)+5.0D0*SELF(:,:,5) &
             -4.0D0*SELF(:,:,6)+SELF(:,:,7)) &
            /2.0D0*(DFLOAT(NSCALES)**5)/120.0D0
     CALL DUMP5(A,IALL(0,0,0)-IVIRTCORE-ICORE)
     ENDDO
    ELSE IF (ISELF <= 4) THEN
     WRITE(6,'(/,A)') 'FORWARD SEVEN- TO NINE-POINT FORMULA'
     DO I=1,9
      NROOTS=IOPTN(56)
      CALL CI_GUESS(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS)
      B(I)=DFLOAT(I-1)/DFLOAT(NSCALES)
      CALL HIGHORDER_CI_SCALE(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS,B(I))
      C=B(I)-NUCLEAR_REPULSION ! EXACT N-ELECTRON GS ENERGY
      B(I)=DFLOAT(I-1)/DFLOAT(NSCALES)
      CALL FULL_IP_HAMILTONIAN_SCALE(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE+1),B(I))
      B(I)=DFLOAT(I-1)/DFLOAT(NSCALES)
      CALL FULL_EA_HAMILTONIAN_SCALE(MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE+1,IALL(0,0,0)-IOCC-IVIRTCORE),B(I))
      B(I)=DFLOAT(I-1)/DFLOAT(NSCALES)
      CALL FULL_MBGF(OMEGA,C,SELF(:,:,I),ISELF)
     ENDDO
!do i=1,9
!write(*,'(i3,10f13.8)') i,(self(j,1,i),j=1,10)
!enddo
!do i=1,9
!write(*,'(i3,10f13.8)') i,(self(j,1,i)-self(j,1,1)-(self(j,1,2)-self(j,1,1))*dfloat(i-1),j=1,10)
!enddo
     J=0
     DO MOP=1,IALL(0,0,0)-IVIRTCORE-ICORE
      DO MOQ=1,IALL(0,0,0)-IVIRTCORE-ICORE
       J=J+1
       IF ((ISELF==5).AND.(J>=2)) CYCLE
       WRITE(6,'(A,I3,A)') 'STATE ',J,'  (CAUTION !!! STATES CAN STILL BE REORDERED DURING FINITE DIFFERENTIATION)'
       A(MOQ,MOP)=SELF(MOQ,MOP,1)
       C=A(MOQ,MOP)+NUCLEAR_REPULSION
       WRITE(6,'(A,2F15.10)') '0TH-DERIV ',A(MOQ,MOP),C
       A(MOQ,MOP)=(-49.0D0/20.0D0*SELF(MOQ,MOP,1)+6.0D0*SELF(MOQ,MOP,2)-15.0D0/2.0D0*SELF(MOQ,MOP,3) &
                   +20.0D0/3.0D0*SELF(MOQ,MOP,4)-15.0D0/4.0D0*SELF(MOQ,MOP,5)+6.0D0/5.0D0*SELF(MOQ,MOP,6) &
                   -1.0D0/6.0D0*SELF(MOQ,MOP,7)) &
                  *DFLOAT(NSCALES)
       C=C+A(MOQ,MOP)
       WRITE(6,'(A,2F15.10)') '1ST-DERIV ',A(MOQ,MOP),C
       A(MOQ,MOP)=(+469.0D0/90.0D0*SELF(MOQ,MOP,1)-223.0D0/10.0D0*SELF(MOQ,MOP,2)+879.0D0/20.0D0*SELF(MOQ,MOP,3) &
                   -949.0D0/18.0D0*SELF(MOQ,MOP,4)+41.0D0*SELF(MOQ,MOP,5)-201.0D0/10.0D0*SELF(MOQ,MOP,6) &
                   +1019.0D0/180.0D0*SELF(MOQ,MOP,7)-7.0D0/10.0D0*SELF(MOQ,MOP,8)) &
                   *(DFLOAT(NSCALES)**2)/2.0D0
       C=C+A(MOQ,MOP)
       WRITE(6,'(A,2F15.10)') '2ND-DERIV ',A(MOQ,MOP),C
       A(MOQ,MOP)=(-801.0D0/80.0D0*SELF(MOQ,MOP,1)+349.0D0/6.0D0*SELF(MOQ,MOP,2)-18353.0D0/120.0D0*SELF(MOQ,MOP,3) &
                   +2391.0D0/10.0D0*SELF(MOQ,MOP,4)-1457.0D0/6.0D0*SELF(MOQ,MOP,5)+4891.0D0/30.0D0*SELF(MOQ,MOP,6) &
                   -561.0D0/8.0D0*SELF(MOQ,MOP,7)+527.0D0/30.0D0*SELF(MOQ,MOP,8)-469.0D0/240.0D0*SELF(MOQ,MOP,9)) &
                   *(DFLOAT(NSCALES)**3)/6.0D0
       C=C+A(MOQ,MOP)
       WRITE(6,'(A,2F15.10)') '3RD-DERIV ',A(MOQ,MOP),C
       A(MOQ,MOP)=(1069.0D0/80.0D0*SELF(MOQ,MOP,1)-1316.0D0/15.0D0*SELF(MOQ,MOP,2)+15289.0D0/60.0D0*SELF(MOQ,MOP,3) &
                  -2144.0D0/5.0D0*SELF(MOQ,MOP,4)+10993.0D0/24.0D0*SELF(MOQ,MOP,5)-4772.0D0/15.0D0*SELF(MOQ,MOP,6) &
                  +2803.0D0/20.0D0*SELF(MOQ,MOP,7)-536.0D0/15.0D0*SELF(MOQ,MOP,8)+967.0D0/240.0D0*SELF(MOQ,MOP,9)) &
                   *(DFLOAT(NSCALES)**4)/24.0D0
       C=C+A(MOQ,MOP)
       WRITE(6,'(A,2F15.10)') '4TH-DERIV ',A(MOQ,MOP),C
      ENDDO
     ENDDO
    ELSE IF (ISELF==5) THEN
     WRITE(6,'(/,A)') 'SEVEN-POINT FORMULA'
     DO I=1,7
      NROOTS=IOPTN(56)
      CALL CI_GUESS(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS)
      B(I)=DFLOAT(I-4)/DFLOAT(NSCALES)
      CALL HIGHORDER_CI_SCALE(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),NROOTS,NTRIALS,B(I))
     ENDDO
     WRITE(6,'(A)') 'CONSTANT'
     C=B(4)-NUCLEAR_REPULSION
     WRITE(6,'(F15.10)') C
     WRITE(6,'(A)') 'FIRST DERIVATIVE'
     C=(-B(1)+9.0D0*B(2)-45.0D0*B(3)+45.0D0*B(5) &
             -9.0D0*B(6)+B(7))/60.0D0*DFLOAT(NSCALES)
     WRITE(6,'(F15.10)') C
     WRITE(6,'(A)') 'SECOND DERIVATIVE'
     C=(2.0D0*B(1)-27.0D0*B(2)+270.0D0*B(3)-490.0D0*B(4) &
             +270.0D0*B(5)-27.0D0*B(6)+2.0D0*B(7)) &
            /180.0D0*(DFLOAT(NSCALES)**2)/2.0D0
     WRITE(6,'(F15.10)') C
     WRITE(6,'(A)') 'THIRD DERIVATIVE'
     C=(B(1)-8.0D0*B(2)+13.0D0*B(3)-13.0D0*B(5) &
             +8.0D0*B(6)-B(7))/8.0D0*(DFLOAT(NSCALES)**3)/6.0D0
     WRITE(6,'(F15.10)') C
     WRITE(6,'(A)') 'FOURTH DERIVATIVE'
     C=(-B(1)+12.0D0*B(2)-39.0D0*B(3)+56.0D0*B(4) &
             -39.0D0*B(5)+12.0D0*B(6)-B(7)) &
            /6.0D0*(DFLOAT(NSCALES)**4)/24.0D0
     WRITE(6,'(F15.10)') C
     WRITE(6,'(A)') 'FIFTH DERIVATIVE'
     C=(-B(1)+4.0D0*B(2)-5.0D0*B(3)+5.0D0*B(5) &
             -4.0D0*B(6)+B(7)) &
            /2.0D0*(DFLOAT(NSCALES)**5)/120.0D0
     WRITE(6,'(F15.10)') C
    ENDIF
    DEALLOCATE(B,SELF,A)
   ENDIF
   ENDIF
! ---------------------------------------------------------------
! HIGH-ORDER CI
! ---------------------------------------------------------------
   IF ((.NOT.LOPTN(60)).AND.(.NOT.LOPTN(61))) THEN
    ! CI_EE
    IF (IOPTN(51) >= MAX(IOPTN(50),1)) THEN
     DO I=MAX(IOPTN(50),1),MIN(IOPTN(51),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE))
      IF (IOPTN(59) >= 999) THEN
       CALL FULL_HAMILTONIAN(I)
      ELSE
       NROOTS=IOPTN(56)
       CALL CI_GUESS(I,NROOTS,NTRIALS)
       CALL HIGHORDER_CI(I,NROOTS,NTRIALS)
      ENDIF
     ENDDO
    ELSE IF ((IOPTN(50) >= MAX(IOPTN(51),1)).AND.(IOPTN(51) /= 0)) THEN
     DO I=MIN(IOPTN(50),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),MAX(IOPTN(51),1),-1
      IF (IOPTN(9) == 0) CALL FULL_HAMILTONIAN(I)
      NROOTS=IOPTN(56)
      CALL CI_GUESS(I,NROOTS,NTRIALS)
      CALL HIGHORDER_CI(I,NROOTS,NTRIALS)
     ENDDO
    ENDIF
   ENDIF
   IF (LOPTN(60)) THEN
    ! CI_IP
    IF (IOPTN(51) >= MAX(IOPTN(50),1)) THEN
     DO I=MAX(IOPTN(50),1),MIN(IOPTN(51),MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE+1))
      IF (IOPTN(59) >= 999) THEN
       CALL FULL_IP_HAMILTONIAN(I)
      ELSE
       NROOTS=IOPTN(56)
       CALL CI_IP_GUESS(I,NROOTS,NTRIALS)
       CALL HIGHORDER_CI_IP(I,NROOTS,NTRIALS)
      ENDIF
     ENDDO
    ELSE IF ((IOPTN(50) >= MAX(IOPTN(51),1)).AND.(IOPTN(51) /= 0)) THEN
     DO I=MIN(IOPTN(50),MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE+1)),MAX(IOPTN(51),1),-1
      IF (IOPTN(59) >= 999) THEN
       CALL FULL_IP_HAMILTONIAN(I)
      ELSE
       NROOTS=IOPTN(56)
       CALL CI_IP_GUESS(I,NROOTS,NTRIALS)
       CALL HIGHORDER_CI_IP(I,NROOTS,NTRIALS)
      ENDIF
     ENDDO
    ENDIF
   ENDIF
   IF (LOPTN(61)) THEN
    ! CI_EA
    IF (IOPTN(51) >= MAX(IOPTN(50),1)) THEN
     DO I=MAX(IOPTN(50),1),MIN(IOPTN(51),MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE+1,IALL(0,0,0)-IOCC-IVIRTCORE))
      IF (IOPTN(59) >= 999) THEN
       CALL FULL_EA_HAMILTONIAN(I)
      ELSE
       NROOTS=IOPTN(56)
       CALL CI_EA_GUESS(I,NROOTS,NTRIALS)
       CALL HIGHORDER_CI_EA(I,NROOTS,NTRIALS)
      ENDIF
     ENDDO
    ELSE IF ((IOPTN(50) >= MAX(IOPTN(51),1)).AND.(IOPTN(51) /= 0)) THEN
     DO I=MIN(IOPTN(50),MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE+1,IALL(0,0,0)-IOCC-IVIRTCORE)), &
          MAX(IOPTN(51),1),-1
      IF (IOPTN(59) >= 999) THEN
       CALL FULL_EA_HAMILTONIAN(I)
      ELSE
       NROOTS=IOPTN(56)
       CALL CI_EA_GUESS(I,NROOTS,NTRIALS)
       CALL HIGHORDER_CI_EA(I,NROOTS,NTRIALS)
      ENDIF
     ENDDO
    ENDIF
   ENDIF
! ---------------------------------------------------------------
! HIGH-ORDER CC
! ---------------------------------------------------------------
   IF (IOPTN(53) >= MAX(IOPTN(52),1)) THEN
!   DO I=MAX(IOPTN(52),1),MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE))
    DO I=MAX(IOPTN(52),1),IOPTN(53)
     IF (IOPTN(54) > 0) THEN
      IF (I == MAX(IOPTN(52),1)) CALL HIGHORDER_CC(I,.FALSE.,.TRUE.,.FALSE.)
      IF (I /= MAX(IOPTN(52),1)) CALL HIGHORDER_CC(I,.TRUE.,.TRUE.,.FALSE.)
     ELSE
      IF (I == MAX(IOPTN(52),1)) CALL HIGHORDER_CC(I,.FALSE.,.FALSE.,.FALSE.)
      IF (I /= MAX(IOPTN(52),1)) CALL HIGHORDER_CC(I,.TRUE.,.FALSE.,.FALSE.)
     ENDIF
     IF (LOPTN(65)) CALL HIGHORDER_CC(I,.TRUE.,.TRUE.,.TRUE.)
     IF (LOPTN(109)) THEN
      CALL DEGENERATE_CI(I) ! FULL CI
      CALL DEGENERATE_CC(I)
     ENDIF 
    ENDDO
   ELSE IF ((IOPTN(52) >= MAX(IOPTN(53),1)).AND.(IOPTN(53) /= 0)) THEN
!   DO I=MIN(IOPTN(52),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),MAX(IOPTN(53),1),-1
    DO I=IOPTN(52),MAX(IOPTN(53),1),-1
     IF (IOPTN(54) > 0) THEN
      IF (I == MAX(IOPTN(52),1)) CALL HIGHORDER_CC(I,.FALSE.,.TRUE.,.FALSE.)
      IF (I /= MAX(IOPTN(52),1)) CALL HIGHORDER_CC(I,.TRUE.,.TRUE.,.FALSE.)
     ELSE
      IF (I == MAX(IOPTN(52),1)) CALL HIGHORDER_CC(I,.FALSE.,.FALSE.,.FALSE.)
      IF (I /= MAX(IOPTN(52),1)) CALL HIGHORDER_CC(I,.TRUE.,.FALSE.,.FALSE.)
     ENDIF
     IF (LOPTN(65)) CALL HIGHORDER_CC(I,.TRUE.,.TRUE.,.TRUE.)
     IF (LOPTN(109)) THEN
      CALL DEGENERATE_CI(I) ! FULL CI
      CALL DEGENERATE_CC(I)
     ENDIF 
    ENDDO
   ENDIF
! ---------------------------------------------------------------
! HIGH-ORDER EOM-CC
! ---------------------------------------------------------------
   IF (IOPTN(59) > 0) THEN
    IF (IOPTN(53) == 0) CALL PABORT('CC CALCULATION MUST PRECEED EOMCC CALCULATION')
    IF ((.NOT.LOPTN(60)).AND.(.NOT.LOPTN(61))) THEN
     ! EOMEE-CC
     IF (IOPTN(57) <= IOPTN(58)) THEN
      DO I=MAX(IOPTN(57),1),MIN(IOPTN(58),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE))
       IF (IOPTN(59) >= 999) THEN
        IF ((IOPTN(66) > 0).AND.(IOPTN(66) <= IOPTN(59))) THEN
         IF (I == IOPTN(53)) &
         CALL FULL_EFF_H_PERTURBATION(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),MAX(2,IOPTN(49)))
        ELSE
         CALL FULL_EFF_H(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I)
        ENDIF
       ELSE
        NROOTS=IOPTN(59)
        CALL CI_GUESS(I,NROOTS,NTRIALS)
        CALL EOMEECC(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I,NROOTS,NTRIALS,.FALSE.)
        IF ((IOPTN(66) > 0).AND.(IOPTN(66) <= NROOTS)) THEN
         NROOTS=IOPTN(59)
         CALL CI_GUESS(I,NROOTS,NTRIALS)
         CALL EOMEECC(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I,NROOTS,NTRIALS,.TRUE.)
        ENDIF
       ENDIF
      ENDDO
     ELSE
      DO I=MIN(IOPTN(57),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),MAX(IOPTN(58),1),-1
       IF (IOPTN(59) >= 999) THEN
        IF ((IOPTN(66) > 0).AND.(IOPTN(66) <= IOPTN(59))) THEN
         IF (I == IOPTN(53)) &
         CALL FULL_EFF_H_PERTURBATION(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),MAX(2,IOPTN(49)))
        ELSE
         CALL FULL_EFF_H(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I)
        ENDIF
       ELSE
        NROOTS=IOPTN(59)
        CALL CI_GUESS(I,NROOTS,NTRIALS)
        CALL EOMEECC(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I,NROOTS,NTRIALS,.TRUE.)
       ENDIF
       IF ((IOPTN(66) > 0).AND.(IOPTN(66) <= NROOTS)) THEN
        NROOTS=IOPTN(59)
        CALL CI_GUESS(I,NROOTS,NTRIALS)
        CALL EOMEECC(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I,NROOTS,NTRIALS,.FALSE.)
       ENDIF
      ENDDO
     ENDIF
     IF ((IOPTN(66) > 0).AND.(IOPTN(66) <= NROOTS)) THEN
      IF (IOPTN(58) /= MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE))) THEN
       CALL WARNING('NON-STANDARD EOM-CC; PERTURBATIVE CORRECTION CALCULATIONS WILL NOT BE PERFORMED')
      ELSE
       CALL CCPT(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),MAX(2,IOPTN(49)))
      ENDIF
     ENDIF
    ENDIF
    IF (LOPTN(60)) THEN
     ! EOMIP-CC
     IF (IOPTN(57) <= IOPTN(58)) THEN
      DO I=MAX(IOPTN(57),1),MIN(IOPTN(58),MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE+1))
       IF (IOPTN(59) >= 999) THEN
        CALL FULL_IP_EFF_H(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I)
       ELSE
        NROOTS=IOPTN(59)
        CALL CI_IP_GUESS(I,NROOTS,NTRIALS)
        CALL EOMIPCC(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I,NROOTS,NTRIALS)
       ENDIF
      ENDDO
     ELSE
      DO I=MIN(IOPTN(57),MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE+1)), &
           MAX(IOPTN(58),1),-1
       IF (IOPTN(59) >= 999) THEN
        CALL FULL_IP_EFF_H(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I)
       ELSE
        NROOTS=IOPTN(59)
        CALL CI_IP_GUESS(I,NROOTS,NTRIALS)
        CALL EOMIPCC(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I,NROOTS,NTRIALS)
       ENDIF
      ENDDO
      DEALLOCATE(IP_CFHALF,IP_NORDER,IP_ADDRSS)
     ENDIF
    ENDIF
    IF (LOPTN(61)) THEN
     ! EOMEA-CC
     IF (IOPTN(57) <= IOPTN(58)) THEN
      DO I=MAX(IOPTN(57),1),MIN(IOPTN(58),MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE+1,IALL(0,0,0)-IOCC-IVIRTCORE))
       IF (IOPTN(59) >= 999) THEN
        CALL FULL_EA_EFF_H(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I)
       ELSE
        NROOTS=IOPTN(59)
        CALL CI_EA_GUESS(I,NROOTS,NTRIALS)
        CALL EOMEACC(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I,NROOTS,NTRIALS)
       ENDIF
      ENDDO
     ELSE
      DO I=MIN(IOPTN(57),MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+MIN(IOCC-ICORE+1,IALL(0,0,0)-IOCC-IVIRTCORE)), &
           MAX(IOPTN(58),1),-1
       IF (IOPTN(59) >= 999) THEN
        CALL FULL_EA_EFF_H(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I)
       ELSE
        NROOTS=IOPTN(59)
        CALL CI_EA_GUESS(I,NROOTS,NTRIALS)
        CALL EOMEACC(MIN(IOPTN(53),2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)),I,NROOTS,NTRIALS)
       ENDIF
      ENDDO
      DEALLOCATE(EA_CFHALF,EA_NORDER,EA_ADDRSS)
     ENDIF
    ENDIF
   ENDIF
!  IF (IOPTN(97) > 0) THEN
!   ! FULL EXACT MBGF
!   CALL CI_GUESS(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),1,1)
!   CALL HIGHORDER_CI(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),1,1)
!   CALL FULL_IP_HAMILTONIAN(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE))
!   CALL FULL_EA_HAMILTONIAN(2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)+1)
!   DO I=6,6
!    CALL FULL_MBGF(DFLOAT(I)/100.0D0)
!    CALL MBGF(0.2699D0)
!   ENDDO
!   ! MBGF
!   CALL MBGF(0.0D0)
!   CALL HIGHORDER_MP_IP(IOPTN(49))
!   CALL HIGHORDER_MP_EA(IOPTN(49))
!   DEALLOCATE(IP_CFHALF,IP_NORDER,IP_ADDRSS)
!   DEALLOCATE(EA_CFHALF,EA_NORDER,EA_ADDRSS)
!  ENDIF
   IF (LOPTN(60).OR.(IOPTN(97) > 0)) DEALLOCATE(IP_CFHALF,IP_NORDER,IP_ADDRSS)
   IF (LOPTN(61).OR.(IOPTN(97) > 0)) DEALLOCATE(EA_CFHALF,EA_NORDER,EA_ADDRSS)
   DEALLOCATE(H,G)
   DEALLOCATE(CFHALF,ADDRSS,NORDER)

   RETURN
END SUBROUTINE



SUBROUTINE ONE_ELECTRON_INTEGRALS
! FORM ONE ELECTRON PART OF THE HAMILTONIAN MATRIX.

   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI
   
   IMPLICIT NONE
   INTEGER :: I,J,MOP,MOQ
   DOUBLE PRECISION,ALLOCATABLE :: WR(:,:)
   
   WRITE(6,'(A)') 'INTEGRAL TRANSFORMATION OF ONE-ELECTRON INTEGRALS WILL BE PERFORMED'

   ALLOCATE(WR(IALLMAX-IVIRTCORE,NCGS))
   DO I=1,NCGS 
    DO MOP=1,IALL(0,0,0)-IVIRTCORE
     WR(MOP,I)=0.0D0
     DO J=1,NCGS
      WR(MOP,I)=WR(MOP,I)+DREAL(CO(J,MOP,0,0,0))*(T_C(J,I,0,0,0)+N_C(J,I,0,0,0))
     ENDDO
    ENDDO
   ENDDO
   DO MOP=1,IALL(0,0,0)-IVIRTCORE
    DO MOQ=1,IALL(0,0,0)-IVIRTCORE
     H(MOP,MOQ)=0.0D0
     DO I=1,NCGS
      H(MOP,MOQ)=H(MOP,MOQ)+DREAL(CO(I,MOQ,0,0,0))*WR(MOP,I)
     ENDDO
    ENDDO
   ENDDO
   DEALLOCATE(WR)
   
   IF (IOPTN(9) == 3) THEN
    WRITE(6,'(A)') 'ONE-ELECTRON PART OF THE HAMILTONIAN MATRIX'
    CALL DUMP5(H,IALL(0,0,0)-IVIRTCORE)
   ENDIF
   
   RETURN
END SUBROUTINE



SUBROUTINE TWO_ELECTRON_INTEGRALS
! FORM TWO ELECTRON PART OF THE HAMILTONIAN MATRIX.

   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI
   
   IMPLICIT NONE
   INTEGER,PARAMETER :: CASHESIZE = 1000 ! CASHE SIZE MUST BE EXACTLY THE SAME IN THE STORE & RESTORE SUBROUTINES
   INTEGER :: I,J,K,L
   INTEGER :: MOP,MOQ,MOR,MOS,MOI,MOJ,MOA,MOB
   INTEGER :: EOF
   INTEGER :: ICASHECOUNT,ICOUNT
   INTEGER(4),ALLOCATABLE :: ICASHE1X(:),ICASHE1Y(:),ICASHE1Z(:),ICASHE2(:)
   DOUBLE PRECISION,ALLOCATABLE :: DCASHE(:)
   DOUBLE PRECISION,ALLOCATABLE :: WR1(:,:,:),WR2(:,:,:)
   DOUBLE PRECISION :: EMP2
   
   WRITE(6,'(A)') 'INTEGRAL TRANSFORMATION OF TWO-ELECTRON INTEGRALS WILL BE PERFORMED'

   ALLOCATE(ICASHE1X(CASHESIZE),ICASHE1Y(CASHESIZE),ICASHE1Z(CASHESIZE),ICASHE2(CASHESIZE),DCASHE(CASHESIZE))
   ALLOCATE(WR1(NCGS,NCGS,NCGS),WR2(NCGS,NCGS,NCGS))
   OPEN(31,FILE=TRIM(COPTN(1))//'.ao.00000',FORM='UNFORMATTED')
   DO MOP=1,IALL(0,0,0)-IVIRTCORE
    REWIND(31)
    ICOUNT=0
    WR1=0.0D0
    DO
     READ(31,IOSTAT=EOF) ICASHE1X,ICASHE1Y,ICASHE1Z,ICASHE2,DCASHE
     IF (EOF /= 0) EXIT
     DO ICASHECOUNT=1,CASHESIZE
      IF (ICASHE1X(ICASHECOUNT) == -1) EXIT
      ICOUNT=ICOUNT+1
      I=0
      J=0
      K=0
      L=0
      CALL MVBITS(ICASHE2(ICASHECOUNT),24,8,I,0)
      CALL MVBITS(ICASHE2(ICASHECOUNT),16,8,J,0)
      CALL MVBITS(ICASHE2(ICASHECOUNT), 8,8,K,0)
      CALL MVBITS(ICASHE2(ICASHECOUNT), 0,8,L,0)
      WR1(I,J,K)=WR1(I,J,K)+DREAL(CO(L,MOP,0,0,0))*DCASHE(ICASHECOUNT)
     ENDDO
    ENDDO
    DO J=1,NCGS
     DO I=1,NCGS
      DO MOQ=1,IALL(0,0,0)-IVIRTCORE
       WR2(I,J,MOQ)=0.0D0
       DO K=1,NCGS
        WR2(I,J,MOQ)=WR2(I,J,MOQ)+DREAL(CO(K,MOQ,0,0,0))*WR1(I,J,K)
       ENDDO
      ENDDO
     ENDDO
    ENDDO
    DO MOQ=1,IALL(0,0,0)-IVIRTCORE
     DO I=1,NCGS
      DO MOR=1,IALL(0,0,0)-IVIRTCORE
       WR1(I,MOR,MOQ)=0.0D0
       DO J=1,NCGS
        WR1(I,MOR,MOQ)=WR1(I,MOR,MOQ)+DREAL(CO(J,MOR,0,0,0))*WR2(I,J,MOQ)
       ENDDO
      ENDDO
     ENDDO
    ENDDO
    DO MOQ=1,IALL(0,0,0)-IVIRTCORE
     DO MOR=1,IALL(0,0,0)-IVIRTCORE
      DO MOS=1,IALL(0,0,0)-IVIRTCORE
       G(MOS,MOR,MOQ,MOP)=0.0D0
       DO I=1,NCGS
        G(MOS,MOR,MOQ,MOP)=G(MOS,MOR,MOQ,MOP)+DREAL(CO(I,MOS,0,0,0))*WR1(I,MOR,MOQ)
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDDO
   CLOSE(31)
   DEALLOCATE(ICASHE1X,ICASHE1Y,ICASHE1Z,ICASHE2,DCASHE)
   DEALLOCATE(WR1,WR2)

!do moi=icore+1,iall(0,0,0)-ivirtcore
!do moj=icore+1,iall(0,0,0)-ivirtcore
! write(*,'(2i3,A,3F20.10)') moi,moj,"(II|JJ), (IJ|JI) =",G(moi,moi,moj,moj), G(moi,moj,moj,moi), &
! G(moi,moi,moj,moj)-G(moi,moj,moj,moi)
!enddo
!enddo

   EMP2=0.0D0
   DO MOI=ICORE+1,IOCC
    DO MOA=IOCC+1,IALL(0,0,0)-IVIRTCORE
     DO MOJ=ICORE+1,IOCC
      DO MOB=IOCC+1,IALL(0,0,0)-IVIRTCORE
       EMP2=EMP2+(2.0D0*G(MOI,MOA,MOJ,MOB)**2-G(MOI,MOA,MOJ,MOB)*G(MOI,MOB,MOJ,MOA)) &
       /(EPSILON(MOI,0,0,0)+EPSILON(MOJ,0,0,0)-EPSILON(MOA,0,0,0)-EPSILON(MOB,0,0,0))
      ENDDO
     ENDDO
    ENDDO
   ENDDO
   WRITE(6,'(A,F20.15,A)') 'MP2 ENERGY = ',EMP2,' HARTREE'

   RETURN
END SUBROUTINE



SUBROUTINE GENERATE_CONFIGURATIONS(ORDER)
! GENERATE CONFIGURATIONS FOR ALPHA ELECTRONS

   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI
   
   IMPLICIT NONE
   INTEGER :: ORDER
   INTEGER :: I,J,K,L,M
   INTEGER :: I1,I2,I3,I4,I5,I6,I7,I8
   INTEGER(4) :: ICF
   INTEGER :: COMBINATION
   REAL :: MEM
   
   WRITE(6,'(A,I3)') 'GENERATE AND ORDER CONFIGURATIONS FOR ALPHA (BETA) ELECTRONS UP TO EXCITATION ',ORDER
   IF (ICORE > 0) WRITE(6,'(A,I3,A)') '***** WARNING : ',ICORE,' CORE ALPHA (BETA) ORBITALS ARE ALWAYS OCCUPIED'
   IF (IVIRTCORE > 0) WRITE(6,'(A,I3,A)') '***** WARNING : ', &
   IVIRTCORE,' HIGHEST VIRTUAL ALPHA (BETA) ORBITALS ARE ALWAYS UNOCCUPIED'
   WRITE(6,'(A,I3,A,I3,A)') 'THERE ARE ',IOCC-ICORE,' ACTIVE ALPHA (BETA) ELECTRONS IN ', &
                                         IALL(0,0,0)-ICORE-IVIRTCORE,' ACTIVE ORBITALS'
   MEM=4.0*2**(IALL(0,0,0)-IVIRTCORE)
   IF (MEM > 1000000.0) WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE FOR THE ALPHA (BETA) STRINGS WILL BE ',MEM/1000000.0,' MB'
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY') 
   NCF=0
   DO I=0,ORDER
    J=COMBINATION(IOCC-ICORE,IOCC-ICORE-I)*COMBINATION(IALL(0,0,0)-IOCC-IVIRTCORE,I)
    WRITE(6,'(I4,A,I7)') I,' EXCITATION CONFIGURATIONS ',J
    NCF=NCF+J
   ENDDO
   WRITE(6,'(A,I7)') '    TOTAL ALPHA (BETA) CONFIGURATIONS ',NCF
   IF (NCF /= COMBINATION(IALL(0,0,0)-ICORE-IVIRTCORE,IOCC-ICORE)) CALL PABORT('AN ERROR OCCURRED IN COMBINATION FUNCTION')

   ALLOCATE(CFHALF(NCF),NORDER(NCF),ADDRSS(2**(IALLMAX-IVIRTCORE)))
   ADDRSS=0
   I=0
   IF (IOCC == 1) THEN
    DO I1=1,IALL(0,0,0)-IVIRTCORE
     J=0
     IF (I1 <= ICORE) J=J+1
     IF (J /= ICORE) CYCLE
     J=0
     IF (I1 > IOCC) J=J+1
     IF (J > ORDER) CYCLE
     ICF=0
     ICF=IBSET(ICF,I1-1) 
     I=I+1
     CFHALF(I)=ICF
     NORDER(I)=J
     ADDRSS(ICF)=I
     IF (NORDER(I) <= 2) THEN
      K=0
      DO L=0,IALL(0,0,0)-IVIRTCORE-1
       IF (BTEST(ICF,L)) K=K+10**L
       IF (BTEST(ICF,L)) M=L
      ENDDO
      IF (M < 10) WRITE(6,'(I6,A,I3,A,I6,A,I12)') I,' ORDER = ',J,' CF(NUM) = ',ICF,' CF(BIT) = ',K
     ENDIF
    ENDDO
   ELSE IF (IOCC == 2) THEN
    DO I1=1,IALL(0,0,0)-IVIRTCORE-1
     DO I2=I1+1,IALL(0,0,0)-IVIRTCORE
      J=0
      IF (I1 <= ICORE) J=J+1
      IF (I2 <= ICORE) J=J+1
      IF (J /= ICORE) CYCLE
      J=0
      IF (I1 > IOCC) J=J+1
      IF (I2 > IOCC) J=J+1
      IF (J > ORDER) CYCLE
      ICF=0
      ICF=IBSET(ICF,I1-1) 
      ICF=IBSET(ICF,I2-1) 
      I=I+1
      CFHALF(I)=ICF
      NORDER(I)=J
      ADDRSS(ICF)=I
      IF (NORDER(I) <= 2) THEN
       K=0
       DO L=0,IALL(0,0,0)-IVIRTCORE-1
        IF (BTEST(ICF,L)) K=K+10**L
        IF (BTEST(ICF,L)) M=L
       ENDDO
       IF (M < 10) WRITE(6,'(I6,A,I3,A,I6,A,I12)') I,' ORDER = ',J,' CF(NUM) = ',ICF,' CF(BIT) = ',K
      ENDIF
     ENDDO
    ENDDO
   ELSE IF (IOCC == 3) THEN
    DO I1=1,IALL(0,0,0)-IVIRTCORE-2
     DO I2=I1+1,IALL(0,0,0)-IVIRTCORE-1
      DO I3=I2+1,IALL(0,0,0)-IVIRTCORE
       J=0
       IF (I1 <= ICORE) J=J+1
       IF (I2 <= ICORE) J=J+1
       IF (I3 <= ICORE) J=J+1
       IF (J /= ICORE) CYCLE
       J=0
       IF (I1 > IOCC) J=J+1
       IF (I2 > IOCC) J=J+1
       IF (I3 > IOCC) J=J+1
       IF (J > ORDER) CYCLE
       ICF=0
       ICF=IBSET(ICF,I1-1) 
       ICF=IBSET(ICF,I2-1) 
       ICF=IBSET(ICF,I3-1) 
       I=I+1
       CFHALF(I)=ICF
       NORDER(I)=J
       ADDRSS(ICF)=I
       IF (NORDER(I) <= 2) THEN
        K=0
        DO L=0,IALL(0,0,0)-IVIRTCORE-1
         IF (BTEST(ICF,L)) K=K+10**L
         IF (BTEST(ICF,L)) M=L
        ENDDO
        IF (M < 10) WRITE(6,'(I6,A,I3,A,I6,A,I12)') I,' ORDER = ',J,' CF(NUM) = ',ICF,' CF(BIT) = ',K
       ENDIF
      ENDDO
     ENDDO
    ENDDO
   ELSE IF (IOCC == 4) THEN
    DO I1=1,IALL(0,0,0)-IVIRTCORE-3
     DO I2=I1+1,IALL(0,0,0)-IVIRTCORE-2
      DO I3=I2+1,IALL(0,0,0)-IVIRTCORE-1
       DO I4=I3+1,IALL(0,0,0)-IVIRTCORE
        J=0
        IF (I1 <= ICORE) J=J+1
        IF (I2 <= ICORE) J=J+1
        IF (I3 <= ICORE) J=J+1
        IF (I4 <= ICORE) J=J+1
        IF (J /= ICORE) CYCLE
        J=0
        IF (I1 > IOCC) J=J+1
        IF (I2 > IOCC) J=J+1
        IF (I3 > IOCC) J=J+1
        IF (I4 > IOCC) J=J+1
        IF (J > ORDER) CYCLE
        ICF=0
        ICF=IBSET(ICF,I1-1) 
        ICF=IBSET(ICF,I2-1) 
        ICF=IBSET(ICF,I3-1) 
        ICF=IBSET(ICF,I4-1) 
        I=I+1
        CFHALF(I)=ICF
        NORDER(I)=J
        ADDRSS(ICF)=I
        IF (NORDER(I) <= 2) THEN
         K=0
         DO L=0,IALL(0,0,0)-IVIRTCORE-1
          IF (BTEST(ICF,L)) K=K+10**L
          IF (BTEST(ICF,L)) M=L
         ENDDO
         IF (M < 10) WRITE(6,'(I6,A,I3,A,I6,A,I12)') I,' ORDER = ',J,' CF(NUM) = ',ICF,' CF(BIT) = ',K
        ENDIF
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ELSE IF (IOCC == 5) THEN
    DO I1=1,IALL(0,0,0)-IVIRTCORE-4
     DO I2=I1+1,IALL(0,0,0)-IVIRTCORE-3
      DO I3=I2+1,IALL(0,0,0)-IVIRTCORE-2
       DO I4=I3+1,IALL(0,0,0)-IVIRTCORE-1
        DO I5=I4+1,IALL(0,0,0)-IVIRTCORE
         J=0
         IF (I1 <= ICORE) J=J+1
         IF (I2 <= ICORE) J=J+1
         IF (I3 <= ICORE) J=J+1
         IF (I4 <= ICORE) J=J+1
         IF (I5 <= ICORE) J=J+1
         IF (J /= ICORE) CYCLE
         J=0
         IF (I1 > IOCC) J=J+1
         IF (I2 > IOCC) J=J+1
         IF (I3 > IOCC) J=J+1
         IF (I4 > IOCC) J=J+1
         IF (I5 > IOCC) J=J+1
         IF (J > ORDER) CYCLE
         ICF=0
         ICF=IBSET(ICF,I1-1) 
         ICF=IBSET(ICF,I2-1) 
         ICF=IBSET(ICF,I3-1) 
         ICF=IBSET(ICF,I4-1) 
         ICF=IBSET(ICF,I5-1) 
         I=I+1
         CFHALF(I)=ICF
         NORDER(I)=J
         ADDRSS(ICF)=I
         IF (NORDER(I) <= 2) THEN
          K=0
          DO L=0,IALL(0,0,0)-IVIRTCORE-1
           IF (BTEST(ICF,L)) K=K+10**L
           IF (BTEST(ICF,L)) M=L
          ENDDO
          IF (M < 10) WRITE(6,'(I6,A,I3,A,I6,A,I12)') I,' ORDER = ',J,' CF(NUM) = ',ICF,' CF(BIT) = ',K
         ENDIF
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ELSE IF (IOCC == 6) THEN
    DO I1=1,IALL(0,0,0)-IVIRTCORE-5
     DO I2=I1+1,IALL(0,0,0)-IVIRTCORE-4
      DO I3=I2+1,IALL(0,0,0)-IVIRTCORE-3
       DO I4=I3+1,IALL(0,0,0)-IVIRTCORE-2
        DO I5=I4+1,IALL(0,0,0)-IVIRTCORE-1
         DO I6=I5+1,IALL(0,0,0)-IVIRTCORE
          J=0
          IF (I1 <= ICORE) J=J+1
          IF (I2 <= ICORE) J=J+1
          IF (I3 <= ICORE) J=J+1
          IF (I4 <= ICORE) J=J+1
          IF (I5 <= ICORE) J=J+1
          IF (I6 <= ICORE) J=J+1
          IF (J /= ICORE) CYCLE
          J=0
          IF (I1 > IOCC) J=J+1
          IF (I2 > IOCC) J=J+1
          IF (I3 > IOCC) J=J+1
          IF (I4 > IOCC) J=J+1
          IF (I5 > IOCC) J=J+1
          IF (I6 > IOCC) J=J+1
          IF (J > ORDER) CYCLE
          ICF=0
          ICF=IBSET(ICF,I1-1) 
          ICF=IBSET(ICF,I2-1) 
          ICF=IBSET(ICF,I3-1) 
          ICF=IBSET(ICF,I4-1) 
          ICF=IBSET(ICF,I5-1) 
          ICF=IBSET(ICF,I6-1) 
          I=I+1
          CFHALF(I)=ICF
          NORDER(I)=J
          ADDRSS(ICF)=I
          IF (NORDER(I) <= 2) THEN
           K=0
           DO L=0,IALL(0,0,0)-IVIRTCORE-1
            IF (BTEST(ICF,L)) K=K+10**L
            IF (BTEST(ICF,L)) M=L
           ENDDO
           IF (M < 10) WRITE(6,'(I6,A,I3,A,I6,A,I12)') I,' ORDER = ',J,' CF(NUM) = ',ICF,' CF(BIT) = ',K
          ENDIF
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ELSE IF (IOCC == 7) THEN
    DO I1=1,IALL(0,0,0)-IVIRTCORE-6
     DO I2=I1+1,IALL(0,0,0)-IVIRTCORE-5
      DO I3=I2+1,IALL(0,0,0)-IVIRTCORE-4
       DO I4=I3+1,IALL(0,0,0)-IVIRTCORE-3
        DO I5=I4+1,IALL(0,0,0)-IVIRTCORE-2
         DO I6=I5+1,IALL(0,0,0)-IVIRTCORE-1
          DO I7=I6+1,IALL(0,0,0)-IVIRTCORE
           J=0
           IF (I1 <= ICORE) J=J+1
           IF (I2 <= ICORE) J=J+1
           IF (I3 <= ICORE) J=J+1
           IF (I4 <= ICORE) J=J+1
           IF (I5 <= ICORE) J=J+1
           IF (I6 <= ICORE) J=J+1
           IF (I7 <= ICORE) J=J+1
           IF (J /= ICORE) CYCLE
           J=0
           IF (I1 > IOCC) J=J+1
           IF (I2 > IOCC) J=J+1
           IF (I3 > IOCC) J=J+1
           IF (I4 > IOCC) J=J+1
           IF (I5 > IOCC) J=J+1
           IF (I6 > IOCC) J=J+1
           IF (I7 > IOCC) J=J+1
           IF (J > ORDER) CYCLE
           ICF=0
           ICF=IBSET(ICF,I1-1) 
           ICF=IBSET(ICF,I2-1) 
           ICF=IBSET(ICF,I3-1) 
           ICF=IBSET(ICF,I4-1) 
           ICF=IBSET(ICF,I5-1) 
           ICF=IBSET(ICF,I6-1) 
           ICF=IBSET(ICF,I7-1) 
           I=I+1
           CFHALF(I)=ICF
           NORDER(I)=J
           ADDRSS(ICF)=I
           IF (NORDER(I) <= 2) THEN
            K=0
            DO L=0,IALL(0,0,0)-IVIRTCORE-1
             IF (BTEST(ICF,L)) K=K+10**L
             IF (BTEST(ICF,L)) M=L
            ENDDO
            IF (M < 10) WRITE(6,'(I6,A,I3,A,I6,A,I12)') I,' ORDER = ',J,' CF(NUM) = ',ICF,' CF(BIT) = ',K
           ENDIF
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ELSE IF (IOCC == 8) THEN
    DO I1=1,IALL(0,0,0)-IVIRTCORE-7
     DO I2=I1+1,IALL(0,0,0)-IVIRTCORE-6
      DO I3=I2+1,IALL(0,0,0)-IVIRTCORE-5
       DO I4=I3+1,IALL(0,0,0)-IVIRTCORE-4
        DO I5=I4+1,IALL(0,0,0)-IVIRTCORE-3
         DO I6=I5+1,IALL(0,0,0)-IVIRTCORE-2
          DO I7=I6+1,IALL(0,0,0)-IVIRTCORE-1
           DO I8=I7+1,IALL(0,0,0)-IVIRTCORE
            J=0
            IF (I1 <= ICORE) J=J+1
            IF (I2 <= ICORE) J=J+1
            IF (I3 <= ICORE) J=J+1
            IF (I4 <= ICORE) J=J+1
            IF (I5 <= ICORE) J=J+1
            IF (I6 <= ICORE) J=J+1
            IF (I7 <= ICORE) J=J+1
            IF (I8 <= ICORE) J=J+1
            IF (J /= ICORE) CYCLE
            J=0
            IF (I1 > IOCC) J=J+1
            IF (I2 > IOCC) J=J+1
            IF (I3 > IOCC) J=J+1
            IF (I4 > IOCC) J=J+1
            IF (I5 > IOCC) J=J+1
            IF (I6 > IOCC) J=J+1
            IF (I7 > IOCC) J=J+1
            IF (I8 > IOCC) J=J+1
            IF (J > ORDER) CYCLE
            ICF=0
            ICF=IBSET(ICF,I1-1) 
            ICF=IBSET(ICF,I2-1) 
            ICF=IBSET(ICF,I3-1) 
            ICF=IBSET(ICF,I4-1) 
            ICF=IBSET(ICF,I5-1) 
            ICF=IBSET(ICF,I6-1) 
            ICF=IBSET(ICF,I7-1) 
            ICF=IBSET(ICF,I8-1) 
            I=I+1
            CFHALF(I)=ICF
            NORDER(I)=J
            ADDRSS(ICF)=I
            IF (NORDER(I) <= 2) THEN
             K=0
             DO L=0,IALL(0,0,0)-IVIRTCORE-1
              IF (BTEST(ICF,L)) K=K+10**L
              IF (BTEST(ICF,L)) M=L
             ENDDO
             IF (M < 10) WRITE(6,'(I6,A,I3,A,I6,A,I12)') I,' ORDER = ',J,' CF(NUM) = ',ICF,' CF(BIT) = ',K
            ENDIF
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ELSE
    CALL PABORT('NUMBER OF ELECTRONS IS TOO LARGE')
   ENDIF

   RETURN
END SUBROUTINE



SUBROUTINE HAMILTONIAN_PRODUCT(INFILE,OUTFILE,OFFSET,ORDER)
! FORM THE PRODUCT OF HAMILTONIAN MATRIX AND A TRIAL VECTOR IN INFILE
! AND STORE THE PRODUCT VECTOR IN OUTFILE.  THE ORDER LIMITS THE NUMBER
! EXCITATION IN THE CONFIGURATIONS CONSIDERED.

   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI
   
   IMPLICIT NONE
   INTEGER :: INFILE,OUTFILE,OFFSET
   INTEGER :: ORDER
   INTEGER :: ISIGN,JSIGN
   INTEGER :: I,J,K,L,M
   INTEGER :: IA,IB,IC,ID
   INTEGER :: NIA,NIB,NJA,NJB
   INTEGER(4) :: CFONE,CFTWO
   REAL :: MEM,ICPUS,ICPUE,EST
   DOUBLE PRECISION :: X,Y
   DOUBLE PRECISION,ALLOCATABLE :: TRL(:,:),PRD(:,:)
   INTEGER,ALLOCATABLE :: PSIGN(:,:,:),PADRS(:,:,:)

   CALL CPU_TIME(ICPUS)
   MEM=16.0*2.0*NCF**2+4.0*2.0*NCF*(IALL(0,0,0)-IVIRTCORE)**2
!  IF (MEM > 1000000.0) THEN
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000.0,' MB'
!  ELSE IF (MEM > 1000.0) THEN
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000.0,' KB'
!  ELSE
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM,' B'
!  ENDIF
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY') 

   ALLOCATE(TRL(NCF,NCF),PRD(NCF,NCF),PSIGN(IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE,NCF),PADRS(IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE,NCF))

   ! READ TRIAL VECTOR FROM FILE INFILE
   REWIND(INFILE)
   DO I=0,OFFSET
    READ(INFILE) TRL
   ENDDO

   ! ZERO SCRATCH TRIAL VECTOR ELEMENTS ASSOCIATED WITH EXCITATION HIGHER THAN THE ORDER
   DO IA=1,NCF
    DO IB=1,NCF
     IF (NORDER(IA)+NORDER(IB) > ORDER) TRL(IA,IB)=0.0D0
    ENDDO
   ENDDO

   ! ZERO SCRATCH PRODUCT VECTOR
   PRD=0.0D0

   ! DIAGONAL ELEMENTS
   DO IA=1,NCF
    DO IB=1,NCF
     DO I=1,IALL(0,0,0)-IVIRTCORE
      IF (BTEST(CFHALF(IA),I-1)) PRD(IB,IA)=PRD(IB,IA)+H(I,I)*TRL(IB,IA)
      IF (BTEST(CFHALF(IB),I-1)) PRD(IB,IA)=PRD(IB,IA)+H(I,I)*TRL(IB,IA)
     ENDDO
    ENDDO
   ENDDO
   DO IA=1,NCF
    DO IB=1,NCF
     DO I=1,IALL(0,0,0)-IVIRTCORE
      DO J=1,IALL(0,0,0)-IVIRTCORE
       IF (BTEST(CFHALF(IA),I-1)) THEN
        NIA=1
       ELSE
        NIA=0
       ENDIF
       IF (BTEST(CFHALF(IA),J-1)) THEN
        NJA=1
       ELSE
        NJA=0
       ENDIF
       IF (BTEST(CFHALF(IB),I-1)) THEN
        NIB=1
       ELSE
        NIB=0
       ENDIF
       IF (BTEST(CFHALF(IB),J-1)) THEN
        NJB=1
       ELSE
        NJB=0
       ENDIF
       PRD(IB,IA)=PRD(IB,IA)+0.5D0*(DFLOAT((NJA+NJB)*(NIA+NIB))*G(J,J,I,I) &
                 -DFLOAT(NJA*NIA+NJB*NIB)*G(J,I,I,J))*TRL(IB,IA)
      ENDDO
     ENDDO
    ENDDO
   ENDDO

   ! ONE SPIN ORBITAL DIFFERENCE
   DO IA=1,NCF
    DO I=ICORE+1,IALL(0,0,0)-IVIRTCORE
     IF (BTEST(CFHALF(IA),I-1)) THEN
      DO K=1,IALL(0,0,0)-IVIRTCORE
       IF (.NOT.(BTEST(CFHALF(IA),K-1))) THEN
        CFONE=IBCLR(CFHALF(IA),I-1)
        CFONE=IBSET(CFONE,K-1)
        IC=ADDRSS(CFONE)
        X=H(K,I)
        DO J=1,IALL(0,0,0)-IVIRTCORE
         IF ((J /= I).AND.(J /= K).AND.(BTEST(CFONE,J-1))) X=X+G(K,I,J,J)-G(K,J,J,I)
        ENDDO
        ISIGN=1
        DO J=1,I
         IF (BTEST(CFHALF(IA),J-1)) ISIGN=-ISIGN
        ENDDO
        DO J=1,K
         IF (BTEST(CFONE,J-1)) ISIGN=-ISIGN
        ENDDO
        DO IB=1,NCF
         IF (DABS(TRL(IB,IA)) > 1.0D-15) THEN
          Y=0.0D0
          DO J=1,IALL(0,0,0)-IVIRTCORE
           IF (BTEST(CFHALF(IB),J-1)) Y=Y+G(K,I,J,J)
          ENDDO
          PRD(IB,IC)=PRD(IB,IC)+(X+Y)*DFLOAT(ISIGN)*TRL(IB,IA)
         ENDIF
        ENDDO
       ENDIF
      ENDDO
     ENDIF
    ENDDO
   ENDDO
   DO IB=1,NCF
    DO I=ICORE+1,IALL(0,0,0)-IVIRTCORE
     IF (BTEST(CFHALF(IB),I-1)) THEN
      DO K=1,IALL(0,0,0)-IVIRTCORE
       IF (.NOT.(BTEST(CFHALF(IB),K-1))) THEN
        CFONE=IBCLR(CFHALF(IB),I-1)
        CFONE=IBSET(CFONE,K-1)
        IC=ADDRSS(CFONE)
        X=H(K,I)
        DO J=1,IALL(0,0,0)-IVIRTCORE
         IF ((J /= I).AND.(J /= K).AND.(BTEST(CFONE,J-1))) X=X+G(K,I,J,J)-G(K,J,J,I)
        ENDDO
        ISIGN=1
        DO J=1,I
         IF (BTEST(CFHALF(IB),J-1)) ISIGN=-ISIGN
        ENDDO
        DO J=1,K
         IF (BTEST(CFONE,J-1)) ISIGN=-ISIGN
        ENDDO
        DO IA=1,NCF
         IF (DABS(TRL(IB,IA)) > 1.0D-15) THEN
          Y=0.0D0
          DO J=1,IALL(0,0,0)-IVIRTCORE
           IF (BTEST(CFHALF(IA),J-1)) Y=Y+G(K,I,J,J)
          ENDDO
          PRD(IC,IA)=PRD(IC,IA)+(X+Y)*DFLOAT(ISIGN)*TRL(IB,IA)
         ENDIF
        ENDDO
       ENDIF
      ENDDO
     ENDIF
    ENDDO
   ENDDO

   ! TWO SPIN ORBITAL DIFFERENCES, CASE 1
   DO IA=1,NCF
    DO I=ICORE+1,IALL(0,0,0)-IVIRTCORE-1
     IF (BTEST(CFHALF(IA),I-1)) THEN
      DO J=I+1,IALL(0,0,0)-IVIRTCORE
       IF (BTEST(CFHALF(IA),J-1)) THEN
        DO K=1,IALL(0,0,0)-IVIRTCORE-1
         IF (.NOT.(BTEST(CFHALF(IA),K-1))) THEN
          DO L=K+1,IALL(0,0,0)-IVIRTCORE
           IF (.NOT.(BTEST(CFHALF(IA),L-1))) THEN
            CFTWO=IBCLR(CFHALF(IA),I-1)
            CFTWO=IBCLR(CFTWO,J-1)
            CFTWO=IBSET(CFTWO,K-1)
            CFTWO=IBSET(CFTWO,L-1)
            IC=ADDRSS(CFTWO)
            ISIGN=1
            DO M=1,I
             IF (BTEST(CFHALF(IA),M-1)) ISIGN=-ISIGN
            ENDDO
            DO M=1,J
             IF (BTEST(CFHALF(IA),M-1)) ISIGN=-ISIGN
            ENDDO
            DO M=1,K
             IF (BTEST(CFTWO,M-1)) ISIGN=-ISIGN
            ENDDO
            DO M=1,L
             IF (BTEST(CFTWO,M-1)) ISIGN=-ISIGN
            ENDDO
            Y=DFLOAT(ISIGN)*(G(K,I,L,J)-G(K,J,L,I))
            DO IB=1,NCF
             PRD(IB,IC)=PRD(IB,IC)+Y*TRL(IB,IA)
            ENDDO
           ENDIF
          ENDDO
         ENDIF
        ENDDO
       ENDIF
      ENDDO
     ENDIF
    ENDDO
   ENDDO
   DO IB=1,NCF
    DO I=ICORE+1,IALL(0,0,0)-IVIRTCORE-1
     IF (BTEST(CFHALF(IB),I-1)) THEN
      DO J=I+1,IALL(0,0,0)-IVIRTCORE
       IF (BTEST(CFHALF(IB),J-1)) THEN
        DO K=1,IALL(0,0,0)-IVIRTCORE-1
         IF (.NOT.(BTEST(CFHALF(IB),K-1))) THEN
          DO L=K+1,IALL(0,0,0)-IVIRTCORE
           IF (.NOT.(BTEST(CFHALF(IB),L-1))) THEN
            CFTWO=IBCLR(CFHALF(IB),I-1)
            CFTWO=IBCLR(CFTWO,J-1)
            CFTWO=IBSET(CFTWO,K-1)
            CFTWO=IBSET(CFTWO,L-1)
            IC=ADDRSS(CFTWO)
            ISIGN=1
            DO M=1,I
             IF (BTEST(CFHALF(IB),M-1)) ISIGN=-ISIGN
            ENDDO
            DO M=1,J
             IF (BTEST(CFHALF(IB),M-1)) ISIGN=-ISIGN
            ENDDO
            DO M=1,K
             IF (BTEST(CFTWO,M-1)) ISIGN=-ISIGN
            ENDDO
            DO M=1,L
             IF (BTEST(CFTWO,M-1)) ISIGN=-ISIGN
            ENDDO
            Y=DFLOAT(ISIGN)*(G(K,I,L,J)-G(K,J,L,I))
            DO IA=1,NCF
             PRD(IC,IA)=PRD(IC,IA)+Y*TRL(IB,IA)
            ENDDO
           ENDIF
          ENDDO
         ENDIF
        ENDDO
       ENDIF
      ENDDO
     ENDIF
    ENDDO
   ENDDO
!  CALL CPU_TIME(ICPUE)
!  EST=ICPUE-ICPUS
!  CALL CPU_TIME(ICPUS)

   ! TWO SPIN ORBITAL DIFFERENCES, CASE 2
   DO IB=1,NCF
    DO K=ICORE+1,IALL(0,0,0)-IVIRTCORE
     DO L=1,IALL(0,0,0)-IVIRTCORE
      PSIGN(L,K,IB)=0
      PADRS(L,K,IB)=0
      IF ((BTEST(CFHALF(IB),K-1)).AND.(.NOT.(BTEST(CFHALF(IB),L-1)))) THEN
       CFONE=IBCLR(CFHALF(IB),K-1)
       CFONE=IBSET(CFONE,L-1)
       PADRS(L,K,IB)=ADDRSS(CFONE)
       JSIGN=1
       DO M=1,K
        IF (BTEST(CFHALF(IB),M-1)) JSIGN=-JSIGN
       ENDDO
       DO M=1,L
        IF (BTEST(CFONE,M-1)) JSIGN=-JSIGN
       ENDDO
       PSIGN(L,K,IB)=JSIGN
      ENDIF
     ENDDO
    ENDDO
   ENDDO
   DO IA=1,NCF
    DO I=ICORE+1,IALL(0,0,0)-IVIRTCORE
     DO J=1,IALL(0,0,0)-IVIRTCORE
      IF ((BTEST(CFHALF(IA),I-1)).AND.(.NOT.(BTEST(CFHALF(IA),J-1)))) THEN
       CFONE=IBCLR(CFHALF(IA),I-1)
       CFONE=IBSET(CFONE,J-1)
       IC=ADDRSS(CFONE)
       ISIGN=1
       DO K=1,I
        IF (BTEST(CFHALF(IA),K-1)) ISIGN=-ISIGN
       ENDDO
       DO K=1,J
        IF (BTEST(CFONE,K-1)) ISIGN=-ISIGN
       ENDDO
       DO IB=1,NCF
        IF (DABS(TRL(IB,IA)) > 1.0D-15) THEN
         DO K=ICORE+1,IALL(0,0,0)-IVIRTCORE
          DO L=1,IALL(0,0,0)-IVIRTCORE
           IF (PADRS(L,K,IB) /= 0) PRD(PADRS(L,K,IB),IC)=PRD(PADRS(L,K,IB),IC)+DFLOAT(ISIGN*PSIGN(L,K,IB))*G(J,I,L,K)*TRL(IB,IA)
          ENDDO
         ENDDO
        ENDIF
       ENDDO
      ENDIF
     ENDDO
    ENDDO
!   CALL CPU_TIME(ICPUE)
!   IF (IA == 1) THEN
!    EST=EST+(ICPUE-ICPUS)*NCF
!    EST=EST/3600.0
!    IF (EST > 0.1) WRITE(6,'(A,F20.1,A)') '***** WARNING : ESTIMATED PRODUCT FORMATION TIME = ',EST,' HOURS'
!   ENDIF
   ENDDO

   ! ZERO SCRATCH PRODUCT VECTOR ELEMENTS ASSOCIATED WITH EXCITATION HIGHER THAN THE ORDER
   DO IA=1,NCF
    DO IB=1,NCF
     IF (NORDER(IA)+NORDER(IB) > ORDER) PRD(IA,IB)=0.0D0
    ENDDO
   ENDDO

   ! STORE PRODUCT VECTOR
   REWIND(OUTFILE)
   IF (OFFSET == 0) THEN
    WRITE(OUTFILE) PRD
   ELSE
    DO I=1,OFFSET
     READ(OUTFILE) TRL
    ENDDO
    WRITE(OUTFILE) PRD
   ENDIF

   DEALLOCATE(TRL,PRD,PSIGN,PADRS)

   RETURN
END SUBROUTINE



SUBROUTINE FULL_HAMILTONIAN(ORDER)
! FORM FULL HAMILTONIAN AND DIAGONALIZE IT BY THE STANDARD ALGORITHMS.
! CAUTION, DO NOT USE THIS FOR PRODUCTION RUN.

   USE CONTROL
   USE GRADIENT
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI

   IMPLICIT NONE
   LOGICAL,PARAMETER :: LEVEC = .FALSE.
   INTEGER :: ORDER
   INTEGER :: IA,IB,IC,ID
   INTEGER :: INFO
   INTEGER :: NDIM
   REAL :: MEM,DEV
   DOUBLE PRECISION,ALLOCATABLE :: HAM(:,:)
   DOUBLE PRECISION,ALLOCATABLE :: VL(:,:),VR(:,:),ER(:),EI(:),WK(:)
   DOUBLE PRECISION,ALLOCATABLE :: TRL(:,:)

   WRITE(6,'(I2,A)') ORDER,'-ORDER CONFIGURATION INTERACTION CALCULATION FOR ALL STATES'
   IF (LEVEC) THEN 
    MEM=16.0*(2.0*NCF**4+8.0*NCF**2)
   ELSE
    MEM=16.0*(NCF**4+9.0*NCF**2)
   ENDIF
   IF (MEM > 1000000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000.0,' MB'
   ELSE IF (MEM > 1000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000.0,' KB'
   ELSE
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM,' B'
   ENDIF
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY') 
   ALLOCATE(HAM(NCF**2,NCF**2),VL(1,NCF**2),ER(NCF**2),EI(NCF**2),WK(4*NCF**2))
   IF (LEVEC) THEN 
    ALLOCATE(VR(NCF**2,NCF**2))
   ELSE
    ALLOCATE(VR(1,NCF**2))
   ENDIF
   ALLOCATE(TRL(NCF,NCF))

   OPEN(50,FILE=TRIM(COPTN(1))//'.fi0',FORM='UNFORMATTED')
   OPEN(60,FILE=TRIM(COPTN(1))//'.fo0',FORM='UNFORMATTED')

   ! FORM HAMILTONIAN AND KEEP IT IN MEMORY
   NDIM=0
   DO IA=1,NCF
    DO IB=1,NCF
     TRL=0.0D0
     IF (NORDER(IA)+NORDER(IB) <= ORDER) TRL(IA,IB)=1.0D0
     IF (NORDER(IA)+NORDER(IB) <= ORDER) NDIM=NDIM+1
     REWIND(50)
     WRITE(50) TRL
     CALL HAMILTONIAN_PRODUCT(50,60,0,ORDER)
     REWIND(60)
     READ(60) TRL
     DO IC=1,NCF
      DO ID=1,NCF
       HAM((IA-1)*NCF+IB,(IC-1)*NCF+ID)=TRL(IC,ID)
      ENDDO
     ENDDO
    ENDDO
   ENDDO
   IF (NDIM==NCF**2) WRITE(6,'(A)') 'FULL CI LIMIT IS REACHED'
!  CALL DUMP5(HAM,NCF**2)

   DEALLOCATE(TRL)
   CLOSE(50)
   CLOSE(60)

   ! DIAGONALIZE HAMILTONIAN
   IF (LEVEC) THEN
    CALL DGEEV('N','V',NCF**2,HAM,NCF**2,ER,EI,VL,1,VR,NCF**2,WK,4*NCF**2,INFO)
    IF (INFO /= 0) CALL PABORT('DGEEV FAILED TO DIAGONALIZE A MATRIX')
    DEV=0.0D0
    DO IA=1,NCF**2
     DEV=DEV+EI(IA)**2
    ENDDO
    IF (DEV > 1.0E-5) THEN
     CALL WARNING('THE IMAGINARY PART OF THE EIGENVALUES IS NOT ZERO')
     WRITE(6,'(A,F20.10)') 'DEVIATION = ',DEV
    ENDIF
    CALL PIKSRT(NCF**2,NCF**2,ER,VR,EI)
    DO IA=1,MIN(25,NCF**2)
     WRITE(6,'(A,I3,A,F20.15,A)') 'STATE ',IA,' ENERGY = ', ER(IA)+NUCLEAR_REPULSION,' HARTREE'
     DO IB=1,NCF**2
      WRITE(6,'(I10,F20.15)') IB,VR(IB,IA)
     ENDDO
    ENDDO
   ELSE
    CALL DGEEV('N','N',NCF**2,HAM,NCF**2,ER,EI,VL,1,VR,1,WK,4*NCF**2,INFO)
    IF (INFO /= 0) CALL PABORT('DGEEV FAILED TO DIAGONALIZE A MATRIX')
    DEV=0.0D0
    DO IA=1,NCF**2
     DEV=DEV+EI(IA)**2
    ENDDO
    IF (DEV > 1.0E-5) THEN
     CALL WARNING('THE IMAGINARY PART OF THE EIGENVALUES IS NOT ZERO')
     WRITE(6,'(A,F20.10)') 'DEVIATION = ',DEV
    ENDIF
    CALL PIKSRT_EVALONLY(NCF**2,ER)
    DO IA=1,NDIM
     WRITE(6,'(A,I3,A,F20.15,A)') 'STATE ',IA,' ENERGY = ', ER(IA)+NUCLEAR_REPULSION,' HARTREE'
    ENDDO
   ENDIF
   CALL PFLUSH(6)

   DEALLOCATE(HAM,VL,VR,ER,EI,WK)

   RETURN
END SUBROUTINE



SUBROUTINE HIGHORDER_MP(ORDER)
! PERFORM HIGH-ORDER MOELLER-PLESSET PERTURBATION CALCULATIONS IN A RECURSIVE ALGORITHM.

   USE CONTROL
   USE GRADIENT
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI

   IMPLICIT NONE
   LOGICAL,PARAMETER :: LTOMONAGA = .FALSE.
   DOUBLE PRECISION,ALLOCATABLE :: TEPS(:)
   INTEGER,PARAMETER :: MAXFILE = 100
   INTEGER :: ORDER
   INTEGER :: MOI,MOJ
   INTEGER :: IA,IB
   INTEGER :: I,J,K,IFILE
   REAL :: MEM,ICPUS,ICPUE
   DOUBLE PRECISION :: HA,HB
   DOUBLE PRECISION :: EMP,DMP(0:ORDER)
   DOUBLE PRECISION,ALLOCATABLE :: VEC1(:,:),VEC2(:,:)

   CALL CPU_TIME(ICPUS)
   WRITE(6,'(A)') 'RECURSIVE HIGH-ORDER MOELLER-PLESSET PERTURBATION CALCULATIONS WILL BE PERFORMED'
   MEM=16.0*2.0*NCF**2
   IF (MEM > 1000000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000.0,' MB'
   ELSE IF (MEM > 1000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000.0,' KB'
   ELSE
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM,' B'
   ENDIF
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY') 
   WRITE(6,'(A)') '---------------------------------------------------------------------------'
   WRITE(6,'(A)') 'ORDER      CORRELATION          CORRELATION        TOTAL ENERGY   CPU / SEC'
   ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF))
   OPEN(50,FILE=TRIM(COPTN(1))//'.fi0',FORM='UNFORMATTED')
   OPEN(51,FILE=TRIM(COPTN(1))//'.fo0',FORM='UNFORMATTED')

   ALLOCATE(TEPS(1:IALL(0,0,0)))
   TEPS(:)=EPSILON(:,0,0,0)
   IF (LTOMONAGA) THEN
    TEPS(1)=-7.25520068332896D0
    TEPS(2)=-0.55477505254599D0
    TEPS(3)=-0.25684391057854D0
    TEPS(4)=0.27479957146139D0
    TEPS(5)=0.27479957146139D0
    WRITE(6,'(A)') '*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!'
    WRITE(6,'(A)') '                  TOMONAGA RENORMALIZATION IS BEING APPLIED '
    DO I=1,IALL(0,0,0)
     WRITE(6,'(A,I2,A,F20.10)') '                  EPSILON(',I,') = ',TEPS(I)
    ENDDO
    WRITE(6,'(A)') '*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!*!'
   ENDIF

   ! INITIALIZE WAVEFUNCTION TO THE HARTREE-FOCK DETERMINANT
   EMP=NUCLEAR_REPULSION
   VEC1=0.0D0
   VEC1(1,1)=1.0D0
   REWIND(50)
   WRITE(50) VEC1

   ! DMP(0)
   DMP(0)=0.0D0
   DO MOI=1,IOCC
    DMP(0)=DMP(0)+2.0D0*TEPS(MOI)
   ENDDO
   EMP=EMP+DMP(0)
   WRITE(6,'(I2,F20.10,A,F20.10,F12.1)') 0,DMP(0),'     ---------------',EMP,0.0
   MP_STORED(0)=EMP

   ! DMP(1) ONWARD
   DO IFILE=0,ORDER-1
    IF (IOPTN(9) >= 3) THEN
     REWIND(50)
     DO I=0,IFILE
      READ(50) VEC1
     ENDDO
     WRITE(6,'(I3,A)') IFILE,'TH ORDER WAVE FUNCTION'
     CALL DUMP5(VEC1,NCF)
    ENDIF
    DEALLOCATE(VEC1,VEC2)
    CALL HAMILTONIAN_PRODUCT(50,51,IFILE,2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE))
    ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF))
    REWIND(51)
    DO I=0,IFILE
     READ(51) VEC1
    ENDDO
    IF (IOPTN(9) >= 3) THEN
     WRITE(6,'(A,I1,A)') 'H|',IFILE,'>'
     CALL DUMP5(VEC1,NCF)
    ENDIF
!   IF (IFILE == 0) THEN
!    DMP(1)=-DMP(0)
!    DO MOI=1,IOCC
!     DMP(1)=DMP(1)+2.0D0*H(MOI,MOI)
!    ENDDO
!    DO MOI=1,IOCC
!     DO MOJ=1,IOCC
!      DMP(1)=DMP(1)+(2.0D0*G(MOI,MOI,MOJ,MOJ)-G(MOI,MOJ,MOJ,MOI))
!     ENDDO
!    ENDDO
!   ELSE
!    DMP(IFILE+1)=VEC1(1,1)
!   ENDIF
    DMP(IFILE+1)=VEC1(1,1)
    IF (IFILE == 0) DMP(1)=DMP(1)-DMP(0)
    EMP=EMP+DMP(IFILE+1)
    CALL CPU_TIME(ICPUE)
    IF (IFILE == 0) THEN
     WRITE(6,'(I2,F20.10,A,F20.10,F12.1)') IFILE+1,DMP(IFILE+1),'     ---------------',EMP,ICPUE-ICPUS
    ELSE
     WRITE(6,'(I2,F20.10,F20.10,F20.10,F12.1)') IFILE+1,DMP(IFILE+1),EMP-DMP(0)-DMP(1)-NUCLEAR_REPULSION,EMP,ICPUE-ICPUS
    ENDIF
    MP_STORED(IFILE+1)=EMP
    CALL CPU_TIME(ICPUS)
    CALL PFLUSH(6)
!   IF (DABS(DMP(IFILE+1)) < 1.0D-15) EXIT
    VEC1=-VEC1
    REWIND(50)
    DO K=0,IFILE
     READ(50) VEC2
     DO IA=1,NCF
      HA=0.0D0
      IF (K == IFILE) THEN
       DO I=1,IALL(0,0,0)-IVIRTCORE
        IF (BTEST(CFHALF(IA),I-1)) HA=HA+TEPS(I)
       ENDDO
      ENDIF
      DO IB=1,NCF
       HB=0.0D0
       IF (K == IFILE) THEN
        DO I=1,IALL(0,0,0)-IVIRTCORE
         IF (BTEST(CFHALF(IB),I-1)) HB=HB+TEPS(I)
        ENDDO
       ENDIF
       IF (K == IFILE) THEN
        VEC1(IB,IA)=VEC1(IB,IA)+(DMP(IFILE-K+1)+HA+HB)*VEC2(IB,IA)
       ELSE
        VEC1(IB,IA)=VEC1(IB,IA)+DMP(IFILE-K+1)*VEC2(IB,IA)
       ENDIF
      ENDDO
     ENDDO
    ENDDO
    DO IA=1,NCF
     HA=0.0D0
     DO I=1,IALL(0,0,0)-IVIRTCORE
      IF (BTEST(CFHALF(IA),I-1)) HA=HA+TEPS(I)
     ENDDO
     DO IB=1,NCF
      HB=0.0D0
      DO I=1,IALL(0,0,0)-IVIRTCORE
       IF (BTEST(CFHALF(IB),I-1)) HB=HB+TEPS(I)
      ENDDO
      IF ((IA == 1).AND.(IB == 1)) THEN
       VEC2(IB,IA)=0.0D0
      ELSE
       VEC2(IB,IA)=VEC1(IB,IA)/(HA+HB-DMP(0))
      ENDIF
     ENDDO
    ENDDO
    IF (IFILE == MAXFILE) EXIT
    WRITE(50) VEC2
   ENDDO

   WRITE(6,'(A)') '---------------------------------------------------------------------------'
   IF (IOPTN(97) > 0) THEN
    OPEN(97,FILE=TRIM(COPTN(1))//'.gf0',FORM='UNFORMATTED')
    REWIND(97)
    VEC2=0.0D0
    REWIND(50)
    DO I=0,ORDER-1
     READ(50) VEC1
!    VEC2=VEC2+VEC1 ! ACCUMULATE
     VEC2=VEC1      ! DO NOT ACCUMULATE
     WRITE(97) VEC2
    ENDDO
    WRITE(6,'(A,A)') 'REFERENCE WAVE FUNCTION FOR MBGF STORED IN ',TRIM(COPTN(1))//'.gf0'
   ENDIF
   CLOSE(97)

   DEALLOCATE(VEC1,VEC2,TEPS)
   CLOSE(50)
   CLOSE(51)
   RETURN
END SUBROUTINE



SUBROUTINE CI_GUESS(ORDER,NROOTS,NTRIALS)
! GENERATE INITIAL GUESS TRIAL VECTORS FOR CI CALCULATIONS.

   USE CONTROL
   USE GRADIENT
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI

   IMPLICIT NONE
   INTEGER :: ORDER
   INTEGER :: NROOTS
   INTEGER :: NTRIALS
   INTEGER :: IA,IB,JA,JB
   INTEGER :: I
   DOUBLE PRECISION :: H1,H2
   DOUBLE PRECISION,ALLOCATABLE :: H3(:)
   DOUBLE PRECISION,ALLOCATABLE :: VEC1(:,:)

   IF (NROOTS < 1) CALL PABORT('ILLEGAL NUMBER OF CI ROOTS')
   IF ((ORDER < 1).OR.(ORDER > 2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE))) CALL PABORT('ILLEGAL NUMBER OF CI ORDER')

!  WRITE(6,'(A)') 'INITIAL GUESS FOR THE CI/EOMCC VECTORS WILL BE GENERATED'

   OPEN(50,FILE=TRIM(COPTN(1))//'.fi0',FORM='UNFORMATTED')
   REWIND(50)

   ALLOCATE(H3(NCF),VEC1(NCF,NCF))
   ! CALCULATE THE SUM OF EPSILONS FOR EACH CONFIGURATION
   DO IA=1,NCF
    H3(IA)=0.0D0
    DO I=1,IALL(0,0,0)-IVIRTCORE
     IF (BTEST(CFHALF(IA),I-1)) H3(IA)=H3(IA)+EPSILON(I,0,0,0)
    ENDDO
   ENDDO

   ! DETERMINE THE THRESHOLD FOR NULLY AND SINGLY SUBSTITUTED DETERMINANTS
   DO IA=1,NCF
    DO IB=1,NCF
     VEC1(IB,IA)=H3(IB)+H3(IA)
    ENDDO
   ENDDO
   NTRIALS=0
   DO
    H1=1.0D90
    JB=0
    DO IA=1,NCF
     DO IB=1,NCF
      IF (NORDER(IA)+NORDER(IB) >= 2) CYCLE
      IF (VEC1(IB,IA) < H1) THEN
       H1=VEC1(IB,IA)
       JB=IB
       JA=IA
      ENDIF
     ENDDO
    ENDDO
    IF (JB == 0) CALL PABORT('DECREASE THE NUMBER OF ROOTS')
    NTRIALS=NTRIALS+1
    IF (((NROOTS == 1).AND.(NTRIALS == NROOTS)).OR.(NTRIALS >= NROOTS*3/2)) EXIT
    VEC1(JB,JA)=1.0D99
   ENDDO
   IF (IOPTN(9) >= 2) WRITE(6,'(A,F24.10)') 'THRESHOLD (0+1 DETERMINANTS) = ',H1

   ! DETERMINE THE THRESHOLD FOR DOUBLY AND HIGHER SUBSTITUTED DETERMINANTS
   IF (ORDER >= 2) THEN
    DO IA=1,NCF
     DO IB=1,NCF
      VEC1(IB,IA)=H3(IB)+H3(IA)
     ENDDO
    ENDDO
    NTRIALS=0
    DO
     H2=1.0D90
     JB=0
     DO IA=1,NCF
      DO IB=1,NCF
       IF ((NORDER(IA)+NORDER(IB) < 2).OR.(NORDER(IA)+NORDER(IB) > ORDER)) CYCLE
       IF (VEC1(IB,IA) < H2) THEN
        H2=VEC1(IB,IA)
        JB=IB
        JA=IA
       ENDIF
      ENDDO
     ENDDO
     IF (JB == 0) CALL PABORT('DECREASE THE NUMBER OF ROOTS')
     NTRIALS=NTRIALS+1
     IF (NTRIALS >= NROOTS/2) EXIT
     VEC1(JB,JA)=1.0D99
    ENDDO
    IF (IOPTN(9) >= 2) WRITE(6,'(A,F24.10)') 'THRESHOLD (2 DETERMINANTS)   = ',H2
   ENDIF

   ! GENERATE INITIAL TRIAL VECTORS
   NTRIALS=0
   REWIND(50)
   DO IA=1,NCF
    DO IB=1,NCF
     IF (NORDER(IA)+NORDER(IB) > ORDER) CYCLE
     IF (((H3(IB)+H3(IA) <= H1).AND.(NORDER(IA)+NORDER(IB) <= 1)).OR. &
         ((NROOTS > 1).AND.(H3(IB)+H3(IA) <= H2).AND.(NORDER(IA)+NORDER(IB) >= 2))) THEN
      NTRIALS=NTRIALS+1
      VEC1=0.0D0
      VEC1(IB,IA)=1.0D0
      WRITE(50) VEC1
     ENDIF
    ENDDO
   ENDDO 
   DEALLOCATE(H3,VEC1)
   CLOSE(50)

   RETURN
END SUBROUTINE



SUBROUTINE HIGHORDER_CI(ORDER,NROOTS,NTRIALS)
! PERFORM HIGH-ORDER CONFIGURATION INTERACTION CALCULATIONS BY THE DAVIDSON'S ALGORITHM.

   USE CONSTANTS
   USE CONTROL
   USE GRADIENT
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI

   IMPLICIT NONE
   INTEGER,PARAMETER :: MAXFILE = 1000
   INTEGER,PARAMETER :: MAXCYCLE = 1000
   INTEGER :: ICYCLE
   INTEGER :: ORDER
   INTEGER :: NROOTS
   INTEGER :: NTRIALS
   INTEGER :: NTRIALS_NEXT
   INTEGER :: IA,IB
   INTEGER :: I,J,K,L,NOPEN
   INTEGER,ALLOCATABLE :: N(:)
   REAL :: MEM,ICPUS,ICPUE
   LOGICAL :: LDONE(MAXFILE)
   DOUBLE PRECISION :: DEVSQ,DEVMAX,D,F,S2
   DOUBLE PRECISION :: SMALLH(MAXFILE,MAXFILE),LAMBDA(MAXFILE),ECI(MAXFILE)
   DOUBLE PRECISION :: W1(MAXFILE,MAXFILE),W2(MAXFILE)
   DOUBLE PRECISION :: PERCENTS,PERCENTD,EONE
   DOUBLE PRECISION,ALLOCATABLE :: VEC1(:,:),VEC2(:,:)

   CALL CPU_TIME(ICPUS)
   WRITE(6,'(A)') 'HIGH-ORDER CONFIGURATION INTERACTION CALCULATIONS WILL BE PERFORMED'
   WRITE(6,'(A,I2)') 'CI ORDER IS ',ORDER
   WRITE(6,'(A,I4)') 'THE NUMBER OF CI ROOTS SOUGHT ',NROOTS
   WRITE(6,'(A,I4)') 'THE NUMBER OF INITIAL TRIAL VECTORS ',NTRIALS
   MEM=16.0*2.0*NCF**2
   IF (MEM > 1000000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000.0,' MB'
   ELSE IF (MEM > 1000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000.0,' KB'
   ELSE
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM,' B'
   ENDIF
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY') 
   IF (NROOTS > 1) THEN
    WRITE(6,'(A)') '-----------------------------------------------------------------------'
    WRITE(6,'(A)') '             DEVIATION        OMEGA / EV       TOTAL ENERGY   CPU / SEC'
   ELSE
    WRITE(6,'(A)') '----------------------------------------------------------------'
    WRITE(6,'(A)') 'ITR    DEVIATION     CORRELATION        TOTAL ENERGY   CPU / SEC'
   ENDIF
   ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF),N(IALLMAX-IVIRTCORE))
   OPEN(50,FILE=TRIM(COPTN(1))//'.fi0',FORM='UNFORMATTED')
   OPEN(51,FILE=TRIM(COPTN(1))//'.fi1',FORM='UNFORMATTED')

   ! INITIALIZE
   LDONE=.FALSE.
   IF (NTRIALS > MAXFILE) CALL PABORT('TOO MANY INITIAL TRIAL VECTORS')

   ! DAVIDSON ITERATION
   DO ICYCLE=1,MAXCYCLE

    ! CALCULATE H|0> AND STORE IT IN FILE 70
    DO I=1,NTRIALS
     IF (.NOT.LDONE(I)) THEN
      CALL HAMILTONIAN_PRODUCT(50,51,I-1,ORDER)
      LDONE(I)=.TRUE.
     ENDIF
    ENDDO

    ! CONSTRUCT SUBSPACE HAMILTONIAN MATRIX <N|N> FOR DEBUG
!   DO I=1,NTRIALS
!    REWIND(50)
!    DO J=1,I
!     READ(50) VEC1
!    ENDDO
!    REWIND(50)
!    DO L=1,NTRIALS
!     READ(50) VEC2
!     SMALLH(I,L)=0.0D0
!     DO IA=1,NCF
!      DO IB=1,NCF
!       SMALLH(I,L)=SMALLH(I,L)+VEC1(IB,IA)*VEC2(IB,IA)
!      ENDDO
!     ENDDO
!    ENDDO
!    WRITE(6,'(I3,100F10.5:)') I,(SMALLH(I,L),L=1,NTRIALS)
!   ENDDO

    ! CONSTRUCT SUBSPACE HAMILTONIAN MATRIX <N|H|N>
    REWIND(50)
    DO I=1,NTRIALS
     READ(50) VEC1
     REWIND(51)
     DO L=1,NTRIALS
      READ(51) VEC2
      SMALLH(I,L)=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        SMALLH(I,L)=SMALLH(I,L)+VEC1(IB,IA)*VEC2(IB,IA)
       ENDDO
      ENDDO
     ENDDO
    ENDDO
      
    ! DIAGONALIZE SUBSPACE HAMILTONIAN
    IF (NTRIALS == 1) THEN
     LAMBDA(1)=SMALLH(1,1)
     W1(1,1)=1.0D0
    ELSE
     W1=SMALLH
     CALL TRED2(W1,NTRIALS,MAXFILE,LAMBDA,W2)
     CALL TQLI(LAMBDA,W2,NTRIALS,MAXFILE,W1)
     CALL PIKSRT(NTRIALS,MAXFILE,LAMBDA,W1,W2)
    ENDIF
      
    ! FORM RESIDUAL VECTORS
    CALL CPU_TIME(ICPUE)
    DEVMAX=0.0D0
    IF (NROOTS > 1) WRITE(6,'(A,I3)') ' ITER ',ICYCLE
    NTRIALS_NEXT=NTRIALS
    DO K=1,NROOTS
     VEC1=0.0D0
     REWIND(50)
     REWIND(51)
     DO L=1,NTRIALS
      READ(51) VEC2
      VEC1=VEC1+VEC2*W1(L,K)
      READ(50) VEC2
      VEC1=VEC1-LAMBDA(K)*VEC2*W1(L,K)
     ENDDO
     DEVSQ=0.0D0
     DO IA=1,NCF
      DO IB=1,NCF
       DEVSQ=DEVSQ+VEC1(IB,IA)**2
      ENDDO
     ENDDO
     DEVSQ=DSQRT(DEVSQ)
     IF (DEVSQ > DEVMAX) DEVMAX=DEVSQ
     ECI(K)=LAMBDA(K)+NUCLEAR_REPULSION
     IF (NROOTS == 1) THEN
      WRITE(6,'(I2,F15.10,F15.10,F20.10,F12.1)') ICYCLE,DEVSQ,ECI(K)-EHFKS,ECI(K),ICPUE-ICPUS
     ELSE
      WRITE(6,'(A,I3,F15.10,F15.5,F20.10,F12.1)') ' ROOT ',K,DEVSQ,(ECI(K)-ECI(1))*EV,ECI(K),(ICPUE-ICPUS)/DFLOAT(NROOTS)
     ENDIF
      
     IF (DEVSQ < DOPTN(67)) CYCLE

     ! FORM A NEW SUBSPACE VECTOR BY KNOWLES-HANDY SCHEME
     DO IA=1,NCF
      DO IB=1,NCF
       NOPEN=0
       DO I=1,IALL(0,0,0)-IVIRTCORE
        N(I)=0
        IF (BTEST(CFHALF(IA),I-1)) N(I)=1
        IF (BTEST(CFHALF(IB),I-1)) N(I)=N(I)+1
        NOPEN=NOPEN+N(I)*(2-N(I))
       ENDDO
       IF (NOPEN == 0) THEN
        F=0.0D0
       ELSE
        F=-DFLOAT(NOPEN)/DFLOAT(NOPEN*(NOPEN-1))
       ENDIF
       D=0.0D0
       DO I=1,IALL(0,0,0)-IVIRTCORE
        D=D+DFLOAT(N(I))*H(I,I)-DFLOAT(N(I)*(2-N(I)))*G(I,I,I,I)/4.0D0
        DO J=1,IALL(0,0,0)-IVIRTCORE
         D=D+DFLOAT(N(I)*N(J))*(2.0D0*G(I,I,J,J)-G(I,J,J,I))/4.0D0
         IF (I /= J) D=D-F*DFLOAT(N(I)*(2-N(I))*N(J)*(2-N(J)))*G(I,J,J,I)/4.0D0
        ENDDO
       ENDDO
       IF ((LAMBDA(K)-D) /= 0.0D0) VEC1(IB,IA)=VEC1(IB,IA)/(LAMBDA(K)-D)
      ENDDO
     ENDDO
      
     ! ORTHONORMALIZE THE NEW SUBSPACE VECTOR
     D=0.0D0
     DO IA=1,NCF
      DO IB=1,NCF
       D=D+VEC1(IB,IA)**2
      ENDDO
     ENDDO
     D=DSQRT(D)
     VEC1=VEC1/D
     REWIND(50)
     DO L=1,NTRIALS_NEXT
      READ(50) VEC2
      D=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        D=D+VEC1(IB,IA)*VEC2(IB,IA)
       ENDDO
      ENDDO
      VEC1=VEC1-D*VEC2
     ENDDO
     D=0.0D0
     DO IA=1,NCF
      DO IB=1,NCF
       D=D+VEC1(IB,IA)**2
      ENDDO
     ENDDO
     D=DSQRT(D)
     VEC1=VEC1/D
     IF (D > DOPTN(67)*100.0D0) THEN
      NTRIALS_NEXT=NTRIALS_NEXT+1
      IF (NTRIALS_NEXT > MAXFILE) CALL PABORT('TOO MANY TRIAL VECTORS')
      REWIND(50)
      DO L=1,NTRIALS_NEXT-1
       READ(50) VEC2
      ENDDO
      WRITE(50) VEC1
     ENDIF

    ENDDO

    CALL CPU_TIME(ICPUS)
    CALL PFLUSH(6)

    IF (DEVMAX < DOPTN(67)) THEN
     IF (NROOTS > 1) THEN
      WRITE(6,'(A)') '-----------------------------------------------------------------------'
     ELSE
      WRITE(6,'(A)') '----------------------------------------------------------------'
     ENDIF
     REWIND(51)
     DO I=1,NROOTS
      WRITE(6,'(A,I3,F15.10,A)') 'ROOT ',I,(ECI(I)-ECI(1))*EV,' EV'
      REWIND(50)
      VEC1=0.0D0
      DO L=1,NTRIALS
       READ(50) VEC2
       VEC1=VEC1+VEC2*W1(L,I)
      ENDDO
      D=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        D=D+VEC1(IA,IB)**2
       ENDDO
      ENDDO
      VEC1=VEC1/DSQRT(D)
      PERCENTS=0.0D0
      PERCENTD=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        IF (NORDER(IA)+NORDER(IB) == 1) PERCENTS=PERCENTS+VEC1(IA,IB)**2
        IF (NORDER(IA)+NORDER(IB) == 2) PERCENTD=PERCENTD+VEC1(IA,IB)**2
       ENDDO
      ENDDO
      WRITE(6,'(A,F6.2)') ' %SINGLES = ',PERCENTS*1.0D2
      WRITE(6,'(A,F6.2)') ' %DOUBLES = ',PERCENTD*1.0D2
      WRITE(51) VEC1
      CALL S_SQUARED(51,I-1,ORDER,S2)
      WRITE(6,'(A,F8.4)') ' < S**2 > = ',S2
! /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\ ONE-ELECTRON ENERGY CALC FROM HERE ...
!     DO IA=1,NCF
!      DO IB=1,NCF
!       IF (DABS(VEC1(IA,IB)) > 0.1D0) WRITE(6,'(A,I6,I6,A,F8.4)') ' DETERMINANT (',IA,IB,' ) ',VEC1(IA,IB)
!      ENDDO
!     ENDDO
!     CALL HAMILTONIAN0_PRODUCT(51,50,I-1,ORDER)
!     REWIND(50)
!     READ(50) VEC2
!     EONE=0.0D0
!     DO IA=1,NCF
!      DO IB=1,NCF
!       EONE=EONE+VEC1(IA,IB)*VEC2(IA,IB)
!      ENDDO
!     ENDDO
!     WRITE(6,'(A)')        ' ***********************************************'
!     WRITE(6,'(A,F25.15)') ' ONE-ELECTRON ENERGY = ',EONE
!     WRITE(6,'(A)')        ' ***********************************************'
! \/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/ ... TO HERE
      IF (IOPTN(9) == 0) THEN
       WRITE(6,'(A)') 'CI WAVEFUNCTION'
       CALL DUMP5(VEC1,NCF)
      ENDIF
      IF ((IOPTN(97) > 0).AND.(I == 1)) THEN
       OPEN(97,FILE=TRIM(COPTN(1))//'.gf0',FORM='UNFORMATTED')
       REWIND(97)
       WRITE(97) VEC1
       CLOSE(97)
       WRITE(6,'(A,A)') 'REFERENCE WAVE FUNCTION FOR MBGF STORED IN ',TRIM(COPTN(1))//'.gf0'
      ENDIF

     ENDDO
!    REWIND(50)
!    REWIND(51)
!    DO I=1,NROOTS
!     READ(51) VEC1
!     WRITE(50) VEC1
!    ENDDO
     DEALLOCATE(VEC1,VEC2,N)
     CLOSE(50)
     CLOSE(51)
     RETURN
    ELSE IF (NTRIALS_NEXT == NTRIALS) THEN
     CALL PABORT('ALGORITHM FAILED TO INCREASE THE SUBSPACE')
    ELSE IF (NROOTS > 1) THEN
     WRITE(6,'(I3,A)') NTRIALS_NEXT-NTRIALS,' TRIAL VECTORS HAVE BEEN ADDED'
    ENDIF
    NTRIALS=NTRIALS_NEXT

   ENDDO
   CALL PABORT('DAVIDSON ALGORITHM FAILED TO CONVERGE')

END SUBROUTINE



SUBROUTINE HIGHORDER_CC(ORDER,LGUESS,LDIIS,LXCC)
! PERFORM HIGH-ORDER COUPLED-CLUSTER CALCULATIONS BY DAVIDSON'S ALGORITHM.

   USE CONTROL
   USE GRADIENT
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI

   IMPLICIT NONE
   LOGICAL :: LGUESS
   LOGICAL :: LCYCLE
   LOGICAL :: LDIIS
   LOGICAL :: LXCC
   INTEGER,PARAMETER :: MAXITER=1000
   INTEGER :: IDIIS
   INTEGER :: ITER,IORDER
   INTEGER :: ORDER,IPOWER
   INTEGER :: IA,IB,IC,ID
   INTEGER :: I,J
   INTEGER :: HOLEA(ORDER),HOLEB(ORDER),PARTA(ORDER),PARTB(ORDER)
   INTEGER :: KSIGN(0:2*MIN(IOCC,IALL(0,0,0)-IOCC-IVIRTCORE)),ISIGN,JSIGN
   INTEGER(4) :: CFALPH,CFBETA
   INTEGER :: EOF
   INTEGER :: INDX(MAXITER+1)
   INTEGER :: NHOLES, NACTIVEHOLES
   INTEGER :: NPARTICLES, NACTIVEPARTICLES
   REAL :: MEM,ICPUS,ICPUE
   DOUBLE PRECISION,ALLOCATABLE :: VEC1(:,:),VEC2(:,:),VEC3(:,:)
   DOUBLE PRECISION :: DEVSQ,D
   DOUBLE PRECISION :: HA,HB
   DOUBLE PRECISION :: B(21,21),BS(21,21),C(21),CS(21)
   DOUBLE PRECISION :: XCC

   IF (.NOT.LXCC) THEN
    WRITE(6,'(A)') 'HIGH-ORDER COUPLED-CLUSTER CALCULATIONS WILL BE PERFORMED'
   ELSE
    WRITE(6,'(A)') 'HIGH-ORDER EXPECTATION-VALUE COUPLED-CLUSTER CALCULATIONS WILL BE PERFORMED WITH THE EXISTING T-AMPLITUDES'
   ENDIF
   IF ((IOPTN(64) == 1).AND.(.NOT.LXCC)) THEN
    WRITE(6,'(A)') 'H EXP(T) ALGORITHM WILL BE USED'
   ELSE IF ((IOPTN(64) == 2).OR.(LXCC)) THEN
    IF (.NOT.LXCC) WRITE(6,'(A)') 'EXP(-T) H EXP(T) ALGORITHM WILL BE USED'
    IF (LXCC) WRITE(6,'(A)') 'EXP(T^DAGGER) H EXP(T) ALGORITHM WILL BE USED'
   ELSE
    CALL PABORT('UNKNOWN CC ALGORITHM')
   ENDIF
   IF (.NOT.LXCC) THEN
    WRITE(6,'(A,I2)') 'CC ORDER IS ',ORDER
   ELSE
    WRITE(6,'(A,I2)') 'XCC ORDER IS ',ORDER
   ENDIF
   IF ((LDIIS).AND.(.NOT.LXCC)) THEN
    IDIIS=IOPTN(54)
    WRITE(6,'(A,I3,A)') 'DIIS EXTRAPOLATION WILL BE PERFORMED ONCE IN',IDIIS,' ITERATIONS'
   ELSE
    IDIIS=1
   ENDIF
   CALL CPU_TIME(ICPUS)
   MEM=16.0*2.0*NCF**2
   IF (MEM > 1000000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000.0,' MB'
   ELSE IF (MEM > 1000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000.0,' KB'
   ELSE
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM,' B'
   ENDIF
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY') 
   ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF),VEC3(NCF,NCF))
   OPEN(50,FILE=TRIM(COPTN(1))//'.fi0',FORM='UNFORMATTED')
   OPEN(51,FILE=TRIM(COPTN(1))//'.fi1',FORM='UNFORMATTED')
   OPEN(52,FILE=TRIM(COPTN(1))//'.fi2',FORM='UNFORMATTED')
   OPEN(53,FILE=TRIM(COPTN(1))//'.fi3',FORM='UNFORMATTED')
   OPEN(54,FILE=TRIM(COPTN(1))//'.fi4',FORM='UNFORMATTED')
   OPEN(55,FILE=TRIM(COPTN(1))//'.fi5',FORM='UNFORMATTED')
   OPEN(56,FILE=TRIM(COPTN(1))//'.fi6',FORM='UNFORMATTED')
   OPEN(57,FILE=TRIM(COPTN(1))//'.fi7',FORM='UNFORMATTED')
   OPEN(58,FILE=TRIM(COPTN(1))//'.fi8',FORM='UNFORMATTED')
   OPEN(59,FILE=TRIM(COPTN(1))//'.fi9',FORM='UNFORMATTED')
   OPEN(60,FILE=TRIM(COPTN(1))//'.fj0',FORM='UNFORMATTED')
   OPEN(61,FILE=TRIM(COPTN(1))//'.fj1',FORM='UNFORMATTED')
   OPEN(62,FILE=TRIM(COPTN(1))//'.fj2',FORM='UNFORMATTED')
   OPEN(63,FILE=TRIM(COPTN(1))//'.fj3',FORM='UNFORMATTED')
   OPEN(64,FILE=TRIM(COPTN(1))//'.fj4',FORM='UNFORMATTED')
   OPEN(65,FILE=TRIM(COPTN(1))//'.fj5',FORM='UNFORMATTED')
   OPEN(66,FILE=TRIM(COPTN(1))//'.fj6',FORM='UNFORMATTED')
   OPEN(67,FILE=TRIM(COPTN(1))//'.fj7',FORM='UNFORMATTED')
   OPEN(68,FILE=TRIM(COPTN(1))//'.fj8',FORM='UNFORMATTED')
   OPEN(69,FILE=TRIM(COPTN(1))//'.fj9',FORM='UNFORMATTED')
   OPEN(70,FILE=TRIM(COPTN(1))//'.fo0',FORM='UNFORMATTED')
   OPEN(71,FILE=TRIM(COPTN(1))//'.fo1',FORM='UNFORMATTED')
   OPEN(72,FILE=TRIM(COPTN(1))//'.fo2',FORM='UNFORMATTED')
   OPEN(73,FILE=TRIM(COPTN(1))//'.fo3',FORM='UNFORMATTED')
   OPEN(74,FILE=TRIM(COPTN(1))//'.fo4',FORM='UNFORMATTED')
   OPEN(75,FILE=TRIM(COPTN(1))//'.fo5',FORM='UNFORMATTED')
   OPEN(76,FILE=TRIM(COPTN(1))//'.fo6',FORM='UNFORMATTED')
   OPEN(77,FILE=TRIM(COPTN(1))//'.fo7',FORM='UNFORMATTED')
   OPEN(78,FILE=TRIM(COPTN(1))//'.fo8',FORM='UNFORMATTED')
   OPEN(79,FILE=TRIM(COPTN(1))//'.fo9',FORM='UNFORMATTED')
   OPEN(80,FILE=TRIM(COPTN(1))//'.fp0',FORM='UNFORMATTED')
   OPEN(81,FILE=TRIM(COPTN(1))//'.fp1',FORM='UNFORMATTED')
   OPEN(82,FILE=TRIM(COPTN(1))//'.fp2',FORM='UNFORMATTED')
   OPEN(83,FILE=TRIM(COPTN(1))//'.fp3',FORM='UNFORMATTED')
   OPEN(84,FILE=TRIM(COPTN(1))//'.fp4',FORM='UNFORMATTED')
   OPEN(85,FILE=TRIM(COPTN(1))//'.fp5',FORM='UNFORMATTED')
   OPEN(86,FILE=TRIM(COPTN(1))//'.fp6',FORM='UNFORMATTED')
   OPEN(87,FILE=TRIM(COPTN(1))//'.fp7',FORM='UNFORMATTED')
   OPEN(88,FILE=TRIM(COPTN(1))//'.fp8',FORM='UNFORMATTED')
   OPEN(89,FILE=TRIM(COPTN(1))//'.fp9',FORM='UNFORMATTED')
   OPEN(90,FILE=TRIM(COPTN(1))//'.fq0',FORM='UNFORMATTED')
   OPEN(91,FILE=TRIM(COPTN(1))//'.fq1',FORM='UNFORMATTED')
   OPEN(92,FILE=TRIM(COPTN(1))//'.fq2',FORM='UNFORMATTED')
   VEC1=0.0D0
   DO I=50,89
    REWIND(I)
    WRITE(I) VEC1
   ENDDO

   ! SIGNS ASSOCIATED WITH THE PERMUTATION OF THE CLUSTER OPERATORS
   DO I=0,2*MIN(IOCC,IALL(0,0,0)-IOCC-IVIRTCORE)
    KSIGN(I)=(-1)**((I*(I-1))/2)
   ENDDO

   ! COUPLED-CLUSTER ITERATION
   ! FILE 90: COUPLED-CLUSTER T AMPLITUDES
   ! FILE 91: COUPLED-CLUSTER WAVE FUNCTION EXP(T)|0>
   ! FILE 92: H EXP(T)|0>
   IF (LGUESS) THEN
    WRITE(6,'(A)') 'INITIAL GUESS FOR COUPLED-CLUSTER AMPLITUDES HAS BEEN RESTORED FROM FILE'
    REWIND(90)
    READ(90) VEC1
   ELSE
    VEC1=0.0D0
    REWIND(90)
    WRITE(90) VEC1
   ENDIF
   IF (.NOT.LXCC) THEN
    WRITE(6,'(A)') '-----------------------------------------------------------------'
    WRITE(6,'(A)') 'ITR     DEVIATION     CORRELATION        TOTAL ENERGY   CPU / SEC'
   ENDIF
   DO ITER=1,MAXITER
    IF (IOPTN(9) >= 2) THEN
     WRITE(6,'(A)') 'T-AMPLITUDE'
     CALL DUMP5(VEC1,NCF)
    ENDIF
    DO I=68,50,-1
     REWIND(I)
     READ(I) VEC2
     REWIND(I+1)
     WRITE(I+1) VEC2
    ENDDO
    REWIND(50)
    WRITE(50) VEC1
    
    ! XCC
    IF (LXCC) THEN

     ! FORM NUMERATOR <0|EXP(T^DAGGER) H EXP(T)|0>
     VEC2=0.0D0
     VEC2(1,1)=1.0D0
     REWIND(92)
     WRITE(92) VEC2
     DEALLOCATE(VEC1,VEC2,VEC3)
     CALL EXPONENTIAL_OPERATOR(50,92,91,ORDER,.FALSE.,.FALSE.)
     CALL HAMILTONIAN_PRODUCT(91,92,0,2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE))
     CALL EXPONENTIAL_OPERATOR(50,92,91,ORDER,.FALSE.,.TRUE.)
     ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF),VEC3(NCF,NCF))
     REWIND(91)
     READ(91) VEC1
     XCC=VEC1(1,1)
     WRITE(6,'(A,F20.10)') 'XCC NUMERATOR =   ',XCC

     ! FORM DENOMINATOR <0|EXP(T^DAGGER) EXP(T)|0>
     VEC2=0.0D0
     VEC2(1,1)=1.0D0
     REWIND(92)
     WRITE(92) VEC2
     DEALLOCATE(VEC1,VEC2,VEC3)
     CALL EXPONENTIAL_OPERATOR(50,92,91,ORDER,.FALSE.,.FALSE.)
     CALL EXPONENTIAL_OPERATOR(50,91,92,ORDER,.FALSE.,.TRUE.)
     ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF),VEC3(NCF,NCF))
     REWIND(92)
     READ(92) VEC1
     D=VEC1(1,1)
     XCC=XCC/D
     WRITE(6,'(A,F20.10)') 'XCC DENOMINATOR = ',D
     WRITE(6,'(A,F20.10)') 'XCC ENERGY      = ',XCC+NUCLEAR_REPULSION

     DEALLOCATE(VEC1,VEC2,VEC3)
     CLOSE(50)
     CLOSE(51)
     CLOSE(52)
     CLOSE(53)
     CLOSE(54)
     CLOSE(55)
     CLOSE(56)
     CLOSE(57)
     CLOSE(58)
     CLOSE(59)
     CLOSE(60)
     CLOSE(61)
     CLOSE(62)
     CLOSE(63)
     CLOSE(64)
     CLOSE(65)
     CLOSE(66)
     CLOSE(67)
     CLOSE(68)
     CLOSE(69)
     CLOSE(70)
     CLOSE(71)
     CLOSE(72)
     CLOSE(73)
     CLOSE(74)
     CLOSE(75)
     CLOSE(76)
     CLOSE(77)
     CLOSE(78)
     CLOSE(79)
     CLOSE(80)
     CLOSE(81)
     CLOSE(82)
     CLOSE(83)
     CLOSE(84)
     CLOSE(85)
     CLOSE(86)
     CLOSE(87)
     CLOSE(88)
     CLOSE(89)
     CLOSE(90)
     CLOSE(91)
     CLOSE(92)
     RETURN

    ENDIF

    ! FORM EXP(T)|0>
    VEC2=0.0D0
    VEC2(1,1)=1.0D0
    REWIND(92)
    WRITE(92) VEC2
    DEALLOCATE(VEC1,VEC2,VEC3)
    CALL EXPONENTIAL_OPERATOR(50,92,91,ORDER,.FALSE.,.FALSE.)
    ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF),VEC3(NCF,NCF))

    ! FORM H EXP(T)|0>
    DEALLOCATE(VEC1,VEC2,VEC3)
    CALL HAMILTONIAN_PRODUCT(91,92,0,2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE))
    ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF),VEC3(NCF,NCF))

    ! FORM EXP(-T) H EXP(T)|0>
    IF (IOPTN(64) == 2) THEN
     DEALLOCATE(VEC1,VEC2,VEC3)
     CALL EXPONENTIAL_OPERATOR(50,92,91,ORDER,.TRUE.,.FALSE.)
     ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF),VEC3(NCF,NCF))
    ENDIF

    ! FORM H EXP(T)|0> - E EXP(T)|0> OR EXP(-T) H EXP(T)|0> - E|0>
    REWIND(91)
    READ(91) VEC3
!do ia=1,ncf
!do ib=1,ncf
!write(*,*) 'ia,ib = ',ia,ib,' expT|0> = ',vec3(ia,ib)
!enddo
!enddo
    REWIND(92)
    READ(92) VEC1
    IF (IOPTN(64) == 2) THEN
     VEC1=VEC3
     VEC3=0.0D0
     VEC3(1,1)=1.0D0
    ENDIF
    ECC=VEC1(1,1)
!   VEC1=VEC1/ECC
    VEC2=0.0D0
    DO IA=1,NCF
     DO IB=1,NCF
      IF (NORDER(IA)+NORDER(IB) <= ORDER) VEC2(IB,IA)=VEC1(IB,IA)-VEC3(IB,IA)*ECC
! FOR CCD, COMMENT OUT FROM HERE ...
!     IF (NORDER(IA)+NORDER(IB) == 2) VEC2(IB,IA)=VEC1(IB,IA)-VEC3(IB,IA)*ECC
! ... TO HERE
     ENDDO
    ENDDO
!   \/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/
!   ACTIVE SPACE EXTENSION FROM HERE ...
!   WRITE(*,'(A)') '**** WARNING **** ACTIVE SPACE IS BEING SET !'
!   DO IA=1,NCF
!    DO IB=1,NCF
!     NHOLES=0
!     NACTIVEHOLES=0
!     DO I=ICORE+1,IOCC
!      IF (.NOT.BTEST(CFHALF(IA),I-1)) THEN
!       NHOLES=NHOLES+1
! CHANGE THE NEXT LINE FOR DIFFERENT DEFINITIONS OF ACTIVE SPACES !
!       IF (I >= IOCC-1) NACTIVEHOLES=NACTIVEHOLES+1
!      ENDIF
!      IF (.NOT.BTEST(CFHALF(IB),I-1)) THEN
!       NHOLES=NHOLES+1
! CHANGE THE NEXT LINE FOR DIFFERENT DEFINITIONS OF ACTIVE SPACES !
!       IF (I >= IOCC-1) NACTIVEHOLES=NACTIVEHOLES+1
!      ENDIF
!     ENDDO
!     NPARTICLES=0
!     NACTIVEPARTICLES=0
!     DO I=IOCC+1,IALL(0,0,0)-IVIRTCORE
!      IF (BTEST(CFHALF(IA),I-1)) THEN
!       NPARTICLES=NPARTICLES+1
! CHANGE THE NEXT LINE FOR DIFFERENT DEFINITIONS OF ACTIVE SPACES !
!       IF (I <= IOCC+2) NACTIVEPARTICLES=NACTIVEPARTICLES+1
!      ENDIF
!      IF (BTEST(CFHALF(IB),I-1)) THEN
!       NPARTICLES=NPARTICLES+1
! CHANGE THE NEXT LINE FOR DIFFERENT DEFINITIONS OF ACTIVE SPACES !
!       IF (I <= IOCC+2) NACTIVEPARTICLES=NACTIVEPARTICLES+1
!      ENDIF
!     ENDDO
!     IF (NHOLES /= NPARTICLES) &
!     CALL PABORT('NUMBERS OF HOLES AND PARTICLES DO NOT MATCH')
!     IF (NHOLES == 3) THEN
! CHANGE THE NEXT LINES FOR DIFFERENT DEFINITION OF SMALL T3 !
!      IF ((NACTIVEHOLES == 0).OR.(NACTIVEPARTICLES == 0)) VEC2(IB,IA)=0.0D0
!     ENDIF
!     IF (NHOLES == 4) THEN
! CHANGE THE NEXT LINES FOR DIFFERENT DEFINITION OF SMALL T4 !
!      IF ((NACTIVEHOLES == 0).OR.(NACTIVEPARTICLES == 0)) VEC2(IB,IA)=0.0D0
!     ENDIF
!    ENDDO
!   ENDDO
!   ... TO HERE
!   /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\
    DO I=88,70,-1
     REWIND(I)
     READ(I) VEC3
     REWIND(I+1)
     WRITE(I+1) VEC3
    ENDDO
    REWIND(70)
    WRITE(70) VEC2
    DEVSQ=0.0D0
    DO IA=1,NCF
     DO IB=1,NCF
      DEVSQ=DEVSQ+VEC2(IB,IA)**2
     ENDDO
    ENDDO
    DEVSQ=DSQRT(DEVSQ)
    CALL CPU_TIME(ICPUE)
    WRITE(6,'(I3,F15.10,F15.10,F20.10,F12.1)') ITER,DEVSQ,ECC+NUCLEAR_REPULSION-EHFKS,ECC+NUCLEAR_REPULSION,ICPUE-ICPUS
    CALL CPU_TIME(ICPUS)
    IF (DEVSQ > 100.0D0) CALL PABORT('COUPLED-CLUSTER ITERATION FAILED TO CONVERGE')
    IF (DEVSQ < DOPTN(62)) THEN
     WRITE(6,'(A)') '-----------------------------------------------------------------'
     REWIND(90)
     READ(90) VEC3
     WRITE(6,'(A)') 'CONVERGED T-AMPLITUDES'
     DO IA=1,NCF
      DO IB=1,NCF
       IF (DABS(VEC3(IB,IA)) > 0.05D0) WRITE(6,'(A,I6,A,I6,A,F10.5)') ' T(',IA,'-ALPHA ',IB,'-BETA ) = ',VEC3(IB,IA)
      ENDDO
     ENDDO
     IF (IOPTN(97) > 0) THEN
      OPEN(97,FILE=TRIM(COPTN(1))//'.gf0',FORM='UNFORMATTED')
      REWIND(91)
      READ(91) VEC1
      REWIND(97)
      WRITE(97) VEC1
      CLOSE(97)
      WRITE(6,'(A,A)') 'REFERENCE WAVE FUNCTION FOR MBGF STORED IN ',TRIM(COPTN(1))//'.gf0'
     ENDIF
     DEALLOCATE(VEC1,VEC2,VEC3)
     CLOSE(50)
     CLOSE(51)
     CLOSE(52)
     CLOSE(53)
     CLOSE(54)
     CLOSE(55)
     CLOSE(56)
     CLOSE(57)
     CLOSE(58)
     CLOSE(59)
     CLOSE(60)
     CLOSE(61)
     CLOSE(62)
     CLOSE(63)
     CLOSE(64)
     CLOSE(65)
     CLOSE(66)
     CLOSE(67)
     CLOSE(68)
     CLOSE(69)
     CLOSE(70)
     CLOSE(71)
     CLOSE(72)
     CLOSE(73)
     CLOSE(74)
     CLOSE(75)
     CLOSE(76)
     CLOSE(77)
     CLOSE(78)
     CLOSE(79)
     CLOSE(80)
     CLOSE(81)
     CLOSE(82)
     CLOSE(83)
     CLOSE(84)
     CLOSE(85)
     CLOSE(86)
     CLOSE(87)
     CLOSE(88)
     CLOSE(89)
     CLOSE(90)
     CLOSE(91)
     CLOSE(92)
     RETURN
    ENDIF

    IF ((ITER < IDIIS).OR.(MOD(ITER,IDIIS) /= 0).OR.(.NOT.LDIIS)) THEN
     ! FORM NEW T AMPLITUDE BY SIMPLE RELAXATION
     REWIND(90)
     READ(90) VEC1
     DO IA=1,NCF
      CFALPH=CFHALF(1)
      HA=0.0D0
      ISIGN=1
      DO I=1,IOCC
       IF (.NOT.BTEST(CFHALF(IA),I-1)) THEN
        IF (I /= 1) THEN
         DO J=1,I-1
          IF (BTEST(CFALPH,J-1)) ISIGN=-ISIGN
         ENDDO
        ENDIF
        CFALPH=IBCLR(CFALPH,I-1)
        HA=HA+EPSILON(I,0,0,0)
       ENDIF
      ENDDO
      DO I=IOCC+1,IALL(0,0,0)-IVIRTCORE
       IF (BTEST(CFHALF(IA),I-1)) THEN
        DO J=1,I-1
         IF (BTEST(CFALPH,J-1)) ISIGN=-ISIGN
        ENDDO
        CFALPH=IBSET(CFALPH,I-1)
        HA=HA-EPSILON(I,0,0,0)
       ENDIF
      ENDDO
      DO IB=1,NCF
       CFBETA=CFHALF(1)
       HB=0.0D0
       JSIGN=1
       DO I=1,IOCC
        IF (.NOT.BTEST(CFHALF(IB),I-1)) THEN
         IF (I /= 1) THEN
          DO J=1,I-1
           IF (BTEST(CFBETA,J-1)) JSIGN=-JSIGN
          ENDDO
         ENDIF
         CFBETA=IBCLR(CFBETA,I-1)
         HB=HB+EPSILON(I,0,0,0)
        ENDIF
       ENDDO
       DO I=IOCC+1,IALL(0,0,0)-IVIRTCORE
        IF (BTEST(CFHALF(IB),I-1)) THEN
         DO J=1,I-1
          IF (BTEST(CFBETA,J-1)) JSIGN=-JSIGN
         ENDDO
         CFBETA=IBSET(CFBETA,I-1)
         HB=HB-EPSILON(I,0,0,0)
        ENDIF
       ENDDO
       IF ((IA == 1).AND.(IB == 1)) THEN
        VEC1(IA,IB)=0.0D0
       ELSE
        IF (IOPTN(64) == 2) THEN
!        VEC1(IA,IB)=VEC1(IA,IB)-VEC2(IA,IB)*DFLOAT(ISIGN*JSIGN*KSIGN(NORDER(IA))*KSIGN(NORDER(IB)))*DOPTN(63)
         VEC1(IA,IB)=VEC1(IA,IB)+VEC2(IA,IB)*DFLOAT(ISIGN*JSIGN*KSIGN(NORDER(IA))*KSIGN(NORDER(IB)))/(HA+HB)*DOPTN(63)
        ELSE
         VEC1(IA,IB)=VEC1(IA,IB)+VEC2(IA,IB)*DFLOAT(ISIGN*JSIGN*KSIGN(NORDER(IA))*KSIGN(NORDER(IB)))/(HA+HB)*DOPTN(63)
!write(*,*) 'ia,ib = ',ia,ib,' ha+hb = ',ha+hb, ' vec2 = ',vec2(ia,ib)
!write(*,*) 'ia,ib = ',ia,ib,' T amp = ',vec1(ia,ib)
        ENDIF
       ENDIF
      ENDDO
     ENDDO
     REWIND(90)
     WRITE(90) VEC1
    ELSE
     IORDER=MIN(ITER,20)
     B=0.0D0
     DO I=1,IORDER
      REWIND(69+I)
      READ(69+I) VEC1
      DO J=1,IORDER
       REWIND(69+J)
       READ(69+J) VEC2
       DO IA=1,NCF
        DO IB=1,NCF
         B(J,I)=B(J,I)+VEC1(IB,IA)*VEC2(IB,IA)
        ENDDO
       ENDDO
      ENDDO
      B(I,IORDER+1)=-1.0D0
      B(IORDER+1,I)=-1.0D0
      C(I)=0.0D0
     ENDDO
     B(IORDER+1,IORDER+1)=0.0D0
     C(IORDER+1)=-1.0D0
     BS=B
     CS=C
!    WRITE(6,'(A)') 'PULAY MATRIX'
!    CALL DUMP5(B,21)
     CALL LUDCMP(B,IORDER+1,21,INDX,D)
     CALL LUBKSB(B,IORDER+1,21,INDX,C)
     CALL MPROVE(BS,B,IORDER+1,21,INDX,CS,C)
     WRITE(6,'(A,100F7.3:)') 'DIIS VECTOR',(C(I),I=1,IORDER+1)
     
     VEC1=0.0D0
     DO I=1,IORDER
      REWIND(49+I)
      READ(49+I) VEC2
      VEC1=VEC1+VEC2*C(I)
     ENDDO
     REWIND(90)
     WRITE(90) VEC1
    ENDIF

   ENDDO
   CALL PABORT('COUPLED-CLUSTER ITERATION FAILED TO CONVERGE')
   RETURN
END SUBROUTINE



FUNCTION COMBINATION(M,N)
! RETURNS M!/(N!(M-N)!)

   IMPLICIT NONE
   INTEGER :: COMBINATION
   INTEGER :: M,N,I,J
   
   IF ((N == 0).OR.(N == M)) THEN
    COMBINATION=1
    RETURN
   ENDIF

   IF ((N < 0).OR.(N > M).OR.(M < 1)) THEN
    COMBINATION=0
    RETURN
   ENDIF
   
   COMBINATION=1
   DO I=M-N+1,M
    COMBINATION=COMBINATION*I/(I-M+N)
   ENDDO
   RETURN
END FUNCTION



SUBROUTINE EXPONENTIAL_OPERATOR(TFILE,INFILE,OUTFILE,ORDER,LMINUS,LDAGGER)
! OPERATE WITH AN EXPONENTIAL OPERATOR ON ANY GIVEN WAVEFUNCTION.  THE T-AMPLITUDES
! ARE STORED IN TFILE, THE WAVEFUNCTION ON WHICH THE EXPONENTIAL OPERATOR ACTS IS 
! IN INFILE, AND THE EXPONENTIAL WAVEFUNCTION IS IN OUTFILE.  IF LMINUS=.TRUE.
! THE OPERATOR IS EXP(-T), OTHERWISE, EXP(T).  IF LDAGGER=.TRUE., THE OPERATOR IS
! THE CONJUGATE COMPLEX OF EXP(T), OTHERWISE, JUST EXP(T).

   USE CONTROL
   USE GRADIENT
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI

   IMPLICIT NONE
   LOGICAL :: LDAGGER
   LOGICAL :: LMINUS
   LOGICAL :: LCYCLE
   INTEGER :: TFILE,INFILE,OUTFILE
   INTEGER :: ORDER
   INTEGER :: I,J,IPOWER
   INTEGER :: IA,IB,IC,ID
   INTEGER :: HOLEA(ORDER),HOLEB(ORDER),PARTA(ORDER),PARTB(ORDER)
   INTEGER :: KSIGN(0:2*MIN(IOCC,IALL(0,0,0)-IOCC-IVIRTCORE)),ISIGN,JSIGN
   INTEGER(4) :: CFALPH,CFBETA
   DOUBLE PRECISION,ALLOCATABLE :: VEC1(:,:),VEC2(:,:),VEC3(:,:)
   INTEGER,ALLOCATABLE :: AD1(:),AD2(:),SN1(:),SN2(:)
   LOGICAL,ALLOCATABLE :: LC1(:),LC2(:)

   ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF),VEC3(NCF,NCF))
   ALLOCATE(AD1(NCF),AD2(NCF),SN1(NCF),SN2(NCF),LC1(NCF),LC2(NCF))

   ! SIGNS ASSOCIATED WITH THE PERMUTATION OF THE CLUSTER OPERATORS
   DO I=0,2*MIN(IOCC,IALL(0,0,0)-IOCC-IVIRTCORE)
    KSIGN(I)=(-1)**((I*(I-1))/2)
   ENDDO

   ! RETRIEVE T-AMPLITUDES FROM FILE
   REWIND(TFILE)
   READ(TFILE) VEC1
   IF (LMINUS) VEC1=-VEC1
   ! RETRIEVE INITIAL WAVEFUNCTION FROM FILE
   REWIND(INFILE)
   READ(INFILE) VEC2
   ! INITIALIZE OUTFILE WITH THE INITIAL WAVEFUNCTION
   REWIND(OUTFILE)
   WRITE(OUTFILE) VEC2

   ! LOOP OVER THE POWER
   DO IPOWER=1,2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE)
    VEC3=0.0D0

    ! LOOP OVER ALPHA T-AMPLITUDE
    DO IA=1,NCF
     IF (NORDER(IA) > ORDER) CYCLE
     J=0
     DO I=1,IOCC
      IF (.NOT.BTEST(CFHALF(IA),I-1)) THEN
       J=J+1
       HOLEA(J)=I
      ENDIF
     ENDDO
     J=0
     DO I=IOCC+1,IALL(0,0,0)-IVIRTCORE
      IF (BTEST(CFHALF(IA),I-1)) THEN
       J=J+1
       PARTA(J)=I
      ENDIF
     ENDDO

     ! LOOP OVER ALPHA CONFIGURATIONS
     DO IC=1,NCF
      LCYCLE=.FALSE.
      CFALPH=CFHALF(IC)
      IF (.NOT.LDAGGER) THEN
       DO I=1,NORDER(IA)
        IF (.NOT.BTEST(CFALPH,HOLEA(I)-1)) LCYCLE=.TRUE.
        IF (BTEST(CFALPH,PARTA(I)-1)) LCYCLE=.TRUE.
       ENDDO
      ELSE
       DO I=1,NORDER(IA)
        IF (BTEST(CFALPH,HOLEA(I)-1)) LCYCLE=.TRUE.
        IF (.NOT.BTEST(CFALPH,PARTA(I)-1)) LCYCLE=.TRUE.
       ENDDO
      ENDIF
      LC1(IC)=LCYCLE
      IF (LCYCLE) CYCLE
      ISIGN=1
      IF (.NOT.LDAGGER) THEN
       DO I=1,NORDER(IA)
        IF (HOLEA(I) /= 1) THEN
         DO J=1,HOLEA(I)-1
          IF (BTEST(CFALPH,J-1)) ISIGN=-ISIGN
         ENDDO
        ENDIF
        CFALPH=IBCLR(CFALPH,HOLEA(I)-1)
       ENDDO
       DO I=1,NORDER(IA)
        DO J=1,PARTA(I)-1
         IF (BTEST(CFALPH,J-1)) ISIGN=-ISIGN
        ENDDO
        CFALPH=IBSET(CFALPH,PARTA(I)-1)
       ENDDO
      ELSE
       DO I=NORDER(IA),1,-1
        DO J=1,PARTA(I)-1
         IF (BTEST(CFALPH,J-1)) ISIGN=-ISIGN
        ENDDO
        CFALPH=IBCLR(CFALPH,PARTA(I)-1)
       ENDDO
       DO I=NORDER(IA),1,-1
        IF (HOLEA(I) /= 1) THEN
         DO J=1,HOLEA(I)-1
          IF (BTEST(CFALPH,J-1)) ISIGN=-ISIGN
         ENDDO
        ENDIF
        CFALPH=IBSET(CFALPH,HOLEA(I)-1)
       ENDDO
      ENDIF
      AD1(IC)=ADDRSS(CFALPH)
      SN1(IC)=ISIGN*KSIGN(NORDER(IA))
     ENDDO

     ! LOOP OVER BETA T-AMPLITUDE
     DO IB=1,NCF
      IF (NORDER(IA)+NORDER(IB) > ORDER) CYCLE
      IF (DABS(VEC1(IA,IB)) < 1.0D-15) CYCLE
      J=0
      DO I=1,IOCC
       IF (.NOT.BTEST(CFHALF(IB),I-1)) THEN
        J=J+1
        HOLEB(J)=I
       ENDIF
      ENDDO
      J=0
      DO I=IOCC+1,IALL(0,0,0)-IVIRTCORE
       IF (BTEST(CFHALF(IB),I-1)) THEN
        J=J+1
        PARTB(J)=I
       ENDIF
      ENDDO

      ! LOOP OVER BETA CONFIGURATIONS
      DO ID=1,NCF
       LCYCLE=.FALSE.
       CFBETA=CFHALF(ID)
       IF (.NOT.LDAGGER) THEN
        DO I=1,NORDER(IB)
         IF (.NOT.BTEST(CFBETA,HOLEB(I)-1)) LCYCLE=.TRUE.
         IF (BTEST(CFBETA,PARTB(I)-1)) LCYCLE=.TRUE.
        ENDDO
       ELSE
        DO I=1,NORDER(IB)
         IF (BTEST(CFBETA,HOLEB(I)-1)) LCYCLE=.TRUE.
         IF (.NOT.BTEST(CFBETA,PARTB(I)-1)) LCYCLE=.TRUE.
        ENDDO
       ENDIF
       LC2(ID)=LCYCLE
       IF (LCYCLE) CYCLE
       JSIGN=1
       IF (.NOT.LDAGGER) THEN
        DO I=1,NORDER(IB)
         IF (HOLEB(I) /= 1) THEN
          DO J=1,HOLEB(I)-1
           IF (BTEST(CFBETA,J-1)) JSIGN=-JSIGN
          ENDDO
         ENDIF
         CFBETA=IBCLR(CFBETA,HOLEB(I)-1)
        ENDDO
        DO I=1,NORDER(IB)
         DO J=1,PARTB(I)-1
          IF (BTEST(CFBETA,J-1)) JSIGN=-JSIGN
         ENDDO
         CFBETA=IBSET(CFBETA,PARTB(I)-1)
        ENDDO
       ELSE
        DO I=NORDER(IB),1,-1
         DO J=1,PARTB(I)-1
          IF (BTEST(CFBETA,J-1)) JSIGN=-JSIGN
         ENDDO
         CFBETA=IBCLR(CFBETA,PARTB(I)-1)
        ENDDO
        DO I=NORDER(IB),1,-1
         IF (HOLEB(I) /= 1) THEN
          DO J=1,HOLEB(I)-1
           IF (BTEST(CFBETA,J-1)) JSIGN=-JSIGN
          ENDDO
         ENDIF
         CFBETA=IBSET(CFBETA,HOLEB(I)-1)
        ENDDO
       ENDIF
       AD2(ID)=ADDRSS(CFBETA)
       SN2(ID)=JSIGN*KSIGN(NORDER(IB))
      ENDDO

      ! LOOP OVER ALPHA CONFIGURATIONS
      DO IC=1,NCF
       IF (LC1(IC)) CYCLE
       ! LOOP OVER BETA CONFIGURATIONS
       DO ID=1,NCF
        IF (LC2(ID)) CYCLE
        IF (DABS(VEC2(IC,ID)) < 1.0D-15) CYCLE
        VEC3(AD1(IC),AD2(ID))=VEC3(AD1(IC),AD2(ID))+DFLOAT(SN1(IC)*SN2(ID))*VEC1(IA,IB)*VEC2(IC,ID)
       ENDDO
      ENDDO

     ENDDO
    ENDDO
    ! AT THIS POINT, VEC3 CONTAINS T TO THE POWER OF IPOWER.  VEC3 IS COPIED TO VEC2 FOR NEXT ITERATION,
    ! AND VEC3 IS ADDED TO THE VECTOR STORED IN 91.
    VEC3=VEC3/DFLOAT(IPOWER)
    REWIND(OUTFILE)
    READ(OUTFILE) VEC2
    VEC2=VEC2+VEC3
    REWIND(OUTFILE)
    WRITE(OUTFILE) VEC2
    VEC2=VEC3
   ENDDO
   
   DEALLOCATE(VEC1,VEC2,VEC3)
   DEALLOCATE(AD1,AD2,SN1,SN2,LC1,LC2)

   RETURN
END SUBROUTINE



SUBROUTINE FULL_EFF_H(TORDER,RORDER)
! FORM FULL EFFECTIVE HAMILTONIAN EXP(-T)HEXP(T) AND DIAGONALIZE IT BY LINPACK/LAPACK/BLAS SUBROUTINES.
! CAUTION, DO NOT USE THIS FOR PRODUCTION RUN.  THE ORDER OF CLUSTER EXPANSION SHOULD BE GIVEN BY TORDER
! AND THE CORRESPONDING T-AMPLITUDES MUST BE PROVIDED IN FILE 90.  THE ORDER OF LINEAR OPERATOR SHOULD BE
! GIVEN BY RORDER.

   USE CONTROL
   USE GRADIENT
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI

   IMPLICIT NONE
   LOGICAL,PARAMETER :: LEVEC = .FALSE.
   INTEGER :: RORDER,TORDER
   INTEGER :: IA,IB,IC,ID
   INTEGER :: INFO
   INTEGER :: NDIM
   REAL :: MEM,DEV
   DOUBLE PRECISION,ALLOCATABLE :: HAM(:,:),VL(:,:),VR(:,:),ER(:),EI(:),WK(:)
   DOUBLE PRECISION,ALLOCATABLE :: TRL(:,:)

   WRITE(6,'(A)') 'EQUATION-OF-MOTION COUPLED CLUSTER CALCULATION FOR ALL STATES'
   WRITE(6,'(I2,A)') TORDER,'-ORDER COUPLED CLUSTER SIMILARITY TRANSFORMATION'
   WRITE(6,'(I2,A)') RORDER,'-ORDER LINEAR EXCITATION OPERATOR'
   IF (LEVEC) THEN
    MEM=16.0*(3.0D0*NCF**4+7.0*NCF**2)
   ELSE
    MEM=16.0*(NCF**4+9.0*NCF**2)
   ENDIF
   IF (MEM > 1000000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000.0,' MB'
   ELSE IF (MEM > 1000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000.0,' KB'
   ELSE
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM,' B'
   ENDIF
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY') 
   ALLOCATE(HAM(NCF**2,NCF**2),ER(NCF**2),EI(NCF**2),WK(4*NCF**2))
   IF (LEVEC) THEN
    ALLOCATE(VR(NCF**2,NCF**2),VL(NCF**2,NCF**2))
   ELSE
    ALLOCATE(VR(1,NCF**2),VL(1,NCF**2))
   ENDIF
   ALLOCATE(TRL(NCF,NCF))

   OPEN(50,FILE=TRIM(COPTN(1))//'.fi0',FORM='UNFORMATTED')
   OPEN(60,FILE=TRIM(COPTN(1))//'.fo0',FORM='UNFORMATTED')
   OPEN(90,FILE=TRIM(COPTN(1))//'.fq0',FORM='UNFORMATTED')

   ! FORM HAMILTONIAN AND KEEP IT IN MEMORY
   NDIM=0
   HAM=0.0D0
   DO IA=1,NCF
    DO IB=1,NCF
     TRL=0.0D0
     IF (NORDER(IA)+NORDER(IB) <= RORDER) TRL(IA,IB)=1.0D0
     IF (NORDER(IA)+NORDER(IB) <= RORDER) NDIM=NDIM+1
     ! |B>=EXP(T)|A>
     REWIND(50)
     WRITE(50) TRL
     CALL EXPONENTIAL_OPERATOR(90,50,60,TORDER,.FALSE.,.FALSE.)
     REWIND(60)
     READ(60) TRL
     ! |C>=H|B>
     REWIND(50)
     WRITE(50) TRL
     CALL HAMILTONIAN_PRODUCT(50,60,0,2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE))
     REWIND(60)
     READ(60) TRL
     ! |D>=EXP(-T)|C>
     REWIND(50)
     WRITE(50) TRL
     CALL EXPONENTIAL_OPERATOR(90,50,60,TORDER,.TRUE.,.FALSE.)
     REWIND(60)
     READ(60) TRL
     DO IC=1,NCF
      DO ID=1,NCF
       HAM((IC-1)*NCF+ID,(IA-1)*NCF+IB)=TRL(IC,ID)
      ENDDO
     ENDDO
    ENDDO
   ENDDO
   IF (NDIM==NCF**2) WRITE(6,'(A)') 'FULL CI LIMIT IS REACHED'
!  IF (NCF**2 <= 49) CALL DUMP5(HAM,NCF**2)

   ! CHECK IF THE TRANSFORMED HAMILTONIAN HAS ZERO FIRST ROW
   DEV=0.0D0
   DO IA=1,NCF
    DO IB=1,NCF
     IF ((IA == 1).AND.(IB == 1)) CYCLE
     IF (NORDER(IA)+NORDER(IB) > TORDER) CYCLE
     DEV=DEV+HAM((IA-1)*NCF+IB,1)**2
    ENDDO
   ENDDO
   IF (DEV > 1.0E-5) THEN
    CALL WARNING('THE FIRST COLUMN IN THE H BAR DEVIATES FROM ZERO')
    WRITE(6,'(A,F20.10)') 'DEVIATION = ',DEV
    CALL DUMP5(HAM,NCF**2)
   ENDIF

   DEALLOCATE(TRL)
   CLOSE(50)
   CLOSE(60)
   CLOSE(90)

   ! DIAGONALIZE HAMILTONIAN
   IF (LEVEC) THEN
    CALL DGEEV('V','V',NCF**2,HAM,NCF**2,ER,EI,VL,NCF**2,VR,NCF**2,WK,4*NCF**2,INFO)
!   CALL DUMP5(VR,NCF**2)
!   CALL DUMP5(VL,NCF**2)
    IF (INFO /= 0) CALL PABORT('DGEEV FAILED TO DIAGONALIZE A MATRIX')
    DEV=0.0D0
    DO IA=1,NCF**2
     DEV=DEV+EI(IA)**2
    ENDDO
    IF (DEV > 1.0E-5) THEN
     CALL WARNING('THE IMAGINARY PART OF THE EIGENVALUES IS NOT ZERO')
     WRITE(6,'(A,F20.10)') 'DEVIATION = ',DEV
    ENDIF
    EI=ER
    CALL PIKSRT(NCF**2,NCF**2,ER,VR,WK)
    CALL PIKSRT(NCF**2,NCF**2,EI,VL,WK)
    DO IA=1,MIN(10,NCF**2)
     WRITE(6,'(A,I3,A,F20.15,A)') 'STATE ',IA,' ENERGY = ', ER(IA)+NUCLEAR_REPULSION,' HARTREE'
     WRITE(6,'(A)') '             RIGHT-HAND VECTOR    LEFT-HAND VECTOR'
     DO IB=1,NCF**2
      WRITE(6,'(I10,2F20.15)') IB,VR(IB,IA),VL(IB,IA)
     ENDDO
    ENDDO
    ! BIORTHOGONALITY CHECK
    DO IA=1,NCF**2
     DO IB=1,NCF**2
      HAM(IB,IA)=0.0D0
      DO IC=1,NCF**2
       HAM(IB,IA)=HAM(IB,IA)+VR(IC,IB)*VL(IC,IA)
      ENDDO
     ENDDO
    ENDDO
!   CALL DUMP5(HAM,NCF**2)
!   WRITE(6,'(A)') 'DIAGONALS'
!   DO IA=1,NCF**2
!    WRITE(6,'(F20.15)') HAM(IA,IA)
!   ENDDO
    DEV=0.0D0
    DO IA=1,(NCF**2)-1
     DO IB=IA+1,NCF**2
      DEV=DEV+HAM(IB,IA)**2
     ENDDO
    ENDDO
!   WRITE(6,'(A)') 'OFF DIAGONALS'
!   WRITE(6,'(F20.15)') DEV
   ELSE
    CALL DGEEV('N','N',NCF**2,HAM,NCF**2,ER,EI,VL,1,VR,1,WK,4*NCF**2,INFO)
    IF (INFO /= 0) CALL PABORT('DGEEV FAILED TO DIAGONALIZE A MATRIX')
    DEV=0.0D0
    DO IA=1,NCF**2
     DEV=DEV+EI(IA)**2
    ENDDO
    IF (DEV > 1.0E-5) THEN
     CALL WARNING('THE IMAGINARY PART OF THE EIGENVALUES IS NOT ZERO')
     WRITE(6,'(A,F20.10)') 'DEVIATION = ',DEV
    ENDIF
    CALL PIKSRT_EVALONLY(NCF**2,ER)
    DO IA=1,NDIM
     WRITE(6,'(A,I3,A,F20.15,A)') 'STATE ',IA,' ENERGY = ', ER(IA)+NUCLEAR_REPULSION,' HARTREE'
    ENDDO
   ENDIF
   CALL PFLUSH(6)

   DEALLOCATE(HAM,ER,EI,VL,VR,WK)

   RETURN
END SUBROUTINE



SUBROUTINE EOMEECC(TORDER,RORDER,NROOTS,NTRIALS,LLEFT)
! PERFORM HIGH-ORDER EQUATION-OF-MOTION COUPLED CLUSTER CALCULATIONS BY HIRAO-NAKATSUJI ALGORITHM.
! K.HIRAO & H.NAKATSUJI,J.COMPUT.PHYS.45,246(1982).
! IF LLEFT=.TRUE., LEFT-HAND EIGENVECTORS WILL BE SOUGHT INSTEAD OF RIGHT-HAND EIGENVECTORS.

   USE CONSTANTS
   USE CONTROL
   USE GRADIENT
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI

   IMPLICIT NONE
   LOGICAL :: LLEFT
   INTEGER,PARAMETER :: MAXFILE = 1000
   INTEGER,PARAMETER :: MAXCYCLE = 1000
   INTEGER :: ICYCLE
   INTEGER :: TORDER
   INTEGER :: RORDER
   INTEGER :: NROOTS
   INTEGER :: NTRIALS
   INTEGER :: NTRIALS_NEXT
   INTEGER :: IA,IB
   INTEGER :: I,J,K,L,NOPEN
   INTEGER :: INFO
   INTEGER,ALLOCATABLE :: N(:)
   REAL :: MEM,ICPUS,ICPUE
   LOGICAL :: LDONE(MAXFILE)
   DOUBLE PRECISION :: DEVSQ,DEVMAX,D,F,S2
   DOUBLE PRECISION :: SMALLH(MAXFILE,MAXFILE),EEOMCC(MAXFILE)
   DOUBLE PRECISION :: VR(MAXFILE,MAXFILE),VL(1,MAXFILE),ER(MAXFILE),EI(MAXFILE),WK(4*MAXFILE)
   DOUBLE PRECISION :: PERCENTS,PERCENTD
   DOUBLE PRECISION,ALLOCATABLE :: VEC1(:,:),VEC2(:,:)

   CALL CPU_TIME(ICPUS)
   WRITE(6,'(A)') 'HIGH-ORDER EQUATION-OF-MOTION COUPLED CLUSTER CALCULATIONS WILL BE PERFORMED'
   IF (.NOT.LLEFT) THEN
    WRITE(6,'(A)') 'RIGHT-HAND EIGENVECTORS WILL BE SOUGHT'
   ELSE
    WRITE(6,'(A)') 'LEFT-HAND EIGENVECTORS WILL BE SOUGHT'
   ENDIF
   WRITE(6,'(A,I2)') 'THE ORDER OF COUPLED CLUSTER            = ',TORDER
   WRITE(6,'(A,I2)') 'THE ORDER OF LINEAR EXCITATION OPERATOR = ',RORDER
   WRITE(6,'(A,I4)') 'THE NUMBER OF EOMCC ROOTS SOUGHT ',NROOTS
   WRITE(6,'(A,I4)') 'THE NUMBER OF INITIAL TRIAL VECTORS ',NTRIALS
   MEM=16.0*2.0*NCF**2
   IF (MEM > 1000000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000.0,' MB'
   ELSE IF (MEM > 1000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000.0,' KB'
   ELSE
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM,' B'
   ENDIF
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY') 
   WRITE(6,'(A)') '-----------------------------------------------------------------------'
   WRITE(6,'(A)') '             DEVIATION        OMEGA / EV       TOTAL ENERGY   CPU / SEC'
   ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF),N(IALLMAX-IVIRTCORE))
   OPEN(50,FILE=TRIM(COPTN(1))//'.fi0',FORM='UNFORMATTED')
   OPEN(51,FILE=TRIM(COPTN(1))//'.fo0',FORM='UNFORMATTED')
   OPEN(60,FILE=TRIM(COPTN(1))//'.fw0',FORM='UNFORMATTED')
   OPEN(61,FILE=TRIM(COPTN(1))//'.fw1',FORM='UNFORMATTED')
   OPEN(90,FILE=TRIM(COPTN(1))//'.fq0',FORM='UNFORMATTED')

   ! INITIALIZE
   LDONE=.FALSE.
   IF (NTRIALS > MAXFILE) CALL PABORT('TOO MANY INITIAL TRIAL VECTORS')

   ! HIRAO-NAKATSUJI ITERATION
   DO ICYCLE=1,MAXCYCLE

    ! FORM EXP(-T) H EXP(T)|A> AND STORE THEM IN FILE 51
    REWIND(50)
    REWIND(51)
    DO I=1,NTRIALS
     READ(50) VEC1 ! RETRIEVE A TRIAL VECTOR
     IF (.NOT.LDONE(I)) THEN
      ! |B>=EXP(T)|A>
      REWIND(60)
      WRITE(60) VEC1
      DEALLOCATE(VEC1,VEC2)
      IF (.NOT.LLEFT) THEN
       CALL EXPONENTIAL_OPERATOR(90,60,61,TORDER,.FALSE.,.FALSE.)
      ELSE
       CALL EXPONENTIAL_OPERATOR(90,60,61,TORDER,.TRUE.,.TRUE.)
      ENDIF
      ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF))
      REWIND(61)
      READ(61) VEC1
      ! |C>=H|B>
      REWIND(60)
      WRITE(60) VEC1
      DEALLOCATE(VEC1,VEC2)
      CALL HAMILTONIAN_PRODUCT(60,61,0,2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE))
      ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF))
      REWIND(61)
      READ(61) VEC1
      ! |D>=EXP(-T)|C>
      REWIND(60)
      WRITE(60) VEC1
      DEALLOCATE(VEC1,VEC2)
      IF (.NOT.LLEFT) THEN
       CALL EXPONENTIAL_OPERATOR(90,60,61,TORDER,.TRUE.,.FALSE.)
      ELSE
       CALL EXPONENTIAL_OPERATOR(90,60,61,TORDER,.FALSE.,.TRUE.)
      ENDIF
      ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF))
      REWIND(61)
      READ(61) VEC1
      WRITE(51) VEC1
      LDONE(I)=.TRUE.
     ELSE
      READ(51) VEC2 ! DUMMY READING
     ENDIF
    ENDDO

    ! CONSTRUCT SUBSPACE HAMILTONIAN MATRIX <N|N> FOR DEBUG
!   DO I=1,NTRIALS
!    REWIND(50)
!    DO J=1,I
!     READ(50) VEC1
!    ENDDO
!    REWIND(50)
!    DO L=1,NTRIALS
!     READ(50) VEC2
!     SMALLH(I,L)=0.0D0
!     DO IA=1,NCF
!      DO IB=1,NCF
!       SMALLH(I,L)=SMALLH(I,L)+VEC1(IB,IA)*VEC2(IB,IA)
!      ENDDO
!     ENDDO
!    ENDDO
!   ENDDO
!   CALL DUMP10(SMALLH,NTRIALS,MAXFILE)

    ! CONSTRUCT SUBSPACE HAMILTONIAN MATRIX <N|H|N>
    REWIND(50)
    DO I=1,NTRIALS
     READ(50) VEC1
     REWIND(51)
     DO L=1,NTRIALS
      READ(51) VEC2
      SMALLH(I,L)=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        SMALLH(I,L)=SMALLH(I,L)+VEC1(IB,IA)*VEC2(IB,IA)
       ENDDO
      ENDDO
     ENDDO
    ENDDO
!   CALL DUMP10(SMALLH,NTRIALS,MAXFILE)
      
    ! DIAGONALIZE SUBSPACE HAMILTONIAN
    IF (NTRIALS == 1) THEN
     ER(1)=SMALLH(1,1)
     VR(1,1)=1.0D0
    ELSE
     CALL DGEEV('N','V',NTRIALS,SMALLH,MAXFILE,ER,EI,VL,1,VR,MAXFILE,WK,4*MAXFILE,INFO)
     IF (INFO /= 0) CALL PABORT('DGEEV FAILED TO DIAGONALIZE A MATRIX')
     CALL PIKSRT(NTRIALS,MAXFILE,ER,VR,EI)
    ENDIF
!   DO I=1,NTRIALS
!    WRITE(6,'(A,I2,A,F20.15)') 'ROOT ',I,' = ',ER(I)+NUCLEAR_REPULSION
!   ENDDO
      
    ! FORM RESIDUAL VECTORS
    CALL CPU_TIME(ICPUE)
    DEVMAX=0.0D0
    WRITE(6,'(A,I3)') ' ITER ',ICYCLE
    NTRIALS_NEXT=NTRIALS
    DO K=1,NROOTS
     VEC1=0.0D0
     REWIND(50)
     REWIND(51)
     DO L=1,NTRIALS
      READ(51) VEC2
      VEC1=VEC1+VEC2*VR(L,K)
      READ(50) VEC2
      VEC1=VEC1-ER(K)*VEC2*VR(L,K)
     ENDDO
     DEVSQ=0.0D0
     DO IA=1,NCF
      DO IB=1,NCF
       IF (NORDER(IA)+NORDER(IB) > RORDER) VEC1(IB,IA)=0.0D0
       DEVSQ=DEVSQ+VEC1(IB,IA)**2
      ENDDO
     ENDDO
     DEVSQ=DSQRT(DEVSQ)
     IF (DEVSQ > DEVMAX) DEVMAX=DEVSQ
     EEOMCC(K)=ER(K)+NUCLEAR_REPULSION
     WRITE(6,'(A,I3,F15.10,F15.5,F20.10,F12.1)') ' ROOT ',K,DEVSQ,(EEOMCC(K)-EEOMCC(1))*EV,EEOMCC(K),(ICPUE-ICPUS)/DFLOAT(NROOTS)

     IF (DEVSQ < DOPTN(67)) CYCLE

     ! FORM A NEW SUBSPACE VECTOR BY HIRAO-NAKATSUJI SCHEME
     DO IA=1,NCF
      DO IB=1,NCF
       NOPEN=0
       DO I=1,IALL(0,0,0)-IVIRTCORE
        N(I)=0
        IF (BTEST(CFHALF(IA),I-1)) N(I)=1
        IF (BTEST(CFHALF(IB),I-1)) N(I)=N(I)+1
        NOPEN=NOPEN+N(I)*(2-N(I))
       ENDDO
       IF (NOPEN == 0) THEN
        F=0.0D0
       ELSE
        F=-DFLOAT(NOPEN)/DFLOAT(NOPEN*(NOPEN-1))
       ENDIF
       D=0.0D0
       DO I=1,IALL(0,0,0)-IVIRTCORE
        D=D+DFLOAT(N(I))*H(I,I)-DFLOAT(N(I)*(2-N(I)))*G(I,I,I,I)/4.0D0
        DO J=1,IALL(0,0,0)-IVIRTCORE
         D=D+DFLOAT(N(I)*N(J))*(2.0D0*G(I,I,J,J)-G(I,J,J,I))/4.0D0
         IF (I /= J) D=D-F*DFLOAT(N(I)*(2-N(I))*N(J)*(2-N(J)))*G(I,J,J,I)/4.0D0
        ENDDO
       ENDDO
       IF ((ER(K)-D) /= 0.0D0) VEC1(IB,IA)=VEC1(IB,IA)/(ER(K)-D)
      ENDDO
     ENDDO
      
     ! ORTHONORMALIZE THE NEW SUBSPACE VECTOR
     D=0.0D0
     DO IA=1,NCF
      DO IB=1,NCF
       D=D+VEC1(IB,IA)**2
      ENDDO
     ENDDO
     D=DSQRT(D)
     VEC1=VEC1/D
     REWIND(50)
     DO L=1,NTRIALS_NEXT
      READ(50) VEC2
      D=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        D=D+VEC1(IB,IA)*VEC2(IB,IA)
       ENDDO
      ENDDO
      VEC1=VEC1-D*VEC2
     ENDDO
     D=0.0D0
     DO IA=1,NCF
      DO IB=1,NCF
       D=D+VEC1(IB,IA)**2
      ENDDO
     ENDDO
     D=DSQRT(D)
     VEC1=VEC1/D
     IF (D > DOPTN(67)*100.0D0) THEN
      NTRIALS_NEXT=NTRIALS_NEXT+1
      IF (NTRIALS_NEXT > MAXFILE) CALL PABORT('TOO MANY TRIAL VECTORS')
      REWIND(50)
      DO L=1,NTRIALS_NEXT-1
       READ(50) VEC2
      ENDDO
      WRITE(50) VEC1
     ENDIF

    ENDDO

    CALL CPU_TIME(ICPUS)
    CALL PFLUSH(6)

    IF (DEVMAX < DOPTN(67)) THEN
     WRITE(6,'(A)') '-----------------------------------------------------------------------'
     IF ((IOPTN(66) > 0).AND.(IOPTN(66) <= NROOTS)) THEN
      N_DEGENERATE_ROOTS=0
      DO I=1,NROOTS
       IF (DABS(EEOMCC(IOPTN(66))-EEOMCC(I)) < 1.0D-5) THEN
        N_DEGENERATE_ROOTS=N_DEGENERATE_ROOTS+1
        IF (N_DEGENERATE_ROOTS > 10) CALL PABORT('TOO MANY DEGENERACIES')
        DEGENERATE_ROOTS(N_DEGENERATE_ROOTS)=I
       ENDIF
      ENDDO
      IOPTN(66)=DEGENERATE_ROOTS(1)
      IF (N_DEGENERATE_ROOTS > 1) WRITE(6,'(A,I3,A,A,A)') '***** WARNING : ROOT ',IOPTN(66),' IS ',CNUMBER(N_DEGENERATE_ROOTS),&
      '-FOLD DEGENERATE'
     ENDIF
      
     IF (LLEFT) THEN
      OPEN(70,FILE=TRIM(COPTN(1))//'.lft',FORM='UNFORMATTED')
      REWIND(70)
     ELSE
      OPEN(71,FILE=TRIM(COPTN(1))//'.rgt',FORM='UNFORMATTED')
      REWIND(71)
     ENDIF
     DO I=1,NROOTS
      WRITE(6,'(A,I3,F15.10,A)') 'ROOT ',I,(EEOMCC(I)-EEOMCC(1))*EV,' EV'
      REWIND(50)
      VEC1=0.0D0
      DO L=1,NTRIALS
       READ(50) VEC2
       VEC1=VEC1+VEC2*VR(L,I)
      ENDDO
      D=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        D=D+VEC1(IA,IB)**2
       ENDDO
      ENDDO
      VEC1=VEC1/DSQRT(D)
      IF ((IOPTN(66) > 0).AND.(IOPTN(66) <= NROOTS)) THEN
       IF (LLEFT) THEN
        DO J=1,N_DEGENERATE_ROOTS
         IF (I == DEGENERATE_ROOTS(J)) THEN
          WRITE(6,'(A,I3,A)') 'LEFT-HAND EIGENVECTOR OF ROOT ',I,' HAS BEEN STORED'
          WRITE(70) VEC1
         ENDIF
        ENDDO
       ELSE
        DO J=1,N_DEGENERATE_ROOTS
         IF (I == DEGENERATE_ROOTS(J)) THEN
          WRITE(6,'(A,I3,A)') 'RIGHT-HAND EIGENVECTOR OF ROOT ',I,' HAS BEEN STORED'
          WRITE(71) VEC1
         ENDIF
        ENDDO
       ENDIF
      ENDIF
      PERCENTS=0.0D0
      PERCENTD=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        IF (NORDER(IA)+NORDER(IB) == 1) PERCENTS=PERCENTS+VEC1(IA,IB)**2
        IF (NORDER(IA)+NORDER(IB) == 2) PERCENTD=PERCENTD+VEC1(IA,IB)**2
       ENDDO
      ENDDO
      WRITE(6,'(A,F6.2)') ' %SINGLES (CC) = ',PERCENTS*1.0D2
      WRITE(6,'(A,F6.2)') ' %DOUBLES (CC) = ',PERCENTD*1.0D2
      REWIND(60)
      WRITE(60) VEC1
      DEALLOCATE(VEC1,VEC2)
      CALL EXPONENTIAL_OPERATOR(90,60,61,TORDER,.FALSE.,.FALSE.)
      ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF))
      CALL S_SQUARED(61,0,2*MIN(IOCC-ICORE,IALL(0,0,0)-IOCC-IVIRTCORE),S2)
      WRITE(6,'(A,F8.4)') ' < S**2 > = ',S2
      REWIND(61)
      READ(61) VEC2
      D=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        D=D+VEC2(IA,IB)**2
       ENDDO
      ENDDO
      VEC2=VEC2/DSQRT(D)
      PERCENTS=0.0D0
      PERCENTD=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        IF (NORDER(IA)+NORDER(IB) == 1) PERCENTS=PERCENTS+VEC2(IA,IB)**2
        IF (NORDER(IA)+NORDER(IB) == 2) PERCENTD=PERCENTD+VEC2(IA,IB)**2
       ENDDO
      ENDDO
      WRITE(6,'(A,F6.2)') ' %SINGLES (CI) = ',PERCENTS*1.0D2
      WRITE(6,'(A,F6.2)') ' %DOUBLES (CI) = ',PERCENTD*1.0D2
      DO IA=1,NCF
       DO IB=1,NCF
        IF (DABS(VEC2(IA,IB)) > 0.1D0) WRITE(6,'(A,I6,I6,A,F8.4)') ' EFFFECTIVE DETERMINANT (',IA,IB,' ) ',VEC2(IA,IB)
       ENDDO
      ENDDO
     ENDDO
!    REWIND(50)
!    REWIND(51)
!    DO I=1,NROOTS
!     READ(51) VEC1
!     WRITE(50) VEC1
!    ENDDO
     DEALLOCATE(VEC1,VEC2,N)
     IF (LLEFT) THEN
      CLOSE(70)
     ELSE
      CLOSE(71)
     ENDIF
     CLOSE(50)
     CLOSE(51)
     CLOSE(60)
     CLOSE(61)
     CLOSE(90)
     RETURN
    ELSE IF (NTRIALS_NEXT == NTRIALS) THEN
     CALL PABORT('ALGORITHM FAILED TO INCREASE THE SUBSPACE')
    ELSE IF (NROOTS > 1) THEN
     WRITE(6,'(I3,A)') NTRIALS_NEXT-NTRIALS,' TRIAL VECTORS HAVE BEEN ADDED'
    ENDIF
    NTRIALS=NTRIALS_NEXT

   ENDDO
   CALL PABORT('HIRAO-NAKATSUJI ALGORITHM FAILED TO CONVERGE')

END SUBROUTINE



SUBROUTINE S_SQUARED(INFILE,OFFSET,ORDER,S2)
! OPERATE WITH S**2 = S+ S- - SZ + SZ**2 A INPUT VECTOR IN INFILE
! AND INTEGRATE WITH ITSELF TO GET AN EXPECTATION VALUE OF S**2.  
! THE ORDER LIMITS THE NUMBER EXCITATION IN THE CONFIGURATIONS CONSIDERED.

   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI
   
   IMPLICIT NONE
   INTEGER :: INFILE,OFFSET
   INTEGER :: ORDER
   INTEGER :: ISIGN,JSIGN
   INTEGER :: I,J,K
   INTEGER :: IA,IB,IC,ID
   INTEGER(4) :: CFA0,CFB0,CFA1,CFB1,CFA2,CFB2
   REAL :: MEM
   DOUBLE PRECISION :: S2,NORM
   DOUBLE PRECISION,ALLOCATABLE :: INP(:,:),OUT(:,:)

!  CALL CPU_TIME(ICPUS)
   MEM=16.0*2.0*NCF**2+4.0*2.0*NCF*(IALL(0,0,0)-IVIRTCORE)**2
!  IF (MEM > 1000000.0) THEN
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000.0,' MB'
!  ELSE IF (MEM > 1000.0) THEN
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000.0,' KB'
!  ELSE
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM,' B'
!  ENDIF
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY') 

   ALLOCATE(INP(NCF,NCF),OUT(NCF,NCF))

   ! READ INPUT VECTOR FROM FILE INFILE
   REWIND(INFILE)
   DO I=0,OFFSET
    READ(INFILE) INP
   ENDDO

   ! ZERO SCRATCH INPUT VECTOR ELEMENTS ASSOCIATED WITH EXCITATION HIGHER THAN THE ORDER
   NORM=0.0D0
   DO IA=1,NCF
    DO IB=1,NCF
     IF (NORDER(IA)+NORDER(IB) > ORDER) INP(IA,IB)=0.0D0
     NORM=NORM+INP(IA,IB)**2
    ENDDO
   ENDDO

   ! ZERO SCRATCH OUTPUT VECTOR
   OUT=0.0D0

   ! S+ S-
   DO IA=1,NCF
    DO IB=1,NCF
     CFA0=CFHALF(IA)
     CFB0=CFHALF(IB)
     ! OPERATE WITH S-
     DO I=1,IALL(0,0,0)-IVIRTCORE
      IF (BTEST(CFA0,I-1).AND.(.NOT.BTEST(CFB0,I-1))) THEN
       CFA1=IBCLR(CFA0,I-1)
       CFB1=IBSET(CFB0,I-1)
       ISIGN=1
       DO J=1,I
        IF (BTEST(CFA0,J-1)) ISIGN=-ISIGN
        IF (BTEST(CFB1,J-1)) ISIGN=-ISIGN
       ENDDO
       ! OPERATE WITH S+
       DO J=1,IALL(0,0,0)-IVIRTCORE
        IF ((.NOT.BTEST(CFA1,J-1)).AND.BTEST(CFB1,J-1)) THEN
         CFA2=IBSET(CFA1,J-1)
         CFB2=IBCLR(CFB1,J-1)
         JSIGN=1
         DO K=1,J
          IF (BTEST(CFA2,K-1)) JSIGN=-JSIGN
          IF (BTEST(CFB1,K-1)) JSIGN=-JSIGN
         ENDDO
         IC=ADDRSS(CFA2)
         ID=ADDRSS(CFB2)
         OUT(IC,ID)=OUT(IC,ID)+DFLOAT(ISIGN*JSIGN)*INP(IA,IB)
        ENDIF
       ENDDO
      ENDIF
     ENDDO
    ENDDO
   ENDDO

   S2=0.0D0
   DO IA=1,NCF
    DO IB=1,NCF
     IF (NORDER(IA)+NORDER(IB) > ORDER) CYCLE
     S2=S2+INP(IA,IB)*OUT(IA,IB)/NORM
    ENDDO
   ENDDO

   DEALLOCATE(INP,OUT)

   RETURN
END SUBROUTINE



SUBROUTINE HAMILTONIAN_P_DEBUG(INFILE,OUTFILE,OFFSET,ORDER)
! FORM THE PRODUCT OF HAMILTONIAN MATRIX AND A TRIAL VECTOR IN INFILE
! AND STORE THE PRODUCT VECTOR IN OUTFILE.  THE ORDER LIMITS THE NUMBER
! EXCITATION IN THE CONFIGURATIONS CONSIDERED.

   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI
   
   IMPLICIT NONE
   INTEGER :: INFILE,OUTFILE,OFFSET
   INTEGER :: ORDER
   INTEGER :: ISIGN,JSIGN
   INTEGER :: I,J,K,L,M
   INTEGER :: IA,IB,IC,ID
   INTEGER :: NIA,NIB,NJA,NJB
   INTEGER(4) :: CFONE,CFTWO
   REAL :: MEM,ICPUS,ICPUE,EST
   DOUBLE PRECISION :: X,Y
   DOUBLE PRECISION,ALLOCATABLE :: TRL(:,:),PRD(:,:)
   DOUBLE PRECISION,ALLOCATABLE :: GG(:,:,:,:)
   INTEGER,ALLOCATABLE :: PSIGN(:,:,:),PADRS(:,:,:)

   CALL CPU_TIME(ICPUS)
   MEM=16.0*2.0*NCF**2+4.0*2.0*NCF*(IALL(0,0,0)-IVIRTCORE)**2
!  IF (MEM > 1000000.0) THEN
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000.0,' MB'
!  ELSE IF (MEM > 1000.0) THEN
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000.0,' KB'
!  ELSE
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM,' B'
!  ENDIF
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY') 

   ALLOCATE(TRL(NCF,NCF),PRD(NCF,NCF),PSIGN(IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE,NCF),PADRS(IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE,NCF))

   ALLOCATE(GG(IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE))
   GG = G
   DO I = IOCC+1,IALL(0,0,0)-IVIRTCORE
   DO J = ICORE+1,IOCC
   DO K = IOCC+1,IALL(0,0,0)-IVIRTCORE
   DO L = ICORE+1,IOCC
    GG(I,J,K,L) = G(I,J,K,L) / (EPSILON(J,0,0,0)+EPSILON(L,0,0,0)-EPSILON(I,0,0,0)-EPSILON(K,0,0,0))
    GG(J,I,L,K) = G(J,I,L,K) / (EPSILON(J,0,0,0)+EPSILON(L,0,0,0)-EPSILON(I,0,0,0)-EPSILON(K,0,0,0))
   ENDDO
   ENDDO
   ENDDO
   ENDDO

   ! READ TRIAL VECTOR FROM FILE INFILE
   REWIND(INFILE)
   DO I=0,OFFSET
    READ(INFILE) TRL
   ENDDO

   ! ZERO SCRATCH TRIAL VECTOR ELEMENTS ASSOCIATED WITH EXCITATION HIGHER THAN THE ORDER
   DO IA=1,NCF
    DO IB=1,NCF
     IF (NORDER(IA)+NORDER(IB) > ORDER) TRL(IA,IB)=0.0D0
    ENDDO
   ENDDO

   ! ZERO SCRATCH PRODUCT VECTOR
   PRD=0.0D0

   ! DIAGONAL ELEMENTS
   DO IA=1,NCF
    DO IB=1,NCF
     DO I=1,IALL(0,0,0)-IVIRTCORE
      IF (BTEST(CFHALF(IA),I-1)) PRD(IB,IA)=PRD(IB,IA)+H(I,I)*TRL(IB,IA)
      IF (BTEST(CFHALF(IB),I-1)) PRD(IB,IA)=PRD(IB,IA)+H(I,I)*TRL(IB,IA)
     ENDDO
     DO I=1,IALL(0,0,0)-IVIRTCORE
      DO J=1,IALL(0,0,0)-IVIRTCORE
       IF (BTEST(CFHALF(IA),I-1)) THEN
        NIA=1
       ELSE
        NIA=0
       ENDIF
       IF (BTEST(CFHALF(IA),J-1)) THEN
        NJA=1
       ELSE
        NJA=0
       ENDIF
       IF (BTEST(CFHALF(IB),I-1)) THEN
        NIB=1
       ELSE
        NIB=0
       ENDIF
       IF (BTEST(CFHALF(IB),J-1)) THEN
        NJB=1
       ELSE
        NJB=0
       ENDIF
       PRD(IB,IA)=PRD(IB,IA)+0.5D0*(DFLOAT((NJA+NJB)*(NIA+NIB))*GG(J,J,I,I) &
                 -DFLOAT(NJA*NIA+NJB*NIB)*GG(J,I,I,J))*TRL(IB,IA)
      ENDDO
     ENDDO
    ENDDO
   ENDDO

   ! ONE SPIN ORBITAL DIFFERENCE
   DO IA=1,NCF
    DO I=ICORE+1,IALL(0,0,0)-IVIRTCORE
     IF (BTEST(CFHALF(IA),I-1)) THEN
      DO K=1,IALL(0,0,0)-IVIRTCORE
       IF (.NOT.(BTEST(CFHALF(IA),K-1))) THEN
        CFONE=IBCLR(CFHALF(IA),I-1)
        CFONE=IBSET(CFONE,K-1)
        IC=ADDRSS(CFONE)
        X=H(K,I)
        DO J=1,IALL(0,0,0)-IVIRTCORE
         IF ((J /= I).AND.(J /= K).AND.(BTEST(CFONE,J-1))) X=X+GG(K,I,J,J)-GG(K,J,J,I)
        ENDDO
        ISIGN=1
        DO J=1,I
         IF (BTEST(CFHALF(IA),J-1)) ISIGN=-ISIGN
        ENDDO
        DO J=1,K
         IF (BTEST(CFONE,J-1)) ISIGN=-ISIGN
        ENDDO
        DO IB=1,NCF
         IF (DABS(TRL(IB,IA)) > 1.0D-15) THEN
          Y=0.0D0
          DO J=1,IALL(0,0,0)-IVIRTCORE
           IF (BTEST(CFHALF(IB),J-1)) Y=Y+GG(K,I,J,J)
          ENDDO
          PRD(IB,IC)=PRD(IB,IC)+(X+Y)*DFLOAT(ISIGN)*TRL(IB,IA)
         ENDIF
        ENDDO
       ENDIF
      ENDDO
     ENDIF
    ENDDO
   ENDDO
   DO IB=1,NCF
    DO I=ICORE+1,IALL(0,0,0)-IVIRTCORE
     IF (BTEST(CFHALF(IB),I-1)) THEN
      DO K=1,IALL(0,0,0)-IVIRTCORE
       IF (.NOT.(BTEST(CFHALF(IB),K-1))) THEN
        CFONE=IBCLR(CFHALF(IB),I-1)
        CFONE=IBSET(CFONE,K-1)
        IC=ADDRSS(CFONE)
        X=H(K,I)
        DO J=1,IALL(0,0,0)-IVIRTCORE
         IF ((J /= I).AND.(J /= K).AND.(BTEST(CFONE,J-1))) X=X+GG(K,I,J,J)-GG(K,J,J,I)
        ENDDO
        ISIGN=1
        DO J=1,I
         IF (BTEST(CFHALF(IB),J-1)) ISIGN=-ISIGN
        ENDDO
        DO J=1,K
         IF (BTEST(CFONE,J-1)) ISIGN=-ISIGN
        ENDDO
        DO IA=1,NCF
         IF (DABS(TRL(IB,IA)) > 1.0D-15) THEN
          Y=0.0D0
          DO J=1,IALL(0,0,0)-IVIRTCORE
           IF (BTEST(CFHALF(IA),J-1)) Y=Y+GG(K,I,J,J)
          ENDDO
          PRD(IC,IA)=PRD(IC,IA)+(X+Y)*DFLOAT(ISIGN)*TRL(IB,IA)
         ENDIF
        ENDDO
       ENDIF
      ENDDO
     ENDIF
    ENDDO
   ENDDO

   ! TWO SPIN ORBITAL DIFFERENCES, CASE 1
   DO IA=1,NCF
    DO I=ICORE+1,IALL(0,0,0)-IVIRTCORE-1
     IF (BTEST(CFHALF(IA),I-1)) THEN
      DO J=I+1,IALL(0,0,0)-IVIRTCORE
       IF (BTEST(CFHALF(IA),J-1)) THEN
        DO K=1,IALL(0,0,0)-IVIRTCORE-1
         IF (.NOT.(BTEST(CFHALF(IA),K-1))) THEN
          DO L=K+1,IALL(0,0,0)-IVIRTCORE
           IF (.NOT.(BTEST(CFHALF(IA),L-1))) THEN
            CFTWO=IBCLR(CFHALF(IA),I-1)
            CFTWO=IBCLR(CFTWO,J-1)
            CFTWO=IBSET(CFTWO,K-1)
            CFTWO=IBSET(CFTWO,L-1)
            IC=ADDRSS(CFTWO)
            ISIGN=1
            DO M=1,I
             IF (BTEST(CFHALF(IA),M-1)) ISIGN=-ISIGN
            ENDDO
            DO M=1,J
             IF (BTEST(CFHALF(IA),M-1)) ISIGN=-ISIGN
            ENDDO
            DO M=1,K
             IF (BTEST(CFTWO,M-1)) ISIGN=-ISIGN
            ENDDO
            DO M=1,L
             IF (BTEST(CFTWO,M-1)) ISIGN=-ISIGN
            ENDDO
            Y=DFLOAT(ISIGN)*(GG(K,I,L,J)-GG(K,J,L,I))
            DO IB=1,NCF
             PRD(IB,IC)=PRD(IB,IC)+Y*TRL(IB,IA)
            ENDDO
           ENDIF
          ENDDO
         ENDIF
        ENDDO
       ENDIF
      ENDDO
     ENDIF
    ENDDO
   ENDDO
   DO IB=1,NCF
    DO I=ICORE+1,IALL(0,0,0)-IVIRTCORE-1
     IF (BTEST(CFHALF(IB),I-1)) THEN
      DO J=I+1,IALL(0,0,0)-IVIRTCORE
       IF (BTEST(CFHALF(IB),J-1)) THEN
        DO K=1,IALL(0,0,0)-IVIRTCORE-1
         IF (.NOT.(BTEST(CFHALF(IB),K-1))) THEN
          DO L=K+1,IALL(0,0,0)-IVIRTCORE
           IF (.NOT.(BTEST(CFHALF(IB),L-1))) THEN
            CFTWO=IBCLR(CFHALF(IB),I-1)
            CFTWO=IBCLR(CFTWO,J-1)
            CFTWO=IBSET(CFTWO,K-1)
            CFTWO=IBSET(CFTWO,L-1)
            IC=ADDRSS(CFTWO)
            ISIGN=1
            DO M=1,I
             IF (BTEST(CFHALF(IB),M-1)) ISIGN=-ISIGN
            ENDDO
            DO M=1,J
             IF (BTEST(CFHALF(IB),M-1)) ISIGN=-ISIGN
            ENDDO
            DO M=1,K
             IF (BTEST(CFTWO,M-1)) ISIGN=-ISIGN
            ENDDO
            DO M=1,L
             IF (BTEST(CFTWO,M-1)) ISIGN=-ISIGN
            ENDDO
            Y=DFLOAT(ISIGN)*(GG(K,I,L,J)-GG(K,J,L,I))
            DO IA=1,NCF
             PRD(IC,IA)=PRD(IC,IA)+Y*TRL(IB,IA)
            ENDDO
           ENDIF
          ENDDO
         ENDIF
        ENDDO
       ENDIF
      ENDDO
     ENDIF
    ENDDO
   ENDDO
!  CALL CPU_TIME(ICPUE)
!  EST=ICPUE-ICPUS
!  CALL CPU_TIME(ICPUS)

   ! TWO SPIN ORBITAL DIFFERENCES, CASE 2
   DO IB=1,NCF
    DO K=ICORE+1,IALL(0,0,0)-IVIRTCORE
     DO L=1,IALL(0,0,0)-IVIRTCORE
      PSIGN(L,K,IB)=0
      PADRS(L,K,IB)=0
      IF ((BTEST(CFHALF(IB),K-1)).AND.(.NOT.(BTEST(CFHALF(IB),L-1)))) THEN
       CFONE=IBCLR(CFHALF(IB),K-1)
       CFONE=IBSET(CFONE,L-1)
       PADRS(L,K,IB)=ADDRSS(CFONE)
       JSIGN=1
       DO M=1,K
        IF (BTEST(CFHALF(IB),M-1)) JSIGN=-JSIGN
       ENDDO
       DO M=1,L
        IF (BTEST(CFONE,M-1)) JSIGN=-JSIGN
       ENDDO
       PSIGN(L,K,IB)=JSIGN
      ENDIF
     ENDDO
    ENDDO
   ENDDO
   DO IA=1,NCF
    DO I=ICORE+1,IALL(0,0,0)-IVIRTCORE
     DO J=1,IALL(0,0,0)-IVIRTCORE
      IF ((BTEST(CFHALF(IA),I-1)).AND.(.NOT.(BTEST(CFHALF(IA),J-1)))) THEN
       CFONE=IBCLR(CFHALF(IA),I-1)
       CFONE=IBSET(CFONE,J-1)
       IC=ADDRSS(CFONE)
       ISIGN=1
       DO K=1,I
        IF (BTEST(CFHALF(IA),K-1)) ISIGN=-ISIGN
       ENDDO
       DO K=1,J
        IF (BTEST(CFONE,K-1)) ISIGN=-ISIGN
       ENDDO
       DO IB=1,NCF
        IF (DABS(TRL(IB,IA)) > 1.0D-15) THEN
         DO K=ICORE+1,IALL(0,0,0)-IVIRTCORE
          DO L=1,IALL(0,0,0)-IVIRTCORE
           IF (PADRS(L,K,IB) /= 0) PRD(PADRS(L,K,IB),IC)=PRD(PADRS(L,K,IB),IC)+DFLOAT(ISIGN*PSIGN(L,K,IB))*GG(J,I,L,K)*TRL(IB,IA)
          ENDDO
         ENDDO
        ENDIF
       ENDDO
      ENDIF
     ENDDO
    ENDDO
!   CALL PCPU_TIME(ICPUE)
!   IF (IA == 1) THEN
!    EST=EST+(ICPUE-ICPUS)*NCF
!    EST=EST/3600.0
!    IF (EST > 0.1) WRITE(6,'(A,F20.1,A)') '***** WARNING : ESTIMATED PRODUCT FORMATION TIME = ',EST,' HOURS'
!   ENDIF
   ENDDO

   ! ZERO SCRATCH PRODUCT VECTOR ELEMENTS ASSOCIATED WITH EXCITATION HIGHER THAN THE ORDER
   DO IA=1,NCF
    DO IB=1,NCF
     IF (NORDER(IA)+NORDER(IB) > ORDER) PRD(IA,IB)=0.0D0
    ENDDO
   ENDDO

   ! STORE PRODUCT VECTOR
   REWIND(OUTFILE)
   IF (OFFSET == 0) THEN
    WRITE(OUTFILE) PRD
   ELSE
    DO I=1,OFFSET
     READ(OUTFILE) TRL
    ENDDO
    WRITE(OUTFILE) PRD
   ENDIF

   DEALLOCATE(GG)
   DEALLOCATE(TRL,PRD,PSIGN,PADRS)

   RETURN
END SUBROUTINE



SUBROUTINE HIGHORDER_CI_SCALE(ORDER,NROOTS,NTRIALS,SCALEFACTOR)
! PERFORM HIGH-ORDER CONFIGURATION INTERACTION CALCULATIONS BY THE DAVIDSON'S ALGORITHM.

   USE CONSTANTS
   USE CONTROL
   USE GRADIENT
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI

   IMPLICIT NONE
   INTEGER,PARAMETER :: MAXFILE = 1000
   INTEGER,PARAMETER :: MAXCYCLE = 1000
   INTEGER :: ICYCLE
   INTEGER :: ORDER
   INTEGER :: NROOTS
   INTEGER :: NTRIALS
   INTEGER :: NTRIALS_NEXT
   INTEGER :: IA,IB
   INTEGER :: I,J,K,L,NOPEN
   INTEGER,ALLOCATABLE :: N(:)
   REAL :: MEM,ICPUS,ICPUE
   LOGICAL :: LDONE(MAXFILE)
   DOUBLE PRECISION :: SCALEFACTOR
   DOUBLE PRECISION :: DEVSQ,DEVMAX,D,F,S2
   DOUBLE PRECISION :: SMALLH(MAXFILE,MAXFILE),LAMBDA(MAXFILE),ECI(MAXFILE)
   DOUBLE PRECISION :: W1(MAXFILE,MAXFILE),W2(MAXFILE)
   DOUBLE PRECISION :: PERCENTS,PERCENTD
   DOUBLE PRECISION,ALLOCATABLE :: VEC1(:,:),VEC2(:,:)

   CALL CPU_TIME(ICPUS)
!  WRITE(6,'(A)') 'HIGH-ORDER CONFIGURATION INTERACTION CALCULATIONS WILL BE PERFORMED'
   IF (SCALEFACTOR /= 1.0D0) THEN
!   WRITE(6,'(A)') '*************************************************************'
!   WRITE(6,'(A,F7.4,A)') '* WARNING: SCALE FACTOR FOR FLUCTUATION POTENTIAL = ',SCALEFACTOR,' *'
!   WRITE(6,'(A)') '*************************************************************'
   ENDIF
!  WRITE(6,'(A)') 'HIGH-ORDER CONFIGURATION INTERACTION CALCULATIONS WILL BE PERFORMED'
!  WRITE(6,'(A,I2)') 'CI ORDER IS ',ORDER
!  WRITE(6,'(A,I4)') 'THE NUMBER OF CI ROOTS SOUGHT ',NROOTS
!  WRITE(6,'(A,I4)') 'THE NUMBER OF INITIAL TRIAL VECTORS ',NTRIALS
   MEM=16.0*2.0*NCF**2
   IF (MEM > 1000000.0) THEN
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000.0,' MB'
   ELSE IF (MEM > 1000.0) THEN
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000.0,' KB'
   ELSE
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM,' B'
   ENDIF
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY') 
   IF (NROOTS > 1) THEN
!   WRITE(6,'(A)') '-----------------------------------------------------------------------'
!   WRITE(6,'(A)') '             DEVIATION        OMEGA / EV       TOTAL ENERGY   CPU / SEC'
   ELSE
!   WRITE(6,'(A)') '----------------------------------------------------------------'
!   WRITE(6,'(A)') 'ITR    DEVIATION     CORRELATION        TOTAL ENERGY   CPU / SEC'
   ENDIF
   ALLOCATE(VEC1(NCF,NCF),VEC2(NCF,NCF),N(IALLMAX-IVIRTCORE))
   OPEN(50,FILE=TRIM(COPTN(1))//'.fi0',FORM='UNFORMATTED')
   OPEN(51,FILE=TRIM(COPTN(1))//'.fi1',FORM='UNFORMATTED')
   OPEN(52,FILE=TRIM(COPTN(1))//'.fi2',FORM='UNFORMATTED')
   OPEN(53,FILE=TRIM(COPTN(1))//'.fi3',FORM='UNFORMATTED')

   ! INITIALIZE
   LDONE=.FALSE.
   IF (NTRIALS > MAXFILE) CALL PABORT('TOO MANY INITIAL TRIAL VECTORS')

   ! DAVIDSON ITERATION
   DO ICYCLE=1,MAXCYCLE

    ! CALCULATE H|0> AND STORE IT IN FILE 70
!   DO I=1,NTRIALS
!    IF (.NOT.LDONE(I)) THEN
!     CALL HAMILTONIAN_PRODUCT(50,51,I-1,ORDER)
!     LDONE(I)=.TRUE.
!    ENDIF
!   ENDDO
    DO I=1,NTRIALS
     IF (.NOT.LDONE(I)) THEN
      REWIND(50)
      DO J=1,I
       READ(50) VEC1
      ENDDO
      REWIND(52)
      WRITE(52) VEC1
      CALL HAMILTONIAN_PRODUCT(52,53,0,ORDER)
      REWIND(53)
      READ(53) VEC2
      CALL H0_PRODUCT(52,53,0)
      REWIND(53)
      READ(53) VEC1
      VEC2=VEC1+(VEC2-VEC1)*SCALEFACTOR
      REWIND(51)
      IF (I >= 2) THEN
       DO J=1,I-1
        READ(51) VEC1
       ENDDO
      ENDIF
      WRITE(51) VEC2
      LDONE(I)=.TRUE.
     ENDIF
    ENDDO

    ! CONSTRUCT SUBSPACE HAMILTONIAN MATRIX <N|N> FOR DEBUG
!   DO I=1,NTRIALS
!    REWIND(50)
!    DO J=1,I
!     READ(50) VEC1
!    ENDDO
!    REWIND(50)
!    DO L=1,NTRIALS
!     READ(50) VEC2
!     SMALLH(I,L)=0.0D0
!     DO IA=1,NCF
!      DO IB=1,NCF
!       SMALLH(I,L)=SMALLH(I,L)+VEC1(IB,IA)*VEC2(IB,IA)
!      ENDDO
!     ENDDO
!    ENDDO
!    WRITE(6,'(I3,100F10.5:)') I,(SMALLH(I,L),L=1,NTRIALS)
!   ENDDO

    ! CONSTRUCT SUBSPACE HAMILTONIAN MATRIX <N|H|N>
    REWIND(50)
    DO I=1,NTRIALS
     READ(50) VEC1
     REWIND(51)
     DO L=1,NTRIALS
      READ(51) VEC2
      SMALLH(I,L)=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        SMALLH(I,L)=SMALLH(I,L)+VEC1(IB,IA)*VEC2(IB,IA)
       ENDDO
      ENDDO
     ENDDO
    ENDDO
      
    ! DIAGONALIZE SUBSPACE HAMILTONIAN
    IF (NTRIALS == 1) THEN
     LAMBDA(1)=SMALLH(1,1)
     W1(1,1)=1.0D0
    ELSE
     W1=SMALLH
     CALL TRED2(W1,NTRIALS,MAXFILE,LAMBDA,W2)
     CALL TQLI(LAMBDA,W2,NTRIALS,MAXFILE,W1)
     CALL PIKSRT(NTRIALS,MAXFILE,LAMBDA,W1,W2)
    ENDIF
      
    ! FORM RESIDUAL VECTORS
    CALL CPU_TIME(ICPUE)
    DEVMAX=0.0D0
!   IF (NROOTS > 1) WRITE(6,'(A,I3)') ' ITER ',ICYCLE
    NTRIALS_NEXT=NTRIALS
    DO K=1,NROOTS
     VEC1=0.0D0
     REWIND(50)
     REWIND(51)
     DO L=1,NTRIALS
      READ(51) VEC2
      VEC1=VEC1+VEC2*W1(L,K)
      READ(50) VEC2
      VEC1=VEC1-LAMBDA(K)*VEC2*W1(L,K)
     ENDDO
     DEVSQ=0.0D0
     DO IA=1,NCF
      DO IB=1,NCF
       DEVSQ=DEVSQ+VEC1(IB,IA)**2
      ENDDO
     ENDDO
     DEVSQ=DSQRT(DEVSQ)
     IF (DEVSQ > DEVMAX) DEVMAX=DEVSQ
     ECI(K)=LAMBDA(K)+NUCLEAR_REPULSION
     IF (NROOTS == 1) THEN
!     WRITE(6,'(I2,F15.10,F15.10,F20.10,F12.1)') ICYCLE,DEVSQ,ECI(K)-EHFKS,ECI(K),ICPUE-ICPUS
     ELSE
!     WRITE(6,'(A,I3,F15.10,F15.5,F20.10,F12.1)') ' ROOT ',K,DEVSQ,(ECI(K)-ECI(1))*EV,ECI(K),(ICPUE-ICPUS)/DFLOAT(NROOTS)
     ENDIF
      
     IF (DEVSQ < DOPTN(67)) CYCLE

     ! FORM A NEW SUBSPACE VECTOR BY KNOWLES-HANDY SCHEME
     DO IA=1,NCF
      DO IB=1,NCF
       NOPEN=0
       DO I=1,IALL(0,0,0)-IVIRTCORE
        N(I)=0
        IF (BTEST(CFHALF(IA),I-1)) N(I)=1
        IF (BTEST(CFHALF(IB),I-1)) N(I)=N(I)+1
        NOPEN=NOPEN+N(I)*(2-N(I))
       ENDDO
       IF (NOPEN == 0) THEN
        F=0.0D0
       ELSE
        F=-DFLOAT(NOPEN)/DFLOAT(NOPEN*(NOPEN-1))
       ENDIF
       D=0.0D0
       DO I=1,IALL(0,0,0)-IVIRTCORE
        D=D+DFLOAT(N(I))*H(I,I)-DFLOAT(N(I)*(2-N(I)))*G(I,I,I,I)/4.0D0
        DO J=1,IALL(0,0,0)-IVIRTCORE
         D=D+DFLOAT(N(I)*N(J))*(2.0D0*G(I,I,J,J)-G(I,J,J,I))/4.0D0
         IF (I /= J) D=D-F*DFLOAT(N(I)*(2-N(I))*N(J)*(2-N(J)))*G(I,J,J,I)/4.0D0
        ENDDO
       ENDDO
       IF ((LAMBDA(K)-D) /= 0.0D0) VEC1(IB,IA)=VEC1(IB,IA)/(LAMBDA(K)-D)
      ENDDO
     ENDDO
      
     ! ORTHONORMALIZE THE NEW SUBSPACE VECTOR
     D=0.0D0
     DO IA=1,NCF
      DO IB=1,NCF
       D=D+VEC1(IB,IA)**2
      ENDDO
     ENDDO
     D=DSQRT(D)
     VEC1=VEC1/D
     REWIND(50)
     DO L=1,NTRIALS_NEXT
      READ(50) VEC2
      D=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        D=D+VEC1(IB,IA)*VEC2(IB,IA)
       ENDDO
      ENDDO
      VEC1=VEC1-D*VEC2
     ENDDO
     D=0.0D0
     DO IA=1,NCF
      DO IB=1,NCF
       D=D+VEC1(IB,IA)**2
      ENDDO
     ENDDO
     D=DSQRT(D)
     VEC1=VEC1/D
     IF (D > DOPTN(67)*100.0D0) THEN
      NTRIALS_NEXT=NTRIALS_NEXT+1
      IF (NTRIALS_NEXT > MAXFILE) CALL PABORT('TOO MANY TRIAL VECTORS')
      REWIND(50)
      DO L=1,NTRIALS_NEXT-1
       READ(50) VEC2
      ENDDO
      WRITE(50) VEC1
     ENDIF

    ENDDO

    CALL CPU_TIME(ICPUS)
    CALL PFLUSH(6)

    IF (DEVMAX < DOPTN(67)) THEN
     IF (NROOTS > 1) THEN
!     WRITE(6,'(A)') '-----------------------------------------------------------------------'
     ELSE
!     WRITE(6,'(A)') '----------------------------------------------------------------'
     ENDIF
     REWIND(51)
     DO I=1,NROOTS
!     WRITE(6,'(A,I3,F15.10,A)') 'ROOT ',I,(ECI(I)-ECI(1))*EV,' EV'
      REWIND(50)
      VEC1=0.0D0
      DO L=1,NTRIALS
       READ(50) VEC2
       VEC1=VEC1+VEC2*W1(L,I)
      ENDDO
      D=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        D=D+VEC1(IA,IB)**2
       ENDDO
      ENDDO
      VEC1=VEC1/DSQRT(D)
      PERCENTS=0.0D0
      PERCENTD=0.0D0
      DO IA=1,NCF
       DO IB=1,NCF
        IF (NORDER(IA)+NORDER(IB) == 1) PERCENTS=PERCENTS+VEC1(IA,IB)**2
        IF (NORDER(IA)+NORDER(IB) == 2) PERCENTD=PERCENTD+VEC1(IA,IB)**2
       ENDDO
      ENDDO
!     WRITE(6,'(A,F6.2)') ' %SINGLES = ',PERCENTS*1.0D2
!     WRITE(6,'(A,F6.2)') ' %DOUBLES = ',PERCENTD*1.0D2
      WRITE(51) VEC1
      CALL S_SQUARED(51,I-1,ORDER,S2)
!     WRITE(6,'(A,F8.4)') ' < S**2 > = ',S2
      DO IA=1,NCF
       DO IB=1,NCF
!       IF (DABS(VEC1(IA,IB)) > 0.1D0) WRITE(6,'(A,I6,I6,A,F8.4)') ' DETERMINANT (',IA,IB,' ) ',VEC1(IA,IB)
       ENDDO
      ENDDO
      IF (IOPTN(9) >= 2) THEN
       WRITE(6,'(A)') 'CI WAVEFUNCTION'
       CALL DUMP5(VEC1,NCF)
      ENDIF
      IF ((IOPTN(97) > 0).AND.(I == 1)) THEN
       OPEN(97,FILE=TRIM(COPTN(1))//'.gf0',FORM='UNFORMATTED')
       REWIND(97)
       WRITE(97) VEC1
       CLOSE(97)
!      WRITE(6,'(A,A)') 'REFERENCE WAVE FUNCTION FOR MBGF STORED IN ',TRIM(COPTN(1))//'.gf0'
      ENDIF

     ENDDO
!    REWIND(50)
!    REWIND(51)
!    DO I=1,NROOTS
!     READ(51) VEC1
!     WRITE(50) VEC1
!    ENDDO
     DEALLOCATE(VEC1,VEC2,N)
     CLOSE(50)
     CLOSE(51)

     SCALEFACTOR=ECI(1)

     RETURN
    ELSE IF (NTRIALS_NEXT == NTRIALS) THEN
     CALL PABORT('ALGORITHM FAILED TO INCREASE THE SUBSPACE')
    ELSE IF (NROOTS > 1) THEN
!    WRITE(6,'(I3,A)') NTRIALS_NEXT-NTRIALS,' TRIAL VECTORS HAVE BEEN ADDED'
    ENDIF
    NTRIALS=NTRIALS_NEXT

   ENDDO
   CALL PABORT('DAVIDSON ALGORITHM FAILED TO CONVERGE')

END SUBROUTINE



SUBROUTINE HAMILTONIAN0_PRODUCT(INFILE,OUTFILE,OFFSET,ORDER)
! FORM THE PRODUCT OF HAMILTONIAN MATRIX AND A TRIAL VECTOR IN INFILE
! AND STORE THE PRODUCT VECTOR IN OUTFILE.  THE ORDER LIMITS THE NUMBER
! EXCITATION IN THE CONFIGURATIONS CONSIDERED.

   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE BASISSET
   USE FULLCI
   
   IMPLICIT NONE
   INTEGER :: INFILE,OUTFILE,OFFSET
   INTEGER :: ORDER
   INTEGER :: ISIGN,JSIGN
   INTEGER :: I,J,K,L,M
   INTEGER :: IA,IB,IC,ID
   INTEGER :: NIA,NIB,NJA,NJB
   INTEGER(4) :: CFONE,CFTWO
   REAL :: MEM,ICPUS,ICPUE,EST
   DOUBLE PRECISION :: X,Y
   DOUBLE PRECISION,ALLOCATABLE :: TRL(:,:),PRD(:,:)
   INTEGER,ALLOCATABLE :: PSIGN(:,:,:),PADRS(:,:,:)

   CALL CPU_TIME(ICPUS)
   MEM=16.0*2.0*NCF**2+4.0*2.0*NCF*(IALL(0,0,0)-IVIRTCORE)**2
!  IF (MEM > 1000000.0) THEN
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000.0,' MB'
!  ELSE IF (MEM > 1000.0) THEN
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000.0,' KB'
!  ELSE
!   WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM,' B'
!  ENDIF
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY') 

   ALLOCATE(TRL(NCF,NCF),PRD(NCF,NCF),PSIGN(IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE,NCF),PADRS(IALLMAX-IVIRTCORE,IALLMAX-IVIRTCORE,NCF))

   ! READ TRIAL VECTOR FROM FILE INFILE
   REWIND(INFILE)
   DO I=0,OFFSET
    READ(INFILE) TRL
   ENDDO

   ! ZERO SCRATCH TRIAL VECTOR ELEMENTS ASSOCIATED WITH EXCITATION HIGHER THAN THE ORDER
   DO IA=1,NCF
    DO IB=1,NCF
     IF (NORDER(IA)+NORDER(IB) > ORDER) TRL(IA,IB)=0.0D0
    ENDDO
   ENDDO

   ! ZERO SCRATCH PRODUCT VECTOR
   PRD=0.0D0

   ! DIAGONAL ELEMENTS
   DO IA=1,NCF
    DO IB=1,NCF
     DO I=1,IALL(0,0,0)-IVIRTCORE
      IF (BTEST(CFHALF(IA),I-1)) PRD(IB,IA)=PRD(IB,IA)+H(I,I)*TRL(IB,IA)
      IF (BTEST(CFHALF(IB),I-1)) PRD(IB,IA)=PRD(IB,IA)+H(I,I)*TRL(IB,IA)
     ENDDO
    ENDDO
   ENDDO

   ! ONE SPIN ORBITAL DIFFERENCE
   DO IA=1,NCF
    DO I=ICORE+1,IALL(0,0,0)-IVIRTCORE
     IF (BTEST(CFHALF(IA),I-1)) THEN
      DO K=1,IALL(0,0,0)-IVIRTCORE
       IF (.NOT.(BTEST(CFHALF(IA),K-1))) THEN
        CFONE=IBCLR(CFHALF(IA),I-1)
        CFONE=IBSET(CFONE,K-1)
        IC=ADDRSS(CFONE)
        X=H(K,I)
        ISIGN=1
        DO J=1,I
         IF (BTEST(CFHALF(IA),J-1)) ISIGN=-ISIGN
        ENDDO
        DO J=1,K
         IF (BTEST(CFONE,J-1)) ISIGN=-ISIGN
        ENDDO
        DO IB=1,NCF
         IF (DABS(TRL(IB,IA)) > 1.0D-15) THEN
          PRD(IB,IC)=PRD(IB,IC)+X*DFLOAT(ISIGN)*TRL(IB,IA)
         ENDIF
        ENDDO
       ENDIF
      ENDDO
     ENDIF
    ENDDO
   ENDDO
   DO IB=1,NCF
    DO I=ICORE+1,IALL(0,0,0)-IVIRTCORE
     IF (BTEST(CFHALF(IB),I-1)) THEN
      DO K=1,IALL(0,0,0)-IVIRTCORE
       IF (.NOT.(BTEST(CFHALF(IB),K-1))) THEN
        CFONE=IBCLR(CFHALF(IB),I-1)
        CFONE=IBSET(CFONE,K-1)
        IC=ADDRSS(CFONE)
        X=H(K,I)
        ISIGN=1
        DO J=1,I
         IF (BTEST(CFHALF(IB),J-1)) ISIGN=-ISIGN
        ENDDO
        DO J=1,K
         IF (BTEST(CFONE,J-1)) ISIGN=-ISIGN
        ENDDO
        DO IA=1,NCF
         IF (DABS(TRL(IB,IA)) > 1.0D-15) THEN
          PRD(IC,IA)=PRD(IC,IA)+X*DFLOAT(ISIGN)*TRL(IB,IA)
         ENDIF
        ENDDO
       ENDIF
      ENDDO
     ENDIF
    ENDDO
   ENDDO

   ! ZERO SCRATCH PRODUCT VECTOR ELEMENTS ASSOCIATED WITH EXCITATION HIGHER THAN THE ORDER
   DO IA=1,NCF
    DO IB=1,NCF
     IF (NORDER(IA)+NORDER(IB) > ORDER) PRD(IA,IB)=0.0D0
    ENDDO
   ENDDO

   ! STORE PRODUCT VECTOR
   REWIND(OUTFILE)
   IF (OFFSET == 0) THEN
    WRITE(OUTFILE) PRD
   ELSE
    DO I=1,OFFSET
     READ(OUTFILE) TRL
    ENDDO
    WRITE(OUTFILE) PRD
   ENDIF

   DEALLOCATE(TRL,PRD,PSIGN,PADRS)

   RETURN
END SUBROUTINE
