SUBROUTINE COUPLED_CLUSTER_ENERGY
! MAIN SUBROUTINE FOR COUPLED-CLUSTER ENERGY CALCULATIONS.

   USE MPI_F08
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   USE FULLCI
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER,PARAMETER :: MAXITER=1000
   LOGICAL :: LDIIS
   LOGICAL :: LDONE
   INTEGER :: ITER
   INTEGER :: IDIIS
   DOUBLE PRECISION :: DEV1,E1
   DOUBLE PRECISION :: DEV2,E2
   REAL :: DISK,MEM
   REAL :: ICPUS,ICPUE
   INTEGER :: KVC2
   INTEGER :: NMOD

   IF ((KVCY /= 0).OR.(KVCZ /= 0)) CALL PABORT('MOLECULES AND POLYMERS ONLY')
   NMOD = IOPTN(99)
   IF (NMOD < 1) CALL PABORT('ILLEGAL MODULO PARAMETER')
   IF (NMOD > 1) THEN
    WRITE(6,'(A)') '****************************'
    WRITE(6,'(A,I3,A)') '* CC ENERGY WITH MOD = ',NMOD,' *'
    WRITE(6,'(A)') '****************************'
    IF (KVCX/NMOD*NMOD /= KVCX) CALL PABORT('MODULO MUST EVENLY DIVIDE KPOINTS')
    KVC2=KVCX/NMOD
   ELSE
    KVC2=KVCX
   ENDIF

   IF (COPTN(70) == 'CCD') THEN
    WRITE(6,'(A)') 'COUPLED-CLUSTER DOUBLES (CCD) ENERGY CALCULATION WILL BE PERFORMED'
   ELSE IF (COPTN(70) == 'LCCD') THEN
    WRITE(6,'(A)') 'LINEARIZED COUPLED-CLUSTER DOUBLES (LCCD) ENERGY CALCULATION WILL BE PERFORMED'
   ELSE IF (COPTN(70) == 'CCSD') THEN
    WRITE(6,'(A)') 'COUPLED-CLUSTER SINGLES DOUBLES (CCSD) ENERGY CALCULATION WILL BE PERFORMED'
   ELSE IF (COPTN(70) == 'LCCSD') THEN
    WRITE(6,'(A)') 'LINEARIZED COUPLED-CLUSTER SINGLES DOUBLES (LCCSD) ENERGY CALCULATION WILL BE PERFORMED'
   ELSE IF (COPTN(70) == 'QCISD') THEN
    WRITE(6,'(A)') &
    'QUADRATIC CONFIGURATION INTERACTION SINGLES DOUBLES (QCISD) ENERGY CALCULATION WILL BE PERFORMED'
   ELSE IF (COPTN(70) == 'D123') THEN
    WRITE(6,'(A)') 'APPROXIMATE COUPLED-CLUSTER DOUBLES (D123) ENERGY CALCULATION WILL BE PERFORMED'
   ELSE IF (COPTN(70) == 'D45') THEN
    WRITE(6,'(A)') 'APPROXIMATE COUPLED-CLUSTER DOUBLES (D45) ENERGY CALCULATION WILL BE PERFORMED'
   ELSE
    CALL PABORT('UNKNOWN OR UNIMPLEMENTED COUPLED-CLUSTER METHOD')
   ENDIF

   IF (IOPTN(27) /= 0) CALL WARNING('FROZEN CORE APPROXIMATION WILL BE USED')
   IF (IOPTN(55) /= 0) CALL WARNING('FROZEN VIRTUAL APPROXIMATION WILL BE USED')
   IF (IALLMAX-IOCC-IVIRTCORE <= 0) THEN
    CALL WARNING('THERE IS NO VIRTUAL ORBITAL --- CORRELATION ENERGY IS NULL')
    RETURN
   ENDIF

   IF ((IOPTN(54) > 1).AND.(IOPTN(54) <= 20)) THEN
    LDIIS=.TRUE.
    IDIIS=IOPTN(54)
    WRITE(6,'(A,I3,A)') 'DIIS EXTRAPOLATION WILL BE PERFORMED ONCE IN',IDIIS,' ITERATIONS'
   ELSE
    IDIIS=1
   ENDIF

   DISK=16.0*((IALLMAX-IVIRTCORE)**4)*(MAX(1,2*KVC2))**3 &
       +32.0*((IALLMAX-IVIRTCORE)**2)*((IOCC-ICORE)**2)*(MAX(1,2*KVC2))**3
   IF (LDIIS) DISK=DISK+DFLOAT(IDIIS)*32.0*((IALLMAX-IVIRTCORE)**2)*((IOCC-ICORE)**2)*(MAX(1,2*KVC2))**3
   IF (DISK > 1000000000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED DISK USAGE WILL BE ',DISK/1000000000.0,' GB'
   ELSE IF (DISK > 1000000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED DISK USAGE WILL BE ',DISK/1000000.0,' MB'
   ELSE IF (DISK > 1000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED DISK USAGE WILL BE ',DISK/1000.0,' KB'
   ELSE
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED DISK USAGE WILL BE ',DISK,' B'
   ENDIF
   IF (DISK > DOPTN(26)*1000000.0) CALL PABORT('OUT OF DISK')

   MEM=16.0*(NCGS**4)*(CEL1X*2+1)+48.0*(NCGS**4)
   IF (MEM > 1000000000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000000.0,' GB'
   ELSE IF (MEM > 1000000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000000.0,' MB'
   ELSE IF (MEM > 1000.0) THEN
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM/1000.0,' KB'
   ELSE
    WRITE(6,'(A,F7.1,A)') 'ESTIMATED MEMORY USAGE WILL BE ',MEM,' B'
   ENDIF
   IF (MEM > DOPTN(28)*1000000.0) CALL PABORT('OUT OF MEMORY')

   ! OPEN DIRECT-ACCESS FILES
   ! THE FILE 50 STORES ALL THE MO-BASED INTEGRALS THAT ARE ORDERED IN TERMS OF WAVEVECTORS
   ! EACH RECORD CONTAINS THE ENTIRE SET OF INTEGRALS FOR ONE SPECIFIC COMBINATION OF THREE INDEPENDENT WAVEVECTORS
   ! INTEGRALS ARE IN DIRAC'S NOTATION, AND NOT ANTI SYMMETRIZED

   OPEN(31,FILE=TRIM(COPTN(1))//'.ao.00000',FORM='UNFORMATTED')

   OPEN(40,FILE=TRIM(COPTN(1))//'.mo4',FORM='UNFORMATTED',ACCESS='DIRECT',RECL=16*(IALLMAX-IVIRTCORE-ICORE)**4)
   OPEN(41,FILE=TRIM(COPTN(1))//'.ma4',FORM='UNFORMATTED',ACCESS='DIRECT',RECL=16*(IALLMAX-IVIRTCORE-ICORE)**4)
   OPEN(42,FILE=TRIM(COPTN(1))//'.mo2',FORM='UNFORMATTED',ACCESS='SEQUENTIAL')
   OPEN(51,FILE=TRIM(COPTN(1))//'.t2a',FORM='UNFORMATTED',ACCESS='DIRECT',RECL=16*(IALLMAX-IVIRTCORE-IOCC)**2*(IOCC-ICORE)**2)
   OPEN(52,FILE=TRIM(COPTN(1))//'.t2b',FORM='UNFORMATTED',ACCESS='DIRECT',RECL=16*(IALLMAX-IVIRTCORE-IOCC)**2*(IOCC-ICORE)**2)
   OPEN(53,FILE=TRIM(COPTN(1))//'.t1a',FORM='UNFORMATTED',ACCESS='SEQUENTIAL')
   OPEN(54,FILE=TRIM(COPTN(1))//'.t1b',FORM='UNFORMATTED',ACCESS='SEQUENTIAL')
   OPEN(55,FILE=TRIM(COPTN(1))//'.kpa',FORM='UNFORMATTED',ACCESS='SEQUENTIAL')
   OPEN(56,FILE=TRIM(COPTN(1))//'.lmd',FORM='UNFORMATTED',ACCESS='SEQUENTIAL')
   OPEN(57,FILE=TRIM(COPTN(1))//'.chi',FORM='UNFORMATTED',ACCESS='DIRECT',RECL=16*(IALLMAX-IVIRTCORE-ICORE)**4)
   IF (LDIIS) THEN
    OPEN(61,FILE=TRIM(COPTN(1))//'.t2c',FORM='UNFORMATTED',ACCESS='DIRECT',RECL=16*(IALLMAX-IVIRTCORE-IOCC)**2*(IOCC-ICORE)**2)
    OPEN(62,FILE=TRIM(COPTN(1))//'.t2d',FORM='UNFORMATTED',ACCESS='DIRECT',RECL=16*(IALLMAX-IVIRTCORE-IOCC)**2*(IOCC-ICORE)**2)
    OPEN(63,FILE=TRIM(COPTN(1))//'.t1c',FORM='UNFORMATTED',ACCESS='DIRECT', &
    RECL=16*(IALLMAX-IVIRTCORE-IOCC)*(IOCC-ICORE)*MAX(1,2*KVC2))
    OPEN(64,FILE=TRIM(COPTN(1))//'.t1d',FORM='UNFORMATTED',ACCESS='DIRECT', &
    RECL=16*(IALLMAX-IVIRTCORE-IOCC)*(IOCC-ICORE)*MAX(1,2*KVC2))
   ENDIF

   ! INTEGRAL TRANSFORMATION FROM AO TO MO BASIS
   CALL INTEGRAL_TRANSFORM_DIRECTACCESS(40,42,NMOD,KVC2)
   CALL GENERATE_AUXILIARY_INTEGRALS(40,41,NMOD,KVC2)

   ! MBPT CALCULATIONS FOR DEBUG
   CALL MP2ENERGY_DISK(40,41,NMOD,KVC2)
   CALL MP3ENERGY_DISK(40,41,NMOD,KVC2)

   ! INITIAL GUESS FOR THE T-AMPLITUDE
   CALL ZERO_T2_AMPLITUDE_FILE(51,NMOD,KVC2)
   CALL ZERO_T1_AMPLITUDE_FILE(53,NMOD,KVC2)
   IF (LDIIS) CALL ZERO_T_AMPLITUDE_STOREFILES(61,62,63,64,IDIIS,NMOD,KVC2)

   ! ITERATION
   WRITE(6,'(A)') TRIM(COPTN(70))//' ITERATION HAS STARTED'
   WRITE(6,'(A)') '------------------------------------------------------'
   WRITE(6,'(A)') 'ITER            RESIDUUM         CORRELATION   CPU/SEC'
!  CALL PCPU_TIME(ICPUS)
   DO ITER=1,MAXITER

    CALL ZERO_T2_AMPLITUDE_FILE(52,NMOD,KVC2)
    CALL ZERO_T1_AMPLITUDE_FILE(54,NMOD,KVC2)

    CALL CCSD_KAPPA_INTERMEDIATES(41,42,51,53,55,NMOD,KVC2)
    CALL CCSD_LAMBDA_INTERMEDIATES(41,42,53,55,56,NMOD,KVC2)
    CALL CCSD_CHI_INTERMEDIATES(40,41,51,53,57,NMOD,KVC2)
    CALL CCSD_DOUBLES_CONTRACTION(40,42,51,52,53,56,57,NMOD,KVC2)
    CALL CCSD_SINGLES_CONTRACTION(41,42,51,53,54,55,NMOD,KVC2)

    CALL CHECK_CONVERGENCE_T2_AMPLITUDES(51,52,DEV2,NMOD,KVC2)
    CALL CHECK_CONVERGENCE_T1_AMPLITUDES(53,54,DEV1,NMOD,KVC2)
    CALL ENERGY_T2_CONTRIBUTION(41,51,E2,NMOD,KVC2)
    CALL ENERGY_T1_CONTRIBUTION(41,42,53,E1,NMOD,KVC2)

!   CALL PCPU_TIME(ICPUE)
    IF (COPTN(70) == 'CCSD') THEN
     WRITE(6,'(I4,2F20.15,F10.1)') ITER,DSQRT(DEV1+DEV2),E1+E2,ICPUE-ICPUS
    ELSE
     WRITE(6,'(I4,2F20.15,F10.1)') ITER,DSQRT(DEV1+DEV2),E2,ICPUE-ICPUS
    ENDIF
    CALL PFLUSH(6)
!   CALL PCPU_TIME(ICPUS)
    IF (DSQRT(DEV1+DEV2) < DOPTN(62)) THEN
     WRITE(6,'(A)') '------------------------------------------------------'
     LDONE=.TRUE.
     EXIT
    ELSE
     LDONE=.FALSE.
    ENDIF

    IF (LDIIS) CALL STORE_T_AMPLITUDES(51,52,53,54,61,62,63,64,IDIIS,NMOD,KVC2)

    IF ((LDIIS).AND.(ITER >= IDIIS).AND.(MOD(ITER,IDIIS) == 0)) THEN
     CALL CC_DIIS(51,53,61,62,63,64,IDIIS,NMOD,KVC2)
    ELSE
     CALL COPY_T2_AMPLITUDES(52,51,NMOD,KVC2)
     CALL COPY_T1_AMPLITUDES(54,53,NMOD,KVC2)
    ENDIF

   ENDDO

   ! ENERGY EVALUATION
   IF (LDONE) THEN
    IF (COPTN(70) == 'CCSD') THEN
     ECC=E1+E2
    ELSE
     ECC=E2
    ENDIF
    WRITE(6,'(A,F20.15,A)') 'E(SINGLES)         = ',E1,' HARTREE'
    WRITE(6,'(A,F20.15,A)') 'E(DOUBLES)         = ',E2,' HARTREE'
    WRITE(6,'(A,F20.15,A)') 'E(CORRELATION)     = ',ECC,' HARTREE'
    IF (COPTN(70) == 'LCCD')  WRITE(6,'(A,F20.15,A)') 'E(LCCD)            = ',EHFKS+ECC,' HARTREE'
    IF (COPTN(70) == 'CCD')   WRITE(6,'(A,F20.15,A)') 'E(CCD)             = ',EHFKS+ECC,' HARTREE'
    IF (COPTN(70) == 'LCCSD') WRITE(6,'(A,F20.15,A)') 'E(LCCSD)           = ',EHFKS+ECC,' HARTREE'
    IF (COPTN(70) == 'CCSD')  WRITE(6,'(A,F20.15,A)') 'E(CCSD)            = ',EHFKS+ECC,' HARTREE'
    IF (COPTN(70) == 'QCISD') WRITE(6,'(A,F20.15,A)') 'E(QCISD)           = ',EHFKS+ECC,' HARTREE'
    IF (COPTN(70) == 'D123')  WRITE(6,'(A,F20.15,A)') 'E(D123)            = ',EHFKS+ECC,' HARTREE'
    IF (COPTN(70) == 'D45')   WRITE(6,'(A,F20.15,A)') 'E(D45)             = ',EHFKS+ECC,' HARTREE'
   ELSE
    CALL PABORT('CC ITERATION DID NOT CONVERGE')
   ENDIF

   CALL CC_ANALYSIS(51,53,NMOD,KVC2)

   CLOSE(31)

   CLOSE(40)
   CLOSE(41)
   CLOSE(42)
   CLOSE(51)
   CLOSE(52)
   CLOSE(53)
   CLOSE(54)
   CLOSE(55)
   CLOSE(56)
   CLOSE(57)
   IF (LDIIS) THEN
    CLOSE(61)
    CLOSE(62)
    CLOSE(63)
    CLOSE(64)
   ENDIF

   RETURN
END SUBROUTINE



SUBROUTINE INTEGRAL_TRANSFORM_DIRECTACCESS(INT1FILE,INT3FILE,NMOD,KVC2)
! TRANSFORM TWO-ELECTRON INTEGRALS BY O(N**5) ALGORITHM AND STORE THEM IN A DIRECT ACCESS FILE.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: INT1FILE,INT3FILE
   REAL :: ICPUS,ICPUE
   INTEGER,PARAMETER :: CASHESIZE = 1000 ! CASHE SIZE MUST BE EXACTLY THE SAME IN THE STORE & RESTORE SUBROUTINES
   INTEGER :: MOP,MOQ,MOR,MOS,MOT
   INTEGER :: KP,KQ,KR,KS,KT
   INTEGER :: Q1,Q2,Q3
   INTEGER :: I,J,K,L,P,Q,R
   INTEGER :: ICASHECOUNT,ICOUNT,EOF
   INTEGER(4),ALLOCATABLE :: ICASHE1X(:),ICASHE1Y(:),ICASHE1Z(:),ICASHE2(:)
   DOUBLE PRECISION,ALLOCATABLE :: DCASHE(:)
   DOUBLE COMPLEX,ALLOCATABLE :: PHASE(:)
   DOUBLE COMPLEX,ALLOCATABLE :: CNNNNK(:,:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CNNNN(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CNNNM(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CNNM(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CNM(:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CM(:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: FMM(:,:,:)
   INTEGER :: NMOD,KVC2

   WRITE(6,'(A)') 'FULL INTEGRAL TRANSFORMATION WILL BE PERFORMED'

   ALLOCATE(ICASHE1X(CASHESIZE),ICASHE1Y(CASHESIZE),ICASHE1Z(CASHESIZE),ICASHE2(CASHESIZE),DCASHE(CASHESIZE))
   ALLOCATE(PHASE(-2*(CEL1X+CEL2X)*KVC2:2*(CEL1X+CEL2X)*KVC2))
   ALLOCATE(CNNNNK(NCGS,NCGS,NCGS,NCGS,-CEL1X:CEL1X))
   ALLOCATE(CNNNN(NCGS,NCGS,NCGS,NCGS))
   ALLOCATE(CNNNM(NCGS,NCGS,NCGS,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(CNNM(NCGS,NCGS,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(CNM(NCGS,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(CM(ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(CMMMM(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(FMM(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1)))

   ! PRECALCULATE PHASE FACTOR
   DO I=-2*(CEL1X+CEL2X)*KVC2,2*(CEL1X+CEL2X)*KVC2
    IF (KVC2 == 0) THEN
     PHASE(I)=DCMPLX(1.0D0,0.0D0)
    ELSE
     PHASE(I)=DCMPLX(DCOS(DFLOAT(I)/DFLOAT(KVC2)*PI),DSIN(DFLOAT(I)/DFLOAT(KVC2)*PI))
    ENDIF
   ENDDO

   ! AO TO MO INTEGRAL TRANSFORMATION OF THE ONE-ELECTRON INTEGRALS
   FMM=DCMPLX(0.0D0,0.0D0)
   DO KQ=-KVC2,MAX(0,KVC2-1)
    DO MOQ=ICORE+1,IALL(KQ,0,0)-IVIRTCORE
     DO MOP=ICORE+1,IALL(KQ,0,0)-IVIRTCORE
      DO Q1=-CEL1X,CEL1X
       DO J=1,NCGS
        DO I=1,NCGS
         FMM(MOP,MOQ,KQ)=FMM(MOP,MOQ,KQ)+DCONJG(CO(I,MOP,KQ*NMOD,0,0))*CO(J,MOQ,KQ*NMOD,0,0)*F_C(I,J,Q1,0,0)*PHASE(KQ*Q1)
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDDO
   REWIND(INT3FILE)
   WRITE(INT3FILE) FMM

   ! AO TO MO INTEGRAL TRANSFORMATION OF THE TWO-ELECTRON INTEGRALS
!  CALL PCPU_TIME(ICPUS)
   DO KS=-KVC2,MAX(0,KVC2-1)
    DO KR=-KVC2,MAX(0,KVC2-1)
     REWIND(31)
     ICOUNT=0
     CNNNNK=DCMPLX(0.0D0,0.0D0)
     DO
      READ(31,IOSTAT=EOF) ICASHE1X,ICASHE1Y,ICASHE1Z,ICASHE2,DCASHE
      IF (EOF /= 0) EXIT
      DO ICASHECOUNT=1,CASHESIZE
       IF (ICASHE1X(ICASHECOUNT) == -1) EXIT
       ICOUNT=ICOUNT+1
       P=0
       Q=0
       R=0
       I=0
       J=0
       K=0
       L=0
       CALL MVBITS(ICASHE1X(ICASHECOUNT),16,8,P,0)
       CALL MVBITS(ICASHE1X(ICASHECOUNT), 8,8,Q,0)
       CALL MVBITS(ICASHE1X(ICASHECOUNT), 0,8,R,0)
       CALL MVBITS(ICASHE2(ICASHECOUNT),24,8,I,0)
       CALL MVBITS(ICASHE2(ICASHECOUNT),16,8,J,0)
       CALL MVBITS(ICASHE2(ICASHECOUNT), 8,8,K,0)
       CALL MVBITS(ICASHE2(ICASHECOUNT), 0,8,L,0)
       Q1=P-CEL1X
       Q2=Q-CEL1X
       Q3=R-CEL2X
       IF ((Q1 < -REDUCED_CEL1X).OR.(Q1 > REDUCED_CEL1X) &
       .OR.(Q2 < -REDUCED_CEL1X).OR.(Q2 > REDUCED_CEL1X) &
       .OR.(Q3 < 0).OR.(Q3 > CEL2X)) &
       CALL PABORT('INTEGRAL FILE HAS BEEN DEGRADED')
!------------------------------------------------------------------------------
       if (Q3 > KVC2) cycle
!------------------------------------------------------------------------------
       CNNNNK(I,J,K,L,Q1)=CNNNNK(I,J,K,L,Q1)+PHASE(KS*(Q2+Q3)-KR*Q3)*DCMPLX(DCASHE(ICASHECOUNT),0.0D0)
       IF (Q3 /= 0) CNNNNK(K,L,I,J,Q2)=CNNNNK(K,L,I,J,Q2)+PHASE(KS*(Q1-Q3)+KR*Q3)*DCMPLX(DCASHE(ICASHECOUNT),0.0D0)
      ENDDO
     ENDDO
     IF (IOPTN(9) >= 2) WRITE(6,'(I9,A,F7.3,A)') ICOUNT, &
     ' ERIS (',DFLOAT(ICOUNT)/DFLOAT((CEL1X*2+1)**2*(CEL2X+1)*NCGS**4)*100.0,'% OF TOTAL ERIS) HAVE BEEN RESTORED'
     DO KQ=-KVC2,MAX(0,KVC2-1)
      KP=KQ-KR+KS
      IF (KP >= KVC2) KP=KP-2*KVC2
      IF (KP < -KVC2) KP=KP+2*KVC2
      CNNNN=DCMPLX(0.0D0,0.0D0)
      DO Q1=-CEL1X,CEL1X
       DO L=1,NCGS
        DO K=1,NCGS
         DO J=1,NCGS
          DO I=1,NCGS
           CNNNN(I,J,K,L)=CNNNN(I,J,K,L)+PHASE(KQ*Q1)*CNNNNK(I,J,K,L,Q1)
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
      DO MOS=ICORE+1,IALL(KS,0,0)-IVIRTCORE
       CNNNM=DCMPLX(0.0D0,0.0D0)
       DO L=1,NCGS
        DO K=1,NCGS
         DO J=1,NCGS
          DO I=1,NCGS
           CNNNM(I,J,K,MOS)=CNNNM(I,J,K,MOS)+CO(L,MOS,KS*NMOD,0,0)*CNNNN(I,J,K,L)
          ENDDO
         ENDDO
        ENDDO
       ENDDO
       DO MOR=ICORE+1,IALL(KR,0,0)-IVIRTCORE
        CNNM=DCMPLX(0.0D0,0.0D0)
        DO K=1,NCGS
         DO J=1,NCGS
          DO I=1,NCGS
           CNNM(I,J,MOR)=CNNM(I,J,MOR)+DCONJG(CO(K,MOR,KR*NMOD,0,0))*CNNNM(I,J,K,MOS)
          ENDDO
         ENDDO
        ENDDO
        DO MOQ=ICORE+1,IALL(KQ,0,0)-IVIRTCORE
         CNM=DCMPLX(0.0D0,0.0D0)
         DO J=1,NCGS
          DO I=1,NCGS
           CNM(I,MOQ)=CNM(I,MOQ)+CO(J,MOQ,KQ*NMOD,0,0)*CNNM(I,J,MOR)
          ENDDO
         ENDDO
         DO MOP=ICORE+1,IALL(KP,0,0)-IVIRTCORE
          CM=DCMPLX(0.0D0,0.0D0)
          DO I=1,NCGS
           CM(MOP)=CM(MOP)+DCONJG(CO(I,MOP,KP*NMOD,0,0))*CNM(I,MOQ)
          ENDDO
          CMMMM(MOP,MOR,MOQ,MOS)=CM(MOP)/DFLOAT(MAX(1,2*KVC2))
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      WRITE(INT1FILE,REC=((KS+KVC2)*(2*KVC2)+(KQ+KVC2))*(2*KVC2)+(KR+KVC2)+1) CMMMM
     
     ENDDO
    ENDDO
!   CALL PCPU_TIME(ICPUE)
    WRITE(6,'(A,I3,A,F10.1,A)') ' ... ',INT(DFLOAT(KS+KVC2+1)/DFLOAT(MAX(1,2*KVC2))*100.0), &
    ' % OF INTEGRAL TRANSFORMATION (CPU / SEC = ',ICPUE-ICPUS,')'
!   CALL PCPU_TIME(ICPUS)
    CALL PFLUSH(6)
   ENDDO
       
   DEALLOCATE(ICASHE1X,ICASHE1Y,ICASHE1Z,ICASHE2,DCASHE)
   DEALLOCATE(PHASE)
   DEALLOCATE(CNNNNK,CNNNN,CNNNM,CNNM,CNM,CM,CMMMM,FMM)

   WRITE(6,'(A)') 'FULL INTEGRAL TRANSFORMATION HAS BEEN DONE'

   RETURN
END SUBROUTINE



SUBROUTINE GENERATE_AUXILIARY_INTEGRALS(INT1FILE,INT2FILE,NMOD,KVC2)
! GENERATE AND STORE <PQ||RS>=2<PQ|RS>-<PQ|SR>

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: INT1FILE,INT2FILE
   REAL :: ICPUS,ICPUE
   INTEGER :: MOP,MOQ,MOR,MOS
   INTEGER :: KP,KQ,KR,KS
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM2(:,:,:,:)
   INTEGER :: NMOD,KVC2

   WRITE(6,'(A)') 'AUXILIARY INTEGRALS WILL BE GENERATED'

   ALLOCATE(CMMMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(CMMMM2(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))

!  CALL PCPU_TIME(ICPUS)
   DO KS=-KVC2,MAX(0,KVC2-1)
    DO KR=-KVC2,MAX(0,KVC2-1)
     DO KQ=-KVC2,MAX(0,KVC2-1)

      KP=KR-KQ+KS
      IF (KP >= KVC2) KP=KP-2*KVC2
      IF (KP < -KVC2) KP=KP+2*KVC2

      READ(INT1FILE,REC=((KS+KVC2)*(2*KVC2)+(KR+KVC2))*(2*KVC2)+(KQ+KVC2)+1) CMMMM1
      READ(INT1FILE,REC=((KR+KVC2)*(2*KVC2)+(KS+KVC2))*(2*KVC2)+(KQ+KVC2)+1) CMMMM2
      
      DO MOS=ICORE+1,IALL(KS,0,0)-IVIRTCORE
       DO MOR=ICORE+1,IALL(KR,0,0)-IVIRTCORE
        DO MOQ=ICORE+1,IALL(KQ,0,0)-IVIRTCORE
         DO MOP=ICORE+1,IALL(KP,0,0)-IVIRTCORE
          CMMMM1(MOP,MOQ,MOR,MOS)=2.0D0*CMMMM1(MOP,MOQ,MOR,MOS)-CMMMM2(MOP,MOQ,MOS,MOR)
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      WRITE(INT2FILE,REC=((KS+KVC2)*(2*KVC2)+(KR+KVC2))*(2*KVC2)+(KQ+KVC2)+1) CMMMM1

     ENDDO
    ENDDO
   ENDDO

   DEALLOCATE(CMMMM1,CMMMM2)

   WRITE(6,'(A)') 'AUXILIARY INTEGRALS HAVE BEEN GENERATED'

   RETURN
END SUBROUTINE



SUBROUTINE MP2ENERGY_DISK(INT1FILE,INT2FILE,NMOD,KVC2)
! PERFORM MP2 CALCULATIONS ON THE BASIS OF THE MO-BASED INTEGRALS

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: INT1FILE,INT2FILE
   REAL :: ICPUS,ICPUE
   INTEGER :: MOI,MOJ,MOA,MOB
   INTEGER :: KI,KJ,KA,KB
   DOUBLE COMPLEX :: EMP2
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM2(:,:,:,:)
   INTEGER :: NMOD,KVC2

   WRITE(6,'(A)') 'SECOND-ORDER MANY-BODY PERTURBATION CALCULATION WILL BE PERFORMED'

   ALLOCATE(CMMMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(CMMMM2(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))

   ! COMPUTE E(2)
!  CALL PCPU_TIME(ICPUS)
   EMP2=DCMPLX(0.0D0,0.0D0)
   DO KB=-KVC2,MAX(0,KVC2-1)
    DO KA=-KVC2,MAX(0,KVC2-1)
     DO KJ=-KVC2,MAX(0,KVC2-1)

      KI=KA-KJ+KB
      IF (KI >= KVC2) KI=KI-2*KVC2
      IF (KI < -KVC2) KI=KI+2*KVC2

      READ(INT1FILE,REC=((KB+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KJ+KVC2)+1) CMMMM1
      READ(INT2FILE,REC=((KB+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KJ+KVC2)+1) CMMMM2
      
      DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
       DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
        DO MOJ=ICORE+1,IOCC
         DO MOI=ICORE+1,IOCC
          EMP2=EMP2+CMMMM2(MOI,MOJ,MOA,MOB)*DCONJG(CMMMM1(MOI,MOJ,MOA,MOB)) &
                    /(EPSILON(MOI,KI*NMOD,0,0)+EPSILON(MOJ,KJ*NMOD,0,0)-EPSILON(MOA,KA*NMOD,0,0)-EPSILON(MOB,KB*NMOD,0,0)) 
         ENDDO
        ENDDO
       ENDDO
      ENDDO

     ENDDO
    ENDDO
!   CALL PCPU_TIME(ICPUE)
    WRITE(6,'(A,I3,A,F10.1,A)') ' ... ',INT(DFLOAT(KB+KVC2+1)/DFLOAT(MAX(1,2*KVC2))*100.0), &
    ' % OF MP2 ENERGY EVALUATION (CPU / SEC = ',ICPUE-ICPUS,')'
!   CALL PCPU_TIME(ICPUS)
    CALL PFLUSH(6)
   ENDDO

   EMP2=EMP2/DFLOAT(MAX(1,2*KVC2))
   WRITE(6,'(A,F20.15,A,F20.15,A)') 'E(2) = ',DREAL(EMP2),' (',DIMAG(EMP2),') HARTREE'

   DEALLOCATE(CMMMM1,CMMMM2)

   RETURN
END SUBROUTINE



SUBROUTINE MP3ENERGY_DISK(INT1FILE,INT2FILE,NMOD,KVC2)
! PERFORM MP3 CALCULATIONS ON THE BASIS OF THE MO-BASED INTEGRALS.
! SEE SZABO & OSTLUND, TABLE 6.2.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: INT1FILE,INT2FILE
   REAL :: ICPUS,ICPUE
   INTEGER :: MOI,MOJ,MOK,MOL,MOA,MOB,MOC,MOD
   INTEGER :: KI,KJ,KK,KL,KA,KB,KC,KD
   DOUBLE COMPLEX :: EMP3
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM2(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM3(:,:,:,:)
   INTEGER :: NMOD,KVC2

   WRITE(6,'(A)') 'THIRD-ORDER MANY-BODY PERTURBATION CALCULATION WILL BE PERFORMED'

   ALLOCATE(CMMMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(CMMMM2(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(CMMMM3(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))

   ! COMPUTE E(3)
!  CALL PCPU_TIME(ICPUS)
   EMP3=DCMPLX(0.0D0,0.0D0)
   DO KI=-KVC2,MAX(0,KVC2-1)
    ! TERM 1
    DO KC=-KVC2,MAX(0,KVC2-1)
     DO KA=-KVC2,MAX(0,KVC2-1)
      DO KJ=-KVC2,MAX(0,KVC2-1)
       KB=KI-KA+KJ
       IF (KB >= KVC2) KB=KB-2*KVC2
       IF (KB < -KVC2) KB=KB+2*KVC2
       KD=KA-KC+KB
       IF (KD >= KVC2) KD=KD-2*KVC2
       IF (KD < -KVC2) KD=KD+2*KVC2
       READ(INT1FILE,REC=((KB+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KJ+KVC2)+1) CMMMM1
       READ(INT1FILE,REC=((KD+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KB+KVC2)+1) CMMMM2
       READ(INT2FILE,REC=((KD+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KJ+KVC2)+1) CMMMM3
       DO MOD=IOCC+1,IALL(KD,0,0)-IVIRTCORE
        DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
         DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
          DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
           DO MOJ=ICORE+1,IOCC
            DO MOI=ICORE+1,IOCC
             EMP3=EMP3+DREAL(CMMMM1(MOI,MOJ,MOA,MOB)*CMMMM2(MOA,MOB,MOC,MOD)*DCONJG(CMMMM3(MOI,MOJ,MOC,MOD))) &
                      /(EPSILON(MOI,KI*NMOD,0,0)+EPSILON(MOJ,KJ*NMOD,0,0)-EPSILON(MOA,KA*NMOD,0,0)-EPSILON(MOB,KB*NMOD,0,0)) &
                      /(EPSILON(MOI,KI*NMOD,0,0)+EPSILON(MOJ,KJ*NMOD,0,0)-EPSILON(MOC,KC*NMOD,0,0)-EPSILON(MOD,KD*NMOD,0,0)) 
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
    ! TERM 2
    DO KA=-KVC2,MAX(0,KVC2-1)
     DO KJ=-KVC2,MAX(0,KVC2-1)
      DO KK=-KVC2,MAX(0,KVC2-1)
       KB=KI-KA+KJ
       IF (KB >= KVC2) KB=KB-2*KVC2
       IF (KB < -KVC2) KB=KB+2*KVC2
       KL=KA-KK+KB
       IF (KL >= KVC2) KL=KL-2*KVC2
       IF (KL < -KVC2) KL=KL+2*KVC2
       READ(INT1FILE,REC=((KB+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KJ+KVC2)+1) CMMMM1
       READ(INT1FILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KL+KVC2)+1) CMMMM2
       READ(INT2FILE,REC=((KB+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KL+KVC2)+1) CMMMM3
       DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
        DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
         DO MOL=ICORE+1,IOCC
          DO MOK=ICORE+1,IOCC
           DO MOJ=ICORE+1,IOCC
            DO MOI=ICORE+1,IOCC
             EMP3=EMP3+DREAL(CMMMM1(MOI,MOJ,MOA,MOB)*CMMMM2(MOK,MOL,MOI,MOJ)*DCONJG(CMMMM3(MOK,MOL,MOA,MOB))) &
                      /(EPSILON(MOI,KI*NMOD,0,0)+EPSILON(MOJ,KJ*NMOD,0,0)-EPSILON(MOA,KA*NMOD,0,0)-EPSILON(MOB,KB*NMOD,0,0)) &
                      /(EPSILON(MOK,KK*NMOD,0,0)+EPSILON(MOL,KL*NMOD,0,0)-EPSILON(MOA,KA*NMOD,0,0)-EPSILON(MOB,KB*NMOD,0,0)) 
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
    ! TERM 3
    DO KC=-KVC2,MAX(0,KVC2-1)
     DO KA=-KVC2,MAX(0,KVC2-1)
      DO KJ=-KVC2,MAX(0,KVC2-1)
       KB=KI-KA+KJ
       IF (KB >= KVC2) KB=KB-2*KVC2
       IF (KB < -KVC2) KB=KB+2*KVC2
       KK=KA-KI+KC
       IF (KK >= KVC2) KK=KK-2*KVC2
       IF (KK < -KVC2) KK=KK+2*KVC2
       READ(INT1FILE,REC=((KB+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KJ+KVC2)+1) CMMMM1
       READ(INT1FILE,REC=((KJ+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KB+KVC2)+1) CMMMM2
       READ(INT2FILE,REC=((KC+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM3
       DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
        DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          DO MOK=ICORE+1,IOCC
           DO MOJ=ICORE+1,IOCC
            DO MOI=ICORE+1,IOCC
             EMP3=EMP3+4.0D0*DREAL(CMMMM1(MOI,MOJ,MOA,MOB)*CMMMM2(MOK,MOB,MOC,MOJ)*DCONJG(CMMMM3(MOI,MOK,MOA,MOC))) &
                      /(EPSILON(MOI,KI*NMOD,0,0)+EPSILON(MOJ,KJ*NMOD,0,0)-EPSILON(MOA,KA*NMOD,0,0)-EPSILON(MOB,KB*NMOD,0,0)) &
                      /(EPSILON(MOI,KI*NMOD,0,0)+EPSILON(MOK,KK*NMOD,0,0)-EPSILON(MOA,KA*NMOD,0,0)-EPSILON(MOC,KC*NMOD,0,0))
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
    ! TERM 4
    DO KC=-KVC2,MAX(0,KVC2-1)
     DO KA=-KVC2,MAX(0,KVC2-1)
      DO KJ=-KVC2,MAX(0,KVC2-1)
       KB=KI-KA+KJ
       IF (KB >= KVC2) KB=KB-2*KVC2
       IF (KB < -KVC2) KB=KB+2*KVC2
       KK=KI-KA+KC
       IF (KK >= KVC2) KK=KK-2*KVC2
       IF (KK < -KVC2) KK=KK+2*KVC2
       READ(INT1FILE,REC=((KB+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KJ+KVC2)+1) CMMMM1
       READ(INT1FILE,REC=((KC+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KA+KVC2)+1) CMMMM2
       READ(INT2FILE,REC=((KC+KVC2)*(2*KVC2)+(KB+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM3
       DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
        DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          DO MOK=ICORE+1,IOCC
           DO MOJ=ICORE+1,IOCC
            DO MOI=ICORE+1,IOCC
             EMP3=EMP3-2.0D0*DREAL(CMMMM1(MOI,MOJ,MOA,MOB)*CMMMM2(MOK,MOA,MOI,MOC)*DCONJG(CMMMM3(MOJ,MOK,MOB,MOC))) &
                      /(EPSILON(MOI,KI*NMOD,0,0)+EPSILON(MOJ,KJ*NMOD,0,0)-EPSILON(MOA,KA*NMOD,0,0)-EPSILON(MOB,KB*NMOD,0,0)) &
                      /(EPSILON(MOJ,KJ*NMOD,0,0)+EPSILON(MOK,KK*NMOD,0,0)-EPSILON(MOB,KB*NMOD,0,0)-EPSILON(MOC,KC*NMOD,0,0)) 
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
    ! TERM 5
    DO KC=-KVC2,MAX(0,KVC2-1)
     DO KA=-KVC2,MAX(0,KVC2-1)
      DO KJ=-KVC2,MAX(0,KVC2-1)
       KB=KI-KA+KJ
       IF (KB >= KVC2) KB=KB-2*KVC2
       IF (KB < -KVC2) KB=KB+2*KVC2
       KK=KJ-KA+KC
       IF (KK >= KVC2) KK=KK-2*KVC2
       IF (KK < -KVC2) KK=KK+2*KVC2
       READ(INT1FILE,REC=((KB+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KJ+KVC2)+1) CMMMM1
       READ(INT2FILE,REC=((KC+KVC2)*(2*KVC2)+(KJ+KVC2))*(2*KVC2)+(KA+KVC2)+1) CMMMM2
       READ(INT1FILE,REC=((KB+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM3
       DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
        DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          DO MOK=ICORE+1,IOCC
           DO MOJ=ICORE+1,IOCC
            DO MOI=ICORE+1,IOCC
             EMP3=EMP3-2.0D0*DREAL(CMMMM1(MOI,MOJ,MOA,MOB)*CMMMM2(MOK,MOA,MOJ,MOC)*DCONJG(CMMMM3(MOI,MOK,MOC,MOB))) &
                      /(EPSILON(MOI,KI*NMOD,0,0)+EPSILON(MOJ,KJ*NMOD,0,0)-EPSILON(MOA,KA*NMOD,0,0)-EPSILON(MOB,KB*NMOD,0,0)) &
                      /(EPSILON(MOI,KI*NMOD,0,0)+EPSILON(MOK,KK*NMOD,0,0)-EPSILON(MOB,KB*NMOD,0,0)-EPSILON(MOC,KC*NMOD,0,0)) 
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
    ! TERM 6
    DO KC=-KVC2,MAX(0,KVC2-1)
     DO KA=-KVC2,MAX(0,KVC2-1)
      DO KJ=-KVC2,MAX(0,KVC2-1)
       KB=KI-KA+KJ
       IF (KB >= KVC2) KB=KB-2*KVC2
       IF (KB < -KVC2) KB=KB+2*KVC2
       KK=KA-KJ+KC
       IF (KK >= KVC2) KK=KK-2*KVC2
       IF (KK < -KVC2) KK=KK+2*KVC2
       READ(INT1FILE,REC=((KB+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KJ+KVC2)+1) CMMMM1
       READ(INT2FILE,REC=((KI+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KB+KVC2)+1) CMMMM2
       READ(INT1FILE,REC=((KC+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM3
       DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
        DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          DO MOK=ICORE+1,IOCC
           DO MOJ=ICORE+1,IOCC
            DO MOI=ICORE+1,IOCC
             EMP3=EMP3-2.0D0*DREAL(CMMMM1(MOI,MOJ,MOA,MOB)*CMMMM2(MOK,MOB,MOC,MOI)*DCONJG(CMMMM3(MOJ,MOK,MOA,MOC))) &
                      /(EPSILON(MOI,KI*NMOD,0,0)+EPSILON(MOJ,KJ*NMOD,0,0)-EPSILON(MOA,KA*NMOD,0,0)-EPSILON(MOB,KB*NMOD,0,0)) &
                      /(EPSILON(MOJ,KJ*NMOD,0,0)+EPSILON(MOK,KK*NMOD,0,0)-EPSILON(MOA,KA*NMOD,0,0)-EPSILON(MOC,KC*NMOD,0,0)) 
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
!   CALL PCPU_TIME(ICPUE)
    WRITE(6,'(A,I3,A,F10.1,A)') ' ... ',INT(DFLOAT(KI+KVC2+1)/DFLOAT(MAX(1,2*KVC2))*100.0), &
    ' % OF MP3 ENERGY EVALUATION (CPU / SEC = ',ICPUE-ICPUS,')'
!   CALL PCPU_TIME(ICPUS)
    CALL PFLUSH(6)
   ENDDO

   EMP3=EMP3/DFLOAT(MAX(1,2*KVC2))
   WRITE(6,'(A,F20.15,A,F20.15,A)') 'E(3) = ',DREAL(EMP3),' (',DIMAG(EMP3),') HARTREE'

   DEALLOCATE(CMMMM1,CMMMM2,CMMMM3)

   RETURN
END SUBROUTINE



SUBROUTINE ZERO_T2_AMPLITUDE_FILE(TFILE,NMOD,KVC2)
! ZERO SCRATCH A T2-AMPLITUDE FILE

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: TFILE
   INTEGER :: KB,KI,KJ
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ(:,:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TABIJ(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))

   TABIJ=DCMPLX(0.0D0,0.0D0)
   DO KJ=-KVC2,MAX(0,KVC2-1)
    DO KI=-KVC2,MAX(0,KVC2-1)
     DO KB=-KVC2,MAX(0,KVC2-1)
      WRITE(TFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ
     ENDDO
    ENDDO
   ENDDO

   DEALLOCATE(TABIJ)

   RETURN
END SUBROUTINE



SUBROUTINE ZERO_T1_AMPLITUDE_FILE(TFILE,NMOD,KVC2)
! ZERO SCRATCH A T1-AMPLITUDE FILE

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: TFILE
   DOUBLE COMPLEX,ALLOCATABLE :: TAI(:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TAI(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))

   TAI=DCMPLX(0.0D0,0.0D0)
   REWIND(TFILE)
   WRITE(TFILE) TAI
   DEALLOCATE(TAI)

   RETURN
END SUBROUTINE
   


SUBROUTINE COPY_T2_AMPLITUDES(TINFILE,TOUTFILE,NMOD,KVC2)
! COPY T2-AMPLITUDES FROM INFILE TO OUTFILE

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: TINFILE,TOUTFILE
   INTEGER :: KB,KI,KJ
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ(:,:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TABIJ(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))

   TABIJ=DCMPLX(0.0D0,0.0D0)
   DO KJ=-KVC2,MAX(0,KVC2-1)
    DO KI=-KVC2,MAX(0,KVC2-1)
     DO KB=-KVC2,MAX(0,KVC2-1)
      READ(TINFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ
      WRITE(TOUTFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ
     ENDDO
    ENDDO
   ENDDO

   DEALLOCATE(TABIJ)

   RETURN
END SUBROUTINE
   


SUBROUTINE COPY_T1_AMPLITUDES(TINFILE,TOUTFILE,NMOD,KVC2)
! COPY T1-AMPLITUDES FROM INFILE TO OUTFILE

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: TINFILE,TOUTFILE
   DOUBLE COMPLEX,ALLOCATABLE :: TAI(:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TAI(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   TAI=DCMPLX(0.0D0,0.0D0)
   REWIND(TINFILE)
   READ(TINFILE) TAI
   REWIND(TOUTFILE)
   WRITE(TOUTFILE) TAI
   DEALLOCATE(TAI)

   RETURN
END SUBROUTINE
   


SUBROUTINE CHECK_CONVERGENCE_T2_AMPLITUDES(IFILE,JFILE,DEV,NMOD,KVC2)
! EVALUATE THE ROOT MEAN SQUARE DEVIATION BETWEEN TWO SETS OF T2-AMPLITUDES

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: IFILE,JFILE
   INTEGER :: MOI,MOJ,MOA,MOB
   INTEGER :: KI,KJ,KA,KB
   DOUBLE PRECISION :: DEV
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ2(:,:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TABIJ1(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(TABIJ2(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))

   DEV=0.0D0
   DO KJ=-KVC2,MAX(0,KVC2-1)
    DO KI=-KVC2,MAX(0,KVC2-1)
     DO KB=-KVC2,MAX(0,KVC2-1)
      KA=KI-KB+KJ
      IF (KA >= KVC2) KA=KA-2*KVC2
      IF (KA < -KVC2) KA=KA+2*KVC2
      READ(IFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1
      READ(JFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ2
      DO MOJ=ICORE+1,IOCC
       DO MOI=ICORE+1,IOCC
        DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          DEV=DEV+(TABIJ1(MOA,MOB,MOI,MOJ)-TABIJ2(MOA,MOB,MOI,MOJ))*DCONJG(TABIJ1(MOA,MOB,MOI,MOJ)-TABIJ2(MOA,MOB,MOI,MOJ))
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDDO

   DEALLOCATE(TABIJ1,TABIJ2)

   RETURN
END SUBROUTINE
   


SUBROUTINE CHECK_CONVERGENCE_T1_AMPLITUDES(IFILE,JFILE,DEV,NMOD,KVC2)
! EVALUATE THE ROOT MEAN SQUARE DEVIATION BETWEEN TWO SETS OF T1-AMPLITUDES

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: IFILE,JFILE
   INTEGER :: MOI,MOA
   INTEGER :: KI
   DOUBLE PRECISION :: DEV
   DOUBLE COMPLEX,ALLOCATABLE :: TAI1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI2(:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TAI1(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(TAI2(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))

   REWIND(IFILE)
   READ(IFILE) TAI1
   REWIND(JFILE)
   READ(JFILE) TAI2

   DEV=0.0D0
   DO KI=-KVC2,MAX(0,KVC2-1)
    DO MOI=ICORE+1,IOCC
     DO MOA=IOCC+1,IALL(KI,0,0)-IVIRTCORE
      DEV=DEV+(TAI1(MOA,MOI,KI)-TAI2(MOA,MOI,KI))*DCONJG(TAI1(MOA,MOI,KI)-TAI2(MOA,MOI,KI))
     ENDDO
    ENDDO
   ENDDO

   DEALLOCATE(TAI1,TAI2)

   RETURN
END SUBROUTINE



SUBROUTINE ENERGY_T2_CONTRIBUTION(INT2FILE,TFILE,E,NMOD,KVC2)
! EVALUATE THE <IJ||AB>T^AB_IJ CONTRIBUTION TO THE CC ENERGY

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: INT2FILE,TFILE
   INTEGER :: MOI,MOJ,MOA,MOB
   INTEGER :: KI,KJ,KA,KB
   DOUBLE PRECISION :: E
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM1(:,:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TABIJ(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(CMMMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))

   E=0.0D0
   DO KB=-KVC2,MAX(0,KVC2-1)
    DO KA=-KVC2,MAX(0,KVC2-1)
     DO KJ=-KVC2,MAX(0,KVC2-1)

      KI=KA-KJ+KB
      IF (KI >= KVC2) KI=KI-2*KVC2
      IF (KI < -KVC2) KI=KI+2*KVC2

      READ(INT2FILE,REC=((KB+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KJ+KVC2)+1) CMMMM1
      READ(TFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ
      
      DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
       DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
        DO MOJ=ICORE+1,IOCC
         DO MOI=ICORE+1,IOCC
          E=E+CMMMM1(MOI,MOJ,MOA,MOB)*TABIJ(MOA,MOB,MOI,MOJ)
         ENDDO
        ENDDO
       ENDDO
      ENDDO

     ENDDO
    ENDDO
   ENDDO
   E=E/DFLOAT(MAX(1,2*KVC2))

   DEALLOCATE(CMMMM1,TABIJ)

   RETURN
END SUBROUTINE



SUBROUTINE ENERGY_T1_CONTRIBUTION(INT2FILE,INT3FILE,TFILE,E,NMOD,KVC2)
! EVALUATE THE 2 F^I_A*T^A_I+<IJ||AB>T^A_I*T^B_J CONTRIBUTION TO THE CC ENERGY

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: INT2FILE,INT3FILE,TFILE
   INTEGER :: MOI,MOJ,MOA,MOB
   INTEGER :: KI,KJ,KA,KB
   DOUBLE PRECISION :: E
   DOUBLE COMPLEX,ALLOCATABLE :: TAI(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: FMM1(:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TAI(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(CMMMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(FMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1)))

   REWIND(TFILE)
   READ(TFILE) TAI

   E=0.0D0
   DO KI=-KVC2,MAX(0,KVC2-1)
    DO KJ=-KVC2,MAX(0,KVC2-1)

     KA=KI
     KB=KJ

     READ(INT2FILE,REC=((KB+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KJ+KVC2)+1) CMMMM1
      
     DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
      DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         E=E+CMMMM1(MOI,MOJ,MOA,MOB)*TAI(MOA,MOI,KI)*TAI(MOB,MOJ,KJ)
        ENDDO
       ENDDO
      ENDDO
     ENDDO

    ENDDO
   ENDDO

   REWIND(INT3FILE)
   READ(INT3FILE) FMM1

   DO KI=-KVC2,MAX(0,KVC2-1)
    DO MOA=IOCC+1,IALL(KI,0,0)-IVIRTCORE
     DO MOI=ICORE+1,IOCC
      E=E+2.0D0*FMM1(MOI,MOA,KI)*TAI(MOA,MOI,KI)
     ENDDO
    ENDDO
   ENDDO

   E=E/DFLOAT(MAX(1,2*KVC2))

   DEALLOCATE(CMMMM1,TAI,FMM1)

   RETURN
END SUBROUTINE



SUBROUTINE CCSD_KAPPA_INTERMEDIATES(INT2FILE,INT3FILE,T2INFILE,T1INFILE,KAPPAFILE,NMOD,KVC2)
! GENERATES KAPPA INTERMEDIATES FOR CCSD

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: INT2FILE,INT3FILE
   INTEGER :: T2INFILE,T1INFILE
   INTEGER :: KAPPAFILE
   INTEGER :: MOI,MOJ,MOA,MOB
   INTEGER :: MOK,MOL,MOC,MOD
   INTEGER :: MOP,MOQ
   INTEGER :: KI,KJ,KA,KB
   INTEGER :: KK,KL,KC,KD
   INTEGER :: KP
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: KMM1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: FMM1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM1(:,:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TABIJ1(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(TAI1(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(KMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(FMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(CMMMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))

   REWIND(T1INFILE)
   READ(T1INFILE) TAI1

   REWIND(INT3FILE)
   READ(INT3FILE) FMM1

   ! F^P_Q (P /= Q)
   DO KP=-KVC2,MAX(0,KVC2-1)
    DO MOP=ICORE+1,IALL(KP,0,0)-IVIRTCORE
     DO MOQ=ICORE+1,IALL(KP,0,0)-IVIRTCORE
      IF (MOP /= MOQ) THEN
       KMM1(MOQ,MOP,KP)=FMM1(MOQ,MOP,KP)
      ELSE
       KMM1(MOQ,MOP,KP)=DCMPLX(0.0D0,0.0D0)
      ENDIF
     ENDDO
    ENDDO
   ENDDO

   IF ((COPTN(70) == 'CCD').OR.(COPTN(70) == 'QCISD').OR.(COPTN(70) == 'CCSD').OR.(COPTN(70) == 'D45')) THEN
    ! <KL||CD> T^CD_IL
    DO KI=-KVC2,MAX(0,KVC2-1)
     KK=KI
     DO KL=-KVC2,MAX(0,KVC2-1)
      DO KC=-KVC2,MAX(0,KVC2-1)
       KD=KK-KC+KL
       IF (KD >= KVC2) KD=KD-2*KVC2
       IF (KD < -KVC2) KD=KD+2*KVC2
       READ(INT2FILE,REC=((KD+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KL+KVC2)+1) CMMMM1
       READ(T2INFILE,REC=((KL+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KD+KVC2)+1) TABIJ1
       DO MOI=ICORE+1,IOCC
        DO MOK=ICORE+1,IOCC
         DO MOL=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
           DO MOD=IOCC+1,IALL(KD,0,0)-IVIRTCORE
            KMM1(MOK,MOI,KI)=KMM1(MOK,MOI,KI)+CMMMM1(MOK,MOL,MOC,MOD)*TABIJ1(MOC,MOD,MOI,MOL)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDIF

   IF ((COPTN(70) == 'CCD').OR.(COPTN(70) == 'QCISD').OR.(COPTN(70) == 'CCSD').OR.(COPTN(70) == 'D123')) THEN
    ! -<KL||CD> T^AD_KL
    DO KA=-KVC2,MAX(0,KVC2-1)
     KC=KA
     DO KK=-KVC2,MAX(0,KVC2-1)
      DO KL=-KVC2,MAX(0,KVC2-1)
       KD=KK-KC+KL
       IF (KD >= KVC2) KD=KD-2*KVC2
       IF (KD < -KVC2) KD=KD+2*KVC2
       READ(INT2FILE,REC=((KD+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KL+KVC2)+1) CMMMM1
       READ(T2INFILE,REC=((KL+KVC2)*(2*KVC2)+(KK+KVC2))*(2*KVC2)+(KD+KVC2)+1) TABIJ1
       DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
        DO MOK=ICORE+1,IOCC
         DO MOL=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
           DO MOD=IOCC+1,IALL(KD,0,0)-IVIRTCORE
            KMM1(MOA,MOC,KA)=KMM1(MOA,MOC,KA)-CMMMM1(MOK,MOL,MOC,MOD)*TABIJ1(MOA,MOD,MOK,MOL)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDIF

   IF (COPTN(70) == 'CCSD') THEN
    ! <KL||CD> T^C_I*T^D_L
    DO KI=-KVC2,MAX(0,KVC2-1)
     DO KL=-KVC2,MAX(0,KVC2-1)
      READ(INT2FILE,REC=((KL+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KL+KVC2)+1) CMMMM1
      DO MOI=ICORE+1,IOCC
       DO MOK=ICORE+1,IOCC
        DO MOL=ICORE+1,IOCC
         DO MOC=IOCC+1,IALL(KI,0,0)-IVIRTCORE
          DO MOD=IOCC+1,IALL(KL,0,0)-IVIRTCORE
           KMM1(MOK,MOI,KI)=KMM1(MOK,MOI,KI)+CMMMM1(MOK,MOL,MOC,MOD)*TAI1(MOC,MOI,KI)*TAI1(MOD,MOL,KL)
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO

    ! -<KL||CD> T^A_K*T^D_L
    DO KA=-KVC2,MAX(0,KVC2-1)
     DO KL=-KVC2,MAX(0,KVC2-1)
      READ(INT2FILE,REC=((KL+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KL+KVC2)+1) CMMMM1
      DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
       DO MOK=ICORE+1,IOCC
        DO MOL=ICORE+1,IOCC
         DO MOC=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          DO MOD=IOCC+1,IALL(KL,0,0)-IVIRTCORE
           KMM1(MOA,MOC,KA)=KMM1(MOA,MOC,KA)-CMMMM1(MOK,MOL,MOC,MOD)*TAI1(MOA,MOK,KA)*TAI1(MOD,MOL,KL)
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDIF

   IF ((COPTN(70) == 'CCSD').OR.(COPTN(70) == 'QCISD')) THEN
    ! <KL||CD>T^D_L
    DO KK=-KVC2,MAX(0,KVC2-1)
     DO KL=-KVC2,MAX(0,KVC2-1)
      READ(INT2FILE,REC=((KL+KVC2)*(2*KVC2)+(KK+KVC2))*(2*KVC2)+(KL+KVC2)+1) CMMMM1
      DO MOK=ICORE+1,IOCC
       DO MOL=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KK,0,0)-IVIRTCORE
         DO MOD=IOCC+1,IALL(KL,0,0)-IVIRTCORE
          KMM1(MOK,MOC,KK)=KMM1(MOK,MOC,KK)+CMMMM1(MOK,MOL,MOC,MOD)*TAI1(MOD,MOL,KL)
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDIF

   REWIND(KAPPAFILE)
   WRITE(KAPPAFILE) KMM1

   DEALLOCATE(TABIJ1,TAI1,KMM1,FMM1,CMMMM1)

   RETURN
END SUBROUTINE



SUBROUTINE CCSD_LAMBDA_INTERMEDIATES(INT2FILE,INT3FILE,T1INFILE,KAPPAFILE,LAMBDAFILE,NMOD,KVC2)
! GENERATES LAMBDA INTERMEDIATES FOR CCSD

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: INT2FILE,INT3FILE
   INTEGER :: T1INFILE
   INTEGER :: KAPPAFILE,LAMBDAFILE
   INTEGER :: MOI,MOJ,MOA,MOB
   INTEGER :: MOK,MOL,MOC,MOD
   INTEGER :: KI,KJ,KA,KB
   INTEGER :: KK,KL,KC,KD
   DOUBLE COMPLEX,ALLOCATABLE :: TAI1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: LMM1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: FMM1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM1(:,:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TAI1(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(LMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(FMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(CMMMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))

   REWIND(T1INFILE)
   READ(T1INFILE) TAI1
   REWIND(KAPPAFILE)
   READ(KAPPAFILE) LMM1
   REWIND(INT3FILE)
   READ(INT3FILE) FMM1

   IF (COPTN(70) == 'CCSD') THEN
    ! <KL||IC>T^C_L
    DO KI=-KVC2,MAX(0,KVC2-1)
     DO KL=-KVC2,MAX(0,KVC2-1)
      READ(INT2FILE,REC=((KL+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KL+KVC2)+1) CMMMM1
      DO MOK=ICORE+1,IOCC
       DO MOL=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOC=IOCC+1,IALL(KL,0,0)-IVIRTCORE
          LMM1(MOK,MOI,KI)=LMM1(MOK,MOI,KI)+CMMMM1(MOK,MOL,MOI,MOC)*TAI1(MOC,MOL,KL)
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO

    ! F^K_C T^C_I
    DO KI=-KVC2,MAX(0,KVC2-1)
     DO MOK=ICORE+1,IOCC
      DO MOI=ICORE+1,IOCC
       DO MOC=IOCC+1,IALL(KI,0,0)-IVIRTCORE
        LMM1(MOK,MOI,KI)=LMM1(MOK,MOI,KI)+FMM1(MOK,MOC,KI)*TAI1(MOC,MOI,KI)
       ENDDO
      ENDDO
     ENDDO
    ENDDO

    ! <AK||CD>T^D_K
    DO KA=-KVC2,MAX(0,KVC2-1)
     DO KK=-KVC2,MAX(0,KVC2-1)
      READ(INT2FILE,REC=((KK+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM1
      DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
       DO MOK=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KA,0,0)-IVIRTCORE
         DO MOD=IOCC+1,IALL(KK,0,0)-IVIRTCORE
          LMM1(MOA,MOC,KA)=LMM1(MOA,MOC,KA)+CMMMM1(MOA,MOK,MOC,MOD)*TAI1(MOD,MOK,KK)
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO

    ! -F^K_C T^A_K
    DO KA=-KVC2,MAX(0,KVC2-1)
     DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
      DO MOC=IOCC+1,IALL(KA,0,0)-IVIRTCORE
       DO MOK=ICORE+1,IOCC
        LMM1(MOA,MOC,KA)=LMM1(MOA,MOC,KA)-FMM1(MOK,MOC,KA)*TAI1(MOA,MOK,KA)
       ENDDO
      ENDDO
     ENDDO
    ENDDO

   ENDIF

   REWIND(LAMBDAFILE)
   WRITE(LAMBDAFILE) LMM1

   DEALLOCATE(TAI1,LMM1,FMM1,CMMMM1)

   RETURN
END SUBROUTINE



SUBROUTINE CCSD_CHI_INTERMEDIATES(INT1FILE,INT2FILE,T2INFILE,T1INFILE,CHIFILE,NMOD,KVC2)
! GENERATE CHI INTERMEDIATES FOR CCSD

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: INT1FILE,INT2FILE
   INTEGER :: T1INFILE,T2INFILE,CHIFILE
   INTEGER :: MOI,MOJ,MOK,MOL
   INTEGER :: MOA,MOB,MOC,MOD
   INTEGER :: MOP,MOQ,MOR,MOS
   INTEGER :: KI,KJ,KK,KL
   INTEGER :: KA,KB,KC,KD
   INTEGER :: KP,KQ,KR,KS
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: XMMMM1(:,:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TABIJ1(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(TAI1(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(CMMMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(XMMMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))

   REWIND(T1INFILE)
   READ(T1INFILE) TAI1

   DO KS=-KVC2,MAX(0,KVC2-1)
    DO KR=-KVC2,MAX(0,KVC2-1)
     DO KQ=-KVC2,MAX(0,KVC2-1)

      KP=KR-KQ+KS
      IF (KP >= KVC2) KP=KP-2*KVC2
      IF (KP < -KVC2) KP=KP+2*KVC2

      ! <KL|IJ>, <AB|CD>, <AK|IC>, <AK|CI>
      READ(INT1FILE,REC=((KS+KVC2)*(2*KVC2)+(KR+KVC2))*(2*KVC2)+(KQ+KVC2)+1) CMMMM1
      XMMMM1=CMMMM1

      IF (COPTN(70) == 'CCSD') THEN
       ! <KL|IC>T^C_J
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOL=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KS,0,0)-IVIRTCORE
           DO MOK=ICORE+1,IOCC
            XMMMM1(MOK,MOL,MOI,MOJ)=XMMMM1(MOK,MOL,MOI,MOJ)+CMMMM1(MOK,MOL,MOI,MOC)*TAI1(MOC,MOJ,KS)
           ENDDO
          ENDDO 
         ENDDO 
        ENDDO 
       ENDDO 

       ! <KL|CJ>T^C_I
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOL=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KR,0,0)-IVIRTCORE
           DO MOK=ICORE+1,IOCC
            XMMMM1(MOK,MOL,MOI,MOJ)=XMMMM1(MOK,MOL,MOI,MOJ)+CMMMM1(MOK,MOL,MOC,MOJ)*TAI1(MOC,MOI,KR)
           ENDDO
          ENDDO 
         ENDDO 
        ENDDO 
       ENDDO 

       ! -<AK|CD>T^B_K
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOD=IOCC+1,IALL(KS,0,0)-IVIRTCORE
        DO MOC=IOCC+1,IALL(KR,0,0)-IVIRTCORE
         DO MOB=IOCC+1,IALL(KQ,0,0)-IVIRTCORE
          DO MOK=ICORE+1,IOCC
           DO MOA=IOCC+1,IALL(KP,0,0)-IVIRTCORE
            XMMMM1(MOA,MOB,MOC,MOD)=XMMMM1(MOA,MOB,MOC,MOD)-CMMMM1(MOA,MOK,MOC,MOD)*TAI1(MOB,MOK,KQ)
           ENDDO
          ENDDO 
         ENDDO 
        ENDDO 
       ENDDO 

       ! -<KB|CD>T^A_K
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOD=IOCC+1,IALL(KS,0,0)-IVIRTCORE
        DO MOC=IOCC+1,IALL(KR,0,0)-IVIRTCORE
         DO MOB=IOCC+1,IALL(KQ,0,0)-IVIRTCORE
          DO MOK=ICORE+1,IOCC
           DO MOA=IOCC+1,IALL(KP,0,0)-IVIRTCORE
            XMMMM1(MOA,MOB,MOC,MOD)=XMMMM1(MOA,MOB,MOC,MOD)-CMMMM1(MOK,MOB,MOC,MOD)*TAI1(MOA,MOK,KP)
           ENDDO
          ENDDO 
         ENDDO 
        ENDDO 
       ENDDO 

       ! -<LK|IC>T^A_L
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOC=IOCC+1,IALL(KS,0,0)-IVIRTCORE
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOL=ICORE+1,IOCC
           DO MOA=IOCC+1,IALL(KP,0,0)-IVIRTCORE
            XMMMM1(MOA,MOK,MOI,MOC)=XMMMM1(MOA,MOK,MOI,MOC)-CMMMM1(MOL,MOK,MOI,MOC)*TAI1(MOA,MOL,KP)
           ENDDO
          ENDDO 
         ENDDO 
        ENDDO 
       ENDDO 

       ! <AK|DC>T^D_I
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOC=IOCC+1,IALL(KS,0,0)-IVIRTCORE
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOD=IOCC+1,IALL(KR,0,0)-IVIRTCORE
           DO MOA=IOCC+1,IALL(KP,0,0)-IVIRTCORE
            XMMMM1(MOA,MOK,MOI,MOC)=XMMMM1(MOA,MOK,MOI,MOC)+CMMMM1(MOA,MOK,MOD,MOC)*TAI1(MOD,MOI,KR)
           ENDDO
          ENDDO 
         ENDDO 
        ENDDO 
       ENDDO 
 
       ! -<LK|CI>T^A_L
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOI=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KR,0,0)-IVIRTCORE
         DO MOK=ICORE+1,IOCC
          DO MOL=ICORE+1,IOCC
           DO MOA=IOCC+1,IALL(KP,0,0)-IVIRTCORE
            XMMMM1(MOA,MOK,MOC,MOI)=XMMMM1(MOA,MOK,MOC,MOI)-CMMMM1(MOL,MOK,MOC,MOI)*TAI1(MOA,MOL,KP)
           ENDDO
          ENDDO 
         ENDDO 
        ENDDO 
       ENDDO 

       ! <AK|CD>T^D_I
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOI=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KR,0,0)-IVIRTCORE
         DO MOK=ICORE+1,IOCC
          DO MOD=IOCC+1,IALL(KS,0,0)-IVIRTCORE
           DO MOA=IOCC+1,IALL(KP,0,0)-IVIRTCORE
            XMMMM1(MOA,MOK,MOC,MOI)=XMMMM1(MOA,MOK,MOC,MOI)+CMMMM1(MOA,MOK,MOC,MOD)*TAI1(MOD,MOI,KS)
           ENDDO
          ENDDO 
         ENDDO 
        ENDDO 
       ENDDO 
 
       ! <KL|CD>T^C_I*T^D_J
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOL=ICORE+1,IOCC
          DO MOK=ICORE+1,IOCC
           DO MOD=IOCC+1,IALL(KS,0,0)-IVIRTCORE
            DO MOC=IOCC+1,IALL(KR,0,0)-IVIRTCORE
             XMMMM1(MOK,MOL,MOI,MOJ)=XMMMM1(MOK,MOL,MOI,MOJ)+CMMMM1(MOK,MOL,MOC,MOD)*TAI1(MOC,MOI,KR)*TAI1(MOD,MOJ,KS)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO

       ! -<LK|DC>T^A_L*T^D_I
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOA=IOCC+1,IALL(KP,0,0)-IVIRTCORE
        DO MOI=ICORE+1,IOCC
         DO MOL=ICORE+1,IOCC
          DO MOK=ICORE+1,IOCC
           DO MOD=IOCC+1,IALL(KR,0,0)-IVIRTCORE
            DO MOC=IOCC+1,IALL(KS,0,0)-IVIRTCORE
             XMMMM1(MOA,MOK,MOI,MOC)=XMMMM1(MOA,MOK,MOI,MOC)-CMMMM1(MOL,MOK,MOD,MOC)*TAI1(MOA,MOL,KP)*TAI1(MOD,MOI,KR)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
 
       ! -<LK|CD>T^A_L*T^D_I
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOA=IOCC+1,IALL(KP,0,0)-IVIRTCORE
        DO MOI=ICORE+1,IOCC
         DO MOL=ICORE+1,IOCC
          DO MOK=ICORE+1,IOCC
           DO MOC=IOCC+1,IALL(KR,0,0)-IVIRTCORE
            DO MOD=IOCC+1,IALL(KS,0,0)-IVIRTCORE
             XMMMM1(MOA,MOK,MOC,MOI)=XMMMM1(MOA,MOK,MOC,MOI)-CMMMM1(MOL,MOK,MOC,MOD)*TAI1(MOA,MOL,KP)*TAI1(MOD,MOI,KS)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDIF

      IF ((COPTN(70) == 'CCD').OR.(COPTN(70) == 'QCISD').OR.(COPTN(70) == 'CCSD').OR.(COPTN(70) == 'D45')) THEN
       ! <KL|CD>T^CD_IJ
       DO KC=-KVC2,MAX(0,KVC2-1)
        KD=KP-KC+KQ
        IF (KD >= KVC2) KD=KD-2*KVC2
        IF (KD < -KVC2) KD=KD+2*KVC2
        READ(INT1FILE,REC=((KD+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KQ+KVC2)+1) CMMMM1 
        READ(T2INFILE,REC=((KS+KVC2)*(2*KVC2)+(KR+KVC2))*(2*KVC2)+(KD+KVC2)+1) TABIJ1
        DO MOJ=ICORE+1,IOCC
         DO MOI=ICORE+1,IOCC
          DO MOL=ICORE+1,IOCC
           DO MOK=ICORE+1,IOCC
            DO MOD=IOCC+1,IALL(KS,0,0)-IVIRTCORE
             DO MOC=IOCC+1,IALL(KR,0,0)-IVIRTCORE
              XMMMM1(MOK,MOL,MOI,MOJ)=XMMMM1(MOK,MOL,MOI,MOJ)+CMMMM1(MOK,MOL,MOC,MOD)*TABIJ1(MOC,MOD,MOI,MOJ)
             ENDDO
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDIF

      IF ((COPTN(70) == 'CCD').OR.(COPTN(70) == 'QCISD').OR.(COPTN(70) == 'CCSD').OR.(COPTN(70) == 'D123')) THEN
       ! -1/2<LK|DC>T^AD_LI
       DO KL=-KVC2,MAX(0,KVC2-1)
        KD=KQ-KS+KL
        IF (KD >= KVC2) KD=KD-2*KVC2
        IF (KD < -KVC2) KD=KD+2*KVC2
        READ(INT1FILE,REC=((KS+KVC2)*(2*KVC2)+(KD+KVC2))*(2*KVC2)+(KQ+KVC2)+1) CMMMM1 
        READ(T2INFILE,REC=((KR+KVC2)*(2*KVC2)+(KL+KVC2))*(2*KVC2)+(KD+KVC2)+1) TABIJ1
        DO MOA=IOCC+1,IALL(KP,0,0)-IVIRTCORE
         DO MOI=ICORE+1,IOCC
          DO MOL=ICORE+1,IOCC
           DO MOK=ICORE+1,IOCC
            DO MOD=IOCC+1,IALL(KR,0,0)-IVIRTCORE
             DO MOC=IOCC+1,IALL(KS,0,0)-IVIRTCORE
              XMMMM1(MOA,MOK,MOI,MOC)=XMMMM1(MOA,MOK,MOI,MOC)-0.5D0*CMMMM1(MOL,MOK,MOD,MOC)*TABIJ1(MOA,MOD,MOL,MOI)
             ENDDO
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO

       ! 1/2<KL||CD>T^AD_IL
       DO KL=-KVC2,MAX(0,KVC2-1)
        KD=KQ-KS+KL
        IF (KD >= KVC2) KD=KD-2*KVC2
        IF (KD < -KVC2) KD=KD+2*KVC2
        READ(INT2FILE,REC=((KD+KVC2)*(2*KVC2)+(KS+KVC2))*(2*KVC2)+(KL+KVC2)+1) CMMMM1 
        READ(T2INFILE,REC=((KL+KVC2)*(2*KVC2)+(KR+KVC2))*(2*KVC2)+(KD+KVC2)+1) TABIJ1
        DO MOA=IOCC+1,IALL(KP,0,0)-IVIRTCORE
         DO MOI=ICORE+1,IOCC
          DO MOL=ICORE+1,IOCC
           DO MOK=ICORE+1,IOCC
            DO MOD=IOCC+1,IALL(KS,0,0)-IVIRTCORE
             DO MOC=IOCC+1,IALL(KR,0,0)-IVIRTCORE
              XMMMM1(MOA,MOK,MOI,MOC)=XMMMM1(MOA,MOK,MOI,MOC)+0.5D0*CMMMM1(MOK,MOL,MOC,MOD)*TABIJ1(MOA,MOD,MOI,MOL)
             ENDDO
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
 
       ! -1/2<LK|CD>T^AD_LI
       DO KL=-KVC2,MAX(0,KVC2-1)
        KD=KQ-KR+KL
        IF (KD >= KVC2) KD=KD-2*KVC2
        IF (KD < -KVC2) KD=KD+2*KVC2
        READ(INT1FILE,REC=((KD+KVC2)*(2*KVC2)+(KR+KVC2))*(2*KVC2)+(KQ+KVC2)+1) CMMMM1 
        READ(T2INFILE,REC=((KS+KVC2)*(2*KVC2)+(KL+KVC2))*(2*KVC2)+(KD+KVC2)+1) TABIJ1
        DO MOA=IOCC+1,IALL(KP,0,0)-IVIRTCORE
         DO MOI=ICORE+1,IOCC
          DO MOL=ICORE+1,IOCC
           DO MOK=ICORE+1,IOCC
            DO MOC=IOCC+1,IALL(KR,0,0)-IVIRTCORE
             DO MOD=IOCC+1,IALL(KS,0,0)-IVIRTCORE
              XMMMM1(MOA,MOK,MOC,MOI)=XMMMM1(MOA,MOK,MOC,MOI)-0.5D0*CMMMM1(MOL,MOK,MOC,MOD)*TABIJ1(MOA,MOD,MOL,MOI)
             ENDDO
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDIF
 
      WRITE(CHIFILE,REC=((KS+KVC2)*(2*KVC2)+(KR+KVC2))*(2*KVC2)+(KQ+KVC2)+1) XMMMM1

     ENDDO
    ENDDO
   ENDDO

   DEALLOCATE(TABIJ1,TAI1,CMMMM1,XMMMM1)

   RETURN
END SUBROUTINE



SUBROUTINE CCSD_DOUBLES_CONTRACTION(INT1FILE,INT3FILE,T2INFILE,T2OUTFILE,T1INFILE,LAMBDAFILE,CHIFILE,NMOD,KVC2)
! PERFORM DOUBLES CONTRACTION FOR CCSD CALCULATIONS

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: INT1FILE,INT3FILE
   INTEGER :: T2INFILE,T2OUTFILE
   INTEGER :: T1INFILE
   INTEGER :: LAMBDAFILE,CHIFILE
   INTEGER :: MOI,MOJ,MOA,MOB
   INTEGER :: MOK,MOL,MOC,MOD
   INTEGER :: KI,KJ,KA,KB
   INTEGER :: KK,KL,KC,KD
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ2(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM2(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: LMM1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: FMM1(:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TABIJ1(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(TABIJ2(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(TAI1(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(CMMMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(CMMMM2(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(LMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(FMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1)))

   REWIND(LAMBDAFILE)
   READ(LAMBDAFILE) LMM1 

   REWIND(INT3FILE)
   READ(INT3FILE) FMM1 

   REWIND(T1INFILE)
   READ(T1INFILE) TAI1

   DO KB=-KVC2,MAX(0,KVC2-1)
    DO KA=-KVC2,MAX(0,KVC2-1)
     DO KJ=-KVC2,MAX(0,KVC2-1)
      KI=KA-KJ+KB
      IF (KI >= KVC2) KI=KI-2*KVC2
      IF (KI < -KVC2) KI=KI+2*KVC2

      ! <IJ|AB>*
      READ(INT1FILE,REC=((KB+KVC2)*(2*KVC2)+(KA+KVC2))*(2*KVC2)+(KJ+KVC2)+1) CMMMM1
      DO MOJ=ICORE+1,IOCC
       DO MOI=ICORE+1,IOCC
        DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          TABIJ2(MOA,MOB,MOI,MOJ)=DCONJG(CMMMM1(MOI,MOJ,MOA,MOB))
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      ! X^KL_IJ T^AB_KL
      DO KK=-KVC2,MAX(0,KVC2-1)
       KL=KI-KK+KJ
       IF (KL >= KVC2) KL=KL-2*KVC2
       IF (KL < -KVC2) KL=KL+2*KVC2
       READ(CHIFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KL+KVC2)+1) CMMMM1 
       READ(T2INFILE,REC=((KL+KVC2)*(2*KVC2)+(KK+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOL=ICORE+1,IOCC
          DO MOK=ICORE+1,IOCC
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)+CMMMM1(MOK,MOL,MOI,MOJ)*TABIJ1(MOA,MOB,MOK,MOL)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      ! X^AB_CD T^CD_IJ
      DO KC=-KVC2,MAX(0,KVC2-1)
       KD=KA-KC+KB
       IF (KD >= KVC2) KD=KD-2*KVC2
       IF (KD < -KVC2) KD=KD+2*KVC2
       READ(CHIFILE,REC=((KD+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KB+KVC2)+1) CMMMM1 
       READ(T2INFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KD+KVC2)+1) TABIJ1
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOD=IOCC+1,IALL(KD,0,0)-IVIRTCORE
          DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)+CMMMM1(MOA,MOB,MOC,MOD)*TABIJ1(MOC,MOD,MOI,MOJ)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      IF (COPTN(70) == 'CCSD') THEN
       ! X^KL_IJ T^A_K*T^B_L
       READ(CHIFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) CMMMM1 
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOL=ICORE+1,IOCC
          DO MOK=ICORE+1,IOCC
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)+CMMMM1(MOK,MOL,MOI,MOJ)*TAI1(MOA,MOK,KA)*TAI1(MOB,MOL,KB)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO

       ! X^AB_CD T^C_I*T^D_J
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOD=IOCC+1,IALL(KJ,0,0)-IVIRTCORE
          DO MOC=IOCC+1,IALL(KI,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)+CMMMM1(MOA,MOB,MOC,MOD)*TAI1(MOC,MOI,KI)*TAI1(MOD,MOJ,KJ)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDIF

      ! L^A_C T^CB_IJ
      READ(T2INFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1
      DO MOJ=ICORE+1,IOCC
       DO MOI=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KA,0,0)-IVIRTCORE
         DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
          DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
           TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)+LMM1(MOA,MOC,KA)*TABIJ1(MOC,MOB,MOI,MOJ)
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      ! -L^K_I T^AB_KJ
      READ(T2INFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1
      DO MOJ=ICORE+1,IOCC
       DO MOI=ICORE+1,IOCC
        DO MOK=ICORE+1,IOCC
         DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
          DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
           TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-LMM1(MOK,MOI,KI)*TABIJ1(MOA,MOB,MOK,MOJ)
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      ! L^B_C T^CA_JI
      READ(T2INFILE,REC=((KI+KVC2)*(2*KVC2)+(KJ+KVC2))*(2*KVC2)+(KA+KVC2)+1) TABIJ1
      DO MOJ=ICORE+1,IOCC
       DO MOI=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KB,0,0)-IVIRTCORE
         DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
          DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
           TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)+LMM1(MOB,MOC,KB)*TABIJ1(MOC,MOA,MOJ,MOI)
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      ! -L^K_J T^BA_KI
      READ(T2INFILE,REC=((KI+KVC2)*(2*KVC2)+(KJ+KVC2))*(2*KVC2)+(KA+KVC2)+1) TABIJ1
      DO MOJ=ICORE+1,IOCC
       DO MOI=ICORE+1,IOCC
        DO MOK=ICORE+1,IOCC
         DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
          DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
           TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-LMM1(MOK,MOJ,KJ)*TABIJ1(MOB,MOA,MOK,MOI)
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      IF ((COPTN(70) == 'LCCSD').OR.(COPTN(70) == 'QCISD').OR.(COPTN(70) == 'CCSD')) THEN
       ! <AB|CJ> T^C_I
       READ(INT1FILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) CMMMM1
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOC=IOCC+1,IALL(KI,0,0)-IVIRTCORE
          DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
           DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
            TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)+CMMMM1(MOA,MOB,MOC,MOJ)*TAI1(MOC,MOI,KI)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO

       ! -<KB|IJ> T^A_K
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
           DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
            TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-CMMMM1(MOK,MOB,MOI,MOJ)*TAI1(MOA,MOK,KA)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO

       ! <BA|CI> T^C_J
       READ(INT1FILE,REC=((KI+KVC2)*(2*KVC2)+(KJ+KVC2))*(2*KVC2)+(KA+KVC2)+1) CMMMM1
       DO MOI=ICORE+1,IOCC
        DO MOJ=ICORE+1,IOCC
         DO MOC=IOCC+1,IALL(KJ,0,0)-IVIRTCORE
          DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)+CMMMM1(MOB,MOA,MOC,MOI)*TAI1(MOC,MOJ,KJ)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO

       ! -<KA|JI> T^B_K
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
           DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
            TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-CMMMM1(MOK,MOA,MOJ,MOI)*TAI1(MOB,MOK,KB)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDIF

      IF (COPTN(70) == 'CCSD') THEN
       ! -<KB|IC>T^A_K*T^C_J
       READ(INT1FILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) CMMMM1
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KJ,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-CMMMM1(MOK,MOB,MOI,MOC)*TAI1(MOA,MOK,KA)*TAI1(MOC,MOJ,KJ)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO

       ! -<AK|IC>T^C_J*T^B_K
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KJ,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-CMMMM1(MOA,MOK,MOI,MOC)*TAI1(MOC,MOJ,KJ)*TAI1(MOB,MOK,KB)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO

       ! -<KA|JC>T^B_K*T^C_I
       READ(INT1FILE,REC=((KI+KVC2)*(2*KVC2)+(KJ+KVC2))*(2*KVC2)+(KA+KVC2)+1) CMMMM1
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KI,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-CMMMM1(MOK,MOA,MOJ,MOC)*TAI1(MOB,MOK,KB)*TAI1(MOC,MOI,KI)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO

       ! -<BK|JC>T^C_I*T^A_K
       ! CMMMM1 NOT RE-READ; USE CAUTION WHEN RE-ORDER THE LOOPS
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KI,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-CMMMM1(MOB,MOK,MOJ,MOC)*TAI1(MOC,MOI,KI)*TAI1(MOA,MOK,KA)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDIF

      ! (2X^AK_IC-X^AK_CI)T^CB_KJ
      DO KK=-KVC2,MAX(0,KVC2-1)
       KC=KA-KI+KK
       IF (KC >= KVC2) KC=KC-2*KVC2
       IF (KC < -KVC2) KC=KC+2*KVC2
       READ(CHIFILE,REC=((KC+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM1 
       READ(CHIFILE,REC=((KI+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM2 
       READ(T2INFILE,REC=((KJ+KVC2)*(2*KVC2)+(KK+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ) &
             +(2.0D0*CMMMM1(MOA,MOK,MOI,MOC)-CMMMM2(MOA,MOK,MOC,MOI))*TABIJ1(MOC,MOB,MOK,MOJ)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      ! (2X^BK_JC-X^BK_CJ)T^CA_KI
      DO KK=-KVC2,MAX(0,KVC2-1)
       KC=KB-KJ+KK
       IF (KC >= KVC2) KC=KC-2*KVC2
       IF (KC < -KVC2) KC=KC+2*KVC2
       READ(CHIFILE,REC=((KC+KVC2)*(2*KVC2)+(KJ+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM1 
       READ(CHIFILE,REC=((KJ+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM2 
       READ(T2INFILE,REC=((KI+KVC2)*(2*KVC2)+(KK+KVC2))*(2*KVC2)+(KA+KVC2)+1) TABIJ1
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ) &
             +(2.0D0*CMMMM1(MOB,MOK,MOJ,MOC)-CMMMM2(MOB,MOK,MOC,MOJ))*TABIJ1(MOC,MOA,MOK,MOI)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      ! -X^AK_IC T^BC_KJ
      DO KK=-KVC2,MAX(0,KVC2-1)
       KC=KA-KI+KK
       IF (KC >= KVC2) KC=KC-2*KVC2
       IF (KC < -KVC2) KC=KC+2*KVC2
       READ(CHIFILE,REC=((KC+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM1 
       READ(T2INFILE,REC=((KJ+KVC2)*(2*KVC2)+(KK+KVC2))*(2*KVC2)+(KC+KVC2)+1) TABIJ1
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-CMMMM1(MOA,MOK,MOI,MOC)*TABIJ1(MOB,MOC,MOK,MOJ)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      ! -X^BK_JC T^AC_KI
      DO KK=-KVC2,MAX(0,KVC2-1)
       KC=KB-KJ+KK
       IF (KC >= KVC2) KC=KC-2*KVC2
       IF (KC < -KVC2) KC=KC+2*KVC2
       READ(CHIFILE,REC=((KC+KVC2)*(2*KVC2)+(KJ+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM1 
       READ(T2INFILE,REC=((KI+KVC2)*(2*KVC2)+(KK+KVC2))*(2*KVC2)+(KC+KVC2)+1) TABIJ1
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-CMMMM1(MOB,MOK,MOJ,MOC)*TABIJ1(MOA,MOC,MOK,MOI)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      ! -X^BK_CI T^AC_KJ
      DO KK=-KVC2,MAX(0,KVC2-1)
       KC=KB-KI+KK
       IF (KC >= KVC2) KC=KC-2*KVC2
       IF (KC < -KVC2) KC=KC+2*KVC2
       READ(CHIFILE,REC=((KI+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM1 
       READ(T2INFILE,REC=((KJ+KVC2)*(2*KVC2)+(KK+KVC2))*(2*KVC2)+(KC+KVC2)+1) TABIJ1
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-CMMMM1(MOB,MOK,MOC,MOI)*TABIJ1(MOA,MOC,MOK,MOJ)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      ! -X^AK_CJ T^BC_KI
      DO KK=-KVC2,MAX(0,KVC2-1)
       KC=KA-KJ+KK
       IF (KC >= KVC2) KC=KC-2*KVC2
       IF (KC < -KVC2) KC=KC+2*KVC2
       READ(CHIFILE,REC=((KJ+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM1 
       READ(T2INFILE,REC=((KI+KVC2)*(2*KVC2)+(KK+KVC2))*(2*KVC2)+(KC+KVC2)+1) TABIJ1
       DO MOJ=ICORE+1,IOCC
        DO MOI=ICORE+1,IOCC
         DO MOK=ICORE+1,IOCC
          DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
             TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-CMMMM1(MOA,MOK,MOC,MOJ)*TABIJ1(MOB,MOC,MOK,MOI)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO

      ! T^AB_IJ FROM PREVIOUS ITERATION
      READ(T2OUTFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1
      DO MOJ=ICORE+1,IOCC
       DO MOI=ICORE+1,IOCC
        DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          TABIJ1(MOA,MOB,MOI,MOJ)=TABIJ1(MOA,MOB,MOI,MOJ)+TABIJ2(MOA,MOB,MOI,MOJ) &
          /(FMM1(MOI,MOI,KI)+FMM1(MOJ,MOJ,KJ)-FMM1(MOA,MOA,KA)-FMM1(MOB,MOB,KB))
         ENDDO
        ENDDO
       ENDDO
      ENDDO
      WRITE(T2OUTFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1

     ENDDO
    ENDDO
   ENDDO

   DEALLOCATE(TABIJ1,TABIJ2,TAI1,CMMMM1,CMMMM2,LMM1,FMM1)

   RETURN
END SUBROUTINE



SUBROUTINE CCSD_SINGLES_CONTRACTION(INT2FILE,INT3FILE,T2INFILE,T1INFILE,T1OUTFILE,KAPPAFILE,NMOD,KVC2)
! PERFORM SINGLES CONTRACTION FOR CCSD CALCULATIONS

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: INT2FILE,INT3FILE
   INTEGER :: T2INFILE
   INTEGER :: T1INFILE,T1OUTFILE
   INTEGER :: KAPPAFILE
   INTEGER :: MOI,MOJ,MOA,MOB
   INTEGER :: MOK,MOL,MOC,MOD
   INTEGER :: KI,KJ,KA,KB
   INTEGER :: KK,KL,KC,KD
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI2(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI3(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CMMMM1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: KMM1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: FMM1(:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TABIJ1(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(TAI1(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(TAI2(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(TAI3(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(CMMMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE, &
                   ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE))
   ALLOCATE(KMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(FMM1(ICORE+1:IALLMAX-IVIRTCORE,ICORE+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1)))

   IF ((COPTN(70) == 'CCD').OR.(COPTN(70) == 'LCCD').OR.(COPTN(70) == 'D123').OR.(COPTN(70) == 'D45')) THEN

    TAI3=DCMPLX(0.0D0,0.0D0)
    REWIND(T1OUTFILE)
    WRITE(T1OUTFILE) TAI3
    RETURN

   ELSE

    REWIND(KAPPAFILE)
    READ(KAPPAFILE) KMM1 

    REWIND(T1INFILE)
    READ(T1INFILE) TAI1

    REWIND(INT3FILE)
    READ(INT3FILE) FMM1
 
    DO KA=-KVC2,MAX(0,KVC2-1)
     KI=KA
 
     ! F^A_I
     DO MOI=ICORE+1,IOCC
      DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
       TAI2(MOA,MOI,KI)=FMM1(MOA,MOI,KI)
      ENDDO
     ENDDO
 
     IF (COPTN(70) == 'CCSD') THEN
      ! -2 F^K_C T^A_K T^C_I
      DO MOI=ICORE+1,IOCC
       DO MOK=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KI,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          TAI2(MOA,MOI,KI)=TAI2(MOA,MOI,KI)-2.0D0*FMM1(MOK,MOC,KI)*TAI1(MOA,MOK,KI)*TAI1(MOC,MOI,KI)
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDIF
 
     ! K^A_C T^C_I
     DO MOI=ICORE+1,IOCC
      DO MOC=IOCC+1,IALL(KI,0,0)-IVIRTCORE
       DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
        TAI2(MOA,MOI,KI)=TAI2(MOA,MOI,KI)+KMM1(MOA,MOC,KA)*TAI1(MOC,MOI,KI)
       ENDDO
      ENDDO
     ENDDO
 
     ! -K^K_I T^A_K
     DO MOI=ICORE+1,IOCC
      DO MOK=ICORE+1,IOCC
       DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
        TAI2(MOA,MOI,KI)=TAI2(MOA,MOI,KI)-KMM1(MOK,MOI,KI)*TAI1(MOA,MOK,KA)
       ENDDO
      ENDDO
     ENDDO
 
     ! 2*K^K_C T^CA_KI
     DO KK=-KVC2,MAX(0,KVC2-1)
      READ(T2INFILE,REC=((KI+KVC2)*(2*KVC2)+(KK+KVC2))*(2*KVC2)+(KA+KVC2)+1) TABIJ1 
      DO MOI=ICORE+1,IOCC
       DO MOK=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KK,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          TAI2(MOA,MOI,KI)=TAI2(MOA,MOI,KI)+2.0D0*KMM1(MOK,MOC,KK)*TABIJ1(MOC,MOA,MOK,MOI)
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
 
     ! -K^K_C T^CA_IK
     DO KK=-KVC2,MAX(0,KVC2-1)
      READ(T2INFILE,REC=((KK+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KA+KVC2)+1) TABIJ1 
      DO MOI=ICORE+1,IOCC
       DO MOK=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KK,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          TAI2(MOA,MOI,KI)=TAI2(MOA,MOI,KI)-KMM1(MOK,MOC,KK)*TABIJ1(MOC,MOA,MOI,MOK)
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
 
     IF (COPTN(70) == 'CCSD') THEN
      ! K^K_C T^C_I*T^A_K
      DO MOI=ICORE+1,IOCC
       DO MOK=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KI,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          TAI2(MOA,MOI,KI)=TAI2(MOA,MOI,KI)+KMM1(MOK,MOC,KI)*TAI1(MOC,MOI,KI)*TAI1(MOA,MOK,KA)
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDIF
 
     ! <AK||IC> T^C_K
     DO KK=-KVC2,MAX(0,KVC2-1)
      READ(INT2FILE,REC=((KK+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM1 
      DO MOI=ICORE+1,IOCC
       DO MOK=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KK,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          TAI2(MOA,MOI,KI)=TAI2(MOA,MOI,KI)+CMMMM1(MOA,MOK,MOI,MOC)*TAI1(MOC,MOK,KK)
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
 
     ! <AK||CD> T^CD_IK
     DO KK=-KVC2,MAX(0,KVC2-1)
      DO KC=-KVC2,MAX(0,KVC2-1)
       KD=KA-KC+KK
       IF (KD >= KVC2) KD=KD-2*KVC2
       IF (KD < -KVC2) KD=KD+2*KVC2
       READ(INT2FILE,REC=((KD+KVC2)*(2*KVC2)+(KC+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM1 
       READ(T2INFILE,REC=((KK+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KD+KVC2)+1) TABIJ1 
       DO MOI=ICORE+1,IOCC
        DO MOK=ICORE+1,IOCC
         DO MOD=IOCC+1,IALL(KD,0,0)-IVIRTCORE
          DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
           DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
            TAI2(MOA,MOI,KI)=TAI2(MOA,MOI,KI)+CMMMM1(MOA,MOK,MOC,MOD)*TABIJ1(MOC,MOD,MOI,MOK)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
 
     IF (COPTN(70) == 'CCSD') THEN
      ! <AK||CD> T^C_I*T^D_K
      DO KK=-KVC2,MAX(0,KVC2-1)
       READ(INT2FILE,REC=((KK+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KK+KVC2)+1) CMMMM1 
       DO MOI=ICORE+1,IOCC
        DO MOK=ICORE+1,IOCC
         DO MOD=IOCC+1,IALL(KK,0,0)-IVIRTCORE
          DO MOC=IOCC+1,IALL(KI,0,0)-IVIRTCORE
           DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
            TAI2(MOA,MOI,KI)=TAI2(MOA,MOI,KI)+CMMMM1(MOA,MOK,MOC,MOD)*TAI1(MOC,MOI,KI)*TAI1(MOD,MOK,KK)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDIF
 
     ! -<KL||IC> T^AC_KL
     DO KK=-KVC2,MAX(0,KVC2-1)
      DO KL=-KVC2,MAX(0,KVC2-1)
       KC=KK-KI+KL
       IF (KC >= KVC2) KC=KC-2*KVC2
       IF (KC < -KVC2) KC=KC+2*KVC2
       READ(INT2FILE,REC=((KC+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KL+KVC2)+1) CMMMM1 
       READ(T2INFILE,REC=((KL+KVC2)*(2*KVC2)+(KK+KVC2))*(2*KVC2)+(KC+KVC2)+1) TABIJ1 
       DO MOI=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KC,0,0)-IVIRTCORE
         DO MOL=ICORE+1,IOCC
          DO MOK=ICORE+1,IOCC
           DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
            TAI2(MOA,MOI,KI)=TAI2(MOA,MOI,KI)-CMMMM1(MOK,MOL,MOI,MOC)*TABIJ1(MOA,MOC,MOK,MOL)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
 
     IF (COPTN(70) == 'CCSD') THEN
      ! -<KL||IC> T^A_K*T^C_L
      DO KL=-KVC2,MAX(0,KVC2-1)
       READ(INT2FILE,REC=((KL+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KL+KVC2)+1) CMMMM1 
       DO MOI=ICORE+1,IOCC
        DO MOC=IOCC+1,IALL(KL,0,0)-IVIRTCORE
         DO MOL=ICORE+1,IOCC
          DO MOK=ICORE+1,IOCC
           DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
            TAI2(MOA,MOI,KI)=TAI2(MOA,MOI,KI)-CMMMM1(MOK,MOL,MOI,MOC)*TAI1(MOA,MOK,KA)*TAI1(MOC,MOL,KL)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDIF
 
     ! T^A_I FROM PREVIOUS ITERATION
     REWIND(T1OUTFILE)
     READ(T1OUTFILE) TAI3
     DO MOI=ICORE+1,IOCC
      DO MOA=IOCC+1,IALL(KI,0,0)-IVIRTCORE
       TAI3(MOA,MOI,KI)=TAI3(MOA,MOI,KI)+TAI2(MOA,MOI,KI)/(FMM1(MOI,MOI,KI)-FMM1(MOA,MOA,KA))
      ENDDO
     ENDDO
     REWIND(T1OUTFILE)
     WRITE(T1OUTFILE) TAI3
 
    ENDDO

   ENDIF

   DEALLOCATE(TABIJ1,TAI1,TAI2,TAI3,CMMMM1,KMM1,FMM1)

   RETURN
END SUBROUTINE



SUBROUTINE ZERO_T_AMPLITUDE_STOREFILES(T2STOREFILE,T2DSTOREFILE,T1STOREFILE,T1DSTOREFILE,IDIIS,NMOD,KVC2)
! ZERO SCRATCH THE T1 AND T2 AMPLITUDES AND T1 AND T2 RESIDUAL AMPLITUDES IN FILES.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
  
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: IDIIS
   INTEGER :: T2STOREFILE,T2DSTOREFILE
   INTEGER :: T1STOREFILE,T1DSTOREFILE
   INTEGER :: KI,KJ,KA,KB
   INTEGER :: I
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI1(:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TABIJ1(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(TAI1(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))

   ! ZERO SCRATCH THE T1 & T1D FILES
   TAI1=DCMPLX(0.0D0,0.0D0)
   DO I=1,IDIIS
    WRITE(T1STOREFILE,REC=I) TAI1
    WRITE(T1DSTOREFILE,REC=I) TAI1
   ENDDO

   ! ZERO SCRATCH THE T2 & T2D FILES
   DO I=1,IDIIS
    DO KB=-KVC2,MAX(0,KVC2-1)
     DO KI=-KVC2,MAX(0,KVC2-1)
      DO KJ=-KVC2,MAX(0,KVC2-1)
       TABIJ1=DCMPLX(0.0D0,0.0D0)
       WRITE(T2STOREFILE,REC=(I-1)*(MAX(1,2*KVC2)**3)+((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1 
       WRITE(T2DSTOREFILE,REC=(I-1)*(MAX(1,2*KVC2)**3)+((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1 
      ENDDO
     ENDDO
    ENDDO
   ENDDO

   DEALLOCATE(TABIJ1,TAI1)
   RETURN

END SUBROUTINE



SUBROUTINE STORE_T_AMPLITUDES(T2INFILE,T2OUTFILE,T1INFILE,T1OUTFILE,T2STOREFILE,T2DSTOREFILE,T1STOREFILE,T1DSTOREFILE,IDIIS,&
                              NMOD,KVC2)
! STORE THE T1 AND T2 AMPLITUDES AND T1 AND T2 RESIDUAL AMPLITUDES IN FILES.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
  
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: IDIIS
   INTEGER :: T2INFILE,T2OUTFILE
   INTEGER :: T1INFILE,T1OUTFILE
   INTEGER :: T2STOREFILE,T2DSTOREFILE
   INTEGER :: T1STOREFILE,T1DSTOREFILE
   INTEGER :: MOI,MOJ,MOA,MOB
   INTEGER :: KI,KJ,KA,KB
   INTEGER :: I
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ2(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI2(:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(TABIJ1(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(TABIJ2(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(TAI1(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(TAI2(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))

   ! SHIFT THE CONTENTS OF THE T1 & T1D FILES
   DO I=2,IDIIS
    READ(T1STOREFILE,REC=I) TAI1
    WRITE(T1STOREFILE,REC=I-1) TAI1
    READ(T1DSTOREFILE,REC=I) TAI1
    WRITE(T1DSTOREFILE,REC=I-1) TAI1
   ENDDO

   ! SHIFT THE CONTENTS OF THE T2 & T2D FILES
   DO I=2,IDIIS
    DO KB=-KVC2,MAX(0,KVC2-1)
     DO KI=-KVC2,MAX(0,KVC2-1)
      DO KJ=-KVC2,MAX(0,KVC2-1)
       READ(T2STOREFILE,REC=(I-1)*(MAX(1,2*KVC2)**3)+((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1 
       WRITE(T2STOREFILE,REC=(I-2)*(MAX(1,2*KVC2)**3)+((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1 
       READ(T2DSTOREFILE,REC=(I-1)*(MAX(1,2*KVC2)**3)+((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1 
       WRITE(T2DSTOREFILE,REC=(I-2)*(MAX(1,2*KVC2)**3)+((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1 
      ENDDO
     ENDDO
    ENDDO
   ENDDO

   ! STORE THE LATEST T1 & T1D
   REWIND(T1INFILE)
   READ(T1INFILE) TAI1
   REWIND(T1OUTFILE)
   READ(T1OUTFILE) TAI2
   DO KI=-KVC2,MAX(0,KVC2-1)
    DO MOI=ICORE+1,IOCC
     DO MOA=IOCC+1,IALL(KI,0,0)-IVIRTCORE
      TAI2(MOA,MOI,KI)=TAI2(MOA,MOI,KI)-TAI1(MOA,MOI,KI)
     ENDDO
    ENDDO
   ENDDO
   WRITE(T1STOREFILE,REC=IDIIS) TAI1
   WRITE(T1DSTOREFILE,REC=IDIIS) TAI2

   ! STORE THE LATEST T2 & T2D
   DO KB=-KVC2,MAX(0,KVC2-1)
    DO KI=-KVC2,MAX(0,KVC2-1)
     DO KJ=-KVC2,MAX(0,KVC2-1)
      READ(T2INFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1 
      READ(T2OUTFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ2 
      DO MOI=ICORE+1,IOCC
       DO MOJ=ICORE+1,IOCC
        DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
         DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
          TABIJ2(MOA,MOB,MOI,MOJ)=TABIJ2(MOA,MOB,MOI,MOJ)-TABIJ1(MOA,MOB,MOI,MOJ)
         ENDDO
        ENDDO
       ENDDO
      ENDDO
      WRITE(T2STOREFILE,REC=(IDIIS-1)*(MAX(1,2*KVC2)**3)+((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1 
      WRITE(T2DSTOREFILE,REC=(IDIIS-1)*(MAX(1,2*KVC2)**3)+((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ2 
     ENDDO
    ENDDO
   ENDDO

   DEALLOCATE(TABIJ1,TABIJ2,TAI1,TAI2)
   RETURN

END SUBROUTINE



SUBROUTINE CC_DIIS(T2INFILE,T1INFILE,T2STOREFILE,T2DSTOREFILE,T1STOREFILE,T1DSTOREFILE,IDIIS,NMOD,KVC2)
! PERFORM DIIS EXTRAPOLATION FOR T1 AND T2 AMPLITUDES.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
  
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: IDIIS
   INTEGER :: T2INFILE
   INTEGER :: T1INFILE
   INTEGER :: T2STOREFILE,T2DSTOREFILE
   INTEGER :: T1STOREFILE,T1DSTOREFILE
   INTEGER :: MOI,MOJ,MOA,MOB
   INTEGER :: KI,KJ,KA,KB
   INTEGER :: I,J
   INTEGER :: INDX(21)
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ2(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI2(:,:,:)
   DOUBLE PRECISION :: B(21,21),BS(21,21),C(21),CS(21),D
   INTEGER :: NMOD,KVC2

   ALLOCATE(TABIJ1(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(TABIJ2(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(TAI1(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(TAI2(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))

   B=0.0D0
   DO I=1,IDIIS
    READ(T1DSTOREFILE,REC=I) TAI1
    DO J=1,IDIIS
     READ(T1DSTOREFILE,REC=J) TAI2
     DO KI=-KVC2,MAX(0,KVC2-1)
      DO MOI=ICORE+1,IOCC
       DO MOA=IOCC+1,IALL(KI,0,0)-IVIRTCORE
        B(J,I)=B(J,I)+DCONJG(TAI2(MOA,MOI,KI))*TAI1(MOA,MOI,KI)
       ENDDO
      ENDDO
     ENDDO
     DO KJ=-KVC2,MAX(0,KVC2-1)
      DO KI=-KVC2,MAX(0,KVC2-1)
       DO KB=-KVC2,MAX(0,KVC2-1)
        READ(T2DSTOREFILE,REC=(I-1)*(MAX(1,2*KVC2)**3)+((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1 
        READ(T2DSTOREFILE,REC=(J-1)*(MAX(1,2*KVC2)**3)+((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ2 
        DO MOI=ICORE+1,IOCC
         DO MOJ=ICORE+1,IOCC
          DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
           DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
            B(J,I)=B(J,I)+DCONJG(TABIJ2(MOA,MOB,MOI,MOJ))*TABIJ1(MOA,MOB,MOI,MOJ)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
    B(I,IDIIS+1)=-1.0D0
    B(IDIIS+1,I)=-1.0D0
    C(I)=0.0D0
   ENDDO
   B(IDIIS+1,IDIIS+1)=0.0D0
   C(IDIIS+1)=-1.0D0
   BS=B
   CS=C
!  WRITE(6,'(A)') 'PULAY MATRIX'
!  CALL DUMP5(B,21)
   CALL LUDCMP(B,IDIIS+1,21,INDX,D)
   CALL LUBKSB(B,IDIIS+1,21,INDX,C)
   CALL MPROVE(BS,B,IDIIS+1,21,INDX,CS,C)
   WRITE(6,'(A,100F7.3:)') 'DIIS VECTOR',(C(I),I=1,IDIIS+1)

   ! FORM NEW T1 & T2 AMPLITUDES
   TAI1=DCMPLX(0.0D0,0.0D0)
   DO I=1,IDIIS
    READ(T1STOREFILE,REC=I) TAI2
    TAI1=TAI1+TAI2*C(I)
   ENDDO
   REWIND(T1INFILE)
   WRITE(T1INFILE) TAI1
   DO KB=-KVC2,MAX(0,KVC2-1)
    DO KI=-KVC2,MAX(0,KVC2-1)
     DO KJ=-KVC2,MAX(0,KVC2-1)
      TABIJ1=DCMPLX(0.0D0,0.0D0)
      DO I=1,IDIIS
       READ(T2STOREFILE,REC=(I-1)*(MAX(1,2*KVC2)**3)+((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ2
       TABIJ1=TABIJ1+TABIJ2*C(I)
      ENDDO
      WRITE(T2INFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1
     ENDDO
    ENDDO
   ENDDO

   DEALLOCATE(TABIJ1,TABIJ2,TAI1,TAI2)
   RETURN

END SUBROUTINE



SUBROUTINE CC_ANALYSIS(T2INFILE,T1INFILE,NMOD,KVC2)
! FIND THE MAXIMUM VALUE OF DENSITY MATRIX, OVERLAP MATRIX, TWO-E INTEGRALS, AND AO-BASED T-AMPLITUDES.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT

   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER,PARAMETER :: CASHESIZE = 1000 ! CASHE SIZE MUST BE EXACTLY THE SAME IN THE STORE & RESTORE SUBROUTINES
   INTEGER :: T2INFILE
   INTEGER :: T1INFILE
   INTEGER :: MOI,MOJ,MOA,MOB
   INTEGER :: KI,KJ,KA,KB
   INTEGER :: Q1,Q2,Q3
   INTEGER :: P1,P2,P3
   INTEGER :: I,J,K,L,P,Q,R
   INTEGER :: ICASHECOUNT,ICOUNT,EOF
   INTEGER(4),ALLOCATABLE :: ICASHE1X(:),ICASHE1Y(:),ICASHE1Z(:),ICASHE2(:)
   INTEGER,ALLOCATABLE :: NE(:)
   DOUBLE PRECISION :: E1,E2
   DOUBLE PRECISION,ALLOCATABLE :: EE1(:),EE2(:)
   DOUBLE PRECISION :: MAXR,MAXI
   DOUBLE PRECISION :: IE
   DOUBLE PRECISION,ALLOCATABLE :: MAXSR(:),MAXSI(:)
   DOUBLE PRECISION,ALLOCATABLE :: MAXT(:)
   DOUBLE PRECISION,ALLOCATABLE :: DCASHE(:)
   DOUBLE COMPLEX,ALLOCATABLE :: TABIJ1(:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: TAI1(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: T1_C(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: PHASE(:)
   DOUBLE COMPLEX,ALLOCATABLE :: CNNMMK(:,:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CNNNMK(:,:,:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: CNNNNK(:,:,:,:,:)
   INTEGER :: NMOD,KVC2

   ALLOCATE(ICASHE1X(CASHESIZE),ICASHE1Y(CASHESIZE),ICASHE1Z(CASHESIZE),ICASHE2(CASHESIZE),DCASHE(CASHESIZE))
   ALLOCATE(PHASE(-2*(CEL1X+CEL2X)*KVC2:2*(CEL1X+CEL2X)*KVC2))
   ALLOCATE(TABIJ1(IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,ICORE+1:IOCC))
   ALLOCATE(TAI1(IOCC+1:IALLMAX-IVIRTCORE,ICORE+1:IOCC,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(T1_C(NCGS,NCGS,-CEL1X:CEL1X))
   ALLOCATE(CNNMMK(NCGS,NCGS,IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(CNNNMK(NCGS,NCGS,NCGS,IOCC+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1)))
   ALLOCATE(CNNNNK(NCGS,NCGS,NCGS,NCGS,-CEL1X:CEL1X))
   ALLOCATE(MAXSR(-CEL1X:CEL1X),MAXSI(-CEL1X:CEL1X))
   ALLOCATE(MAXT(-CEL2X:CEL2X),NE(0:11))
   ALLOCATE(EE1(-CEL2X:CEL2X),EE2(-CEL2X:CEL2X))

   ! PRECALCULATE PHASE FACTOR
   DO I=-2*(CEL1X+CEL2X)*KVC2,2*(CEL1X+CEL2X)*KVC2
    IF (KVC2 == 0) THEN
     PHASE(I)=DCMPLX(1.0D0,0.0D0)
    ELSE
     PHASE(I)=DCMPLX(DCOS(DFLOAT(I)/DFLOAT(KVC2)*PI),DSIN(DFLOAT(I)/DFLOAT(KVC2)*PI))
    ENDIF
   ENDDO

   ! DENSITY MATRIX
   WRITE(6,'(A)') '---------------------'
   WRITE(6,'(A)') 'CELL  MAXIMUM DENSITY'
   DO Q1=0,CEL1X
    MAXR=0.0D0
    DO I=1,NCGS
     DO J=1,NCGS
      IF (DABS(P_C(J,I,Q1,0,0)) > MAXR) MAXR=DABS(P_C(J,I,Q1,0,0))
     ENDDO
    ENDDO
    WRITE(6,'(I3,F18.13)') Q1,MAXR
   ENDDO
   WRITE(6,'(A)') '---------------------'

   ! T1 AMPLITUDES
   WRITE(6,'(A)') '---------------------------------------'
   WRITE(6,'(A)') 'CELL  MAXIMUM REAL T1   MAXIMUM IMAG T1'
   REWIND(T1INFILE)
   READ(T1INFILE) TAI1
   DO Q1=-CEL1X,CEL1X
    MAXR=0.0D0
    MAXI=0.0D0
    DO I=1,NCGS
     DO J=1,NCGS
      T1_C(I,J,Q1)=DCMPLX(0.0D0,0.0D0)
      DO KI=-KVC2,MAX(0,KVC2-1)
       DO MOI=ICORE+1,IOCC
        DO MOA=IOCC+1,IALL(KI,0,0)-IVIRTCORE
         T1_C(I,J,Q1)=T1_C(I,J,Q1)+DCONJG(CO(I,MOI,KI*NMOD,0,0))*CO(J,MOA,KI*NMOD,0,0)*TAI1(MOA,MOI,KI)*PHASE(KI*Q1)
        ENDDO
       ENDDO
      ENDDO
      T1_C(I,J,Q1)=T1_C(I,J,Q1)/DFLOAT(MAX(1,2*KVC2))
      IF (DABS(DREAL(T1_C(I,J,Q1))) > MAXR) MAXR=DABS(DREAL(T1_C(I,J,Q1)))
      IF (DABS(DIMAG(T1_C(I,J,Q1))) > MAXI) MAXI=DABS(DIMAG(T1_C(I,J,Q1)))
     ENDDO
    ENDDO
    WRITE(6,'(I3,2F18.13)') Q1,MAXR,MAXI
   ENDDO
   WRITE(6,'(A)') '---------------------------------------'

   ! CHECK T1 ENERGY CONTRIBUTION
   E1=0.0D0
   EE1=0.0D0
   REWIND(31)
   ICOUNT=0
   DO
    READ(31,IOSTAT=EOF) ICASHE1X,ICASHE1Y,ICASHE1Z,ICASHE2,DCASHE
    IF (EOF /= 0) EXIT
    DO ICASHECOUNT=1,CASHESIZE
     IF (ICASHE1X(ICASHECOUNT) == -1) EXIT
     ICOUNT=ICOUNT+1
     P=0
     Q=0
     R=0
     I=0
     J=0
     K=0
     L=0
     CALL MVBITS(ICASHE1X(ICASHECOUNT),16,8,P,0)
     CALL MVBITS(ICASHE1X(ICASHECOUNT), 8,8,Q,0)
     CALL MVBITS(ICASHE1X(ICASHECOUNT), 0,8,R,0)
     CALL MVBITS(ICASHE2(ICASHECOUNT),24,8,I,0)
     CALL MVBITS(ICASHE2(ICASHECOUNT),16,8,J,0)
     CALL MVBITS(ICASHE2(ICASHECOUNT), 8,8,K,0)
     CALL MVBITS(ICASHE2(ICASHECOUNT), 0,8,L,0)
     Q1=P-CEL1X
     Q2=Q-CEL1X
     Q3=R-CEL2X
     IF ((Q1 < -REDUCED_CEL1X).OR.(Q1 > REDUCED_CEL1X) &
     .OR.(Q2 < -REDUCED_CEL1X).OR.(Q2 > REDUCED_CEL1X) &
     .OR.(Q3 < 0).OR.(Q3 > CEL2X)) &
     CALL PABORT('INTEGRAL FILE HAS BEEN DEGRADED')
     E1=E1+2.0D0*DCASHE(ICASHECOUNT)*T1_C(I,J,Q1)*T1_C(K,L,Q2)
     EE1(Q3)=EE1(Q3)+2.0D0*DCASHE(ICASHECOUNT)*T1_C(I,J,Q1)*T1_C(K,L,Q2)
     IF ((Q3+Q2 >= -CEL1X).AND.(Q3+Q2 <= CEL1X).AND.(Q1-Q3 >= -CEL1X).AND.(Q1-Q3 <= CEL1X)) &
     E1=E1-DCASHE(ICASHECOUNT)*T1_C(I,L,Q3+Q2)*T1_C(K,J,Q1-Q3)
     EE1(Q3)=EE1(Q3)-DCASHE(ICASHECOUNT)*T1_C(I,L,Q3+Q2)*T1_C(K,J,Q1-Q3)
     IF (Q3 /= 0) E1=E1+2.0D0*DCASHE(ICASHECOUNT)*T1_C(I,J,Q1)*T1_C(K,L,Q2)
     IF ((Q3 /= 0).AND.(Q3+Q2 >= -CEL1X).AND.(Q3+Q2 <= CEL1X).AND.(Q1-Q3 >= -CEL1X).AND.(Q1-Q3 <= CEL1X)) &
     E1=E1-DCASHE(ICASHECOUNT)*T1_C(I,L,Q3+Q2)*T1_C(K,J,Q1-Q3)
     EE1(Q3)=EE1(Q3)-DCASHE(ICASHECOUNT)*T1_C(I,L,Q3+Q2)*T1_C(K,J,Q1-Q3)
    ENDDO
   ENDDO
   DO Q1=-CEL1X,CEL1X
    DO J=1,NCGS
     DO I=1,NCGS
      E1=E1+2.0D0*F_C(I,J,Q1,0,0)*T1_C(I,J,Q1)
     ENDDO
    ENDDO
   ENDDO
   WRITE(6,'(A,F20.15,A)') 'E(SINGLES)         = ',E1,' HARTREE'
   DO Q3=-CEL2X,CEL2X
    WRITE(6,'(I3,A,F20.15,A)') Q3,'E(SINGLES)         = ',EE1(Q3),' HARTREE'
   ENDDO

   ! T2 AMPLITUDES
   E2=0.0D0
   EE2=0.0D0
   NE=0
   MAXSR=0.0D0
   MAXSI=0.0D0
   MAXT=0.0D0
   WRITE(6,'(A)') 'REPULSION-TYPE'
   WRITE(6,'(A)') '---------------------------------------'
   WRITE(6,'(A)') 'CELL  MAXIMUM REAL T2   MAXIMUM IMAG T2'
   DO Q3=-CEL2X,CEL2X
    MAXR=0.0D0
    MAXI=0.0D0
    DO Q2=-CEL1X,CEL1X
     CNNNMK(1:NCGS,1:NCGS,1:NCGS,IOCC+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1))=DCMPLX(0.0D0,0.0D0)
     DO KB=-KVC2,MAX(0,KVC2-1)
      CNNMMK(1:NCGS,1:NCGS,IOCC+1:IALLMAX-IVIRTCORE,IOCC+1:IALLMAX-IVIRTCORE,-KVC2:MAX(0,KVC2-1))=DCMPLX(0.0D0,0.0D0)
      DO KJ=-KVC2,MAX(0,KVC2-1)
       DO KI=-KVC2,MAX(0,KVC2-1)
        KA=KI-KB+KJ
        IF (KA >= KVC2) KA=KA-2*KVC2
        IF (KA < -KVC2) KA=KA+2*KVC2
        READ(T2INFILE,REC=((KJ+KVC2)*(2*KVC2)+(KI+KVC2))*(2*KVC2)+(KB+KVC2)+1) TABIJ1
        DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
         DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
          DO K=1,NCGS
           DO I=1,NCGS
            DO MOJ=ICORE+1,IOCC
             DO MOI=ICORE+1,IOCC
              CNNMMK(I,K,MOA,MOB,KA)=CNNMMK(I,K,MOA,MOB,KA) &
              +DCONJG(CO(I,MOI,KI*NMOD,0,0)*CO(K,MOJ,KJ*NMOD,0,0))*TABIJ1(MOA,MOB,MOI,MOJ)*PHASE(-KJ*Q3) 
             ENDDO
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
      DO KA=-KVC2,MAX(0,KVC2-1)
       DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
        DO MOB=IOCC+1,IALL(KB,0,0)-IVIRTCORE
         DO L=1,NCGS
          DO K=1,NCGS
           DO I=1,NCGS
            CNNNMK(I,K,L,MOA,KA)=CNNNMK(I,K,L,MOA,KA)+CO(L,MOB,KB*NMOD,0,0)*CNNMMK(I,K,MOA,MOB,KA)*PHASE(KB*(Q3+Q2))
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
     DO Q1=-CEL1X,CEL1X
      DO L=1,NCGS
       DO K=1,NCGS
        DO J=1,NCGS
         DO I=1,NCGS
          CNNNNK(I,J,K,L,Q1)=DCMPLX(0.0D0,0.0D0)
          DO KA=-KVC2,MAX(0,KVC2-1)
           DO MOA=IOCC+1,IALL(KA,0,0)-IVIRTCORE
            CNNNNK(I,J,K,L,Q1)=CNNNNK(I,J,K,L,Q1)+CO(J,MOA,KA*NMOD,0,0)*CNNNMK(I,K,L,MOA,KA)*PHASE(KA*Q1)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
     DO Q1=-CEL1X,CEL1X
      DO L=1,NCGS
       DO K=1,NCGS
        DO J=1,NCGS
         DO I=1,NCGS
          CNNNNK(I,J,K,L,Q1)=CNNNNK(I,J,K,L,Q1)/DFLOAT(MAX(1,2*KVC2)**2)
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > MAXR) MAXR=DABS(DREAL(CNNNNK(I,J,K,L,Q1)))
          IF (DABS(DIMAG(CNNNNK(I,J,K,L,Q1))) > MAXI) MAXI=DABS(DIMAG(CNNNNK(I,J,K,L,Q1)))
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > MAXSR(Q1)) MAXSR(Q1)=DABS(DREAL(CNNNNK(I,J,K,L,Q1)))
          IF (DABS(DIMAG(CNNNNK(I,J,K,L,Q1))) > MAXSI(Q1)) MAXSI(Q1)=DABS(DIMAG(CNNNNK(I,J,K,L,Q1)))
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > 1.0D0) NE(0)=NE(0)+1
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > 1.0D-1) NE(1)=NE(1)+1
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > 1.0D-2) NE(2)=NE(2)+1
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > 1.0D-3) NE(3)=NE(3)+1
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > 1.0D-4) NE(4)=NE(4)+1
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > 1.0D-5) NE(5)=NE(5)+1
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > 1.0D-6) NE(6)=NE(6)+1
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > 1.0D-7) NE(7)=NE(7)+1
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > 1.0D-8) NE(8)=NE(8)+1
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > 1.0D-9) NE(9)=NE(9)+1
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) > 1.0D-10) NE(10)=NE(10)+1
          IF (DABS(DREAL(CNNNNK(I,J,K,L,Q1))) >= 0.0D0) NE(11)=NE(11)+1
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
     ! CHECK T2 ENERGY CONTRIBUTION
     REWIND(31)
     ICOUNT=0
     DO
      READ(31,IOSTAT=EOF) ICASHE1X,ICASHE1Y,ICASHE1Z,ICASHE2,DCASHE
      IF (EOF /= 0) EXIT
      DO ICASHECOUNT=1,CASHESIZE
       IF (ICASHE1X(ICASHECOUNT) == -1) EXIT
       ICOUNT=ICOUNT+1
       P=0
       Q=0
       R=0
       I=0
       J=0
       K=0
       L=0
       CALL MVBITS(ICASHE1X(ICASHECOUNT),16,8,P,0)
       CALL MVBITS(ICASHE1X(ICASHECOUNT), 8,8,Q,0)
       CALL MVBITS(ICASHE1X(ICASHECOUNT), 0,8,R,0)
       CALL MVBITS(ICASHE2(ICASHECOUNT),24,8,I,0)
       CALL MVBITS(ICASHE2(ICASHECOUNT),16,8,J,0)
       CALL MVBITS(ICASHE2(ICASHECOUNT), 8,8,K,0)
       CALL MVBITS(ICASHE2(ICASHECOUNT), 0,8,L,0)
       P1=P-CEL1X
       P2=Q-CEL1X
       P3=R-CEL2X
       IF ((P1 < -REDUCED_CEL1X).OR.(P1 > REDUCED_CEL1X) &
       .OR.(P2 < -REDUCED_CEL1X).OR.(P2 > REDUCED_CEL1X) &
       .OR.(P3 < 0).OR.(P3 > CEL2X)) &
       CALL PABORT('INTEGRAL FILE HAS BEEN DEGRADED')
       IF (DABS(DCASHE(ICASHECOUNT)) > MAXT(P3)) MAXT(P3)=DABS(DCASHE(ICASHECOUNT))
       IF ((Q2 == P2).AND.(Q3 == P3)) E2=E2+2.0D0*DCASHE(ICASHECOUNT)*CNNNNK(I,J,K,L,P1)
       IF ((Q2 == P2).AND.(Q3 == P3)) EE2(Q3)=EE2(Q3)+2.0D0*DCASHE(ICASHECOUNT)*CNNNNK(I,J,K,L,P1)
       IF ((Q2 == P1-P3).AND.(Q3 == P3).AND.(P3+P2 >= -CEL1X).AND.(P3+P2 <= CEL1X)) &
       E2=E2-DCASHE(ICASHECOUNT)*CNNNNK(I,L,K,J,P3+P2)
       IF ((Q2 == P1-P3).AND.(Q3 == P3).AND.(P3+P2 >= -CEL1X).AND.(P3+P2 <= CEL1X)) &
       EE2(Q3)=EE2(Q3)-DCASHE(ICASHECOUNT)*CNNNNK(I,L,K,J,P3+P2)
       IF ((Q3 /= 0).AND.(Q3 == -P3).AND.(Q2 == P1)) E2=E2+2.0D0*DCASHE(ICASHECOUNT)*CNNNNK(K,L,I,J,P2)
       IF ((Q3 /= 0).AND.(Q3 == -P3).AND.(Q2 == P1)) EE2(-Q3)=EE2(-Q3)+2.0D0*DCASHE(ICASHECOUNT)*CNNNNK(K,L,I,J,P2)
       IF ((Q3 /= 0).AND.(Q3 == -P3).AND.(Q2 == P3+P2).AND.(P1-P3 >= -CEL1X).AND.(P1-P3 <= CEL1X)) &
       E2=E2-DCASHE(ICASHECOUNT)*CNNNNK(K,J,I,L,P1-P3)
       IF ((Q3 /= 0).AND.(Q3 == -P3).AND.(Q2 == P3+P2).AND.(P1-P3 >= -CEL1X).AND.(P1-P3 <= CEL1X)) &
       EE2(-Q3)=EE2(-Q3)-DCASHE(ICASHECOUNT)*CNNNNK(K,J,I,L,P1-P3)
      ENDDO
     ENDDO
    ENDDO
    WRITE(6,'(I3,2F18.13)') Q3,MAXR,MAXI
   ENDDO
   WRITE(6,'(A)') '---------------------------------------'
   WRITE(6,'(A)') 'OVERLAP TYPE'
   WRITE(6,'(A)') '---------------------------------------'
   WRITE(6,'(A)') 'CELL  MAXIMUM REAL T2   MAXIMUM IMAG T2'
   DO Q1=-CEL1X,CEL1X
    WRITE(6,'(I3,2F18.13)') Q1,MAXSR(Q1),MAXSI(Q1)
   ENDDO 
   WRITE(6,'(A)') '---------------------------------------'
   WRITE(6,'(A,F20.15,A)') 'E(DOUBLES)         = ',E2,' HARTREE'
   DO Q3=-CEL2X,CEL2X
    WRITE(6,'(I3,A,F20.15,A)') Q3,'E(DOUBLES)         = ',EE2(Q3),' HARTREE'
   ENDDO
   WRITE(6,'(A)') 'AO TWO-ELECTRON REPULSION INTEGRALS'
   WRITE(6,'(A)') '---------------------'
   WRITE(6,'(A)') 'CELL          MAXIMUM'
   DO Q3=0,CEL2X
    WRITE(6,'(I3,F18.13)') Q3,MAXT(Q3)
   ENDDO 
   WRITE(6,'(A)') '---------------------'

   IF (NE(11) /= NCGS**4*(2*CEL1X+1)**2*(2*CEL2X+1)) CALL PABORT('BUG TRAP')
   WRITE(6,'(A)') 'AO-T2-AMPLITUDE SPECTRUM'
   DO I=0,10
    WRITE(6,'(A,I2,A,F8.3,A)') 'GREATER THAN 1.0D-',I,' = ', &
    DFLOAT(NE(I))/DFLOAT(NCGS**4*(2*CEL1X+1)**2*(2*CEL2X+1))*100.0D0,'%'
   ENDDO
   WRITE(6,'(A,F8.3,A)') 'GREATER THAN 0.0D-00 = ', &
   DFLOAT(NE(11))/DFLOAT(NCGS**4*(2*CEL1X+1)**2*(2*CEL2X+1))*100.0D0,'%'

   DEALLOCATE(ICASHE1X,ICASHE1Y,ICASHE1Z,ICASHE2,DCASHE)
   DEALLOCATE(PHASE,TABIJ1,TAI1,T1_C,CNNMMK,CNNNMK,CNNNNK,MAXSR,MAXSI,MAXT,NE)
   RETURN

END SUBROUTINE
