SUBROUTINE OVERLAP_KINETIC(IGRAD)
! CALCULATE OVERLAP AND KINETIC ENERGY INTEGRALS
! OVERLAP AND KINETIC ENERGY INTEGRAL BETWEEN I(0) AND J(Q) ARE
! STORED IN S_P(I,J,Q), S_C(I,J,Q), AND T_C(I,J,Q).

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: IGRAD
   DOUBLE PRECISION,ALLOCATABLE :: S1(:,:,:,:,:,:)
   DOUBLE PRECISION,ALLOCATABLE :: T1(:,:,:,:,:,:)
   DOUBLE PRECISION,ALLOCATABLE :: T_P(:,:,:,:,:)
   DOUBLE PRECISION,ALLOCATABLE :: W1(:,:),W2(:,:),W3(:,:)
   DOUBLE PRECISION :: PISUB
   DOUBLE PRECISION :: QX,QY,QZ
   DOUBLE PRECISION :: COMZ1,COMZ2,GZI,FS,FT
   DOUBLE PRECISION :: AX,AY,AZ,BX,BY,BZ,PX,PY,PZ,R
   DOUBLE PRECISION :: MAXS
   DOUBLE PRECISION :: ANGLE,CS,SN
   INTEGER :: KX,KY,KZ,I,J,II,JJ,IATM,JATM
   INTEGER :: I1,I2,I3,J1,J2,J3
   
   ALLOCATE(S1(0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1))
   ALLOCATE(T1(0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1))
   ALLOCATE(W1(NPGS,NPGS),W2(NCGS,NPGS),W3(NCGS,NCGS))
   ALLOCATE(T_P(NPGS,NPGS,-CEL1X:CEL1X,-CEL1Y:CEL1Y,-CEL1Z:CEL1Z))

   PISUB=DSQRT(PI**3)
   SELECT CASE(IGRAD)
    CASE(0)
     IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE OVERLAP AND KINETIC ENERGY INTEGRALS'
    CASE(1)
     IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE OVERLAP AND KINETIC ENERGY INTEGRAL DERIVATIVES'
    CASE DEFAULT
     CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
   END SELECT
   
   S_P=0.0D0
   S_C=0.0D0
   T_P=0.0D0
   T_C=0.0D0
   
   DO KX=-CEL1X,CEL1X
   DO KY=-CEL1Y,CEL1Y
   DO KZ=-CEL1Z,CEL1Z
    QX=DFLOAT(KX)*PERIODX
    QY=DFLOAT(KY)*PERIODY
    QZ=DFLOAT(KZ)*PERIODZ
    ANGLE=DFLOAT(KX)*HELIX
    CS=DCOS(ANGLE)
    SN=DSIN(ANGLE)
    DO I=1,NPSHELL
     II=P_SHELL(I,0,0,0)
     IATM=PGS_ATOM(II)
     AX=PGSX(II)
     AY=PGSY(II)
     AZ=PGSZ(II)
     DO J=1,NPSHELL
      JJ=P_SHELL(J,0,0,0)
      JATM=PGS_ATOM(JJ)
      BX=PGSX(JJ)+QX
      BY=PGSY(JJ)*CS-PGSZ(JJ)*SN+QY
      BZ=PGSY(JJ)*SN+PGSZ(JJ)*CS+QZ
      R=(AX-BX)**2+(AY-BY)**2+(AZ-BZ)**2
      COMZ1=ZT(II)+ZT(JJ)
      COMZ2=DSQRT(COMZ1**3)
      GZI=ZT(II)*ZT(JJ)/COMZ1
      
      S1(0,0,0,0,0,0)=PISUB/COMZ2*DEXP(-GZI*R)
      T1(0,0,0,0,0,0)=GZI*(3.0D0-2.0D0*GZI*R)*S1(0,0,0,0,0,0)
      
      PX=(AX*ZT(II)+BX*ZT(JJ))/COMZ1
      PY=(AY*ZT(II)+BY*ZT(JJ))/COMZ1
      PZ=(AZ*ZT(II)+BZ*ZT(JJ))/COMZ1
      
      DO I1=0,P_SHELL_ANG(I)+IGRAD
       DO I2=0,P_SHELL_ANG(I)+IGRAD-I1
        DO I3=0,P_SHELL_ANG(I)+IGRAD-I1-I2
         DO J1=0,P_SHELL_ANG(J)+IGRAD
          DO J2=0,P_SHELL_ANG(J)+IGRAD-J1
           DO J3=0,P_SHELL_ANG(J)+IGRAD-J1-J2
            IF ((I1+I2+I3+J1+J2+J3 == 0).OR. &
            (I1+I2+I3+J1+J2+J3 > P_SHELL_ANG(I)+P_SHELL_ANG(J)+IGRAD)) CYCLE
            ! OVERLAP INTEGRALS (OBARA-SAIKA RECURSION FORMULAS)
            IF (I1 >= 1) THEN
             S1(I1,I2,I3,J1,J2,J3)=(PX-AX)*S1(I1-1,I2,I3,J1,J2,J3)
             IF (I1 >= 2) S1(I1,I2,I3,J1,J2,J3)=S1(I1,I2,I3,J1,J2,J3)+0.5D0*DFLOAT(I1-1)/COMZ1*S1(I1-2,I2,I3,J1,J2,J3)
             IF (J1 >= 1) S1(I1,I2,I3,J1,J2,J3)=S1(I1,I2,I3,J1,J2,J3)+0.5D0*DFLOAT(J1)/COMZ1*S1(I1-1,I2,I3,J1-1,J2,J3)
            ELSE IF (I2 >= 1) THEN
             S1(I1,I2,I3,J1,J2,J3)=(PY-AY)*S1(I1,I2-1,I3,J1,J2,J3)
             IF (I2 >= 2) S1(I1,I2,I3,J1,J2,J3)=S1(I1,I2,I3,J1,J2,J3)+0.5D0*DFLOAT(I2-1)/COMZ1*S1(I1,I2-2,I3,J1,J2,J3)
             IF (J2 >= 1) S1(I1,I2,I3,J1,J2,J3)=S1(I1,I2,I3,J1,J2,J3)+0.5D0*DFLOAT(J2)/COMZ1*S1(I1,I2-1,I3,J1,J2-1,J3)
            ELSE IF (I3 >= 1) THEN
             S1(I1,I2,I3,J1,J2,J3)=(PZ-AZ)*S1(I1,I2,I3-1,J1,J2,J3)
             IF (I3 >= 2) S1(I1,I2,I3,J1,J2,J3)=S1(I1,I2,I3,J1,J2,J3)+0.5D0*DFLOAT(I3-1)/COMZ1*S1(I1,I2,I3-2,J1,J2,J3)
             IF (J3 >= 1) S1(I1,I2,I3,J1,J2,J3)=S1(I1,I2,I3,J1,J2,J3)+0.5D0*DFLOAT(J3)/COMZ1*S1(I1,I2,I3-1,J1,J2,J3-1)
            ELSE IF (J1 >= 1) THEN
             S1(I1,I2,I3,J1,J2,J3)=(PX-BX)*S1(I1,I2,I3,J1-1,J2,J3)
             IF (J1 >= 2) S1(I1,I2,I3,J1,J2,J3)=S1(I1,I2,I3,J1,J2,J3)+0.5D0*DFLOAT(J1-1)/COMZ1*S1(I1,I2,I3,J1-2,J2,J3)
            ELSE IF (J2 >= 1) THEN
             S1(I1,I2,I3,J1,J2,J3)=(PY-BY)*S1(I1,I2,I3,J1,J2-1,J3)
             IF (J2 >= 2) S1(I1,I2,I3,J1,J2,J3)=S1(I1,I2,I3,J1,J2,J3)+0.5D0*DFLOAT(J2-1)/COMZ1*S1(I1,I2,I3,J1,J2-2,J3)
            ELSE IF (J3 >= 1) THEN
             S1(I1,I2,I3,J1,J2,J3)=(PZ-BZ)*S1(I1,I2,I3,J1,J2,J3-1)
             IF (J3 >= 2) S1(I1,I2,I3,J1,J2,J3)=S1(I1,I2,I3,J1,J2,J3)+0.5D0*DFLOAT(J3-1)/COMZ1*S1(I1,I2,I3,J1,J2,J3-2)
            ENDIF   
            ! KINETIC ENERGY INTEGRALS (OBARA-SAIKA RECURSION FORMULAS)
            IF (I1 >= 1) THEN
             T1(I1,I2,I3,J1,J2,J3)=2.0D0*GZI*S1(I1,I2,I3,J1,J2,J3)+(PX-AX)*T1(I1-1,I2,I3,J1,J2,J3)
             IF (I1 >= 2) T1(I1,I2,I3,J1,J2,J3)=T1(I1,I2,I3,J1,J2,J3)+ &
             0.5D0*DFLOAT(I1-1)/COMZ1*T1(I1-2,I2,I3,J1,J2,J3)-DFLOAT(I1-1)*GZI/ZT(II)*S1(I1-2,I2,I3,J1,J2,J3)
             IF (J1 >= 1) T1(I1,I2,I3,J1,J2,J3)=T1(I1,I2,I3,J1,J2,J3)+0.5D0*DFLOAT(J1)/COMZ1*T1(I1-1,I2,I3,J1-1,J2,J3)
            ELSE IF (I2 >= 1) THEN
             T1(I1,I2,I3,J1,J2,J3)=2.0D0*GZI*S1(I1,I2,I3,J1,J2,J3)+(PY-AY)*T1(I1,I2-1,I3,J1,J2,J3)
             IF (I2 >= 2) T1(I1,I2,I3,J1,J2,J3)=T1(I1,I2,I3,J1,J2,J3)+ &
             0.5D0*DFLOAT(I2-1)/COMZ1*T1(I1,I2-2,I3,J1,J2,J3)-DFLOAT(I2-1)*GZI/ZT(II)*S1(I1,I2-2,I3,J1,J2,J3)
             IF (J2 >= 1) T1(I1,I2,I3,J1,J2,J3)=T1(I1,I2,I3,J1,J2,J3)+0.5D0*DFLOAT(J2)/COMZ1*T1(I1,I2-1,I3,J1,J2-1,J3)
            ELSE IF (I3 >= 1) THEN
             T1(I1,I2,I3,J1,J2,J3)=2.0D0*GZI*S1(I1,I2,I3,J1,J2,J3)+(PZ-AZ)*T1(I1,I2,I3-1,J1,J2,J3)
             IF (I3 >= 2) T1(I1,I2,I3,J1,J2,J3)=T1(I1,I2,I3,J1,J2,J3)+ &
             0.5D0*DFLOAT(I3-1)/COMZ1*T1(I1,I2,I3-2,J1,J2,J3)-DFLOAT(I3-1)*GZI/ZT(II)*S1(I1,I2,I3-2,J1,J2,J3)
             IF (J3 >= 1) T1(I1,I2,I3,J1,J2,J3)=T1(I1,I2,I3,J1,J2,J3)+0.5D0*DFLOAT(J3)/COMZ1*T1(I1,I2,I3-1,J1,J2,J3-1)
            ELSE IF (J1 >= 1) THEN
             T1(I1,I2,I3,J1,J2,J3)=2.0D0*GZI*S1(I1,I2,I3,J1,J2,J3)+(PX-BX)*T1(I1,I2,I3,J1-1,J2,J3)
             IF (J1 >= 2) T1(I1,I2,I3,J1,J2,J3)=T1(I1,I2,I3,J1,J2,J3)+ &
             0.5D0*DFLOAT(J1-1)/COMZ1*T1(I1,I2,I3,J1-2,J2,J3)-DFLOAT(J1-1)*GZI/ZT(JJ)*S1(I1,I2,I3,J1-2,J2,J3)
            ELSE IF (J2 >= 1) THEN
             T1(I1,I2,I3,J1,J2,J3)=2.0D0*GZI*S1(I1,I2,I3,J1,J2,J3)+(PY-BY)*T1(I1,I2,I3,J1,J2-1,J3)
             IF (J2 >= 2) T1(I1,I2,I3,J1,J2,J3)=T1(I1,I2,I3,J1,J2,J3)+ &
             0.5D0*DFLOAT(J2-1)/COMZ1*T1(I1,I2,I3,J1,J2-2,J3)-DFLOAT(J2-1)*GZI/ZT(JJ)*S1(I1,I2,I3,J1,J2-2,J3)
            ELSE IF (J3 >= 1) THEN
             T1(I1,I2,I3,J1,J2,J3)=2.0D0*GZI*S1(I1,I2,I3,J1,J2,J3)+(PZ-BZ)*T1(I1,I2,I3,J1,J2,J3-1)
             IF (J3 >= 2) T1(I1,I2,I3,J1,J2,J3)=T1(I1,I2,I3,J1,J2,J3)+ &
             0.5D0*DFLOAT(J3-1)/COMZ1*T1(I1,I2,I3,J1,J2,J3-2)-DFLOAT(J3-1)*GZI/ZT(JJ)*S1(I1,I2,I3,J1,J2,J3-2)
            ENDIF
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
      DO I1=0,P_SHELL_ANG(I)
       DO I2=0,P_SHELL_ANG(I)-I1
        DO I3=0,P_SHELL_ANG(I)-I1-I2
         DO J1=0,P_SHELL_ANG(J)
          DO J2=0,P_SHELL_ANG(J)-J1
           DO J3=0,P_SHELL_ANG(J)-J1-J2
            S_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ)=S1(I1,I2,I3,J1,J2,J3)
            T_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ)=T1(I1,I2,I3,J1,J2,J3)
            IF (IGRAD == 1) THEN
             IF (HELIX == 0.0D0) THEN
              FS=2.0D0*ZT(II)*S1(I1+1,I2,I3,J1,J2,J3)
              FT=2.0D0*ZT(II)*T1(I1+1,I2,I3,J1,J2,J3)
              IF (I1 >= 1) THEN
               FS=FS-DFLOAT(I1)*S1(I1-1,I2,I3,J1,J2,J3)
               FT=FT-DFLOAT(I1)*T1(I1-1,I2,I3,J1,J2,J3)
              ENDIF
              FORCEX(IATM)=FORCEX(IATM)-FS*W_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ) &
              +FT*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ)
              FS=2.0D0*ZT(II)*S1(I1,I2+1,I3,J1,J2,J3)
              FT=2.0D0*ZT(II)*T1(I1,I2+1,I3,J1,J2,J3)
              IF (I2 >= 1) THEN
               FS=FS-DFLOAT(I2)*S1(I1,I2-1,I3,J1,J2,J3)
               FT=FT-DFLOAT(I2)*T1(I1,I2-1,I3,J1,J2,J3)
              ENDIF
              FORCEY(IATM)=FORCEY(IATM)-FS*W_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ) &
              +FT*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ)
              FS=2.0D0*ZT(II)*S1(I1,I2,I3+1,J1,J2,J3)
              FT=2.0D0*ZT(II)*T1(I1,I2,I3+1,J1,J2,J3)
              IF (I3 >= 1) THEN
               FS=FS-DFLOAT(I3)*S1(I1,I2,I3-1,J1,J2,J3)
               FT=FT-DFLOAT(I3)*T1(I1,I2,I3-1,J1,J2,J3)
              ENDIF
              FORCEZ(IATM)=FORCEZ(IATM)-FS*W_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ) &
              +FT*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ)
              FS=2.0D0*ZT(JJ)*S1(I1,I2,I3,J1+1,J2,J3)
              FT=2.0D0*ZT(JJ)*T1(I1,I2,I3,J1+1,J2,J3)
              IF (J1 >= 1) THEN
               FS=FS-DFLOAT(J1)*S1(I1,I2,I3,J1-1,J2,J3)
               FT=FT-DFLOAT(J1)*T1(I1,I2,I3,J1-1,J2,J3)
              ENDIF
              FORCEX(JATM)=FORCEX(JATM)-FS*W_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ) &
              +FT*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ)
              FORCETX=FORCETX+DFLOAT(KX)*(-FS*W_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ) &
              +FT*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ))
              FS=2.0D0*ZT(JJ)*S1(I1,I2,I3,J1,J2+1,J3)
              FT=2.0D0*ZT(JJ)*T1(I1,I2,I3,J1,J2+1,J3)
              IF (J2 >= 1) THEN
               FS=FS-DFLOAT(J2)*S1(I1,I2,I3,J1,J2-1,J3)
               FT=FT-DFLOAT(J2)*T1(I1,I2,I3,J1,J2-1,J3)
              ENDIF
              FORCEY(JATM)=FORCEY(JATM)-FS*W_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ) &
              +FT*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ)
              FORCETY=FORCETY+DFLOAT(KY)*(-FS*W_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ) &
              +FT*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ))
              FS=2.0D0*ZT(JJ)*S1(I1,I2,I3,J1,J2,J3+1)
              FT=2.0D0*ZT(JJ)*T1(I1,I2,I3,J1,J2,J3+1)
              IF (J3 >= 1) THEN
               FS=FS-DFLOAT(J3)*S1(I1,I2,I3,J1,J2,J3-1)
               FT=FT-DFLOAT(J3)*T1(I1,I2,I3,J1,J2,J3-1)
              ENDIF
              FORCEZ(JATM)=FORCEZ(JATM)-FS*W_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ) &
              +FT*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ)
              FORCETZ=FORCETZ+DFLOAT(KZ)*(-FS*W_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ) &
              +FT*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ))
             ELSE ! --- HELIX
              FS=2.0D0*ZT(II)*S1(I1+1,I2,I3,J1,J2,J3)
              FT=2.0D0*ZT(II)*T1(I1+1,I2,I3,J1,J2,J3)
              IF (I1 >= 1) THEN
               FS=FS-DFLOAT(I1)*S1(I1-1,I2,I3,J1,J2,J3)
               FT=FT-DFLOAT(I1)*T1(I1-1,I2,I3,J1,J2,J3)
              ENDIF
              FORCEX(IATM)=FORCEX(IATM)-FS*W_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX) &
              +FT*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
              FS=2.0D0*ZT(II)*S1(I1,I2+1,I3,J1,J2,J3)
              FT=2.0D0*ZT(II)*T1(I1,I2+1,I3,J1,J2,J3)
              IF (I2 >= 1) THEN
               FS=FS-DFLOAT(I2)*S1(I1,I2-1,I3,J1,J2,J3)
               FT=FT-DFLOAT(I2)*T1(I1,I2-1,I3,J1,J2,J3)
              ENDIF
              FORCEY(IATM)=FORCEY(IATM)-FS*W_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX) &
              +FT*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
              FS=2.0D0*ZT(II)*S1(I1,I2,I3+1,J1,J2,J3)
              FT=2.0D0*ZT(II)*T1(I1,I2,I3+1,J1,J2,J3)
              IF (I3 >= 1) THEN
               FS=FS-DFLOAT(I3)*S1(I1,I2,I3-1,J1,J2,J3)
               FT=FT-DFLOAT(I3)*T1(I1,I2,I3-1,J1,J2,J3)
              ENDIF
              FORCEZ(IATM)=FORCEZ(IATM)-FS*W_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX) &
              +FT*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
              FS=2.0D0*ZT(JJ)*S1(I1,I2,I3,J1+1,J2,J3)
              FT=2.0D0*ZT(JJ)*T1(I1,I2,I3,J1+1,J2,J3)
              IF (J1 >= 1) THEN
               FS=FS-DFLOAT(J1)*S1(I1,I2,I3,J1-1,J2,J3)
               FT=FT-DFLOAT(J1)*T1(I1,I2,I3,J1-1,J2,J3)
              ENDIF
              FORCEX(JATM)=FORCEX(JATM)-FS*W_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX) &
              +FT*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
              FORCETX=FORCETX+DFLOAT(KX)*(-FS*W_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX) &
              +FT*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX))
              FS=2.0D0*ZT(JJ)*S1(I1,I2,I3,J1,J2+1,J3)
              FT=2.0D0*ZT(JJ)*T1(I1,I2,I3,J1,J2+1,J3)
              IF (J2 >= 1) THEN
               FS=FS-DFLOAT(J2)*S1(I1,I2,I3,J1,J2-1,J3)
               FT=FT-DFLOAT(J2)*T1(I1,I2,I3,J1,J2-1,J3)
              ENDIF
              FORCEY(JATM)=FORCEY(JATM)+CS*(-FS*W_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX) &
              +FT*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX))
              FORCEZ(JATM)=FORCEZ(JATM)-SN*(-FS*W_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX) &
              +FT*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX))
              FORCETA=FORCETA+DFLOAT(KX)*(-ATOMY(JATM)*SN-ATOMZ(JATM)*CS) &
              *(-FS*W_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX) &
              +FT*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX))
              FS=2.0D0*ZT(JJ)*S1(I1,I2,I3,J1,J2,J3+1)
              FT=2.0D0*ZT(JJ)*T1(I1,I2,I3,J1,J2,J3+1)
              IF (J3 >= 1) THEN
               FS=FS-DFLOAT(J3)*S1(I1,I2,I3,J1,J2,J3-1)
               FT=FT-DFLOAT(J3)*T1(I1,I2,I3,J1,J2,J3-1)
              ENDIF
              FORCEY(JATM)=FORCEY(JATM)+SN*(-FS*W_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX) &
              +FT*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX))
              FORCEZ(JATM)=FORCEZ(JATM)+CS*(-FS*W_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX) &
              +FT*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX))
              FORCETA=FORCETA+DFLOAT(KX)*(ATOMY(JATM)*CS-ATOMZ(JATM)*SN) &
              *(-FS*W_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX) &
              +FT*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX))
              ! BASIS ROTATION FORCE
              FORCETA=FORCETA+(-S1(I1,I2,I3,J1,J2,J3)*W_P_HELIX_THETA(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX) &
                               +T1(I1,I2,I3,J1,J2,J3)*P_P_HELIX_THETA(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX))
             ENDIF ! --- HELIX END
            ENDIF
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
    ! CONTRACTION
    W1=S_P(:,:,KX,KY,KZ)
    W2=MATMUL(CC,W1)
    W3=MATMUL(W2,TRANSPOSE(CC))
    S_C(:,:,KX,KY,KZ)=W3
    W1=T_P(:,:,KX,KY,KZ)
    W2=MATMUL(CC,W1)
    W3=MATMUL(W2,TRANSPOSE(CC))
    T_C(:,:,KX,KY,KZ)=W3
   ENDDO
   ENDDO
   ENDDO
   IF ((IOPTN(9) >= 2).AND.(HELIX /= 0.0D0)) THEN
    IF (MYID == 0) THEN
    WRITE(6,'(A)') 'OVERLAP MATRIX FOR CONTRACTED GAUSSIANS (BEFORE HELICAL CONVERSION)'
    CALL DUMP1(S_C,NCGS,CEL1X,CEL1Y,CEL1Z)
    WRITE(6,'(A)') 'KINETIC ENERGY INTEGRAL MATRIX FOR CONTRACTED GAUSSIANS (BEFORE HELICAL CONVERSION)'
    CALL DUMP1(T_C,NCGS,CEL1X,CEL1Y,CEL1Z)
    ENDIF
   ENDIF
! --- HELIX
   IF (HELIX /= 0.0D0) THEN
    DO KX=-CEL1X,CEL1X
     DO I=1,NCGS
      DO J=1,NCGS
       W3(I,J)=0.0D0
       DO JJ=1,NCGS
        W3(I,J)=W3(I,J)+C2H(J,JJ,KX)*S_C(I,JJ,KX,0,0)
       ENDDO
      ENDDO
     ENDDO
     DO I=1,NCGS
      DO J=1,NCGS
       S_C(I,J,KX,0,0)=W3(I,J)
      ENDDO
     ENDDO
     DO I=1,NCGS
      DO J=1,NCGS
       W3(I,J)=0.0D0
       DO JJ=1,NCGS
        W3(I,J)=W3(I,J)+C2H(J,JJ,KX)*T_C(I,JJ,KX,0,0)
       ENDDO
      ENDDO
     ENDDO
     DO I=1,NCGS
      DO J=1,NCGS
       T_C(I,J,KX,0,0)=W3(I,J)
      ENDDO
     ENDDO
    ENDDO
    ! BUG TRAP COMMENT OUT
    DO KX=1,CEL1X
     DO I=1,NCGS
      DO J=1,NCGS
       IF (DABS(S_C(I,J,KX,0,0)-S_C(J,I,-KX,0,0)) > 1.0D-11) THEN
        WRITE(6,'(3I3,2F20.15)') I,J,KX,S_C(I,J,KX,0,0),S_C(J,I,-KX,0,0)
        CALL PABORT('HELIX IS INCORRECT')
       ENDIF
      ENDDO
     ENDDO
    ENDDO
   ENDIF
! --- HELIX END
   IF (IGRAD == 0) THEN
    IF (IOPTN(9) >= 2) THEN
     IF (MYID == 0) THEN
     WRITE(6,'(A)') 'OVERLAP MATRIX FOR CONTRACTED GAUSSIANS'
     CALL DUMP1(S_C,NCGS,CEL1X,CEL1Y,CEL1Z)
     WRITE(6,'(A)') 'KINETIC ENERGY INTEGRAL MATRIX FOR CONTRACTED GAUSSIANS'
     CALL DUMP1(T_C,NCGS,CEL1X,CEL1Y,CEL1Z)
     ENDIF
    ENDIF
    REDUCED_CEL1X=0
    IF (MYID == 0) WRITE(6,'(A)') '---------------------'
    IF (MYID == 0) WRITE(6,'(A)') '  X   MAXIMUM OVERLAP'
    DO KX=0,CEL1X
     MAXS=0.0D0
     DO I=1,NCGS
      DO J=1,NCGS
       IF (DABS(S_C(J,I,KX,0,0)) > MAXS) MAXS=DABS(S_C(J,I,KX,0,0))
      ENDDO
     ENDDO
     IF (MYID == 0) WRITE(6,'(I3,F18.13)') KX,MAXS
     IF (MAXS > 1.0D-15) REDUCED_CEL1X=KX
    ENDDO
    IF (MYID == 0) WRITE(6,'(A)') '---------------------'
    IF ((CEL1X > 0).AND.(MAXS > 1.0D-15).AND.(MYID == 0)) CALL WARNING('CUTOFF1 PARAMETER MAY BE TOO SMALL')
    IF ((REDUCED_CEL1X < CEL1X).AND.(MYID == 0)) WRITE(6,'(A,I3,A,I3,A)') &
    'CUTOFF1 X PARAMETER WILL BE REDUCED FROM ',CEL1X,' TO ',REDUCED_CEL1X,' IN ERI COMPUTATION'
    REDUCED_CEL1Y=0
    IF (MYID == 0) WRITE(6,'(A)') '---------------------'
    IF (MYID == 0) WRITE(6,'(A)') '  Y   MAXIMUM OVERLAP'
    DO KY=0,CEL1Y
     MAXS=0.0D0
     DO I=1,NCGS
      DO J=1,NCGS
       IF (DABS(S_C(J,I,0,KY,0)) > MAXS) MAXS=DABS(S_C(J,I,0,KY,0))
      ENDDO
     ENDDO
     IF (MYID == 0) WRITE(6,'(I3,F18.13)') KY,MAXS
     IF (MAXS > 1.0D-15) REDUCED_CEL1Y=KY
    ENDDO
    IF (MYID == 0) WRITE(6,'(A)') '---------------------'
    IF ((CEL1Y> 0).AND.(MAXS > 1.0D-15).AND.(MYID == 0)) CALL WARNING('CUTOFF1 PARAMETER MAY BE TOO SMALL')
    IF ((REDUCED_CEL1Y < CEL1Y).AND.(MYID == 0)) WRITE(6,'(A,I3,A,I3,A)') &
    'CUTOFF1 Y PARAMETER WILL BE REDUCED FROM ',CEL1Y,' TO ',REDUCED_CEL1Y,' IN ERI COMPUTATION'
    REDUCED_CEL1Z=0
    IF (MYID == 0) WRITE(6,'(A)') '---------------------'
    IF (MYID == 0) WRITE(6,'(A)') '  Z   MAXIMUM OVERLAP'
    DO KZ=0,CEL1Z
     MAXS=0.0D0
     DO I=1,NCGS
      DO J=1,NCGS
       IF (DABS(S_C(J,I,0,0,KZ)) > MAXS) MAXS=DABS(S_C(J,I,0,0,KZ))
      ENDDO
     ENDDO
     IF (MYID == 0) WRITE(6,'(I3,F18.13)') KZ,MAXS
     IF (MAXS > 1.0D-15) REDUCED_CEL1Z=KZ
    ENDDO
    IF (MYID == 0) WRITE(6,'(A)') '---------------------'
    IF ((CEL1Z > 0).AND.(MAXS > 1.0D-15).AND.(MYID == 0)) CALL WARNING('CUTOFF1 PARAMETER MAY BE TOO SMALL')
    IF ((REDUCED_CEL1Z < CEL1Z).AND.(MYID == 0)) WRITE(6,'(A,I3,A,I3,A)') &
    'CUTOFF1 Z PARAMETER WILL BE REDUCED FROM ',CEL1Z,' TO ',REDUCED_CEL1Z,' IN ERI COMPUTATION'
   ENDIF
   IF ((IGRAD == 1).AND.(IOPTN(9) == 3).AND.(MYID == 0)) CALL DUMP2
   DEALLOCATE(S1,T1,W1,W2,W3,T_P)
   RETURN
END SUBROUTINE



SUBROUTINE NUCLEAR_ATTRACTION(IGRAD)
! CALCULATE NUCLEAR ATTRACTION ENERGY INTEGRALS
! IF IGRAD = 1, NON-HELMANN-FEYNMAN PART OF THE NUCLEAR ATTRACTION 
! ENERGY INTEGRAL DERIVATIVES ARE ALSO CALCULATED.

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   USE FMT

   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: IGRAD
   DOUBLE PRECISION,ALLOCATABLE :: N1(:,:,:,:,:,:,:),N2(:,:,:,:,:,:)
   DOUBLE PRECISION,ALLOCATABLE :: N_P_TMP(:,:,:,:,:),N_P(:,:,:,:,:)
   DOUBLE PRECISION,ALLOCATABLE :: W1(:,:),W2(:,:),W3(:,:)
   DOUBLE PRECISION,ALLOCATABLE :: FORCEX_TMP(:),FORCEY_TMP(:),FORCEZ_TMP(:)
   DOUBLE PRECISION :: FORCETX_TMP,FORCETY_TMP,FORCETZ_TMP,FORCETA_TMP
   DOUBLE PRECISION :: PISUB
   DOUBLE PRECISION :: Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z
   DOUBLE PRECISION :: COMZ,FN
   DOUBLE PRECISION :: AX,AY,AZ,BX,BY,BZ,CX,CY,CZ,PX,PY,PZ,E1,E2
   DOUBLE PRECISION :: T,H,F1(0:2*BASIS_LMAX+1),F2(0:2*BASIS_LMAX+1)
   DOUBLE PRECISION :: ANGLE,CS,SN,ANGLE2,CS2,SN2
   INTEGER :: K1X,K1Y,K1Z,K2X,K2Y,K2Z,I,J,K,II,JJ,IATM,JATM
   INTEGER :: I1,I2,I3,J1,J2,J3
   INTEGER :: M,TS,MMAX
   INTEGER :: TICKET

   ALLOCATE(N1(0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:2*BASIS_LMAX+1))
   ALLOCATE(N2(0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1))
   ALLOCATE(W1(NPGS,NPGS),W2(NCGS,NPGS),W3(NCGS,NCGS))
   ALLOCATE(N_P_TMP(NPGS,NPGS,-CEL1X:CEL1X,-CEL1Y:CEL1Y,-CEL1Z:CEL1Z))
   ALLOCATE(N_P(NPGS,NPGS,-CEL1X:CEL1X,-CEL1Y:CEL1Y,-CEL1Z:CEL1Z))
   ALLOCATE(FORCEX_TMP(NATOM),FORCEY_TMP(NATOM),FORCEZ_TMP(NATOM))

   PISUB=2.0D0/DSQRT(PI)
   F1(0)=1.0D0/PISUB
   F2(0)=-0.5D0
   DO I=1,2*BASIS_LMAX+1
    F1(I)=F1(I-1)*DFLOAT(2*I-1)/2.0D0
    F2(I)=F2(I-1)-1.0D0
   ENDDO

   SELECT CASE(IGRAD)
    CASE(0)
     IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE NUCLEAR ATTRACTION ENERGY INTEGRALS'
    CASE(1)
     IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE NUCLEAR ATTRACTION ENERGY INTEGRAL DERIVATIVES'
    CASE DEFAULT
     CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
   END SELECT

   N_P_TMP=0.0D0
   N_P=0.0D0
   FORCEX_TMP=0.0D0
   FORCEY_TMP=0.0D0
   FORCEZ_TMP=0.0D0
   FORCETX_TMP=0.0D0
   FORCETY_TMP=0.0D0
   FORCETZ_TMP=0.0D0
   FORCETA_TMP=0.0D0

   TICKET=0
   DO K1X=-CEL1X,CEL1X
   DO K1Y=-CEL1Y,CEL1Y
   DO K1Z=-CEL1Z,CEL1Z
    TICKET=TICKET+1
    IF (TICKET == NUMPROCS) TICKET=TICKET-NUMPROCS
! ------------------------ PARALLEL LOOP
    IF (MYID == TICKET) THEN
! ------------------------ PARALLEL LOOP
    Q1X=DFLOAT(K1X)*PERIODX
    Q1Y=DFLOAT(K1Y)*PERIODY
    Q1Z=DFLOAT(K1Z)*PERIODZ
    ANGLE=DFLOAT(K1X)*HELIX
    CS=DCOS(ANGLE)
    SN=DSIN(ANGLE)
    DO I=1,NPSHELL
     II=P_SHELL(I,0,0,0)
     IATM=PGS_ATOM(II)
     AX=PGSX(II)
     AY=PGSY(II)
     AZ=PGSZ(II)
     DO J=1,NPSHELL
      JJ=P_SHELL(J,0,0,0)
      JATM=PGS_ATOM(JJ)
      BX=PGSX(JJ)+Q1X
      BY=PGSY(JJ)*CS-PGSZ(JJ)*SN+Q1Y
      BZ=PGSY(JJ)*SN+PGSZ(JJ)*CS+Q1Z
      COMZ=ZT(II)+ZT(JJ)
      PX=(AX*ZT(II)+BX*ZT(JJ))/COMZ
      PY=(AY*ZT(II)+BY*ZT(JJ))/COMZ
      PZ=(AZ*ZT(II)+BZ*ZT(JJ))/COMZ
      E1=PISUB*DSQRT(COMZ)*S_P(II,JJ,K1X,K1Y,K1Z)
      N2=0.0D0
      DO K2X=-CEL2X,CEL2X
      DO K2Y=-CEL2Y,CEL2Y
      DO K2Z=-CEL2Z,CEL2Z
       Q2X=DFLOAT(K2X)*PERIODX
       Q2Y=DFLOAT(K2Y)*PERIODY
       Q2Z=DFLOAT(K2Z)*PERIODZ
       ANGLE2=DFLOAT(K2X)*HELIX
       CS2=DCOS(ANGLE2)
       SN2=DSIN(ANGLE2)
       DO K=1,NATOM
        E2=-DFLOAT(IATOM(K))*E1
        CX=ATOMX(K)+Q2X
        CY=ATOMY(K)*CS2-ATOMZ(K)*SN2+Q2Y
        CZ=ATOMY(K)*SN2+ATOMZ(K)*CS2+Q2Z
        T=COMZ*((PX-CX)**2+(PY-CY)**2+(PZ-CZ)**2)
        DO M=0,P_SHELL_ANG(I)+P_SHELL_ANG(J)+IGRAD
         IF (T < TF(M)) THEN
          TS=NINT(T*20.0D0)
          H=0.05D0*DFLOAT(TS)-T
          N1(0,0,0,0,0,0,M)=((((((IGAMMA(TS,M+6)*H*0.166666666666667D0+IGAMMA(TS,M+5))*H*0.2D0+IGAMMA(TS,M+4))*H*0.25D0+ &
          IGAMMA(TS,M+3))*H*0.333333333333333D0+IGAMMA(TS,M+2))*H*0.5D0+IGAMMA(TS,M+1))*H+IGAMMA(TS,M))*E2
         ELSE
          N1(0,0,0,0,0,0,M)=E2*F1(M)*DEXP(F2(M)*DLOG(T))
         ENDIF
        ENDDO
        N2(0,0,0,0,0,0)=N2(0,0,0,0,0,0)+N1(0,0,0,0,0,0,0)

        DO I1=0,P_SHELL_ANG(I)+IGRAD
         DO I2=0,P_SHELL_ANG(I)+IGRAD-I1
          DO I3=0,P_SHELL_ANG(I)+IGRAD-I1-I2
           DO J1=0,P_SHELL_ANG(J)+IGRAD
            DO J2=0,P_SHELL_ANG(J)+IGRAD-J1
             DO J3=0,P_SHELL_ANG(J)+IGRAD-J1-J2
              MMAX=P_SHELL_ANG(I)+P_SHELL_ANG(J)+IGRAD-I1-I2-I3-J1-J2-J3
              IF ((I1+I2+I3+J1+J2+J3 == 0).OR.(MMAX < 0)) CYCLE
              DO M=0,MMAX
               IF (I1 >= 1) THEN
                N1(I1,I2,I3,J1,J2,J3,M)=(PX-AX)*N1(I1-1,I2,I3,J1,J2,J3,M)-(PX-CX)*N1(I1-1,I2,I3,J1,J2,J3,M+1)
                IF (I1 >= 2) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(I1-1)/COMZ*(N1(I1-2,I2,I3,J1,J2,J3,M)-N1(I1-2,I2,I3,J1,J2,J3,M+1))
                IF (J1 >= 1) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(J1)/COMZ*(N1(I1-1,I2,I3,J1-1,J2,J3,M)-N1(I1-1,I2,I3,J1-1,J2,J3,M+1))
               ELSE IF (I2 >= 1) THEN
                N1(I1,I2,I3,J1,J2,J3,M)=(PY-AY)*N1(I1,I2-1,I3,J1,J2,J3,M)-(PY-CY)*N1(I1,I2-1,I3,J1,J2,J3,M+1)
                IF (I2 >= 2) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(I2-1)/COMZ*(N1(I1,I2-2,I3,J1,J2,J3,M)-N1(I1,I2-2,I3,J1,J2,J3,M+1))
                IF (J2 >= 1) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(J2)/COMZ*(N1(I1,I2-1,I3,J1,J2-1,J3,M)-N1(I1,I2-1,I3,J1,J2-1,J3,M+1))
               ELSE IF (I3 >= 1) THEN
                N1(I1,I2,I3,J1,J2,J3,M)=(PZ-AZ)*N1(I1,I2,I3-1,J1,J2,J3,M)-(PZ-CZ)*N1(I1,I2,I3-1,J1,J2,J3,M+1)
                IF (I3 >= 2) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(I3-1)/COMZ*(N1(I1,I2,I3-2,J1,J2,J3,M)-N1(I1,I2,I3-2,J1,J2,J3,M+1))
                IF (J3 >= 1) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(J3)/COMZ*(N1(I1,I2,I3-1,J1,J2,J3-1,M)-N1(I1,I2,I3-1,J1,J2,J3-1,M+1))
               ELSE IF (J1 >= 1) THEN
                N1(I1,I2,I3,J1,J2,J3,M)=(PX-BX)*N1(I1,I2,I3,J1-1,J2,J3,M)-(PX-CX)*N1(I1,I2,I3,J1-1,J2,J3,M+1)
                IF (J1 >= 2) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(J1-1)/COMZ*(N1(I1,I2,I3,J1-2,J2,J3,M)-N1(I1,I2,I3,J1-2,J2,J3,M+1))
               ELSE IF (J2 >= 1) THEN
                N1(I1,I2,I3,J1,J2,J3,M)=(PY-BY)*N1(I1,I2,I3,J1,J2-1,J3,M)-(PY-CY)*N1(I1,I2,I3,J1,J2-1,J3,M+1)
                IF (J2 >= 2) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(J2-1)/COMZ*(N1(I1,I2,I3,J1,J2-2,J3,M)-N1(I1,I2,I3,J1,J2-2,J3,M+1))
               ELSE IF (J3 >= 1) THEN
                N1(I1,I2,I3,J1,J2,J3,M)=(PZ-BZ)*N1(I1,I2,I3,J1,J2,J3-1,M)-(PZ-CZ)*N1(I1,I2,I3,J1,J2,J3-1,M+1)
                IF (J3 >= 2) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(J3-1)/COMZ*(N1(I1,I2,I3,J1,J2,J3-2,M)-N1(I1,I2,I3,J1,J2,J3-2,M+1))
               END IF
              ENDDO
              N2(I1,I2,I3,J1,J2,J3)=N2(I1,I2,I3,J1,J2,J3)+N1(I1,I2,I3,J1,J2,J3,0)
             ENDDO
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
      ENDDO
      ENDDO
      DO I1=0,P_SHELL_ANG(I)
       DO I2=0,P_SHELL_ANG(I)-I1
        DO I3=0,P_SHELL_ANG(I)-I1-I2
         DO J1=0,P_SHELL_ANG(J)
          DO J2=0,P_SHELL_ANG(J)-J1
           DO J3=0,P_SHELL_ANG(J)-J1-J2
            N_P_TMP(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),K1X,K1Y,K1Z)=N2(I1,I2,I3,J1,J2,J3)
            IF (IGRAD == 1) THEN
             IF (HELIX == 0.0D0) THEN
              FN=2.0D0*ZT(II)*N2(I1+1,I2,I3,J1,J2,J3)
              IF (I1 >= 1) FN=FN-DFLOAT(I1)*N2(I1-1,I2,I3,J1,J2,J3)
              FORCEX_TMP(IATM)=FORCEX_TMP(IATM)+FN*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),K1X,K1Y,K1Z)
              FN=2.0D0*ZT(II)*N2(I1,I2+1,I3,J1,J2,J3)
              IF (I2 >= 1) FN=FN-DFLOAT(I2)*N2(I1,I2-1,I3,J1,J2,J3)
              FORCEY_TMP(IATM)=FORCEY_TMP(IATM)+FN*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),K1X,K1Y,K1Z)
              FN=2.0D0*ZT(II)*N2(I1,I2,I3+1,J1,J2,J3)
              IF (I3 >= 1) FN=FN-DFLOAT(I3)*N2(I1,I2,I3-1,J1,J2,J3)
              FORCEZ_TMP(IATM)=FORCEZ_TMP(IATM)+FN*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),K1X,K1Y,K1Z)
              FN=2.0D0*ZT(JJ)*N2(I1,I2,I3,J1+1,J2,J3)
              IF (J1 >= 1) FN=FN-DFLOAT(J1)*N2(I1,I2,I3,J1-1,J2,J3)
              FORCEX_TMP(JATM)=FORCEX_TMP(JATM)+FN*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),K1X,K1Y,K1Z)
              FORCETX_TMP=FORCETX_TMP+DFLOAT(K1X)*FN*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),K1X,K1Y,K1Z)
              FN=2.0D0*ZT(JJ)*N2(I1,I2,I3,J1,J2+1,J3)
              IF (J2 >= 1) FN=FN-DFLOAT(J2)*N2(I1,I2,I3,J1,J2-1,J3)
              FORCEY_TMP(JATM)=FORCEY_TMP(JATM)+FN*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),K1X,K1Y,K1Z)
              FORCETY_TMP=FORCETY_TMP+DFLOAT(K1Y)*FN*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),K1X,K1Y,K1Z)
              FN=2.0D0*ZT(JJ)*N2(I1,I2,I3,J1,J2,J3+1)
              IF (J3 >= 1) FN=FN-DFLOAT(J3)*N2(I1,I2,I3,J1,J2,J3-1)
              FORCEZ_TMP(JATM)=FORCEZ_TMP(JATM)+FN*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),K1X,K1Y,K1Z)
              FORCETZ_TMP=FORCETZ_TMP+DFLOAT(K1Z)*FN*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),K1X,K1Y,K1Z)
             ELSE ! --- HELIX
              FN=2.0D0*ZT(II)*N2(I1+1,I2,I3,J1,J2,J3)
              IF (I1 >= 1) FN=FN-DFLOAT(I1)*N2(I1-1,I2,I3,J1,J2,J3)
              FORCEX_TMP(IATM)=FORCEX_TMP(IATM)+FN*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,K1X)
              FN=2.0D0*ZT(II)*N2(I1,I2+1,I3,J1,J2,J3)
              IF (I2 >= 1) FN=FN-DFLOAT(I2)*N2(I1,I2-1,I3,J1,J2,J3)
              FORCEY_TMP(IATM)=FORCEY_TMP(IATM)+FN*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,K1X)
              FN=2.0D0*ZT(II)*N2(I1,I2,I3+1,J1,J2,J3)
              IF (I3 >= 1) FN=FN-DFLOAT(I3)*N2(I1,I2,I3-1,J1,J2,J3)
              FORCEZ_TMP(IATM)=FORCEZ_TMP(IATM)+FN*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,K1X)
              FN=2.0D0*ZT(JJ)*N2(I1,I2,I3,J1+1,J2,J3)
              IF (J1 >= 1) FN=FN-DFLOAT(J1)*N2(I1,I2,I3,J1-1,J2,J3)
              FORCEX_TMP(JATM)=FORCEX_TMP(JATM)+FN*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,K1X)
              FORCETX_TMP=FORCETX_TMP+DFLOAT(K1X)*FN*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,K1X)
              FN=2.0D0*ZT(JJ)*N2(I1,I2,I3,J1,J2+1,J3)
              IF (J2 >= 1) FN=FN-DFLOAT(J2)*N2(I1,I2,I3,J1,J2-1,J3)
              FORCEY_TMP(JATM)=FORCEY_TMP(JATM)+CS*FN*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,K1X)
              FORCEZ_TMP(JATM)=FORCEZ_TMP(JATM)-SN*FN*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,K1X)
              FORCETA_TMP=FORCETA_TMP+DFLOAT(K1X)*(-ATOMY(JATM)*SN-ATOMZ(JATM)*CS) &
                                     *FN*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,K1X)
              FN=2.0D0*ZT(JJ)*N2(I1,I2,I3,J1,J2,J3+1)
              IF (J3 >= 1) FN=FN-DFLOAT(J3)*N2(I1,I2,I3,J1,J2,J3-1)
              FORCEY_TMP(JATM)=FORCEY_TMP(JATM)+SN*FN*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,K1X)
              FORCEZ_TMP(JATM)=FORCEZ_TMP(JATM)+CS*FN*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,K1X)
              FORCETA_TMP=FORCETA_TMP+DFLOAT(K1X)*(ATOMY(JATM)*CS-ATOMZ(JATM)*SN) &
                                     *FN*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,K1X)
              ! BASIS ROTATION FORCE
              FORCETA_TMP=FORCETA_TMP+N2(I1,I2,I3,J1,J2,J3)*P_P_HELIX_THETA(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,K1X)
             ENDIF ! --- HELIX END
            ENDIF
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
! ------------------------ PARALLEL LOOP
    ENDIF
! ------------------------ PARALLEL LOOP

   ENDDO
   ENDDO
   ENDDO

   CALL MPI_ALLREDUCE(N_P_TMP,N_P,NPGS**2*(CEL1X*2+1)*(CEL1Y*2+1)*(CEL1Z*2+1),MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)

! The above ALLREDUCE is where the calculations tend to choke on Cori with more than 2 nodes (128 processors).

   N_C=0.0D0
   DO K1X=-CEL1X,CEL1X
   DO K1Y=-CEL1Y,CEL1Y
   DO K1Z=-CEL1Z,CEL1Z
    ! CONTRACTION
    W1=N_P(:,:,K1X,K1Y,K1Z)
    W2=MATMUL(CC,W1)
    W3=MATMUL(W2,TRANSPOSE(CC))
    N_C(:,:,K1X,K1Y,K1Z)=W3
   ENDDO
   ENDDO
   ENDDO
! --- HELIX
   IF (HELIX /= 0.0D0) THEN
    DO K1X=-CEL1X,CEL1X
     DO I=1,NCGS
      DO J=1,NCGS
       W3(I,J)=0.0D0
       DO JJ=1,NCGS
        W3(I,J)=W3(I,J)+C2H(J,JJ,K1X)*N_C(I,JJ,K1X,0,0)
       ENDDO
      ENDDO
     ENDDO
     N_C(:,:,K1X,0,0)=W3
    ENDDO
   ENDIF
! --- HELIX END
   IF (IGRAD == 1) THEN
    CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEX_TMP,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
    CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEY_TMP,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
    CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCEZ_TMP,NATOM,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
    CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETX_TMP,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
    CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETY_TMP,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
    CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETZ_TMP,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
    CALL MPI_ALLREDUCE(MPI_IN_PLACE,FORCETA_TMP,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
    FORCEX=FORCEX+FORCEX_TMP
    FORCEY=FORCEY+FORCEY_TMP
    FORCEZ=FORCEZ+FORCEZ_TMP
    FORCETX=FORCETX+FORCETX_TMP
    FORCETY=FORCETY+FORCETY_TMP
    FORCETZ=FORCETZ+FORCETZ_TMP
    FORCETA=FORCETA+FORCETA_TMP
   ENDIF

   IF ((IGRAD == 0).AND.(IOPTN(9) >= 2).AND.(MYID == 0)) THEN
    WRITE(6,'(A)') 'NUCLEAR ATTRACTION ENERGY INTEGRAL MATRIX FOR CONTRACTED GAUSSIANS'
    CALL DUMP1(N_C,NCGS,CEL1X,CEL1Y,CEL1Z)
   ENDIF
   IF ((IGRAD == 1).AND.(IOPTN(9) == 3).AND.(MYID == 0)) CALL DUMP2
   DEALLOCATE(N1,N2,W1,W2,W3,N_P,N_P_TMP,FORCEX_TMP,FORCEY_TMP,FORCEZ_TMP)
   RETURN
END SUBROUTINE



SUBROUTINE MOMENT(IGRAD)
! CALCULATE DIPOLE AND QUADRUPOLE MOMENT INTEGRALS

   USE MPI_F08
   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   USE MULTIPOLE
   
   IMPLICIT NONE
!  INCLUDE "mpif.h"
   INTEGER :: IGRAD
   DOUBLE PRECISION,ALLOCATABLE :: M1(:,:,:,:,:,:,:)
   DOUBLE PRECISION,ALLOCATABLE :: W1(:,:),W2(:,:),W3(:,:)
   DOUBLE PRECISION,ALLOCATABLE :: M_P(:,:,:,:,:,:)
   DOUBLE PRECISION :: PISUB
   DOUBLE PRECISION :: QX,QY,QZ
   DOUBLE PRECISION :: COMZ1,COMZ2,GZI
   DOUBLE PRECISION :: AX,AY,AZ,BX,BY,BZ,PX,PY,PZ,R
   DOUBLE PRECISION :: ANGLE,CS,SN
   ! 1=X,2=Y,3=Z,4=XX,5=YY,6=ZZ,7=XY,8=YZ,9=ZX
   INTEGER,PARAMETER :: D1X(9) = (/0,0,0,1,0,0,2,0,3/)
   INTEGER,PARAMETER :: D1Y(9) = (/0,0,0,0,2,0,1,3,0/)
   INTEGER,PARAMETER :: D1Z(9) = (/0,0,0,0,0,3,0,2,1/)
   INTEGER,PARAMETER :: D2X(9) = (/0,0,0,0,0,0,0,0,0/)
   INTEGER,PARAMETER :: D2Y(9) = (/0,0,0,0,0,0,0,0,0/)
   INTEGER,PARAMETER :: D2Z(9) = (/0,0,0,0,0,0,0,0,0/)
   INTEGER,PARAMETER :: NX(9)  = (/1,0,0,2,0,0,1,0,1/)
   INTEGER,PARAMETER :: NY(9)  = (/0,1,0,0,2,0,1,1,0/)
   INTEGER,PARAMETER :: NZ(9)  = (/0,0,1,0,0,2,0,1,1/)
   INTEGER :: KX,KY,KZ,I,J,L,II,JJ
   INTEGER :: IATM,JATM
   INTEGER :: I1,I2,I3,J1,J2,J3
   DOUBLE PRECISION :: F1,F2,F3,A,B,C,D

   ALLOCATE(M1(0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:9))
   ALLOCATE(W1(NPGS,NPGS),W2(NCGS,NPGS),W3(NCGS,NCGS))
   ALLOCATE(M_P(NPGS,NPGS,-CEL1X:CEL1X,-CEL1Y:CEL1Y,-CEL1Z:CEL1Z,9))

   PISUB=DSQRT(PI**3)
   SELECT CASE(IGRAD)
    CASE(0)
     IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE DIPOLE AND QUADRUPOLE MOMENT INTEGRALS'
    CASE(1)
     IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE DIPOLE AND QUADRUPOLE MOMENT INTEGRAL DERIVATIVES'
    CASE DEFAULT
     CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
   END SELECT
   
   M_P=0.0D0
   M_C=0.0D0

   DIPOLEX_X=0.0D0
   DIPOLEX_Y=0.0D0
   DIPOLEX_Z=0.0D0
   DIPOLEX_TX=0.0D0
   DIPOLEX_TA=0.0D0
   DIPOLEY_X=0.0D0
   DIPOLEY_Y=0.0D0
   DIPOLEY_Z=0.0D0
   DIPOLEY_TX=0.0D0
   DIPOLEY_TA=0.0D0
   DIPOLEZ_X=0.0D0
   DIPOLEZ_Y=0.0D0
   DIPOLEZ_Z=0.0D0
   DIPOLEZ_TX=0.0D0
   DIPOLEZ_TA=0.0D0
   DO I=1,NATOM
    DIPOLEX_X(I)=DIPOLEX_X(I)-DFLOAT(IATOM(I))
    DIPOLEY_Y(I)=DIPOLEY_Y(I)-DFLOAT(IATOM(I))
    DIPOLEZ_Z(I)=DIPOLEZ_Z(I)-DFLOAT(IATOM(I))
   ENDDO
   
   DO KX=-CEL1X,CEL1X
   DO KY=-CEL1Y,CEL1Y
   DO KZ=-CEL1Z,CEL1Z
    QX=DFLOAT(KX)*PERIODX
    QY=DFLOAT(KY)*PERIODY
    QZ=DFLOAT(KZ)*PERIODZ
    ANGLE=DFLOAT(KX)*HELIX
    CS=DCOS(ANGLE)
    SN=DSIN(ANGLE)
    DO I=1,NPSHELL
     II=P_SHELL(I,0,0,0)
     IATM=PGS_ATOM(II)
     AX=PGSX(II)
     AY=PGSY(II)
     AZ=PGSZ(II)
     DO J=1,NPSHELL
      JJ=P_SHELL(J,0,0,0)
      JATM=PGS_ATOM(JJ)
      BX=PGSX(JJ)+QX
      BY=PGSY(JJ)*CS-PGSZ(JJ)*SN+QY
      BZ=PGSY(JJ)*SN+PGSZ(JJ)*CS+QZ
      R=(AX-BX)**2+(AY-BY)**2+(AZ-BZ)**2
      COMZ1=ZT(II)+ZT(JJ)
      COMZ2=DSQRT(COMZ1**3)
      GZI=ZT(II)*ZT(JJ)/COMZ1
      PX=(AX*ZT(II)+BX*ZT(JJ))/COMZ1
      PY=(AY*ZT(II)+BY*ZT(JJ))/COMZ1
      PZ=(AZ*ZT(II)+BZ*ZT(JJ))/COMZ1
      
      M1(0,0,0,0,0,0,0)=PISUB/COMZ2*DEXP(-GZI*R)

      DO I1=0,P_SHELL_ANG(I)+IGRAD
       DO I2=0,P_SHELL_ANG(I)+IGRAD-I1
        DO I3=0,P_SHELL_ANG(I)+IGRAD-I1-I2
         DO J1=0,P_SHELL_ANG(J)+IGRAD
          DO J2=0,P_SHELL_ANG(J)+IGRAD-J1
           DO J3=0,P_SHELL_ANG(J)+IGRAD-J1-J2
            IF (I1+I2+I3+J1+J2+J3 > P_SHELL_ANG(I)+P_SHELL_ANG(J)+IGRAD) CYCLE
            IF (I1 >= 1) THEN
             M1(I1,I2,I3,J1,J2,J3,0)=(PX-AX)*M1(I1-1,I2,I3,J1,J2,J3,0)
             IF (I1 >= 2) M1(I1,I2,I3,J1,J2,J3,0)=M1(I1,I2,I3,J1,J2,J3,0)+0.5D0*DFLOAT(I1-1)/COMZ1*M1(I1-2,I2,I3,J1,J2,J3,0)
             IF (J1 >= 1) M1(I1,I2,I3,J1,J2,J3,0)=M1(I1,I2,I3,J1,J2,J3,0)+0.5D0*DFLOAT(J1)/COMZ1*M1(I1-1,I2,I3,J1-1,J2,J3,0)
            ELSE IF (I2 >= 1) THEN
             M1(I1,I2,I3,J1,J2,J3,0)=(PY-AY)*M1(I1,I2-1,I3,J1,J2,J3,0)
             IF (I2 >= 2) M1(I1,I2,I3,J1,J2,J3,0)=M1(I1,I2,I3,J1,J2,J3,0)+0.5D0*DFLOAT(I2-1)/COMZ1*M1(I1,I2-2,I3,J1,J2,J3,0)
             IF (J2 >= 1) M1(I1,I2,I3,J1,J2,J3,0)=M1(I1,I2,I3,J1,J2,J3,0)+0.5D0*DFLOAT(J2)/COMZ1*M1(I1,I2-1,I3,J1,J2-1,J3,0)
            ELSE IF (I3 >= 1) THEN
             M1(I1,I2,I3,J1,J2,J3,0)=(PZ-AZ)*M1(I1,I2,I3-1,J1,J2,J3,0)
             IF (I3 >= 2) M1(I1,I2,I3,J1,J2,J3,0)=M1(I1,I2,I3,J1,J2,J3,0)+0.5D0*DFLOAT(I3-1)/COMZ1*M1(I1,I2,I3-2,J1,J2,J3,0)
             IF (J3 >= 1) M1(I1,I2,I3,J1,J2,J3,0)=M1(I1,I2,I3,J1,J2,J3,0)+0.5D0*DFLOAT(J3)/COMZ1*M1(I1,I2,I3-1,J1,J2,J3-1,0)
            ELSE IF (J1 >= 1) THEN
             M1(I1,I2,I3,J1,J2,J3,0)=(PX-BX)*M1(I1,I2,I3,J1-1,J2,J3,0)
             IF (J1 >= 2) M1(I1,I2,I3,J1,J2,J3,0)=M1(I1,I2,I3,J1,J2,J3,0)+0.5D0*DFLOAT(J1-1)/COMZ1*M1(I1,I2,I3,J1-2,J2,J3,0)
            ELSE IF (J2 >= 1) THEN
             M1(I1,I2,I3,J1,J2,J3,0)=(PY-BY)*M1(I1,I2,I3,J1,J2-1,J3,0)
             IF (J2 >= 2) M1(I1,I2,I3,J1,J2,J3,0)=M1(I1,I2,I3,J1,J2,J3,0)+0.5D0*DFLOAT(J2-1)/COMZ1*M1(I1,I2,I3,J1,J2-2,J3,0)
            ELSE IF (J3 >= 1) THEN
             M1(I1,I2,I3,J1,J2,J3,0)=(PZ-BZ)*M1(I1,I2,I3,J1,J2,J3-1,0)
             IF (J3 >= 2) M1(I1,I2,I3,J1,J2,J3,0)=M1(I1,I2,I3,J1,J2,J3,0)+0.5D0*DFLOAT(J3-1)/COMZ1*M1(I1,I2,I3,J1,J2,J3-2,0)
            ENDIF
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
      DO L=1,9
       DO I1=0,P_SHELL_ANG(I)+IGRAD
        DO I2=0,P_SHELL_ANG(I)+IGRAD-I1
         DO I3=0,P_SHELL_ANG(I)+IGRAD-I1-I2
          DO J1=0,P_SHELL_ANG(J)+IGRAD
           DO J2=0,P_SHELL_ANG(J)+IGRAD-J1
            DO J3=0,P_SHELL_ANG(J)+IGRAD-J1-J2
             IF (I1+I2+I3+J1+J2+J3 > P_SHELL_ANG(I)+P_SHELL_ANG(J)+IGRAD) CYCLE
             ! MOMENT INTEGRALS (OBARA-SAIKA RECURSION FORMULAS)
             IF (I1 >= 1) THEN
              M1(I1,I2,I3,J1,J2,J3,L)=(PX-AX)*M1(I1-1,I2,I3,J1,J2,J3,L)
              IF (I1 >= 2) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L)+0.5D0*DFLOAT(I1-1)/COMZ1*M1(I1-2,I2,I3,J1,J2,J3,L)
              IF (J1 >= 1) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L)+0.5D0*DFLOAT(J1)/COMZ1*M1(I1-1,I2,I3,J1-1,J2,J3,L)
              IF (NX(L) >= 1) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L) &
                              +0.5D0*DFLOAT(NX(L))/COMZ1*M1(I1-1,I2,I3,J1,J2,J3,D1X(L))
             ELSE IF (I2 >= 1) THEN
              M1(I1,I2,I3,J1,J2,J3,L)=(PY-AY)*M1(I1,I2-1,I3,J1,J2,J3,L)
              IF (I2 >= 2) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L)+0.5D0*DFLOAT(I2-1)/COMZ1*M1(I1,I2-2,I3,J1,J2,J3,L)
              IF (J2 >= 1) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L)+0.5D0*DFLOAT(J2)/COMZ1*M1(I1,I2-1,I3,J1,J2-1,J3,L)
              IF (NY(L) >= 1) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L) &
                              +0.5D0*DFLOAT(NY(L))/COMZ1*M1(I1,I2-1,I3,J1,J2,J3,D1Y(L))
             ELSE IF (I3 >= 1) THEN
              M1(I1,I2,I3,J1,J2,J3,L)=(PZ-AZ)*M1(I1,I2,I3-1,J1,J2,J3,L)
              IF (I3 >= 2) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L)+0.5D0*DFLOAT(I3-1)/COMZ1*M1(I1,I2,I3-2,J1,J2,J3,L)
              IF (J3 >= 1) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L)+0.5D0*DFLOAT(J3)/COMZ1*M1(I1,I2,I3-1,J1,J2,J3-1,L)
              IF (NZ(L) >= 1) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L) &
                              +0.5D0*DFLOAT(NZ(L))/COMZ1*M1(I1,I2,I3-1,J1,J2,J3,D1Z(L))
!if ((i==7).and.(j==1).and.(i1==0).and.(i2==0).and.(i3==1).and.(j1==0).and.(j2==0).and.(j3==1).and.(L==3)) &
!write(*,*) L,D1Z(L),(PZ-AZ),M1(I1,I2,I3-1,J1,J2,J3,L),COMZ1,M1(I1,I2,I3-1,J1,J2,J3-1,L),M1(I1,I2,I3-1,J1,J2,J3,D1Z(L)), &
!M1(I1,I2,I3,J1,J2,J3,L)
             ELSE IF (J1 >= 1) THEN
              M1(I1,I2,I3,J1,J2,J3,L)=(PX-BX)*M1(I1,I2,I3,J1-1,J2,J3,L)
              IF (J1 >= 2) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L)+0.5D0*DFLOAT(J1-1)/COMZ1*M1(I1,I2,I3,J1-2,J2,J3,L)
              IF (NX(L) >= 1) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L) &
                              +0.5D0*DFLOAT(NX(L))/COMZ1*M1(I1,I2,I3,J1-1,J2,J3,D1X(L))
             ELSE IF (J2 >= 1) THEN
              M1(I1,I2,I3,J1,J2,J3,L)=(PY-BY)*M1(I1,I2,I3,J1,J2-1,J3,L)
              IF (J2 >= 2) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L)+0.5D0*DFLOAT(J2-1)/COMZ1*M1(I1,I2,I3,J1,J2-2,J3,L)
              IF (NY(L) >= 1) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L) &
                              +0.5D0*DFLOAT(NY(L))/COMZ1*M1(I1,I2,I3,J1,J2-1,J3,D1Y(L))
             ELSE IF (J3 >= 1) THEN
              M1(I1,I2,I3,J1,J2,J3,L)=(PZ-BZ)*M1(I1,I2,I3,J1,J2,J3-1,L)
              IF (J3 >= 2) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L)+0.5D0*DFLOAT(J3-1)/COMZ1*M1(I1,I2,I3,J1,J2,J3-2,L)
              IF (NZ(L) >= 1) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L) &
                              +0.5D0*DFLOAT(NZ(L))/COMZ1*M1(I1,I2,I3,J1,J2,J3-1,D1Z(L))
             ELSE IF (NX(L) >= 1) THEN
              M1(I1,I2,I3,J1,J2,J3,L)=PX*M1(I1,I2,I3,J1,J2,J3,D1X(L))
              IF (NX(L) >= 2) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L) &
                              +0.5D0*DFLOAT(NX(L)-1)/COMZ1*M1(I1,I2,I3,J1,J2,J3,D2X(L))
             ELSE IF (NY(L) >= 1) THEN
              M1(I1,I2,I3,J1,J2,J3,L)=PY*M1(I1,I2,I3,J1,J2,J3,D1Y(L))
              IF (NY(L) >= 2) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L) &
                              +0.5D0*DFLOAT(NY(L)-1)/COMZ1*M1(I1,I2,I3,J1,J2,J3,D2Y(L))
             ELSE IF (NZ(L) >= 1) THEN
              M1(I1,I2,I3,J1,J2,J3,L)=PZ*M1(I1,I2,I3,J1,J2,J3,D1Z(L))
              IF (NZ(L) >= 2) M1(I1,I2,I3,J1,J2,J3,L)=M1(I1,I2,I3,J1,J2,J3,L) &
                              +0.5D0*DFLOAT(NZ(L)-1)/COMZ1*M1(I1,I2,I3,J1,J2,J3,D2Z(L))
             ENDIF
!if (myid == 0) write(*,*) 'M1 PRINT',i,j,i1,i2,i3,j1,j2,j3,L,m1(i1,i2,i3,j1,j2,j3,l)
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
      DO I1=0,P_SHELL_ANG(I)
       DO I2=0,P_SHELL_ANG(I)-I1
        DO I3=0,P_SHELL_ANG(I)-I1-I2
         DO J1=0,P_SHELL_ANG(J)
          DO J2=0,P_SHELL_ANG(J)-J1
           DO J3=0,P_SHELL_ANG(J)-J1-J2
            DO L=1,9
             M_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),KX,KY,KZ,L)=M1(I1,I2,I3,J1,J2,J3,L)
            ENDDO
            ! These dipole derivatives will not agree with finite-difference derivatives
            ! because they do not include the density matrix changes. The latter effects 
            ! are included through Pulay force.
            IF (IGRAD == 1) THEN
             F1=2.0D0*ZT(II)*M1(I1+1,I2,I3,J1,J2,J3,1)
             F2=2.0D0*ZT(II)*M1(I1+1,I2,I3,J1,J2,J3,2)
             F3=2.0D0*ZT(II)*M1(I1+1,I2,I3,J1,J2,J3,3)
             IF (I1 >= 1) THEN
              F1=F1-DFLOAT(I1)*M1(I1-1,I2,I3,J1,J2,J3,1)
              F2=F2-DFLOAT(I1)*M1(I1-1,I2,I3,J1,J2,J3,2)
              F3=F3-DFLOAT(I1)*M1(I1-1,I2,I3,J1,J2,J3,3)
             ENDIF
             DIPOLEX_X(IATM)=DIPOLEX_X(IATM)+F1*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEY_X(IATM)=DIPOLEY_X(IATM)+F2*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEZ_X(IATM)=DIPOLEZ_X(IATM)+F3*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             F1=2.0D0*ZT(II)*M1(I1,I2+1,I3,J1,J2,J3,1)
             F2=2.0D0*ZT(II)*M1(I1,I2+1,I3,J1,J2,J3,2)
             F3=2.0D0*ZT(II)*M1(I1,I2+1,I3,J1,J2,J3,3)
             IF (I2 >= 1) THEN
              F1=F1-DFLOAT(I2)*M1(I1,I2-1,I3,J1,J2,J3,1)
              F2=F2-DFLOAT(I2)*M1(I1,I2-1,I3,J1,J2,J3,2)
              F3=F3-DFLOAT(I2)*M1(I1,I2-1,I3,J1,J2,J3,3)
             ENDIF
             DIPOLEX_Y(IATM)=DIPOLEX_Y(IATM)+F1*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEY_Y(IATM)=DIPOLEY_Y(IATM)+F2*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEZ_Y(IATM)=DIPOLEZ_Y(IATM)+F3*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             F1=2.0D0*ZT(II)*M1(I1,I2,I3+1,J1,J2,J3,1)
             F2=2.0D0*ZT(II)*M1(I1,I2,I3+1,J1,J2,J3,2)
             F3=2.0D0*ZT(II)*M1(I1,I2,I3+1,J1,J2,J3,3)
             IF (I3 >= 1) THEN
              F1=F1-DFLOAT(I3)*M1(I1,I2,I3-1,J1,J2,J3,1)
              F2=F2-DFLOAT(I3)*M1(I1,I2,I3-1,J1,J2,J3,2)
              F3=F3-DFLOAT(I3)*M1(I1,I2,I3-1,J1,J2,J3,3)
             ENDIF
             DIPOLEX_Z(IATM)=DIPOLEX_Z(IATM)+F1*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEY_Z(IATM)=DIPOLEY_Z(IATM)+F2*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEZ_Z(IATM)=DIPOLEZ_Z(IATM)+F3*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             F1=2.0D0*ZT(JJ)*M1(I1,I2,I3,J1+1,J2,J3,1)
             F2=2.0D0*ZT(JJ)*M1(I1,I2,I3,J1+1,J2,J3,2)
             F3=2.0D0*ZT(JJ)*M1(I1,I2,I3,J1+1,J2,J3,3)
             IF (J1 >= 1) THEN
              F1=F1-DFLOAT(J1)*M1(I1,I2,I3,J1-1,J2,J3,1)
              F2=F2-DFLOAT(J1)*M1(I1,I2,I3,J1-1,J2,J3,2)
              F3=F3-DFLOAT(J1)*M1(I1,I2,I3,J1-1,J2,J3,3)
             ENDIF
             DIPOLEX_X(JATM)=DIPOLEX_X(JATM)+F1*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEY_X(JATM)=DIPOLEY_X(JATM)+F2*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEZ_X(JATM)=DIPOLEZ_X(JATM)+F3*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEX_TX=DIPOLEX_TX+DFLOAT(KX)*F1*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEY_TX=DIPOLEY_TX+DFLOAT(KX)*F2*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEZ_TX=DIPOLEZ_TX+DFLOAT(KX)*F3*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             F1=2.0D0*ZT(JJ)*M1(I1,I2,I3,J1,J2+1,J3,1)
             F2=2.0D0*ZT(JJ)*M1(I1,I2,I3,J1,J2+1,J3,2)
             F3=2.0D0*ZT(JJ)*M1(I1,I2,I3,J1,J2+1,J3,3)
             IF (J2 >= 1) THEN
              F1=F1-DFLOAT(J2)*M1(I1,I2,I3,J1,J2-1,J3,1)
              F2=F2-DFLOAT(J2)*M1(I1,I2,I3,J1,J2-1,J3,2)
              F3=F3-DFLOAT(J2)*M1(I1,I2,I3,J1,J2-1,J3,3)
             ENDIF
             DIPOLEX_Y(JATM)=DIPOLEX_Y(JATM)+CS*F1*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEY_Y(JATM)=DIPOLEY_Y(JATM)+CS*F2*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEZ_Y(JATM)=DIPOLEZ_Y(JATM)+CS*F3*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEX_Z(JATM)=DIPOLEX_Z(JATM)-SN*F1*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEY_Z(JATM)=DIPOLEY_Z(JATM)-SN*F2*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEZ_Z(JATM)=DIPOLEZ_Z(JATM)-SN*F3*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEX_TA=DIPOLEX_TA+DFLOAT(KX)*(-ATOMY(JATM)*SN-ATOMZ(JATM)*CS) &
                                  *F1*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEY_TA=DIPOLEY_TA+DFLOAT(KX)*(-ATOMY(JATM)*SN-ATOMZ(JATM)*CS) &
                                  *F2*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEZ_TA=DIPOLEZ_TA+DFLOAT(KX)*(-ATOMY(JATM)*SN-ATOMZ(JATM)*CS) &
                                  *F3*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             F1=2.0D0*ZT(JJ)*M1(I1,I2,I3,J1,J2,J3+1,1)
             F2=2.0D0*ZT(JJ)*M1(I1,I2,I3,J1,J2,J3+1,2)
             F3=2.0D0*ZT(JJ)*M1(I1,I2,I3,J1,J2,J3+1,3)
             IF (J3 >= 1) THEN
              F1=F1-DFLOAT(J3)*M1(I1,I2,I3,J1,J2,J3-1,1)
              F2=F2-DFLOAT(J3)*M1(I1,I2,I3,J1,J2,J3-1,2)
              F3=F3-DFLOAT(J3)*M1(I1,I2,I3,J1,J2,J3-1,3)
             ENDIF
             DIPOLEX_Y(JATM)=DIPOLEX_Y(JATM)+SN*F1*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEY_Y(JATM)=DIPOLEY_Y(JATM)+SN*F2*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEZ_Y(JATM)=DIPOLEZ_Y(JATM)+SN*F3*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEX_Z(JATM)=DIPOLEX_Z(JATM)+CS*F1*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEY_Z(JATM)=DIPOLEY_Z(JATM)+CS*F2*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEZ_Z(JATM)=DIPOLEZ_Z(JATM)+CS*F3*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEX_TA=DIPOLEX_TA+DFLOAT(KX)*(ATOMY(JATM)*CS-ATOMZ(JATM)*SN) &
                                  *F1*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEY_TA=DIPOLEY_TA+DFLOAT(KX)*(ATOMY(JATM)*CS-ATOMZ(JATM)*SN) &
                                  *F2*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEZ_TA=DIPOLEZ_TA+DFLOAT(KX)*(ATOMY(JATM)*CS-ATOMZ(JATM)*SN) &
                                  *F3*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             ! BASIS ROTATION FORCE
             DIPOLEX_TA=DIPOLEX_TA+M1(I1,I2,I3,J1,J2,J3,1)*P_P_HELIX_THETA(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEY_TA=DIPOLEY_TA+M1(I1,I2,I3,J1,J2,J3,2)*P_P_HELIX_THETA(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
             DIPOLEZ_TA=DIPOLEZ_TA+M1(I1,I2,I3,J1,J2,J3,3)*P_P_HELIX_THETA(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,KX)
            ENDIF
!if (myid == 0) write(*,*) i,j,i1,i2,i3,j1,j2,j3,dipolex_x(1)
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
    ! CONTRACTION
    DO L=1,9
     W1=M_P(:,:,KX,KY,KZ,L)
     W2=MATMUL(CC,W1)
     W3=MATMUL(W2,TRANSPOSE(CC))
     M_C(:,:,KX,KY,KZ,L)=W3
    ENDDO
   ENDDO
   ENDDO
   ENDDO
   IF ((IGRAD == 0).AND.(IOPTN(9) >= 2).AND.(MYID == 0)) THEN
    WRITE(6,'(A)') 'DIPOLE MOMENT INTEGRDAL MATRICES FOR CONTRACTED GAUSSIANS'
    CALL DUMP3(M_C,NCGS,CEL1X,CEL1Y,CEL1Z,1,9)
    CALL DUMP3(M_C,NCGS,CEL1X,CEL1Y,CEL1Z,2,9)
    CALL DUMP3(M_C,NCGS,CEL1X,CEL1Y,CEL1Z,3,9)
    WRITE(6,'(A)') 'QUADRUPOLE MOMENT INTEGRDAL MATRICES FOR CONTRACTED GAUSSIANS'
    CALL DUMP3(M_C,NCGS,CEL1X,CEL1Y,CEL1Z,4,9)
    CALL DUMP3(M_C,NCGS,CEL1X,CEL1Y,CEL1Z,5,9)
    CALL DUMP3(M_C,NCGS,CEL1X,CEL1Y,CEL1Z,6,9)
    CALL DUMP3(M_C,NCGS,CEL1X,CEL1Y,CEL1Z,7,9)
    CALL DUMP3(M_C,NCGS,CEL1X,CEL1Y,CEL1Z,8,9)
    CALL DUMP3(M_C,NCGS,CEL1X,CEL1Y,CEL1Z,9,9)
   ENDIF

   IF (IGRAD == 1) THEN
    DO KX=CEL2X+1,HUGECELL
     A=1.0D0/((DFLOAT(KX)*PERIODX)**3)
     B=DCOS(DFLOAT(KX)*HELIX)
     C=-3.0D0*A/PERIODX
     D=DFLOAT(KX)*DSIN(DFLOAT(KX)*HELIX)
     DO I=1,NATOM
      FORCEX(I)=FORCEX(I)+A*(-4.0D0*DIPOLEX*DIPOLEX_X(I)+2.0D0*B*DIPOLEY*DIPOLEY_X(I)+2.0D0*B*DIPOLEZ*DIPOLEZ_X(I))
      FORCEY(I)=FORCEY(I)+A*(-4.0D0*DIPOLEX*DIPOLEX_Y(I)+2.0D0*B*DIPOLEY*DIPOLEY_Y(I)+2.0D0*B*DIPOLEZ*DIPOLEZ_Y(I))
      FORCEZ(I)=FORCEZ(I)+A*(-4.0D0*DIPOLEX*DIPOLEX_Z(I)+2.0D0*B*DIPOLEY*DIPOLEY_Z(I)+2.0D0*B*DIPOLEZ*DIPOLEZ_Z(I))
     ENDDO
     FORCETX=FORCETX+A*(-4.0D0*DIPOLEX*DIPOLEX_TX+2.0D0*B*DIPOLEY*DIPOLEY_TX+2.0D0*B*DIPOLEZ*DIPOLEZ_TX) &
                    +C*(-2.0D0*DIPOLEX**2+B*DIPOLEY**2+B*DIPOLEZ**2)
     FORCETA=FORCETA+A*(-4.0D0*DIPOLEX*DIPOLEX_TA+2.0D0*B*DIPOLEY*DIPOLEY_TA+2.0D0*B*DIPOLEZ*DIPOLEZ_TA &
                        -D*(DIPOLEY**2+DIPOLEZ**2))
    ENDDO
   ENDIF
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) CALL DUMP2

   IF ((IOPTN(9) == 3).AND.(MYID == 0)) THEN
    WRITE(6,'(A)') 'DIPOLE MOMENT DERIVATIVES IN ATOMIC UNITS'
    WRITE(6,'(A)') '-------------------------------'
    DO I=1,NATOM
     WRITE(6,'(A,I0,A,3(A,F15.10))') 'ATOM',I,'X: ','X',DIPOLEX_X(I),'  Y',DIPOLEY_X(I),'  Z',DIPOLEZ_X(I)
     WRITE(6,'(A,I0,A,3(A,F15.10))') 'ATOM',I,'Y: ','X',DIPOLEX_Y(I),'  Y',DIPOLEY_Y(I),'  Z',DIPOLEZ_Y(I)
     WRITE(6,'(A,I0,A,3(A,F15.10))') 'ATOM',I,'Z: ','X',DIPOLEX_Z(I),'  Y',DIPOLEY_Z(I),'  Z',DIPOLEZ_Z(I)
    ENDDO
    WRITE(6,'(A,3(A,F15.10))') 'TRANSLATION: ','X',DIPOLEX_TX,'  Y',DIPOLEY_TX,'  Z',DIPOLEZ_TX
    WRITE(6,'(A,3(A,F15.10))') 'HELICAL    : ','X',DIPOLEX_TA,'  Y',DIPOLEY_TA,'  Z',DIPOLEZ_TA
   ENDIF

   DEALLOCATE(M1,W1,W2,W3,M_P)
   RETURN
END SUBROUTINE
