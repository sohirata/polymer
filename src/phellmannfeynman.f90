SUBROUTINE HELLMANN_FEYNMAN_REPULSION
! CALCULATE NUCLEUS-NUCLEUS REPULSION PART OF THE HELLMANN-FEYNMAN FORCES.
! CALL THIS SUBROUTINE FIRST TO ZERO SCRATCH FORCES.

   USE MPI
   USE CONTROL
   USE STRUCTURE
   USE INTEGRAL
   USE GRADIENT
   
   IMPLICIT NONE
   INTEGER :: I,J,QX,QY,QZ,L,M
   DOUBLE PRECISION :: X0,Y0,Z0,X1,Y1,Z1,X2,Y2,Z2,R
   DOUBLE PRECISION :: ANGLE,CS,SN

   IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE NUCLEAR REPULSION DERIVATIVES (HELLMANN-FEYNMAN REPULSION)'

   ! ZERO SCRATCH FORCES
   FORCEX=0.0D0
   FORCEY=0.0D0
   FORCEZ=0.0D0
   FORCETX=0.0D0
   FORCETY=0.0D0
   FORCETZ=0.0D0
   FORCETA=0.0D0
    
   DO I=1,NATOM
    L=IATOM(I)
    X0=ATOMX(I)
    Y0=ATOMY(I)
    Z0=ATOMZ(I)
    DO J=1,NATOM
     M=IATOM(J)
     X1=ATOMX(J)
     Y1=ATOMY(J)
     Z1=ATOMZ(J)
     DO QX=-CEL2X,CEL2X
     DO QY=-CEL2Y,CEL2Y
     DO QZ=-CEL2Z,CEL2Z
      ANGLE=DFLOAT(QX)*HELIX
      CS=DCOS(ANGLE)
      SN=DSIN(ANGLE)
      X2=X1+DFLOAT(QX)*PERIODX
      Y2=Y1*CS-Z1*SN+DFLOAT(QY)*PERIODY
      Z2=Y1*SN+Z1*CS+DFLOAT(QZ)*PERIODZ
      IF ((QX /= 0).OR.(QY /= 0).OR.(QZ /= 0).OR.(I /= J)) THEN
       R=(DSQRT((X2-X0)**2+(Y2-Y0)**2+(Z2-Z0)**2))**3
       IF (HELIX == 0.0D0) THEN
        FORCEX(I)=FORCEX(I)-0.5D0*DFLOAT(L*M)*(X0-X2)/R
        FORCEY(I)=FORCEY(I)-0.5D0*DFLOAT(L*M)*(Y0-Y2)/R
        FORCEZ(I)=FORCEZ(I)-0.5D0*DFLOAT(L*M)*(Z0-Z2)/R
        FORCEX(J)=FORCEX(J)-0.5D0*DFLOAT(L*M)*(X2-X0)/R
        FORCEY(J)=FORCEY(J)-0.5D0*DFLOAT(L*M)*(Y2-Y0)/R
        FORCEZ(J)=FORCEZ(J)-0.5D0*DFLOAT(L*M)*(Z2-Z0)/R
        FORCETX  =FORCETX  -0.5D0*DFLOAT(L*M)*(X2-X0)/R*DFLOAT(QX)
        FORCETY  =FORCETY  -0.5D0*DFLOAT(L*M)*(Y2-Y0)/R*DFLOAT(QY)
        FORCETZ  =FORCETZ  -0.5D0*DFLOAT(L*M)*(Z2-Z0)/R*DFLOAT(QZ)
       ELSE ! --- HELIX
        FORCEX(I)=FORCEX(I)-0.5D0*DFLOAT(L*M)*(X0-X2)/R
        FORCEY(I)=FORCEY(I)-0.5D0*DFLOAT(L*M)*(Y0-Y2)/R
        FORCEZ(I)=FORCEZ(I)-0.5D0*DFLOAT(L*M)*(Z0-Z2)/R
        FORCEX(J)=FORCEX(J)-0.5D0*DFLOAT(L*M)*(X2-X0)/R
        FORCEY(J)=FORCEY(J)+CS*(-0.5D0*DFLOAT(L*M)*(Y2-Y0)/R)
        FORCEZ(J)=FORCEZ(J)-SN*(-0.5D0*DFLOAT(L*M)*(Y2-Y0)/R)
        FORCEY(J)=FORCEY(J)+SN*(-0.5D0*DFLOAT(L*M)*(Z2-Z0)/R)
        FORCEZ(J)=FORCEZ(J)+CS*(-0.5D0*DFLOAT(L*M)*(Z2-Z0)/R)
        FORCETX  =FORCETX  -0.5D0*DFLOAT(L*M)*(X2-X0)/R*DFLOAT(QX)
        FORCETA  =FORCETA  +DFLOAT(QX)*(-Y1*SN-Z1*CS)*(-0.5D0*DFLOAT(L*M)*(Y2-Y0)/R) &
                           +DFLOAT(QX)*( Y1*CS-Z1*SN)*(-0.5D0*DFLOAT(L*M)*(Z2-Z0)/R)
       ENDIF ! --- HELIX END
      ENDIF
     ENDDO
     ENDDO
     ENDDO
    ENDDO
   ENDDO
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) CALL DUMP2

   RETURN
END SUBROUTINE



SUBROUTINE HELLMANN_FEYNMAN_ATTRACTION
! CALCULATE NUCLEUS-ELECTRON ATTRACTION PART OF THE HELLMANN-FEYNMAN FORCES.

   USE MPI
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   USE FMT
   USE CONSTANTS

   IMPLICIT NONE
   DOUBLE PRECISION,ALLOCATABLE :: N1(:,:,:,:,:,:,:),N2(:,:,:,:,:,:,:)
   DOUBLE PRECISION :: PISUB
   DOUBLE PRECISION :: QX1,QY1,QZ1,QX2,QY2,QZ2
   DOUBLE PRECISION :: ANGLE,CS,SN,ANGLE2,CS2,SN2
   DOUBLE PRECISION :: COMZ
   DOUBLE PRECISION :: AX,AY,AZ,BX,BY,BZ,CX,CY,CZ,PX,PY,PZ,E1,E2
   DOUBLE PRECISION :: T,H,F1(0:2*BASIS_LMAX+1),F2(0:2*BASIS_LMAX+1)
   INTEGER :: Q1X,Q1Y,Q1Z,Q2X,Q2Y,Q2Z,I,J,K,II,JJ,IATM,JATM
   INTEGER :: I1,I2,I3,J1,J2,J3
   INTEGER :: M,TS,MMAX,EO

   ALLOCATE(N1(0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:2*BASIS_LMAX+1))
   ALLOCATE(N2(0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:BASIS_LMAX+1,0:2*BASIS_LMAX))

   PISUB=2.0D0/DSQRT(PI)
   F1(0)=1.0D0/PISUB
   F2(0)=-0.5D0
   DO I=1,2*BASIS_LMAX+1
    F1(I)=F1(I-1)*DFLOAT(2*I-1)/2.0D0
    F2(I)=F2(I-1)-1.0D0
   ENDDO

   IF (MYID == 0) WRITE(6,'(A)') 'CALCULATE ELECTRIC FIELD INTEGRALS (HELLMANN-FEYNMAN ATTRACTION)'

   DO Q1X=-CEL1X,CEL1X
   DO Q1Y=-CEL1Y,CEL1Y
   DO Q1Z=-CEL1Z,CEL1Z
    ANGLE=DFLOAT(Q1X)*HELIX
    CS=DCOS(ANGLE)
    SN=DSIN(ANGLE)
    QX1=DFLOAT(Q1X)*PERIODX
    QY1=DFLOAT(Q1Y)*PERIODY
    QZ1=DFLOAT(Q1Z)*PERIODZ
    DO I=1,NPSHELL
     II=P_SHELL(I,0,0,0)
     IATM=PGS_ATOM(II)
     AX=PGSX(II)
     AY=PGSY(II)
     AZ=PGSZ(II)
     DO J=1,NPSHELL
      JJ=P_SHELL(J,0,0,0)
      JATM=PGS_ATOM(JJ)
      BX=PGSX(JJ)+QX1
      BY=PGSY(JJ)*CS-PGSZ(JJ)*SN+QY1
      BZ=PGSY(JJ)*SN+PGSZ(JJ)*CS+QZ1
      COMZ=ZT(II)+ZT(JJ)
      PX=(AX*ZT(II)+BX*ZT(JJ))/COMZ
      PY=(AY*ZT(II)+BY*ZT(JJ))/COMZ
      PZ=(AZ*ZT(II)+BZ*ZT(JJ))/COMZ
      E1=PISUB*DSQRT(COMZ)*S_P(II,JJ,Q1X,Q1Y,Q1Z)
      DO Q2X=-CEL2X,CEL2X
      DO Q2Y=-CEL2Y,CEL2Y
      DO Q2Z=-CEL2Z,CEL2Z
       ANGLE2=DFLOAT(Q2X)*HELIX
       CS2=DCOS(ANGLE2)
       SN2=DSIN(ANGLE2)
       QX2=DFLOAT(Q2X)*PERIODX
       QY2=DFLOAT(Q2Y)*PERIODY
       QZ2=DFLOAT(Q2Z)*PERIODZ
       DO K=1,NATOM
        E2=-DFLOAT(IATOM(K))*E1
        CX=ATOMX(K)+QX2
        CY=ATOMY(K)*CS2-ATOMZ(K)*SN2+QY2
        CZ=ATOMY(K)*SN2+ATOMZ(K)*CS2+QZ2
        T=COMZ*((PX-CX)**2+(PY-CY)**2+(PZ-CZ)**2)
        DO M=0,P_SHELL_ANG(I)+P_SHELL_ANG(J)+1
         IF (T < TF(M)) THEN
          TS=NINT(T*20.0D0)
          H=0.05D0*DFLOAT(TS)-T
          N1(0,0,0,0,0,0,M)=((((((IGAMMA(TS,M+6)*H*0.166666666666667D0+IGAMMA(TS,M+5))*H*0.2D0+IGAMMA(TS,M+4))*H*0.25D0+ &
          IGAMMA(TS,M+3))*H*0.333333333333333D0+IGAMMA(TS,M+2))*H*0.5D0+IGAMMA(TS,M+1))*H+IGAMMA(TS,M))*E2
         ELSE
          N1(0,0,0,0,0,0,M)=E2*F1(M)*DEXP(F2(M)*DLOG(T))
         ENDIF
        ENDDO

        DO I1=0,P_SHELL_ANG(I)
         DO I2=0,P_SHELL_ANG(I)-I1
          DO I3=0,P_SHELL_ANG(I)-I1-I2
           DO J1=0,P_SHELL_ANG(J)
            DO J2=0,P_SHELL_ANG(J)-J1
             DO J3=0,P_SHELL_ANG(J)-J1-J2
              MMAX=P_SHELL_ANG(I)+P_SHELL_ANG(J)-I1-I2-I3-J1-J2-J3
              IF (I1+I2+I3+J1+J2+J3 == 0) CYCLE
              DO M=0,MMAX+1
               IF (I1 >= 1) THEN
                N1(I1,I2,I3,J1,J2,J3,M)=(PX-AX)*N1(I1-1,I2,I3,J1,J2,J3,M)-(PX-CX)*N1(I1-1,I2,I3,J1,J2,J3,M+1)
                IF (I1 >= 2) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(I1-1)/COMZ*(N1(I1-2,I2,I3,J1,J2,J3,M)-N1(I1-2,I2,I3,J1,J2,J3,M+1))
                IF (J1 >= 1) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(J1)/COMZ*(N1(I1-1,I2,I3,J1-1,J2,J3,M)-N1(I1-1,I2,I3,J1-1,J2,J3,M+1))
               ELSE IF (I2 >= 1) THEN
                N1(I1,I2,I3,J1,J2,J3,M)=(PY-AY)*N1(I1,I2-1,I3,J1,J2,J3,M)-(PY-CY)*N1(I1,I2-1,I3,J1,J2,J3,M+1)
                IF (I2 >= 2) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(I2-1)/COMZ*(N1(I1,I2-2,I3,J1,J2,J3,M)-N1(I1,I2-2,I3,J1,J2,J3,M+1))
                IF (J2 >= 1) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(J2)/COMZ*(N1(I1,I2-1,I3,J1,J2-1,J3,M)-N1(I1,I2-1,I3,J1,J2-1,J3,M+1))
               ELSE IF (I3 >= 1) THEN
                N1(I1,I2,I3,J1,J2,J3,M)=(PZ-AZ)*N1(I1,I2,I3-1,J1,J2,J3,M)-(PZ-CZ)*N1(I1,I2,I3-1,J1,J2,J3,M+1)
                IF (I3 >= 2) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(I3-1)/COMZ*(N1(I1,I2,I3-2,J1,J2,J3,M)-N1(I1,I2,I3-2,J1,J2,J3,M+1))
                IF (J3 >= 1) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(J3)/COMZ*(N1(I1,I2,I3-1,J1,J2,J3-1,M)-N1(I1,I2,I3-1,J1,J2,J3-1,M+1))
               ELSE IF (J1 >= 1) THEN
                N1(I1,I2,I3,J1,J2,J3,M)=(PX-BX)*N1(I1,I2,I3,J1-1,J2,J3,M)-(PX-CX)*N1(I1,I2,I3,J1-1,J2,J3,M+1)
                IF (J1 >= 2) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(J1-1)/COMZ*(N1(I1,I2,I3,J1-2,J2,J3,M)-N1(I1,I2,I3,J1-2,J2,J3,M+1))
               ELSE IF (J2 >= 1) THEN
                N1(I1,I2,I3,J1,J2,J3,M)=(PY-BY)*N1(I1,I2,I3,J1,J2-1,J3,M)-(PY-CY)*N1(I1,I2,I3,J1,J2-1,J3,M+1)
                IF (J2 >= 2) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(J2-1)/COMZ*(N1(I1,I2,I3,J1,J2-2,J3,M)-N1(I1,I2,I3,J1,J2-2,J3,M+1))
               ELSE IF (J3 >= 1) THEN
                N1(I1,I2,I3,J1,J2,J3,M)=(PZ-BZ)*N1(I1,I2,I3,J1,J2,J3-1,M)-(PZ-CZ)*N1(I1,I2,I3,J1,J2,J3-1,M+1)
                IF (J3 >= 2) N1(I1,I2,I3,J1,J2,J3,M)=N1(I1,I2,I3,J1,J2,J3,M)+ &
                0.5D0*DFLOAT(J3-1)/COMZ*(N1(I1,I2,I3,J1,J2,J3-2,M)-N1(I1,I2,I3,J1,J2,J3-2,M+1))
               END IF
              ENDDO
             ENDDO
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
        DO EO=1,3
         DO I1=0,P_SHELL_ANG(I)
          DO I2=0,P_SHELL_ANG(I)-I1
           DO I3=0,P_SHELL_ANG(I)-I1-I2
            DO J1=0,P_SHELL_ANG(J)
             DO J2=0,P_SHELL_ANG(J)-J1
              DO J3=0,P_SHELL_ANG(J)-J1-J2
               MMAX=P_SHELL_ANG(I)+P_SHELL_ANG(J)-I1-I2-I3-J1-J2-J3
               DO M=0,MMAX
                IF (I1+I2+I3+J1+J2+J3 == 0) THEN
                 IF (EO == 1) N2(0,0,0,0,0,0,M)=2.0D0*COMZ*(PX-CX)*N1(0,0,0,0,0,0,M+1)
                 IF (EO == 2) N2(0,0,0,0,0,0,M)=2.0D0*COMZ*(PY-CY)*N1(0,0,0,0,0,0,M+1)
                 IF (EO == 3) N2(0,0,0,0,0,0,M)=2.0D0*COMZ*(PZ-CZ)*N1(0,0,0,0,0,0,M+1)
                ELSE IF (I1 >= 1) THEN
                 N2(I1,I2,I3,J1,J2,J3,M)=(PX-AX)*N2(I1-1,I2,I3,J1,J2,J3,M)-(PX-CX)*N2(I1-1,I2,I3,J1,J2,J3,M+1)
                 IF (I1 >= 2) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+ &
                 0.5D0*DFLOAT(I1-1)/COMZ*(N2(I1-2,I2,I3,J1,J2,J3,M)-N2(I1-2,I2,I3,J1,J2,J3,M+1))
                 IF (J1 >= 1) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+ &
                 0.5D0*DFLOAT(J1)/COMZ*(N2(I1-1,I2,I3,J1-1,J2,J3,M)-N2(I1-1,I2,I3,J1-1,J2,J3,M+1))
                 IF (EO == 1) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+N1(I1-1,I2,I3,J1,J2,J3,M+1)
                ELSE IF (I2 >= 1) THEN
                 N2(I1,I2,I3,J1,J2,J3,M)=(PY-AY)*N2(I1,I2-1,I3,J1,J2,J3,M)-(PY-CY)*N2(I1,I2-1,I3,J1,J2,J3,M+1)
                 IF (I2 >= 2) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+ &
                 0.5D0*DFLOAT(I2-1)/COMZ*(N2(I1,I2-2,I3,J1,J2,J3,M)-N2(I1,I2-2,I3,J1,J2,J3,M+1))
                 IF (J2 >= 1) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+ &
                 0.5D0*DFLOAT(J2)/COMZ*(N2(I1,I2-1,I3,J1,J2-1,J3,M)-N2(I1,I2-1,I3,J1,J2-1,J3,M+1))
                 IF (EO == 2) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+N1(I1,I2-1,I3,J1,J2,J3,M+1)
                ELSE IF (I3 >= 1) THEN
                 N2(I1,I2,I3,J1,J2,J3,M)=(PZ-AZ)*N2(I1,I2,I3-1,J1,J2,J3,M)-(PZ-CZ)*N2(I1,I2,I3-1,J1,J2,J3,M+1)
                 IF (I3 >= 2) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+ &
                 0.5D0*DFLOAT(I3-1)/COMZ*(N2(I1,I2,I3-2,J1,J2,J3,M)-N2(I1,I2,I3-2,J1,J2,J3,M+1))
                 IF (J3 >= 1) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+ &
                 0.5D0*DFLOAT(J3)/COMZ*(N2(I1,I2,I3-1,J1,J2,J3-1,M)-N2(I1,I2,I3-1,J1,J2,J3-1,M+1))
                 IF (EO == 3) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+N1(I1,I2,I3-1,J1,J2,J3,M+1)
                ELSE IF (J1 >= 1) THEN
                 N2(I1,I2,I3,J1,J2,J3,M)=(PX-BX)*N2(I1,I2,I3,J1-1,J2,J3,M)-(PX-CX)*N2(I1,I2,I3,J1-1,J2,J3,M+1)
                 IF (J1 >= 2) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+ &
                 0.5D0*DFLOAT(J1-1)/COMZ*(N2(I1,I2,I3,J1-2,J2,J3,M)-N2(I1,I2,I3,J1-2,J2,J3,M+1))
                 IF (EO == 1) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+N1(I1,I2,I3,J1-1,J2,J3,M+1)
                ELSE IF (J2 >= 1) THEN
                 N2(I1,I2,I3,J1,J2,J3,M)=(PY-BY)*N2(I1,I2,I3,J1,J2-1,J3,M)-(PY-CY)*N2(I1,I2,I3,J1,J2-1,J3,M+1)
                 IF (J2 >= 2) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+ &
                 0.5D0*DFLOAT(J2-1)/COMZ*(N2(I1,I2,I3,J1,J2-2,J3,M)-N2(I1,I2,I3,J1,J2-2,J3,M+1))
                 IF (EO == 2) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+N1(I1,I2,I3,J1,J2-1,J3,M+1)
                ELSE IF (J3 >= 1) THEN
                 N2(I1,I2,I3,J1,J2,J3,M)=(PZ-BZ)*N2(I1,I2,I3,J1,J2,J3-1,M)-(PZ-CZ)*N2(I1,I2,I3,J1,J2,J3-1,M+1)
                 IF (J3 >= 2) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+ &
                 0.5D0*DFLOAT(J3-1)/COMZ*(N2(I1,I2,I3,J1,J2,J3-2,M)-N2(I1,I2,I3,J1,J2,J3-2,M+1))
                 IF (EO == 3) N2(I1,I2,I3,J1,J2,J3,M)=N2(I1,I2,I3,J1,J2,J3,M)+N1(I1,I2,I3,J1,J2,J3-1,M+1)
                END IF
               ENDDO
              ENDDO
             ENDDO
            ENDDO
           ENDDO
          ENDDO
         ENDDO
         DO I1=0,P_SHELL_ANG(I)
          DO I2=0,P_SHELL_ANG(I)-I1
           DO I3=0,P_SHELL_ANG(I)-I1-I2
            DO J1=0,P_SHELL_ANG(J)
             DO J2=0,P_SHELL_ANG(J)-J1
              DO J3=0,P_SHELL_ANG(J)-J1-J2
               IF (HELIX == 0.0D0) THEN
                IF (EO == 1) THEN
                 FORCEX(K)=FORCEX(K)+N2(I1,I2,I3,J1,J2,J3,0)*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),Q1X,Q1Y,Q1Z)
                 FORCETX  =FORCETX  +N2(I1,I2,I3,J1,J2,J3,0)*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),Q1X,Q1Y,Q1Z)*DFLOAT(Q2X)
                ELSE IF (EO == 2) THEN
                 FORCEY(K)=FORCEY(K)+N2(I1,I2,I3,J1,J2,J3,0)*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),Q1X,Q1Y,Q1Z)
                 FORCETY  =FORCETY  +N2(I1,I2,I3,J1,J2,J3,0)*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),Q1X,Q1Y,Q1Z)*DFLOAT(Q2Y)
                ELSE
                 FORCEZ(K)=FORCEZ(K)+N2(I1,I2,I3,J1,J2,J3,0)*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),Q1X,Q1Y,Q1Z)
                 FORCETZ  =FORCETZ  +N2(I1,I2,I3,J1,J2,J3,0)*P_P(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),Q1X,Q1Y,Q1Z)*DFLOAT(Q2Z)
                ENDIF
               ELSE ! --- HELIX
                IF (EO == 1) THEN
                 FORCEX(K)=FORCEX(K)+N2(I1,I2,I3,J1,J2,J3,0)*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,Q1X)
                 FORCETX  =FORCETX  +N2(I1,I2,I3,J1,J2,J3,0)*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,Q1X)*DFLOAT(Q2X)
                ELSE IF (EO == 2) THEN
                 FORCEY(K)=FORCEY(K)+CS2*N2(I1,I2,I3,J1,J2,J3,0)*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,Q1X)
                 FORCEZ(K)=FORCEZ(K)-SN2*N2(I1,I2,I3,J1,J2,J3,0)*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,Q1X)
                 FORCETA  =FORCETA  +DFLOAT(Q2X)*(-ATOMY(K)*SN2-ATOMZ(K)*CS2) &
                                    *N2(I1,I2,I3,J1,J2,J3,0)*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,Q1X)
                ELSE
                 FORCEY(K)=FORCEY(K)+SN2*N2(I1,I2,I3,J1,J2,J3,0)*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,Q1X)
                 FORCEZ(K)=FORCEZ(K)+CS2*N2(I1,I2,I3,J1,J2,J3,0)*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,Q1X)
                 FORCETA  =FORCETA  +DFLOAT(Q2X)*(ATOMY(K)*CS2-ATOMZ(K)*SN2) &
                                    *N2(I1,I2,I3,J1,J2,J3,0)*P_P_HELIX(P_SHELL(I,I1,I2,I3),P_SHELL(J,J1,J2,J3),0,Q1X)
                ENDIF
               ENDIF ! --- HELIX END
              ENDDO
             ENDDO
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
      ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDDO
   ENDDO
   ENDDO
   DEALLOCATE(N1,N2)
   IF ((IOPTN(9) == 3).AND.(MYID == 0)) CALL DUMP2

   RETURN
END SUBROUTINE
